;純愛用口上動作テスト版
;とりあえず主人公の霊夢さんに生贄になってもらいました。
;こんな感じで出せるよー、というサンプルに過ぎないので内容は物凄く適当です
;間違ってもこれで満足しないで下さいね　＞霊夢が嫁の方々（≒口上書いてくれそうな方々）

;とりあえずフラグの内容くらいは全部書くべきだと思った。
;現状で用意しているシチュエーションはこれで全てです。

;いくつかのイベント（オープニング、エンディング、告白、戦闘で全滅、ラスボス前会話など）
;ではデフォルトの文章を用意してあるので、「CALL COMMON_PLANE_TEXT」
;を実行する事で、状況に合わせた文章が表示されます。
;どんな文章が用意されているのか気になる人は、PLANE_TEXT.ERBをご覧下さい。

;自分で書くのが面倒な部分は、この共通の文章に全て任せてしまっても構わないし、
;共通の文章を表示した後で、更に一言二言喋るような感じでも実装できます。
;あるいは、共通の文章を一切使わず、全てのシチュエーションを自分で書く事も可能です。

;全部共通の文章に任せている例	→「その他」の部分（現状ではここに引っかかるシチュエーションは無いですが）
;共通文章＋αの例		→「耐力ゼロ（TFLAG:600 == 13）」の部分
;全部自分で書いた例		→「全滅（TFLAG:600 == 14）」の部分

@KOJO_JUN_K1
;-------------------------------------------------
;ファーストキス　 CFLAG111
;-------------------------------------------------
IF TFLAG:600 == 1
	;初回
	IF CFLAG:111 == 0
		;既に恋慕持ち
		IF TALENT:85
			PRINTFORMW 「ん……ふふっ、やっとあなたとキスできた。ずっと待ってたんだからね？」
		ELSE
			PRINTFORMW 「ん……あーあ、ついにキスまでしちゃったか。ただの異変解決のパートナーだと思ってたんだけどね……」
		ENDIF
		CFLAG:111 = 1
	ENDIF


;-------------------------------------------------
;告白成功　 CFLAG112
;-------------------------------------------------
ELSEIF TFLAG:600 == 2
	;初回
	IF CFLAG:112 == 0
		;主人公から告白
		IF TFLAG:45 == 0
			;既に恋慕持ち
			IF TALENT:85
				PRINTFORMW 「やっと好きって言ってくれたわね。……結構待ってたのよ？」
				PRINTFORMW 「その……ちょっと恥ずかしくてそれっぽい雰囲気から逃げてたのは認めるけど」
			ELSE
				PRINTFORMW 「そうね……最初はこんな関係になるなんて思ってもみなかったけど」
				PRINTFORMW 「私もあんたの事嫌いじゃないし……いいわ、これからもよろしくね？」
			ENDIF

		;霊夢から告白
		ELSE
			;既に恋慕持ち
			IF TALENT:85
				;自分がツンデレ好きなんだなー、と再認識
				PRINTFORMW 「散々私の事やきもきさせて……もう良いわ、私の方からはっきり言ってあげる」
				PRINTFORMW 「あ……あなたの事が好きなのよ！最初は全然何とも思ってなかったけど、もうダメ！」
				PRINTFORMW それを聞いた%CALLNAME:MASTER%は、自分も霊夢のことが好きだと伝えた
				PRINTFORMW 「っ……！！もう、絶対離さないんだからね！これから覚悟しておきなさいよ！？」
			ELSE
				;もはや告白と言うよりプロポーズ
				PRINTFORMW 「ねぇ、私……あんたの事好きになっちゃったのかもしれない」
				PRINTFORMW 「最近何て言うかこう……一緒に居ないと落ち着かないの」
				PRINTFORMW 「だからさ、今は異変解決のために一緒に居るだけだけど……」
				PRINTFORMW 「この異変が解決しても、ずっとこうやって一緒に居られないかしら？」
			ENDIF

		ENDIF

		CFLAG:112 = 1
	ENDIF

;-------------------------------------------------
;告白失敗　 CFLAG113
;-------------------------------------------------
ELSEIF TFLAG:600 == 3
	;初回
	IF CFLAG:113 == 0
		;主人公から告白
		IF TFLAG:45 == 0
			;既に恋慕持ち
			IF TALENT:85
				PRINTFORMW 「えっ……ちょっ、待って、え、あの……あーもう！私には何も聞こえないわよ！！」
				PRINTFORMW 「もう変な事言おうとするの禁止！！その……(恥ずかしいじゃない)」
				PRINTFORMW 最後の方は声が小さくて聞き取り辛かったが、要するに断られてしまったらしい
			ELSE
				;好感度がバカみたいに低い
				IF CFLAG:2 < 100
					PRINTFORMW 「…………あのね、冗談は内容を選んだ方が良いと思うわよ？」
				ELSE
					PRINTFORMW 「冗談……を言ってるわけじゃないみたいね」
					PRINTFORMW 「とりあえず、私はあなたと異変解決のために組んでるだけ。それ以上の関係になるつもりは無いわ」
				ENDIF
			ENDIF

		;霊夢から告白
		ELSE
			;既に恋慕持ち
			IF TALENT:85
				PRINTFORMW 「あのね、その……あーもう！やっぱり無理！！恥ずかしすぎるわ！！」
				PRINTFORMW 何だか訳のわからない台詞を残して、顔を真っ赤にした霊夢は走り去ってしまった……
			ELSE
				;面倒なので使いまわし
				;好感度がバカみたいに低い
				IF CFLAG:2 < 100
					PRINTFORMW 「…………あのね、冗談は内容を選んだ方が良いと思うわよ？」
				ELSE
					PRINTFORMW 「冗談……を言ってるわけじゃないみたいね」
					PRINTFORMW 「とりあえず、私はあなたと異変解決のために組んでるだけ。それ以上の関係になるつもりは無いわ」
				ENDIF
			ENDIF
		ENDIF

		CFLAG:113 = 1
	ENDIF

;-------------------------------------------------
;服を無理矢理脱がせる　 CFLAG114
;-------------------------------------------------
ELSEIF TFLAG:600 == 4
	;初回
	IF CFLAG:114 == 0
		PRINTFORMW 「そんなにがっつかなくても良いじゃない……」
		PRINTFORMW 「もう、分かったわ。脱げば良いんでしょ？」
		PRINTFORMW 「……その、あんまり酷い事はしないでよね？」

		CFLAG:114 = 1
	ENDIF

;-------------------------------------------------
;服を脱がせる　 CFLAG115
;-------------------------------------------------
ELSEIF TFLAG:600 == 5

	;ちゃんと合意の上
	IF TFLAG:48 == 0
		PRINTFORMW 「え、服……脱ぐの？」
		PRINTFORMW 「……うん……分かった。優しくしてよね？」
	;酔った勢い
	ELSEIF TFLAG:48 == 1
		PRINTFORMW 「少し飲みすぎたかしら……体火照ってきちゃった」
		PRINTFORMW 「え？……あ、そうね、暑いなら脱いじゃえば良いわよね」
		PRINTFORMW 「ふふっ、それじゃ折角だしついでに気持ち良い事でもしましょうか」
	ENDIF

;-------------------------------------------------
;服脱がせ失敗　 CFLAG116
;-------------------------------------------------
ELSEIF TFLAG:600 == 6
	;初回
	IF CFLAG:116 == 0
		PRINTFORMW 「ちょっ、コラ！あんた何しようとしてんのよ！？」

		CFLAG:116 = 1
	ELSE
		PRINTFORMW 「ちょっと、また！？何で私が服脱がなきゃならないのよ！？」
	ENDIF

;-------------------------------------------------
;「合意」で服脱がせる　 CFLAG117
;-------------------------------------------------
ELSEIF TFLAG:600 == 7

	PRINTFORMW 「ん……するの？　分かった、ちょっと待っててね」
	PRINTFORMW そう言って霊夢は自分からスルスルと服を脱いでいった

;-------------------------------------------------
;道中攻略成功　 CFLAG118
;-------------------------------------------------
ELSEIF TFLAG:600 == 8
	A = RAND:3
	IF A == 0
		PRINTFORMW 「楽勝楽勝♪」
	ELSEIF A == 1
		PRINTFORMW 「私の勘では……こっちね！こっちにきっと異変を起こした張本人が居るわ！」
	ELSE
		PRINTFORMW 「うーん、何だか疲れてきたわね。今日はもう帰ってお茶でも飲もうかしら」
	ENDIF

;-------------------------------------------------
;道中攻略失敗　 CFLAG119
;-------------------------------------------------
ELSEIF TFLAG:600 == 9
	A = RAND:3
	IF A == 0
		PRINTFORMW 「それにしてもこんな雑魚にやられるなんて……油断したわ……」
	ELSEIF A == 1
		PRINTFORMW 「うぅ……もう最悪……」
	ELSE
		PRINTFORMW 「修行不足かなぁ……でも修行って面倒なのよねぇ」
	ENDIF


;-------------------------------------------------
;ボス戦開始　 CFLAG120
;ボスの名前はSTR:1で参照可能です
;-------------------------------------------------
ELSEIF TFLAG:600 == 10

	;Stage3までは割と普通のバトルと言う設定
	IF FLAG:24 <= 3
		PRINTFORMW 「む、私の勘では%STR:1%、あんたこの異変に心当たりがありそうね」
		PRINTFORMW 「さぁ、洗いざらい吐いて貰うわよ！」
	;それ以外＝Stage4以降は、ボスにネチョい事をして満足させないとだめという設定
	ELSE
		PRINTFORMW 「む、私の勘では%STR:1%、あんたこの異変に心当たりがありそうね」
		PRINTFORMW 「待ってなさい、今すぐ抵抗する気が起きないほどアレしてあげるわ！」
	ENDIF

;-------------------------------------------------
;ボス戦勝利　 CFLAG121
;ボスの名前はSTR:1で参照可能です
;-------------------------------------------------
ELSEIF TFLAG:600 == 11

	;Stage3までは割と普通のバトルと言う設定
	IF FLAG:24 <= 3
		PRINTFORMW 「ま、私にかかればざっとこんな物ね」
		PRINTFORMW 「こいつには後で色々と手伝ってもらうとして……次はあっちね」
		PRINTFORMW 「……え、理由？勿論勘に決まってるじゃない」
	;ラスボス。FLAG:27==10で中ボス、>=20で大ボスになる
	ELSEIF FLAG:24 == 6 && FLAG:27 >= 20
		PRINTFORMW 「どう？これで文句は無いでしょう？　ほら、さっさと霧を止める！！」

	;それ以外＝Stage4以降は、ボスにネチョい事をして満足させないとだめという設定
	ELSE
		PRINTFORMW 「ま、私にかかればざっとこんな物ね……あんなに気持ち良さそうにして、何だかむかつくけど」
		PRINTFORMW 「さて、こいつには後で色々と手伝ってもらうとして……次はあっちね」
		PRINTFORMW 「……え、理由？勿論勘に決まってるじゃない」
	ENDIF


;-------------------------------------------------
;ボス戦敗北　 CFLAG122
;ボスの名前はSTR:1で参照可能です
;-------------------------------------------------
ELSEIF TFLAG:600 == 12

	A = RAND:3
	IF A == 0
		PRINTFORMW 「私が%STR:1%なんかにやられるなんて……油断したわ……」
	ELSEIF A == 1
		PRINTFORMW 「うぅ……もう最悪……」
	ELSE
		PRINTFORMW 「修行不足かなぁ……でも修行って面倒なのよねぇ」
	ENDIF


;-------------------------------------------------
;耐力ゼロ　 CFLAG123
;-------------------------------------------------
ELSEIF TFLAG:600 == 13
	;共通の文章表示
	CALL COMMON_PLANE_TEXT

	PRINTFORMW 「ふぁ…もうダメ……変になっちゃう………」
	PRINTFORMW 「あ、やっと助けに来たのね。　もぅ、遅いわよ……」
	PRINTL  

;-------------------------------------------------
;全滅　 CFLAG124
;-------------------------------------------------
ELSEIF TFLAG:600 == 14
	;ちょっと文章を弄ってみるテスト
	;ぶっちゃけ面倒だし、ここまでする人はいないと思うけど、まぁサンプルだし

	PRINTFORMW %CALLNAME:TARGET%は%STR:1%の攻めに耐え切れず、霧に飲み込まれてしまった！
	PRINTFORMW ぐったりとした%CALLNAME:TARGET%の頬は忽ち赤く染まり、荒い息をついている……
	PRINTFORMW それを見た%STR:1%は%CALLNAME:TARGET%に近付くと、巫女服の上から全身を愛撫し始めた
	PRINTFORMW 更に、服の脇が開いているのを良い事に、直接胸まで触りだした……
	PRINTFORMW 「っ……はぁ、はぁ……ん、あっ………」
	PRINTFORMW 全身が敏感になってしまった%CALLNAME:TARGET%には、喘ぎを止める事が出来なかった……
	PRINTFORML  
	PRINTFORMW  
	PRINTFORMW 暫くその様子を楽しんだ%STR:1%は、ついに%CALLNAME:TARGET%の服を脱がし、胸や秘所を触り始めた
	PRINTFORMW %CALLNAME:TARGET%の嬌声はより一層大きくなり、時折全身が痙攣したかのようにビクビクと震えている
	PRINTFORMW %CALLNAME:TARGET%を助けようとした%CALLNAME:MASTER%も、いつの間にか現れた他の娘達に襲われており、完全に成す術が無くなっていた
	PRINTFORML  
	PRINTFORMW  
	PRINTFORMW 数時間後、%STR:1%達が完全に満足して、ようやく２人は解放された
	PRINTFORMW 「おわ……った？……もう、この霧……すご…すぎる…わよ……」
	PRINTFORMW 「……ダメ、もう…動けない……ごめん…後お願い……」
	PRINTFORMW ……そう言い残して、霊夢は気絶してしまった
	PRINTFORMW 仕方なく%CALLNAME:MASTER%は、霊夢の格好を整えると、ヘトヘトになりながら家まで連れ帰った……

	PRINTL  


;-------------------------------------------------
;主人公だけやられる　 CFLAG125
;-------------------------------------------------
ELSEIF TFLAG:600 == 15

	PRINTFORMW %CALLNAME:MASTER%が目を覚ますと、そこは自分の家だった
	PRINTFORMW 「………あら、やっとお目覚め？」
	PRINTFORMW 「……言われなくても分かってるわよね？次こんな事になったら承知しないんだから」
	PRINTL  


;-------------------------------------------------
;主人公がお金稼ぎ　 CFLAG126
;-------------------------------------------------
ELSEIF TFLAG:600 == 16

	PRINTFORMW 「ん、お金稼いでくるの？」
	IF MONEY <= 10000
		PRINTFORMW 「まぁ確かに最近ちょっと心許ないしね。頑張ってらっしゃいな」
	ELSE
		PRINTFORMW 「その辺の妖怪をちゃんと退治してれば大丈夫なのに。まぁ、頑張ってらっしゃいな」
	ENDIF



;-------------------------------------------------
;２人でお金稼ぎ　 CFLAG127
;-------------------------------------------------
ELSEIF TFLAG:600 == 17

	PRINTFORMW 「さぁ、今日は私も一緒に頑張ってあげるから、じゃんじゃん稼ぐわよー！」


;-------------------------------------------------
;弁当販売　 CFLAG128
;-------------------------------------------------
ELSEIF TFLAG:600 == 18

	PRINTFORMW 「今日はお弁当でも売ってみましょうか」
	PRINTFORMW 「これでも料理には結構自信あるんだから。　ま、見てなさいって」


;-------------------------------------------------
;弁当食べる　 CFLAG129
;-------------------------------------------------
ELSEIF TFLAG:600 == 19

	PRINTFORMW 「うーん、お金稼ぐために売る予定だったんだけど……」
	PRINTFORMW 「ま、いっか。ほら、早く食べちゃいましょう」
	IF TALENT:153
		PRINTFORMW 「はい、あーん………何よ、恋人同士でしょ？こんな事しちゃ悪い？」
	ENDIF


;-------------------------------------------------
;戦闘訓練　 CFLAG130
;-------------------------------------------------
ELSEIF TFLAG:600 == 20

	PRINTFORMW 「……ふぅ、今回はこれくらいにしておきましょうか」
	IF ABL:戦闘技能 <= 2
		PRINTFORMW 「それにしても、２人である程度くっついてないといけないっていうのは動き辛いわね……」
	ELSE
		PRINTFORMW 「大分マシになってきたかしら。これなら割と何とかなりそうね」
	ENDIF

;-------------------------------------------------
;休憩　 CFLAG131
;-------------------------------------------------
ELSEIF TFLAG:600 == 21

	PRINTFORMW 「あら、今日はお休み？　ま、確かに息抜きも大切よね」
	IF TALENT:153
		PRINTFORMW 「あ、そこのお茶取って。　一緒に飲みましょう？」
	ELSE
		PRINTFORMW 「あ、台所からお茶取ってきて。やっぱり休憩にはお茶よね、お茶」
	ENDIF


;-------------------------------------------------
;パートナー交代　 CFLAG132
;交代した後の相手はTで取得可能
;-------------------------------------------------
ELSEIF TFLAG:600 == 22

	PRINTFORMW 「……あれ、私はお休み？」
	PRINTFORMW 「%CALLNAME:T%なんかより私に任せた方が絶対すぐ解決するのに……」


;-------------------------------------------------
;助手交代　 CFLAG133
;-------------------------------------------------
ELSEIF TFLAG:600 == 23

	PRINTFORMW 「……ん、今日からは%CALLNAME:ASSI%がサポートに入るのね」
	PRINTFORMW 「ま、よろしくお願いするわ」


;-------------------------------------------------
;別れる　 CFLAG134
;-------------------------------------------------
ELSEIF TFLAG:600 == 24

	PRINTFORMW 「ふーん、私の助けは要らないって事？」
	PRINTFORMW 「ま、良いわ。帰ってお茶でも飲んでるから」
	PRINTFORMW 「後で泣きついてきても知らないわよ？」
	;まぁ、別れた後はまたボスで出てくるようになるんで、お前⑨かよ！？ってなるんですがｗ


;-------------------------------------------------
;デート中に向こうからファーストキス　 CFLAG135
;-------------------------------------------------
ELSEIF TFLAG:600 == 25

	;初回(１回しか発生しないイベントのはずだけど、一応)
	IF CFLAG:135 == 0
		;既に恋慕持ち
		IF TALENT:85
			PRINTFORMW 「ん……ふふっ、やっとあなたとキスできた。ずっと待ってたんだからね？」
		ELSE
			PRINTFORMW 「ん……あーあ、ついにキスまでしちゃったか。ただの異変解決のパートナーだと思ってたんだけどね……」
		ENDIF
		CFLAG:135 = 1
	ENDIF


;-------------------------------------------------
;デート中に向こうから告白　 CFLAG136
;ちなみにTALENT:151を持ってる＝キス未経験の場合、このタイミングで一緒にファーストキスもする
;-------------------------------------------------
ELSEIF TFLAG:600 == 26

	;初回(１回しか発生しないイベントのはずだけど、一応)
	IF CFLAG:135 == 0
		;既に恋慕持ち
		IF TALENT:85
			;自分がツンデレ好きなんだなー、と再認識
			PRINTFORMW 「散々私の事やきもきさせて……もう良いわ、私の方からはっきり言ってあげる」
			PRINTFORMW 「あ……あなたの事が好きなのよ！最初は全然何とも思ってなかったけど、もうダメ！」
			PRINTFORMW それを聞いた%CALLNAME:MASTER%は、自分も霊夢のことが好きだと伝えた
			PRINTFORMW 「っ……！！もう、絶対離さないんだからね！これから覚悟しておきなさいよ！？」
		ELSE
			;もはや告白と言うよりプロポーズ
			PRINTFORMW 「ねぇ、私……あんたの事好きになっちゃったのかもしれない」
			PRINTFORMW 「最近何て言うかこう……一緒に居ないと落ち着かないの」
			PRINTFORMW 「だからさ、今は異変解決のために一緒に居るだけだけど……」
			PRINTFORMW 「この異変が解決しても、ずっとこうやって一緒に居られないかしら？」
		ENDIF
		CFLAG:136 = 1
	ENDIF



;-------------------------------------------------
;我慢できず押し倒される　 CFLAG137
;-------------------------------------------------
ELSEIF TFLAG:600 == 27
	;共通の文章表示
	CALL COMMON_PLANE_TEXT

	;通常
	IF TFLAG:48 == 0
		PRINTFORMW 「……もう我慢できない。私をこんな風にしちゃった責任、取って貰うわよ？」
	;酔った勢い
	ELSEIF TFLAG:48 == 1
		PRINTFORMW 「ふふっ…それじゃいっぱい気持ち良い事しましょうか？」
	ENDIF

;-------------------------------------------------
;オープニング　 CFLAG138
;-------------------------------------------------
ELSEIF TFLAG:600 == 28
	;共通の文章表示
	CALL COMMON_PLANE_TEXT

	PRINTFORMW 「それじゃあ今日からよろしくね」
	PRINTFORMW 「大丈夫よ、この私がついてるんだからこれくらいの異変、すぐ解決するわ」
	PRINTFORMW 「……だから、いくら霧の無効化のために必要だからって、変な事考えるんじゃないわよ？」


;-------------------------------------------------
;エンディング　 CFLAG139
;-------------------------------------------------
ELSEIF TFLAG:600 == 29
	;共通の文章表示
	CALL COMMON_PLANE_TEXT

	PRINTFORMW 「……ふぅ、何とか終わったわね」
	PRINTFORMW 「もうこんなに疲れる異変は沢山だわ……」
	IF TALENT:153
		PRINTFORMW 「さ、それじゃ早く帰りましょうか？今度は霧とか異変なんて関係なく……ね？」
	ELSE
		PRINTFORMW 「それじゃ、今までありがとうね。あんたの事、嫌いじゃなかったわよ？」
	ENDIF


;-------------------------------------------------
;３ボス後　 CFLAG140
;-------------------------------------------------
ELSEIF TFLAG:600 == 30
	;共通の文章表示
	CALL COMMON_PLANE_TEXT

	PRINTFORMW 「……私の勘だとむしろ黒幕があいつっぽいんだけど」
	PRINTFORMW 「ま、とりあえず言われた通りあいつの所に向かうしか無さそうね」
	IF TALENT:152 == 0
		PRINTFORMW 「……それと、あそこまで言われたんじゃ仕方ないわ」
		PRINTFORMW 「その……す、少しくらいならえっちな事しても良いわよ」
		PRINTFORMW 「その辺の奴らに負けて変な事されるよりは……その……まだマシだし」
	ENDIF


;-------------------------------------------------
;６ボス前　 CFLAG141
;-------------------------------------------------
ELSEIF TFLAG:600 == 31
	;レミリア
	IF FLAG:25 == 10
		PRINTFORMW 『あら？もうこんな所まで来ちゃったの？そんなにこの霧を止めたいのかしら』
		PRINTFORMW 「えぇそうね、色々と迷惑なのよ。この霧も、あんたも」
		PRINTFORMW 『私の作り出したこの霧を吸えば誰だって気持ち良くなれるのに』
		PRINTFORMW 「あら、別にこんな霧なんて無くても気持ち良くなれるわよ」
		PRINTFORMW 『そこまで言うのなら仕方ないわね。……なら、止めるにはどうすれば良いか分かるかしら』
		PRINTFORMW 「あんたを倒す。簡単だわね」
		PRINTFORMW 『フフ、何時ぞやの借りを返すチャンスね。覚悟は良いかしら？』
		PRINTFORMW 「あんたを棺桶に戻す覚悟なら出来てるわよ」
		PRINTFORMW 『こんなに月も紅いから、本気でいくわよ』
		PRINTFORMW 「こんなに月も紅いのに」
		PRINTFORML 『楽しい夜になりそうね』
		PRINTFORMW 「永い夜になりそうね」
		FLAG:27 = 21
	ELSE
		;共通の文章表示
		CALL COMMON_PLANE_TEXT

		PRINTFORMW 「そう、あんたがこの変な霧をばら撒いた犯人だったのね」
		SIF TALENT:153
			PRINTFORMW 「まぁお陰でちょっとは感謝できる事もあったけど」
		PRINTFORMW 「それでも私の仕事はただ一つ、異変を起こす奴を退治するのみ！！」

	ENDIF


;-------------------------------------------------
;ボスとして出現　 CFLAG142
;-------------------------------------------------
ELSEIF TFLAG:600 == 32
	PRINTFORMW 『あら……あんた達、どこに行くの？』
	PRINTFORMW 『フフ、まぁそんな事どうでも良いわよね。一緒に気持ち良くなりましょう……？』

;-------------------------------------------------
;ボスとして倒される　 CFLAG143
;-------------------------------------------------
ELSEIF TFLAG:600 == 33
	;ラスボスだった場合は喋らせない
	IF FLAG:24 != 6 && FLAG:27 < 20
		PRINTFORML 『う～……もう、何なのよ？』
		PRINTFORMW 『あれ、私今まで……あー、なるほど。あんた達が助けてくれたのね』
		PRINTFORML 『……へぇ、あんたと一緒に居ればこの霧平気になるんだ』
		PRINTFORML 『それじゃあ私も一緒について行こうかしら？』
		PRINTFORMW 『博麗の巫女として、異変を放っておくわけにはいかないしね！』
	ENDIF

;-------------------------------------------------
;その他
;-------------------------------------------------
ELSE
	;特に自分で記述しなかったシチュエーションは、デフォルト文章を表示
	CALL COMMON_PLANE_TEXT

ENDIF

TFLAG:600 = 0

RETURN 1
