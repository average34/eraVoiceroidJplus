;595行目付近をコメントアウトすると、パラメータ表示のゲージが
;従来の現在のレベル→次のレベルに必要な量を基準としたものから
;パラメータの総量を基準にしたものになります。
;例えば、パラメータが5000（レベル３）のときはこんな感じに。
;　新型 [****....] Now:5000 / Next:10000 （総量として必要な10000が基準）
;　従来 [**......] Now:2000 / Next: 7000 （LV3→LV4に必要な7000が基準）
;595行目付近にも同じものが書いてあります

@SHOW_INFO
;新型表示
W = TARGET
DRAWLINE
PRINTL □ 経験
CALL NEW_PRINT_EXP
DRAWLINE
PRINTL □ 能力
CALL NEW_PRINT_ABL
DRAWLINE
PRINTL □ 刻印
CALL NEW_PRINT_MARK
DRAWLINE
PRINTL □ 素質
CALL NEW_PRINT_TALENT
CALL SHOW_DAY_INFO
WAIT

@SHOW_STATUS_NAME
;TSTR:0にプレイヤー、TSTR:1にパートナーの名前入力。
;妊娠機能ONなら危険日・生理表示も追加する
IF (FLAG:62 & 1)
	;危険日・生理表示
	VARSET TSTR, ""
	;プレイヤー名
	TSTR:0 = %NAME:PLAYER%
	SIF ASSIPLAY
		TSTR:0 = %TSTR:0%(助手)
	SIF TALENT:PLAYER:危険日
		TSTR:0 = %TSTR:0%(危険日)
	SIF TALENT:PLAYER:生理
		TSTR:0 = %TSTR:0%(生理)
	;パートナー名
	TSTR:1 = %NAME:TARGET%
	SIF TALENT:危険日
		TSTR:1 = %TSTR:1%(危険日)
	SIF TALENT:生理
		TSTR:1 = %TSTR:1%(生理)
ELSE
	VARSET TSTR, ""
	;プレイヤー名
	TSTR:0 = %NAME:PLAYER%
	SIF ASSIPLAY
		TSTR:0 = %TSTR:0%(助手)
	;パートナー名
	TSTR:1 = %NAME:TARGET%
ENDIF

@SHOW_STATUS
;DOTRAIN終了時に折る
;またCALLTRAINがDOTRAINで最後までいけなかったときのために折る
TFLAG:DOTRAIN = 0
TFLAG:CALLTRAIN = 0
;現在のムードの値をTFLAG:95に入れておく
TFLAG:95 = TFLAG:37
DRAWLINE
CALL YOUBI
PRINTFORML {DAY:3}年 %STR:2%月 {DAY:1}日 (%STR:1%) 天気:%STR:3% {DAY+1}日目(%STR:4%)  所持金：${MONEY}
;TSTR:0にプレイヤー、TSTR:1にパートナーの名前入力
CALL SHOW_STATUS_NAME
;主導権を持っている側を先に表示
IF TFLAG:45
	PRINTFORM %TSTR:1%が%TSTR:0%と一緒です
ELSE
	PRINTFORM %TSTR:0%が%TSTR:1%と一緒です
ENDIF
PRINT   

;最大体力等調整処理
CALL LIFE_LIMIT

CALL SHOW_EQUIP_2

PRINT 体力
SIF BASE:体力 < 0
	BASE:体力 = 0
CALL PRINT_BAR_0, BASE:体力, MAXBASE:体力, 32
PRINTFORML ({BASE:体力,4}/{MAXBASE:体力,4})

PRINT 気力
SIF BASE:気力 < 0
	BASE:気力 = 0
CALL PRINT_BAR, BASE:気力, MAXBASE:気力, 32, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("緑"), RESULT:1
PRINTFORML ({BASE:気力,4}/{MAXBASE:気力,4})

IF (FLAG:23 & 262144) != 0
	;ムード、酔い度表示
	CALL SHOW_MOOD
ENDIF

;新型表示
CALL NEW_PRINT_PALAM

SIF EX:Ｃ絶頂 || EX:Ｖ絶頂 || EX:Ａ絶頂 || EX:Ｂ絶頂 || EX:噴乳 || EX:射精 || EX:Ｍ絶頂
	PRINT < 
SIF EX:Ｃ絶頂
	PRINTFORM C絶頂:{EX:Ｃ絶頂}回 
SIF EX:Ｖ絶頂
	PRINTFORM V絶頂:{EX:Ｖ絶頂}回 
SIF EX:Ａ絶頂
	PRINTFORM A絶頂:{EX:Ａ絶頂}回 
SIF EX:Ｂ絶頂
	PRINTFORM B絶頂:{EX:Ｂ絶頂}回 
SIF EX:Ｍ絶頂
	PRINTFORM M絶頂:{EX:Ｍ絶頂}回 
SIF EX:噴乳
	PRINTFORM 噴乳:{EX:噴乳}回 
SIF EX:射精
	PRINTFORM 射精:{EX:射精}回 
SIF EX:Ｃ絶頂 || EX:Ｖ絶頂 || EX:Ａ絶頂 || EX:Ｂ絶頂 || EX:噴乳 || EX:射精 || EX:Ｍ絶頂
	PRINTL >

;射精値設定
IF TALENT:TARGET:早漏
	MAXBASE:TARGET:射精 = 5000
ELSE
	MAXBASE:TARGET:射精 = 10000
ENDIF
IF TALENT:MASTER:早漏
	MAXBASE:MASTER:射精 = 5000
ELSE
	MAXBASE:MASTER:射精 = 10000
ENDIF
IF ASSI >= 0
	IF TALENT:ASSI:早漏
		MAXBASE:ASSI:射精 = 5000
	ELSE
		MAXBASE:ASSI:射精 = 10000
	ENDIF
ENDIF
SIF MAXBASE:MASTER:精神 == 0
	MAXBASE:MASTER:精神 = 10000

IF TALENT:MASTER:半人半妖 || TALENT:MASTER:ふたなり || TALENT:MASTER:オトコ
	PRINT 射精 
	CALL PRINT_BAR, BASE:MASTER:射精, MAXBASE:MASTER:射精, 32, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("しろ"), RESULT:1
	PRINTFORM  ({BASE:MASTER:射精}/{MAXBASE:MASTER:射精})
	IF TEQUIP:MASTER:85 == 1
		PRINTFORML  [コンドーム着用]
	ELSE
		PRINTFORML  
	ENDIF
ENDIF

IF (FLAG:62 & 4096) || TFLAG:34
	PRINTFORM 体力（%NAME:PLAYER%）
	CALL PRINT_BAR_0, BASE:PLAYER:体力, MAXBASE:PLAYER:体力, 32
	PRINTFORML ({BASE:PLAYER:体力,4}/{MAXBASE:PLAYER:体力,4})
ENDIF

IF ASSIPLAY
	IF TALENT:ASSI:半人半妖 || TALENT:ASSI:ふたなり || TALENT:ASSI:オトコ
		PRINT 射精(助手)
		CALL PRINT_BAR, BASE:ASSI:射精, MAXBASE:ASSI:射精, 32, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("しろ"), RESULT:1
		PRINTFORM ({BASE:ASSI:射精}/{MAXBASE:ASSI:射精})
		IF TEQUIP:ASSI:85 == 1
			PRINTFORML  [コンドーム着用]
		ELSE
			PRINTFORML  
		ENDIF
	ENDIF
ENDIF

IF TALENT:TARGET:半人半妖 || TALENT:TARGET:ふたなり || TALENT:TARGET:オトコ
	PRINTFORM 射精（%NAME:TARGET%）
	CALL PRINT_BAR, BASE:射精, MAXBASE:射精, 32, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("しろ"), RESULT:1
	PRINTFORM ({BASE:射精}/{MAXBASE:射精})
	IF TEQUIP:85 == 1
		PRINTFORML  [コンドーム着用]
	ELSE
		PRINTFORML  
	ENDIF
ENDIF

IF TALENT:TARGET:母乳体質
	SIF MAXBASE:母乳 == 0
		MAXBASE:母乳 = 10000
	PRINTFORM 母乳（%NAME:TARGET%）
	CALL PRINT_BAR, BASE:母乳, MAXBASE:母乳, 32, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("しろ"), RESULT:1
	PRINTFORML ({BASE:母乳}/{MAXBASE:母乳})
ENDIF

IF TEQUIP:90
	SIF MAXBASE:MASTER:触手射精 == 0
		MAXBASE:MASTER:触手射精 = 10000
	PRINT 射精（触手）
	CALL PRINT_BAR, BASE:MASTER:触手射精, MAXBASE:MASTER:触手射精, 32, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("しろ"), RESULT:1
	PRINTFORML ({BASE:MASTER:触手射精}/{MAXBASE:MASTER:触手射精})
ENDIF
;eraSQJ用接触部位管理の処理
TRYCALL SHOW_TOUCH
CALL SHOW_EQUIP_1

;完全にパートナーに主導権を握らせている場合
IF TFLAG:34
	;ウフフ中
	IF TFLAG:44
		;REVMODE
		CALL REVMODE_PASSIVEPHASE(TARGET)
	;非ウフフ中
	ELSE
		;パートナーのターン処理
		CALL PASSIVEMODE_PASSIVEPHASE(TARGET)
	ENDIF
ENDIF

;-------------------------------------------------
;ムード、酔い度表示
;-------------------------------------------------
@SHOW_MOOD
;TFLAG:37 現在のムード
;TFLAG:47 酔ってる度合い
IF TFLAG:37 < 10
	LOCAL = 0
ELSEIF TFLAG:37 < 30
	LOCAL = 1
ELSEIF TFLAG:37 < 50
	LOCAL = 2
ELSEIF TFLAG:37 < 100
	LOCAL = 3
ELSEIF TFLAG:37 < 300
	LOCAL = 4
ELSE
	LOCAL = 5
ENDIF

PRINTFORM ﾑｰﾄﾞ
CALL PRINT_BAR, TFLAG:37, 300, 32, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("桃"), RESULT:1
SIF TFLAG:37 >= 300
	SETCOLOR 255,51,153

LOCALS = %GETFONT()%
SETFONT "Times New Roman"
PRINTFORML %UNICODE(0x2665) * LOCAL,10,LEFT% 
SETFONT LOCALS
RESETCOLOR

IF TFLAG:47 < 10
	LOCAL = 0
ELSEIF TFLAG:47 < 30
	LOCAL = 1
ELSEIF TFLAG:47 < 50
	LOCAL = 2
ELSEIF TFLAG:47 < 100
	LOCAL = 3
ELSEIF TFLAG:47 < 300
	LOCAL = 4
ELSE
	LOCAL = 5
ENDIF
PRINT 酔い
CALL PRINT_BAR, TFLAG:47, 300, 32, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("紫"), RESULT:1
SIF TFLAG:47 >= 300
	SETCOLOR 255,255,0
PRINTFORML %"★" * LOCAL,10,LEFT% 
RESETCOLOR

;-------------------------------------------------
;汚れ表示
;-------------------------------------------------
@STAIN_INFO

REPEAT 6
	SIF COUNT == 2 && TALENT:MASTER:半人半妖 == 0 && TALENT:MASTER:ふたなり == 0 && TALENT:MASTER:オトコ == 0
		CONTINUE
	SIF COUNT == 3 && TALENT:MASTER:オトコ
		CONTINUE
	SIF COUNT == 5 && TALENT:MASTER:オトコ
		CONTINUE
	PRINTS NAME:MASTER
	IF COUNT == 0
		PRINT の口：
	ELSEIF COUNT == 1
		PRINT の手：
	ELSEIF COUNT == 2
		PRINT のＰ：
	ELSEIF COUNT == 3
		PRINT のＶ：
	ELSEIF COUNT == 4
		PRINT のＡ：
	ELSEIF COUNT == 5
		PRINT のＢ：
	ENDIF
	SIF STAIN:MASTER:COUNT & 1
		PRINT <Ｖ>
	SIF STAIN:MASTER:COUNT & 2
		PRINT <Ｐ>
	SIF STAIN:MASTER:COUNT & 4
		PRINT <精>
	SIF STAIN:MASTER:COUNT & 8
		PRINT <Ａ>
	SIF STAIN:MASTER:COUNT & 16
		PRINT <乳>
	PRINTL 
REND

REPEAT 6
	SIF COUNT == 2 && TALENT:半人半妖 == 0 && TALENT:ふたなり == 0 && TALENT:オトコ == 0
		CONTINUE
	SIF COUNT == 3 && TALENT:オトコ
		CONTINUE
	SIF COUNT == 5 && TALENT:オトコ
		CONTINUE
	PRINTS NAME:TARGET
	IF COUNT == 0
		PRINT の口：
	ELSEIF COUNT == 1
		PRINT の手：
	ELSEIF COUNT == 2
		PRINT のＰ：
	ELSEIF COUNT == 3
		PRINT のＶ：
	ELSEIF COUNT == 4
		PRINT のＡ：
	ELSEIF COUNT == 5
		PRINT のＢ：
	ENDIF
	
	SIF STAIN:COUNT & 1
		PRINT <Ｖ>
	SIF STAIN:COUNT & 2
		PRINT <Ｐ>
	SIF STAIN:COUNT & 4
		PRINT <精>
	SIF STAIN:COUNT & 8
		PRINT <Ａ>
	SIF STAIN:COUNT & 16
		PRINT <乳>
	PRINTL 
REND

REPEAT 6
	SIF ASSI < 0
		BREAK
	SIF COUNT == 2 && TALENT:ASSI:半人半妖 == 0 && TALENT:ASSI:ふたなり == 0 && TALENT:ASSI:オトコ == 0
		CONTINUE
	SIF COUNT == 3 && TALENT:ASSI:オトコ
		CONTINUE
	SIF COUNT == 5 && TALENT:ASSI:オトコ
		CONTINUE
	PRINTS NAME:ASSI
	IF COUNT == 0
		PRINT の口：
	ELSEIF COUNT == 1
		PRINT の手：
	ELSEIF COUNT == 2
		PRINT のＰ：
	ELSEIF COUNT == 3
		PRINT のＶ：
	ELSEIF COUNT == 4
		PRINT のＡ：
	ELSEIF COUNT == 5
		PRINT のＢ：
	ENDIF
	
	SIF STAIN:ASSI:COUNT & 1
		PRINT <Ｖ>
	SIF STAIN:ASSI:COUNT & 2
		PRINT <Ｐ>
	SIF STAIN:ASSI:COUNT & 4
		PRINT <精>
	SIF STAIN:ASSI:COUNT & 8
		PRINT <Ａ>
	SIF STAIN:ASSI:COUNT & 16
		PRINT <乳>
	PRINTL 
REND

WAIT

@SAVEINFO
IF TIME == 0
	PUTFORM {DAY+1}日目昼 
ELSE
	PUTFORM {DAY+1}日目夜 
ENDIF

IF STRLENS(SAVESTR:11)
	PUTFORM %SAVESTR:11%
ELSE
	IF TARGET >= 0
		PUTFORM パートナー：%NAME:TARGET%
		SIF ((FLAG:23 & 1p26) == 0) && FLAG:2 != 1
			PUTFORM (好感度：{CFLAG:2}) 
	ENDIF
	SIF ASSI >= 0
		PUTFORM 助手：%NAME:ASSI%
ENDIF

IF FLAG:2 == 1
	PUTFORM  I-
ELSE
	PUTFORM  
ENDIF

SIF FLAG:5 == 0
	PUTFORM Easy
SIF FLAG:5 == 1
	PUTFORM Normal
SIF FLAG:5 == 2
	PUTFORM Hard
SIF FLAG:5 == 3
	PUTFORM Lunatic
SIF FLAG:5 == 4
	PUTFORM S-Lunatic
SIF FLAG:5 == 9
	PUTFORM Extra
SIF FLAG:5 == 10
	PUTFORM Phantasm

SIF FLAG:26 > 1
	PUTFORM  {FLAG:26}周目

SIF FLAG:28
	PUTFORM  ☆

;コンフィグでセーブデータにメモを付けるをONにした場合メモを付けられる
;TFLAG:99が0の時でないと表示されないのは、オートセーブ時に毎回メモの入力を求められないようにするため
IF TFLAG:99 == 0 && FLAG:23 & 1p24
	$INPUT
	PRINTL メモを入力して下さい。何も入力しない場合メモが省略されます。
	INPUTS
	IF RESULTS == "]"
		PRINTW ]だけのメモは入力できません
		GOTO INPUT
	ELSEIF RESULTS != ""
		PUTFORM 【%RESULTS%】
	ENDIF
ENDIF

@SHOW_EQUIP_2
;状況の表示
;調教対象には使わない、特殊な着脱式の道具を追加した場合はここに追加をしてください
;（場所変更、マスターや助手が使用する道具等）
IF TEQUIP:1
	IF NAME_CSV(NO:TARGET, 0) == "慧音"
		PRINTFORM  [ハクタク化]
	ELSEIF ASSIPLAY
		PRINTFORM  [ハクタク化](助手)
	ENDIF
ENDIF
IF TEQUIP:2
	CALL PLACE_NAME
	PRINTFORM  [デート中(%STR:4%)]
ENDIF

IF TEQUIP:4
	ADDCHARA TEQUIP:4
	A = CHARANUM-1
	PRINTFORM  [%CALLNAME:A%から貰ったアクセサリ装着中]
	DELCHARA A 
ENDIF

IF TEQUIP:53
	V = 10 - TFLAG:70 + 1
	PRINTFORM  [撮影中(残{V}回)]
ENDIF
SIF TEQUIP:54
	PRINT  [野外プレイ中]
IF TEQUIP:57 == 2
	PRINT  [羞恥（浄玻璃の鏡）プレイ中]
ELSEIF TEQUIP:57
	PRINT  [羞恥（大鏡）プレイ中]
ENDIF
SIF TEQUIP:58
	PRINT  [お風呂場プレイ中]
SIF TEQUIP:59
	PRINT  [新妻プレイ中]
SIF TEQUIP:60
	PRINT  [スク水プレイ中]
SIF TEQUIP:62
	PRINT  [お医者プレイ中]
SIF TEQUIP:69
	PRINT  [女体盛りプレイ中]
SIF TEQUIP:78
	PRINT  [鰻風呂入浴中]
SIF TEQUIP:92
	PRINT  [蟲風呂入浴中]
SIF TEQUIP:80
	PRINT  [暗闇展開中]
SIF TEQUIP:90
	PRINT  [触手召喚中]
SIF TEQUIP:23 > 0
	PRINTFORM  [幽明の苦輪発動中：残り{TEQUIP:23}ターン]
;キャラごと固有のコスチューム表示可能にする関数(Notウフフ用)
CALL COSTUME_SHOW
PRINTL  

@SHOW_EQUIP_1
;使用中の道具を表示する
;一度使用したら解除するまで止まらない道具や調教を
;追加した場合はここに追加をしてください
IF TEQUIP:PLAYER:43 || TEQUIP:PLAYER:44 || TEQUIP:PLAYER:45
	PRINTFORM 使用中(%NAME:PLAYER%) 
	SIF TEQUIP:PLAYER:43
		PRINTFORM [アイマスク]
	SIF TEQUIP:PLAYER:44
		PRINTFORM [縄で緊縛]
	SIF TEQUIP:PLAYER:45
		PRINTFORM [ボールギャグ]
	PRINTL  
ENDIF

;日常用TEQUIP解除
;ウフフ中ならこれらは解除
IF TFLAG:44 == 1
	TEQUIP:7 = 0
	TEQUIP:20 = 0
	TEQUIP:36 = 0
ENDIF

IF TEQUIP:7 || TEQUIP:11 || TEQUIP:13 || TEQUIP:14 || TEQUIP:15 || TEQUIP:16 || TEQUIP:17 || TEQUIP:18 || TEQUIP:19 || TEQUIP:20 || TEQUIP:21 || TEQUIP:22 || TEQUIP:25 || TEQUIP:36 || TEQUIP:43 || TEQUIP:44 || TEQUIP:45 || TEQUIP:46 || TEQUIP:47 || TEQUIP:48 || TEQUIP:49 || TEQUIP:61 || TEQUIP:70 || TEQUIP:71 || TEQUIP:72 || TEQUIP:73 || TEQUIP:74 || TEQUIP:75 || TEQUIP:76 || TEQUIP:79 || TEQUIP:86 || TEQUIP:87 || TEQUIP:98 || TEQUIP:65
	PRINTFORM 使用中(%NAME:TARGET%) 

SIF TEQUIP:21
	PRINT [媚薬効果発揮中]
SIF TEQUIP:22
	PRINT [利尿剤効果発揮中]
SIF TEQUIP:86
	PRINT [排卵誘発剤効果発揮中]
SIF TEQUIP:87
	PRINT [緊急避妊薬準備済み]
SIF TEQUIP:74
	PRINT [鈴蘭の毒効果発揮中]
SIF TEQUIP:75
	PRINT [昆虫採集セットの薬効果発揮中]

IF TEQUIP:11 && TEQUIP:90
	PRINT [触手挿入]
ELSEIF TEQUIP:11
	PRINT [バイブ]
ENDIF

IF TEQUIP:13 && TEQUIP:90
	PRINT [触手アナル挿入]
ELSEIF TEQUIP:13
	PRINT [アナルバイブ]
ENDIF

IF TEQUIP:14 && TEQUIP:90
	PRINT [触手クリ責め]
ELSEIF TEQUIP:14
	PRINT [クリキャップ]
ENDIF

IF TEQUIP:15 && TEQUIP:90
	PRINT [触手乳首責め]
ELSEIF TEQUIP:15
	PRINT [二プルキャップ]
ENDIF

IF TEQUIP:16 && TEQUIP:90
	PRINT [触手搾乳]
ELSEIF TEQUIP:16
	PRINT [搾乳器]
ENDIF

SIF TEQUIP:76
	PRINT [サラシ]

IF TEQUIP:17 && TEQUIP:90
	PRINT [触手ペニス責め]
ELSEIF TEQUIP:17
	PRINT [オナホール]
ENDIF

IF TEQUIP:44 && TEQUIP:90
	PRINT [触手緊縛]
ELSEIF TEQUIP:44 && TEQUIP:72 == 0 && TEQUIP:73 == 0
	PRINT [縄で緊縛]
ELSEIF TEQUIP:44 && TEQUIP:72 && TEQUIP:73 == 0
	PRINT [荊で緊縛]
ELSEIF TEQUIP:44 && TEQUIP:73 && TEQUIP:72 == 0
	PRINT [蜘蛛の糸で緊縛]
ENDIF

IF TEQUIP:46 && TEQUIP:90
	PRINT [触手浣腸]
ELSEIF TEQUIP:46
	PRINT [浣腸＋アナルプラグ]
ENDIF
SIF TEQUIP:98
	PRINT [触手口辱]

SIF TEQUIP:43 && TEQUIP:73 == 0
	PRINT [アイマスク]
SIF TEQUIP:45
	PRINT [ボールギャグ]
SIF TEQUIP:18
	PRINT [シャワー]
SIF TEQUIP:19
	PRINT [アナルビーズ]
SIF TEQUIP:47
	PRINT [空気浣腸]
SIF TEQUIP:48
	PRINT [拡張バルーン]
SIF TEQUIP:49
	PRINT [アナル電極]
SIF TEQUIP:61
	PRINT [クスコ]
SIF TEQUIP:71
	PRINT [焼芋挿入中]
SIF TEQUIP:70
	PRINT [巨峰挿入中]
IF TEQUIP:79
	SIF TEQUIP:79 == 1 || TEQUIP:79 == 2
		PRINT [氷柱挿入中]
	SIF TEQUIP:79 == 3
		PRINT [極太氷柱挿入中]
	SIF TEQUIP:79 >= 4
		PRINT [氷塊挿入中]
ENDIF

;キャラごと固有のコスチューム表示可能にする関数(ウフフ用)
CALL COSTUME_PLAY_SHOW
;コスプレは固有表示にする為、固有表示されてないこと確認
IF RESULT == 1
	;キャラごと固有のコスチューム表示
ELSEIF TEQUIP:65 == 1
	PRINT [学ラン]
ELSEIF TEQUIP:65 == 2
	PRINT [セーラー服]
ELSEIF TEQUIP:65 == 3
	PRINT [ブルマ]
ELSEIF TEQUIP:65 == 4
	PRINT [水着]
ELSEIF TEQUIP:65 == 5
	PRINT [スク水（紺）]
ELSEIF TEQUIP:65 == 6
	PRINT [スク水（白）]
ELSEIF TEQUIP:65 == 7
	PRINT [白衣]
ELSEIF TEQUIP:65 == 8
	PRINT [ナース服]
ELSEIF TEQUIP:65 == 9
	PRINT [女性警察官制服]
ELSEIF TEQUIP:65 == 10
	PRINT [ボンテージスーツ]
ELSEIF TEQUIP:65 == 11
	PRINT [浴衣]
ELSEIF TEQUIP:65 == 12
	PRINT [巫女服]
ELSEIF TEQUIP:65 == 13
	PRINT [割烹着]
ELSEIF TEQUIP:65 == 14
	PRINT [マキのライブ服]
ELSEIF TEQUIP:65 == 15
	PRINT [メイド服]
ELSEIF TEQUIP:65 == 16
	PRINT [メイド服（秋）]
ELSEIF TEQUIP:65 == 17
	PRINT [裸リボン]
ELSEIF TEQUIP:65 == 18
	PRINTFORM [小悪魔風衣装]
ELSEIF TEQUIP:65 == 19
	PRINTFORM [魔法少女コス]
ELSEIF TEQUIP:65 == 20
	PRINTFORM [ゴスロリ]
ELSEIF TEQUIP:65 == 21
	PRINTFORM [バニーガール]
ELSEIF TEQUIP:65 == 22
	PRINTFORM [裸Yシャツ]
ELSEIF TEQUIP:65 == 23
	PRINTFORM [%NAME:TARGET%の服]
ELSEIF TEQUIP:65 == 24
	PRINTFORM [%NAME:MASTER%の服]
ELSEIF TEQUIP:65 == 255
	PRINTFORM [ボロボロの布切れ]
ENDIF

;日常用TEQUIP
SIF TEQUIP:7
	PRINT  [炬燵]
SIF TEQUIP:20
	PRINT  [オムツ着用中]
SIF TEQUIP:36
	PRINT  [ローター装着中]

;ココより↑にTEQUIPに登録したものを書き込む
PRINTL 
ENDIF
IF TEQUIP:3
	PRINT インセンス使用中(
	SIF TEQUIP:3 == 1
		PRINT シダーウッド：体力消費軽減・小
	SIF TEQUIP:3 == 2
		PRINT サイプレスウッド：体力消費軽減・中
	SIF TEQUIP:3 == 3
		PRINT アローズウッド：体力消費軽減・大
	SIF TEQUIP:3 == 4
		PRINT ペパーミント：気力消費軽減・小
	SIF TEQUIP:3 == 5
		PRINT メリッサ：気力消費軽減・中
	SIF TEQUIP:3 == 6
		PRINT ミルトス：気力消費軽減・大
	SIF TEQUIP:3 == 7
		PRINT ジャスミン：ムード上昇・小
	SIF TEQUIP:3 == 8
		PRINT ラヴェンダー：ムード上昇・中
	SIF TEQUIP:3 == 9
		PRINT ムスク：ムード上昇・大
	SIF TEQUIP:3 == 10
		PRINT フランキンセンス：体力気力消費軽減＋ムード上昇
	SIF TEQUIP:3 == 11
		PRINT 竜印香炉：体力回復・小
	SIF TEQUIP:3 == 12
		PRINT ネクロパフューム：亡霊系・体力気力消費軽減＋ムード上昇
	PRINTL )
ENDIF
;調合用TEQUIP
IF TEQUIP:5
	PRINT 調合アイテム使用中(
	SIF TEQUIP:5 == 1
		PRINT 病気平癒守：耐力体力回復・中
	SIF TEQUIP:5 == 2
		PRINT ペンデュラム：探索率上昇
	SIF TEQUIP:5 == 3
		PRINT 魔法瓶：デート中体力気力消費軽減・小
	PRINTL )
ENDIF


@PALAMLV_SET
FOR LOCAL, 0, 22
	SIF PALAM:A < PALAMLV:(LOCAL +1)
		B = LOCAL
NEXT



;↓↓↓新パラメータ表示↓↓↓
@NEW_PRINT_PALAM
;PALAMの並び設定
CALL PALAM_SORT

;SQJ用酒気・情感（ムード）表示
TRYCALL MOOD_SHOW2

;REPEAT回数をLOCALに代入
LOCAL = A:0
;改行用にLOCAL:1を使用
LOCAL:1 = 0

REPEAT LOCAL

;パラメータを表示する順番を並べ替える
A = X:COUNT

;現在のパラメータレベルをBに代入
FOR LOCAL:2, 1, 37
	IF PALAM:A < PALAMLV:(LOCAL:2)
		B = LOCAL:2 - 1
		BREAK
	ENDIF
NEXT

LOCALS:0 = 0
LOCALS:1 = 1
LOCALS:2 = 2
LOCALS:3 = 3
LOCALS:4 = 4
LOCALS:5 = 5
LOCALS:6 = 6
LOCALS:7 = 7
LOCALS:8 = 8
LOCALS:9 = 9
LOCALS:10 = A
LOCALS:11 = B
LOCALS:12 = C
LOCALS:13 = D
LOCALS:14 = E
LOCALS:15 = F
LOCALS:16 = G
LOCALS:17 = H
LOCALS:18 = I
LOCALS:19 = J
LOCALS:20 = K
LOCALS:21 = L
LOCALS:22 = M
LOCALS:23 = N
LOCALS:24 = O
LOCALS:25 = P
LOCALS:26 = Q
LOCALS:27 = R
LOCALS:28 = S
LOCALS:29 = T
LOCALS:30 = U
LOCALS:31 = V
LOCALS:32 = W
LOCALS:33 = X
LOCALS:34 = Y
LOCALS:35 = Z


;パラメータの名前・パラメータレベル・ゲージを表示
PRINTFORM 　%PALAMNAME:A%Lv%LOCALS:B%

E = B + 1
O = PALAMLV:B
P = PALAMLV:E

IF FLAG:23 & 256
	PRINT  
ELSE
	PRINT [
ENDIF

;これだけ特殊
IF A == 3 && PALAM:A >= 20000
	IF FLAG:23 & 256
		CALL PRINT_COLORBAR, 1, 1, 8, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("しろ"), RESULT:1
		PRINT    濡れ
	ELSE
		PRINT ********]  濡れ
	ENDIF
	GOTO NEW_PRINT_PALAM_NUM_END
ENDIF

;パラメータレベルが35(LvZ)なら余計な処理は飛ばす
IF B >= 35
	IF FLAG:23 & 256
		IF A >= 11 && A <= 13
			CALL PRINT_COLORBAR, 1, 1, 8, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("赤"), RESULT:1
		ELSE
			CALL PRINT_COLORBAR, 1, 1, 8, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("薄青"), RESULT:1
		ENDIF
	ELSE
		PRINT ********
	ENDIF
	GOTO NEW_PRINT_PALAM_END
ENDIF


;パラメータレベルごとの閾値の8分の1を1メモリとする
;C = PALAM:A/(PALAMLV:(B+1)/8)
;E = B+1
C = PALAM:A/(P/8)

;↓のコメントアウトすると、パラメータ表示のゲージが
;↓従来の現在のレベル→次のレベルに必要な量を基準としたものから
;↓パラメータの総量を基準にしたものになります。
;↓例えば、パラメータが5000（レベル３）のときはこんな感じに。
;↓　新型 [****....] Now:5000 / Next:10000 （総量として必要な10000が基準）
;↓　従来 [**......] Now:2000 / Next: 7000 （LV3→LV4に必要な7000が基準）

C = (PALAM:A-O)/((P-O)/8)

D = 8-C

;パラメータレベルに応じて溜まっている部分に記号を表示
$NEW_PRINT_PALAM_C
SIF C == 0
	GOTO NEW_PRINT_PALAM_D
IF FLAG:23 & 256
	IF A == 3
		SETCOLOR 0xC0C0C0
	ELSEIF A >= 11 && A <= 13
		IF B <= 2
			SETCOLOR 0xFFCCFF
		ELSEIF B == 3
			SETCOLOR 0xF06050
		ELSE
			SETCOLOR 0xC07070
		ENDIF
	ELSE
		IF B <= 2
			SETCOLOR 0xFFCCFF
		ELSEIF B == 3
			SETCOLOR 0xC0B050
		ELSE
			SETCOLOR 0x50B0FF
		ENDIF
	ENDIF
	PRINTFORM %UNICODE(0x2584)%
	RESETCOLOR
ELSEIF FLAG:23 & 512
	SIF B % 10 == 0
		PRINT -
	SIF B % 10 == 1
		PRINT =
	SIF B % 10 == 2
		PRINT >
	SIF B % 10 == 3
		PRINT *
	SIF B % 10 == 4
		PRINT #
	SIF B % 10 == 5
		PRINT /
	SIF B % 10 == 6
		PRINT %
	SIF B % 10 == 7
		PRINT &
	SIF B % 10 == 8
		PRINT X
	SIF B % 10 == 9
		PRINT !
ELSE
	SIF B >= 0
		PRINT *
ENDIF
C -= 1
GOTO NEW_PRINT_PALAM_C

;溜まっていない部分に1Lv下の記号を表示
$NEW_PRINT_PALAM_D
SIF D == 0
	GOTO NEW_PRINT_PALAM_END
IF FLAG:23 & 256
	IF A == 3
		SETCOLOR 0x202020
	ELSEIF A >= 11 && A <= 13
		IF B <= 2
			SETCOLOR 0x300020
		ELSEIF B == 3
			SETCOLOR 0x701000
		ELSE
			SETCOLOR 0x502020
		ENDIF
	ELSE
		IF B <= 2
			SETCOLOR 0x300020
		ELSEIF B == 3
			SETCOLOR 0x505020
		ELSE
			SETCOLOR 0x002060
		ENDIF
	ENDIF
	PRINTFORM %UNICODE(0x2584)%
	RESETCOLOR
ELSE
	PRINT .
ENDIF
D -= 1
GOTO NEW_PRINT_PALAM_D

$NEW_PRINT_PALAM_END
IF FLAG:23 & 256
	PRINT  
ELSE
	PRINT ]
ENDIF

;↓↓↓↓kMGT表示↓↓↓↓
;SQJ用酒気・情感（ムード）表示だけは別
IF A == 19 || A == 20
	A = PALAM:A / 100
ELSE
	A = PALAM:A
ENDIF
B = 0
;6桁までノーマル
IF A > 999999
	;kキロ(1000)
	A /= 1000
	B += 1
	IF A > 99999
		;Mメガ(百万)
		A /= 1000
		B += 1
		;2^31 ≒ 21億 = 2100M = 2.1G
		;eramakerの32ビット整数ならメガまでで十分
		IF A > 99999
			;Gギガ(十億)
			A /= 1000
			B += 1
			IF A > 99999
				;Tテラ(兆)
				A /= 1000
				B += 1
				IF A > 99999
					;Pペタ(千兆)
					A /= 1000
					B += 1
					;2^63 ≒ 920京 = 9200P = 9.2E
					;Emueraの64ビット整数でもペタまでで十分
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF

;;数値の桁数に応じて間隔を調整
;SIF A < 10
;	PRINT  
;SIF A < 100
;	PRINT  
;SIF A < 1000
;	PRINT  
;SIF A < 10000
;	PRINT  
;;接尾語が入るなら1つ減らす
;SIF A < 100000 && B == 0
;	PRINT  

;PRINTFORM {A}

;接尾語が入るなら1つ減らす
IF B
	PRINTFORM {A,5,RIGHT}
ELSE
	PRINTFORM {A,6,RIGHT}
ENDIF

IF B == 1
	PRINT k
ELSEIF B == 2
	PRINT M
ELSEIF B == 3
	PRINT G
ELSEIF B == 4
	PRINT T
ELSEIF B == 5
	PRINT P
ENDIF
;↑↑↑↑kMGT表示↑↑↑↑

;↓↓↓↓通常表示↓↓↓↓
;
;	;数値の桁数に応じて間隔を調整
;	SIF PALAM:A < 10
;		PRINT  
;	SIF PALAM:A < 100
;		PRINT  
;	SIF PALAM:A < 1000
;		PRINT  
;	SIF PALAM:A < 10000
;		PRINT  
;	SIF PALAM:A < 100000
;		PRINT  
;	;数値を表示・999999を超えたら999999
;	IF PALAM:A > 999999
;		PRINT 999999
;	ELSE
;		PRINTFORM {PALAM:A}
;	ENDIF
;
;↑↑↑↑通常表示↑↑↑↑

$NEW_PRINT_PALAM_NUM_END
LOCAL:1 += 1
;改行するのか？ 続けて出力するのか？
IF LOCAL:1 > 2
	PRINTL 
	LOCAL:1 = 0
ELSE
	PRINT 
ENDIF

REND
;最後のループで改行してなかったら、ここで改行しておく
SIF LOCAL:1 != 0
	PRINTL 
;SQJ用酒気・情感（ムード）表示で使用したPALAM:19,20を初期化
PALAM:19 = 0
PALAM:20 = 0
;↑↑↑新パラメータ表示↑↑↑

;-----------------------------------------------
;最大体力等調整処理
;-----------------------------------------------
@LIFE_LIMIT
;最大体力が9999を超えていたら9999に合わせる
SIF MAXBASE:体力 > 9999
	MAXBASE:体力 = 9999
SIF MAXBASE:MASTER:体力 > 9999
	MAXBASE:MASTER:体力 = 9999
;最大気力が9999を超えていたら9999に合わせる
SIF MAXBASE:気力 > 9999
	MAXBASE:気力 = 9999
SIF MAXBASE:MASTER:気力 > 9999
	MAXBASE:MASTER:気力 = 9999
;最大耐力が999999999999999999を超えていたら999999999999999999に合わせる
SIF MAXBASE:耐力 > 999999999999999999
	MAXBASE:耐力 = 999999999999999999
SIF MAXBASE:MASTER:耐力 > 999999999999999999
	MAXBASE:MASTER:耐力 = 999999999999999999

;体力が9999を超えていたら9999に合わせる
SIF BASE:体力 > 9999
	BASE:体力 = 9999
SIF BASE:MASTER:体力 > 9999
	BASE:MASTER:体力 = 9999
;気力が9999を超えていたら9999に合わせる
SIF BASE:気力 > 9999
	BASE:気力 = 9999
SIF BASE:MASTER:気力 > 9999
	BASE:MASTER:気力 = 9999
;耐力が999999999999999999を超えていたら999999999999999999に合わせる
SIF BASE:耐力 > 999999999999999999
	BASE:耐力 = 999999999999999999
SIF BASE:MASTER:耐力 > 999999999999999999
	BASE:MASTER:耐力 = 999999999999999999

;体力等調整処理(最大値まで)
CALL LIFE_LIMIT_MAXBASE

;-----------------------------------------------
;体力等調整処理(最大値まで)
;-----------------------------------------------
@LIFE_LIMIT_MAXBASE
;J用アイテムにより一時的に体力上限がなくなっていれば最大値に合わせない
IF TEQUIP:41 == 0
	;体力が上限を超えていたら上限に合わせる
	SIF BASE:体力 > MAXBASE:体力
		BASE:体力 = MAXBASE:体力
	SIF BASE:MASTER:体力 > MAXBASE:MASTER:体力
		BASE:MASTER:体力 = MAXBASE:MASTER:体力
ENDIF
;気力が上限を超えていたら上限に合わせる
SIF BASE:気力 > MAXBASE:気力
	BASE:気力 = MAXBASE:気力
SIF BASE:MASTER:気力 > MAXBASE:MASTER:気力
	BASE:MASTER:気力 = MAXBASE:MASTER:気力
;耐力が上限を超えていたら上限に合わせる
SIF BASE:耐力 > MAXBASE:耐力
	BASE:耐力 = MAXBASE:耐力
SIF BASE:MASTER:耐力 > MAXBASE:MASTER:耐力
	BASE:MASTER:耐力 = MAXBASE:MASTER:耐力

;-------------------------------------------------
;記念日
;-------------------------------------------------
@SHOW_DAY_INFO
;表示する記念日がなければRETURN
SIF TALENT:相愛 == 0 && TALENT:妊娠 == 0 && CFLAG:75 == 0 && CFLAG:77 == 0
	RETURN 0

DRAWLINE
PRINTL □ 記念日
IF TIME:2  && NO:TARGET == MASTER
	IF TIME:3 == 0
		TSTR:2 = 春の
	ELSEIF TIME:3 == 1
		TSTR:2 = 夏の
	ELSEIF TIME:3 == 2
		TSTR:2 = 秋の
	ELSEIF TIME:3 == 3
		TSTR:2 = 冬の
	ENDIF
	PRINTFORML 誕生日：%TSTR:2%月 {TIME:2}日
ENDIF

;結婚記念日
IF TALENT:相愛
	IF TIME:10 == 0
		TSTR:2 = 春の
	ELSEIF TIME:10 == 1
		TSTR:2 = 夏の
	ELSEIF TIME:10 == 2
		TSTR:2 = 秋の
	ELSEIF TIME:10 == 3
		TSTR:2 = 冬の
	ENDIF
	
	IF TIME:9
		PRINTFORML 結婚記念日：%TSTR:2%月 {TIME:9}日
	ELSE
		PRINTFORML 婚約者：%NAME:MASTER%
	ENDIF
ENDIF
IF TALENT:妊娠
	PRINTFORML 出産予定日まで{CFLAG:110-DAY:7}日
ENDIF

IF CFLAG:76 == 0
	TSTR:2 = 春の
ELSEIF CFLAG:76 == 1
	TSTR:2 = 夏の
ELSEIF CFLAG:76 == 2
	TSTR:2 = 秋の
ELSEIF CFLAG:76 == 3
	TSTR:2 = 冬の
ENDIF

IF CFLAG:75 > 0 && NO:TARGET != MASTER
	PRINTFORML 仲間になった日：%TSTR:2%月 {CFLAG:75}日
ENDIF

IF CFLAG:77 > 0 && NO:TARGET != MASTER
	PRINTFORML 仲間になってから{CFLAG:77}日
ENDIF

;-------------------------------------------------------------------------------
;BAR表示用関数
;カラーバー表示をOFFにしている場合ARG、ARG:1、ARG:2を利用して従来のBARで表示する
;-------------------------------------------------------------------------------
@PRINT_BAR(ARG, ARG:1, ARG:2, ARGS, ARGS:1, ARG:3, ARG:4)
IF ARG >= 9999999999999999 || ARG:1 >= 9999999999999999
	ARG /= 100
	ARG:1 /= 100
ENDIF

IF FLAG:23 & 256
	PRINT  
	CALL PRINT_COLORBAR, ARG, ARG:1, ARG:2, ARGS, ARGS:1, ARG:3, ARG:4
	PRINT  
ELSE
	BAR ARG, ARG:1, ARG:2
ENDIF

;-------------------------------------------------------------------------------
;BAR表示用関数・体力用
;3段階で色が変わる体力用カラーバー
;-------------------------------------------------------------------------------
@PRINT_BAR_0(ARG, ARG:1, ARG:2)
IF ARG >= 9999999999999999 || ARG:1 >= 9999999999999999
	ARG /= 100
	ARG:1 /= 100
ENDIF

IF (ARG * 100 / ARG:1) > 50 && ARG > 650
	CALL PRINT_BAR, ARG, ARG:1, ARG:2, UNICODE(0x2585), UNICODE(0x2585), 0x0082FF, 0x002050
ELSEIF (ARG * 100 / ARG:1) > 25 && ARG > 500
	CALL PRINT_BAR, ARG, ARG:1, ARG:2, UNICODE(0x2585), UNICODE(0x2585), 0xFFF00A, 0x505000
ELSE
	CALL PRINT_BAR, ARG, ARG:1, ARG:2, UNICODE(0x2585), UNICODE(0x2585), 0xFF0000, 0x500000
ENDIF

;-------------------------------------------------------------------------------
;BAR表示用関数・耐力用
;3段階で色が変わる耐力用カラーバー
;-------------------------------------------------------------------------------
@PRINT_BAR_4(ARG, ARG:1, ARG:2)
IF ARG >= 9999999999999999 || ARG:1 >= 9999999999999999
	ARG /= 100
	ARG:1 /= 100
ENDIF

IF (ARG * 100 / ARG:1) >= 50
	CALL PRINT_BAR, ARG, ARG:1, ARG:2, UNICODE(0x2585), UNICODE(0x2585), 0x0082FF, 0x002050
ELSEIF (ARG * 100 / ARG:1) >= 25
	CALL PRINT_BAR, ARG, ARG:1, ARG:2, UNICODE(0x2585), UNICODE(0x2585), 0xFFF00A, 0x505000
ELSE
	CALL PRINT_BAR, ARG, ARG:1, ARG:2, UNICODE(0x2585), UNICODE(0x2585), 0xFF0000, 0x500000
ENDIF

;-------------------------------------------------------------------------------
;    BARの色違え版(PANCTION.ERBに同関数有)
;-------------------------------------------------------------------------------
;    例  PRINTFORM %PRINT_COLORBAR(40,100,8,%UNICODE(0x2584)%,%UNICODE(0x2584)%,,0x404040)% 
;        [===     ]
;    引数はBARとほぼ一緒 + ARGSで描写文字の変更が可能(指定しない場合はBARと同じ仕様)
;    ARG:3はバーの色(0xFFFFFF形式で指定),ARG:4はバーの背景色(0xFFFFFF形式で指定)
;===============================================================================
;@PRINT_COLORBAR(ARG, ARG:1, ARG:2, ARGS = "*", ARGS:1 = ".", ARG:3, ARG:4)
;#LOCALSIZE 3
;LOCAL:1 = ARG:1 / ARG:2
;LOCAL:2 = GETCOLOR()
;FOR LOCAL, 0, ARG:2
;	ARG -= LOCAL:1
;	IF ARG >= 0
;		SETCOLOR ARG:3
;		PRINTFORM %ARGS%
;	ELSE
;		SETCOLOR ARG:4
;		PRINTFORM %ARGS:1%
;	ENDIF
;NEXT
;SETCOLOR LOCAL:2
;VARSET LOCAL

;-------------------------------------------------
;カスタムバー用色セット関数@BARCOLORSET
;PRINT_COLORBARのARG:3に利用し、ARG;4にRESULTを使う
;-------------------------------------------------
@BARCOLORSET, ARGS
#FUNCTION
;LOCALに色を代入
SELECTCASE ARGS
	CASE "濃赤"
		RESULT:1 = 0x701000
		RETURNF 0xF06050
	CASE "赤"
		RESULT:1 = 0x502020
		RETURNF 0xC07070
	CASE "青"
		RESULT:1 = 0x202050
		RETURNF 0x7070C0
	CASE "薄青"
		RESULT:1 = 0x002060
		RETURNF 0x50B0FF
	CASE "紺"
		RESULT:1 = 0x100060
		RETURNF 0x6666FF
	CASE "緑"
		RESULT:1 = 0x205020
		RETURNF 0x66DD66
	CASE "紫"
		RESULT:1 = 0x502050
		RETURNF 0xC070C0
	CASE "黄"
		RESULT:1 = 0x505020
		RETURNF 0xC0B050
	CASE "桃"
		RESULT:1 = 0x300020
		RETURNF 0xFFCCFF
	CASE "青緑"
		RESULT:1 = 0x205050
		RETURNF 0x70C0C0
	CASE "灰"
		RESULT:1 = 0x333333
		RETURNF 0x666666
	;白
	CASEELSE
		RESULT:1 = 0x202020
		RETURNF 0xC0C0C0
ENDSELECT
