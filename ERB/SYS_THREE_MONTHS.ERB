;三ヶ月分と言う事で

@EVENTTURNEND
SIF (FLAG:62 & 8192) == 0
	RETURN 0
;エンゲージリングを渡していたら、関連フラグをリセット
IF FLAG:29
	REPEAT CHARANUM
		CFLAG:COUNT:27 = 0
		CFLAG:COUNT:28 = 0
	REND
;そうでなければ、貯金フラグの処理
ELSE
	REPEAT CHARANUM
		;マスターならスキップ
		SIF COUNT == MASTER
			CONTINUE
		;既に首輪など贈ってるので無理
		IF TALENT:COUNT:服従
			CFLAG:COUNT:27 = 0
			CFLAG:COUNT:28 = 0
			CONTINUE
		ENDIF
		;霧に飲まれかけていたらリセット
		IF CFLAG:COUNT:17
			CFLAG:COUNT:27 = 0
			CONTINUE
		ENDIF
		;所持金のカンスト
		;今のところ100万までしか見ないけど、1000万まで確保
		SIF CFLAG:COUNT:28 > 10000000
			CFLAG:COUNT:28 = 9999999
		;[親愛]＋[恋人]＋好感度30k＋反感0＋愛情経験300以上
		IF TALENT:COUNT:親愛 && TALENT:COUNT:恋人 && CFLAG:COUNT:2 >= 30000 && MARK:COUNT:反発感情 == 0 && EXP:COUNT:愛情経験 >= 300
			;目標達成
			IF CFLAG:COUNT:28 >= 1000000
				;達成してからの日にちをカウント
				;放置していると殴り込み
				CFLAG:COUNT:27 += 1
				CONTINUE
			;未達成なら貯金フラグ
			ELSE
				CFLAG:COUNT:27 = 1
			ENDIF
		;条件を満たさなければリセット
		ELSE
			CFLAG:COUNT:27 = 0
			CONTINUE
		ENDIF
	REND
ENDIF

;時間(気力)が余ったら金稼ぎ
@EARN_MONEY_TM
SIF (FLAG:62 & 8192) == 0
	RETURN 0
;気力0ならリターン
SIF BASE:気力 < 0
	RETURN 0
;貯金フラグ立って無くても一定確率で金稼ぎ
;その際[恋人]だと確率上昇
IF TALENT:恋人
	A = 1
ELSE
	A = 0
ENDIF
SIF CFLAG:27 == 0 && RAND:3 > A
	RETURN 0

P = (ABL:技巧 * 2 + 15) * (RAND:5 + 8)
A = RAND:100
IF A <= 10
	P *= 60
ELSEIF A <= 40
	P *= 90
ELSEIF A <= 70
	P *= 110
ELSEIF A <= 97
	P *= 140
ELSE
	P *= 400
ENDIF

P *= BASE:気力 / MAXBASE:気力

;100000を超えていたら100000
SIF P >= 100000
	P = 100000

CFLAG:28 += P

;維持費とか計算しているREPEAT内から呼び出す
;回復処理の前
;使用不可：A-F
@TURNEND_EARN_MONEY_TM
SIF (FLAG:62 & 8192) == 0
	RETURN 0
;貯金フラグが立ってないならリターン
SIF CFLAG:COUNT:27 == 0
	RETURN 0
;モンスター退治のコピー
IF BASE:COUNT:耐力 >= 500
	EXP:COUNT:戦闘経験 += RAND:5 + 5
	CFLAG:COUNT:2 -= RAND:10 + 6
	BASE:COUNT:耐力 -= RAND:11 * 30 + 200
	T = COUNT
	Z = COUNT
	CALL GET_BATTLE_POWER
	COUNT = Z
	
	G = P / 10 * (RAND:10 + 5)
	
	;稼ぎが高くなり過ぎないように補正をかける
	;7500を超えた分は25%OFF
	SIF G >= 7500
		G = 7500 + (G - 7500) / 4 * 3
	;補正後、15000を超えていたらその分を更に50%OFF
	SIF G >= 15000
		G = 15000 + (G - 15000) / 2
	;補正後、30000を超えていたらその分を更に75%OFF
	SIF G >= 30000
		G = 30000 + (G - 30000) / 4
	;補正後、50000を超えていたらその分を更に90%OFF
	SIF G >= 50000
		G = 50000 + (G - 50000) / 10
	
	;別れ中でないなら稼ぎは八分の一
	SIF CFLAG:COUNT:21 == 0
		G /= 8
	
	;補正後、100000を超えていたら100000
	SIF G >= 100000
		G = 100000
	
	;稼ぎを所持金へ
	CFLAG:COUNT:28 += G
ENDIF

;リングプレゼント
;開始時の口上の“後”に呼ばれます
;呼ぶタイミングは朝イベント後とかに動かすかも
@EVENTTRAIN
#LATER
SIF (FLAG:62 & 8192) == 0
	RETURN 0
SIF TARGET < 0
	RETURN 0
SIF (TALENT:親愛 && TALENT:恋人 && CFLAG:2 >= 30000 && MARK:反発感情 == 0) == 0
	RETURN 0
SIF CFLAG:28 < 1000000
	RETURN 0
SIF FLAG:29
	RETURN 0

DRAWLINE
;カットフラグを退避
Z:302 = CFLAG:99
;!;LOCAL:100 = CFLAG:99
;口上開始前の地の文をカットするか
TFLAG:13 = 95
CALL SELF_MAEKOJO
;地の文カット
IF (CFLAG:99 & 1) == 0
	PRINTFORMW %NAME:TARGET%は%NAME:MASTER%に渡したいものがあるらしい…
	PRINTFORMW %NAME:MASTER%が%NAME:TARGET%に近づくと、%NAME:TARGET%は%NAME:MASTER%の左手を引き寄せ、その薬指に指輪をはめた
ENDIF

;TFLAG:13 = 95
CALL SELF_KOJO

CALL RING_SET

;カットフラグを戻す
CFLAG:99 = Z:302
;!;CFLAG:99 = LOCAL:100

;リング持って殴り込み
@MORNING_RING
SIF (FLAG:62 & 8192) == 0
	RETURN 0
SIF (TALENT:親愛 && TALENT:恋人 && CFLAG:2 >= 30000 && MARK:反発感情 == 0) == 0
	RETURN 0
SIF CFLAG:28 < 1000000
	RETURN 0
SIF FLAG:29
	RETURN 0
;カットフラグを退避
Z:302 = CFLAG:99
;!;LOCAL:100 = CFLAG:99
;口上開始前の地の文をカットするか
TFLAG:13 = 96
CALL SELF_MAEKOJO
;地の文カット
IF (CFLAG:99 & 1) == 0
	PRINTFORM 朝、%NAME:MASTER%のもとへ
	SIF CFLAG:21
		PRINT 別れていた
	PRINTFORMW %NAME:TARGET%が訪ねてきた
	PRINTFORMW %NAME:TARGET%は渡したいものが有ると言い、%NAME:MASTER%の腕を取った
	PRINTFORMW そして、%NAME:MASTER%の左手を引き寄せ、その薬指に指輪をはめた
ENDIF

;TFLAG:13 = 96
CALL SELF_KOJO

CFLAG:TARGET:21 = 0

CALL RING_SET

;カットフラグを戻す
CFLAG:99 = Z:302
;!;CFLAG:99 = LOCAL:100

;リングイベント共通部分
@RING_SET
IF (CFLAG:99 & 1) == 0
	;散々焦らした挙げ句断るなんて外道はJには居ねぇよな？
	PRINTFORMW %NAME:MASTER%が%NAME:TARGET%の気持ちを受け入れる旨を伝えると、感極まった様子で%NAME:TARGET%が抱きついてきた
	IF TALENT:MASTER:お嫁さん == 0
		PRINTFORMW %NAME:MASTER%は%NAME:TARGET%の[%TALENTNAME:221%]になった
	ENDIF
	IF TALENT:一線越えない
		PRINTFORMW その顔は今までに無く幸福に満ち、すべてを吹っ切ったように見えた
		PRINTFORMW %NAME:TARGET%は[%TALENTNAME:27%]を失った
	ENDIF
	PRINTFORMW そのまま二人は、長い間抱擁を交し合っていた…
	IF TALENT:相愛 == 0
		PRINTFORMW %NAME:TARGET%は[%TALENTNAME:89%]を得た
	ENDIF
ENDIF

TALENT:MASTER:お嫁さん = NO:TARGET
TALENT:一線越えない = 0
TALENT:相愛 = 1
;売却不可フラグ
CFLAG:7 = 1
FLAG:29 = NO:TARGET
IF FLAG:5 == 9 || FLAG:5 == 10 || FLAG:24 > 6
	;結婚記念日フラグ
	TIME:9 = DAY:1
	TIME:10 = DAY:2
ENDIF
WAIT

;殴り込み判定
@EVENT_RING
SIF (FLAG:62 & 8192) == 0
	RETURN 0
;判定値保存用配列の初期化
REPEAT 999
	Z:COUNT = 0
REND
;判定値取得
REPEAT CHARANUM
	;念のため添え字限界
	SIF COUNT >= 999
		BREAK
	CALL CHECK_RING
REND

;目標値は100
Y = 99
T = -1
REPEAT CHARANUM
	;念のため添え字限界
	SIF COUNT >= 999
		BREAK
	;目標値を超える範囲で、一番高い実行値を持つキャラを選ぶ
	IF Z:COUNT > Y
		Y = Z:COUNT
		T = COUNT
	ENDIF
REND

;Tが初期値のまま→目標値に達したキャラが居ない
SIF T == -1
	RETURN 0

;ターゲット待避
Y = TARGET
TARGET = T
CALL MORNING_RING
TARGET = Y

@CHECK_RING
;目標達成したか
IF CFLAG:COUNT:28 < 1000000 && CFLAG:COUNT:27
	Z:COUNT = -1
	RETURN 0
ENDIF

;目標達成からの日数
Y = CFLAG:COUNT:27 * CFLAG:COUNT:27

;親密
Y += ABL:COUNT:親密 * 5

;臆病
SIF TALENT:COUNT:臆病
	Y -= 20
;反抗的
SIF TALENT:COUNT:反抗的
	Y += 5
;気丈
SIF TALENT:COUNT:気丈
	Y -= 5
;大人しい
SIF TALENT:COUNT:大人しい
	Y -= 20
;プライド高い
SIF TALENT:COUNT:プライド高い
	Y += CFLAG:COUNT:27 * 5
;生意気
SIF TALENT:COUNT:生意気
	Y += 10
;自制心
SIF TALENT:COUNT:自制心
	Y -= 15
;無関心
SIF TALENT:COUNT:無関心
	Y -= 30
;保守的
SIF TALENT:COUNT:保守的
	Y -= 20
;楽観的
SIF TALENT:COUNT:楽観的
	Y -= 5
;悲観的
SIF TALENT:COUNT:悲観的
	Y -= 10
;一線越えない
SIF TALENT:COUNT:一線越えない
	Y -= 30
;目立ちたがり
SIF TALENT:COUNT:目立ちたがり
	Y += 20
;抑圧
SIF TALENT:COUNT:抑圧
	Y -= 10
;解放
SIF TALENT:COUNT:解放
	Y += 5
;恥じらい
SIF TALENT:COUNT:恥じらい
	Y -= 10
;恥薄い
SIF TALENT:COUNT:恥薄い
	Y += 10
;倒錯的
SIF TALENT:COUNT:倒錯的 && TALENT:COUNT:オトコ == 0
	Y += 10
;男嫌い
SIF TALENT:COUNT:男嫌い && TALENT:MASTER:オトコ
	Y -= 10
;サド
SIF TALENT:COUNT:サド
	Y += 5
;小悪魔
SIF TALENT:COUNT:小悪魔
	Y += 5
;奴隷と主人
SIF TALENT:COUNT:主人 && TALENT:MASTER:虜
	Y += 30

;主導権基準値
T = COUNT
CALL GET_INITIATIVE_RATE
Y += R / 5

;別れ中
SIF CFLAG:COUNT:21
	Y -= 20

Z:COUNT = Y
