@COM_ABLE200
;乳の揉み合い実行判定
;COM_ABLE共通判定
CALL COM_ABLE_COMMON, SELECTCOM
SIF RESULT == 0
	RETURN 0
;ウフフ中じゃないと無理
SIF TFLAG:44 == 0
	RETURN 0
;調教者がオトコだとダメ
SIF TALENT:PLAYER:オトコ
	RETURN 0
;レズっ気３以上限定
SIF ABL:レズっ気 <= 2
	RETURN 0
;縄使用中は不可
SIF TEQUIP:44
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:90
	RETURN 0
RETURN 1

@COM200
;乳の揉み合い
PRINTL 乳の揉み合い
STR:0 = 乳の揉み合い
CALL TRAIN_MESSAGE_B

LOSEBASE:体力 += 7
LOSEBASE:気力 += 70

SOURCE:4 = 80
SOURCE:8 = 30
SOURCE:12 = 90

;ABL:Ｂ感覚をみる
IF ABL:Ｂ感覚 == 0
	SOURCE:17 = 20
	SOURCE:3 = 50
ELSEIF ABL:Ｂ感覚 == 1
	SOURCE:17 = 100
	SOURCE:3 = 100
ELSEIF ABL:Ｂ感覚 == 2
	SOURCE:17 = 500
	SOURCE:3 = 160
ELSEIF ABL:Ｂ感覚 == 3
	SOURCE:17 = 1200
	SOURCE:3 = 200
ELSEIF ABL:Ｂ感覚 == 4
	SOURCE:17 = 2000
	SOURCE:3 = 230
ELSE
	SOURCE:17 = 800 + (ABL:Ｂ感覚 * 400)
	SOURCE:3 = 200 + (ABL:Ｂ感覚 * 10)
ENDIF
IF TALENT:淫乳
	TIMES SOURCE:17 , 1.40
	TIMES SOURCE:3 , 1.40
ENDIF

;調教者が［幼児退行］
IF TALENT:PLAYER:幼児
	TIMES SOURCE:17 , 1.20
	TIMES SOURCE:3 , 1.20
ENDIF

;調教者が［幼稚］
IF TALENT:PLAYER:幼稚
	TIMES SOURCE:17 , 1.20
	TIMES SOURCE:3 , 1.20
ENDIF

;奴隷のＢ⇔調教者の指の汚れが移動
	STAIN:5 |= STAIN:PLAYER:1
	STAIN:PLAYER:1 |= STAIN:5

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	PRINTFORML %EXPNAME:40%＋7
	EXP:レズ経験 += 7
	EXP:MASTER:レズ経験 += 7
ELSEIF TALENT:オトコ == 1 && TALENT:PLAYER:オトコ == 1
	PRINTFORML %EXPNAME:41%＋7
	EXP:ＢＬ経験 += 7
ENDIF

;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:従順をみる
	IF ABL:親密 <= 1
		SOURCE:21 += 300 + (ABL:親密 * 50)
	ELSEIF ABL:親密 <= 3
		SOURCE:21 += 600 + (ABL:親密 * 100)
	ELSEIF ABL:親密 <= 5
		SOURCE:21 += 800 + (ABL:親密 * 120)
	ELSEIF ABL:親密 <= 8
		SOURCE:21 += 1000 + (ABL:親密 * 140)
	ELSEIF ABL:親密 <= 11
		SOURCE:21 += 1200 + (ABL:親密 * 160)
	ELSE
		SOURCE:21 += 1500 + (ABL:親密 * 180)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:サドっ気 == 0
		SOURCE:20 = 600
	ELSEIF ABL:サドっ気 == 1
		SOURCE:20 = 900
	ELSEIF ABL:サドっ気 == 2
		SOURCE:20 = 1200
	ELSEIF ABL:サドっ気 == 3
		SOURCE:20 = 1600
	ELSEIF ABL:サドっ気 == 4
		SOURCE:20 = 2000
	ELSE
		SOURCE:20 = 1000 + (ABL:サドっ気 * 300)
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:10 + 5
;合意がないと好感度が下がる
IF TALENT:合意 == 0 && TFLAG:48 == 0
	A -= 7
	SOURCE:15 += 200
ENDIF
TFLAG:30 += A
TFLAG:37 += A

RETURN 1

;乳の揉み合い
@TRAIN_MESSAGE_COM200
PRINTFORMW %CALLNAME:PLAYER%と%CALLNAME:TARGET%は互いの胸を揉みあった…

