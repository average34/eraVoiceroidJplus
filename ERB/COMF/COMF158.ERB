@COM_ABLE158
;蜘蛛の糸実行判定
;ヤマメちゃんはいません
RETURN 0

;COM_ABLE共通判定
CALL COM_ABLE_COMMON, SELECTCOM
SIF RESULT == 0
	RETURN 0
;ウフフ中じゃないと無理
SIF TFLAG:44 == 0
	RETURN 0
;調教者が蜘蛛でないとダメ
SIF NAME_CSV(NO:PLAYER, 0) != "アルケニー" && NAME_CSV(NO:PLAYER, 0) != "ヤマメ"
	RETURN 0
;アイマスク装着中はダメ
SIF TEQUIP:43
	RETURN 0
;縄装着中はダメ
SIF TEQUIP:44
	RETURN 0
;既にグルグル巻きにされていたらダメ
SIF TEQUIP:73
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:90
	RETURN 0
;お風呂場プレイ中はダメ
SIF TEQUIP:58
	RETURN 0
;新妻プレイ中はダメ
SIF TEQUIP:59
	RETURN 0
RETURN 1

@COM158
;蜘蛛の糸

PRINTL 蜘蛛の糸
STR:0 = 蜘蛛の糸

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------

;緊縛経験が高いと消費減少
IF EXP:緊縛経験 < EXPLV:3
	LOSEBASE:体力 += 100
	LOSEBASE:気力 += 130
ELSEIF EXP:緊縛経験 < EXPLV:4
	LOSEBASE:体力 += 80
	LOSEBASE:気力 += 100
ELSE
	LOSEBASE:体力 += 60
	LOSEBASE:気力 += 70
ENDIF

A = 250
B = 800
SOURCE:露出 = 900
SOURCE:屈従 = 500
SOURCE:逸脱 = 500

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES A , 0.80
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES A , 0.90
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES A , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES A , 1.10
ELSEIF PALAM:欲情 >= PALAMLV:4
	TIMES A , 1.20
ENDIF

;ABL:従順をみる
IF ABL:親密 == 0
	TIMES A , 0.40
ELSEIF ABL:親密 == 1
	TIMES A , 0.60
ELSEIF ABL:親密 == 2
	TIMES A , 0.80
ELSEIF ABL:親密 == 3
	TIMES A , 1.00
ELSEIF ABL:親密 == 4
	TIMES A , 1.10
ELSE
	TIMES A , 1.20
ENDIF

;ABL:マゾっ気をみる
IF ABL:マゾっ気 == 0
	TIMES A , 0.80
ELSEIF ABL:マゾっ気 == 1
	TIMES A , 1.00
ELSEIF ABL:マゾっ気 == 2
	TIMES A , 1.30
ELSEIF ABL:マゾっ気 == 3
	TIMES A , 1.60
ELSEIF ABL:マゾっ気 == 4
	TIMES A , 2.00
ELSE
	TIMES A , 3.00
ENDIF

;倒錯的
SIF TALENT:倒錯的
	TIMES A , 2.00

;臆病
SIF TALENT:臆病
	TIMES SOURCE:逸脱 , 2.00

;貧乳だと羞恥心が刺激される
SIF TALENT:貧乳
	TIMES SOURCE:露出 , 3.50

UP:恐怖 += B
EXP:緊縛経験 += 7
PRINTL 緊縛経験＋７

;縄の着脱
TEQUIP:43 = 1
TEQUIP:44 = 1
TEQUIP:73 = 1
TFLAG:68 = 100

CALL TRAIN_MESSAGE_B

RETURN 1

@TRAIN_MESSAGE_COM158
PRINTFORML %CALLNAME:PLAYER%はお尻から出した糸で%CALLNAME:TARGET%をがんじがらめに縛った
PRINTFORML %CALLNAME:TARGET%は粘りつく糸に絡み取られ、周りを見ることも動くこともできないでいる
SIF TALENT:貧乳
	PRINTFORML %CALLNAME:TARGET%は胸の周りだけ露出させた縛り方に羞恥心を刺激されているようだ……

@EQUIP_COM73
;緊縛中

PRINTL ＜蜘蛛の糸で緊縛中＞


;緊縛経験が高いと消費減少
IF EXP:緊縛経験 < EXPLV:3
	LOSEBASE:体力 += 50
	LOSEBASE:気力 += 80
ELSEIF EXP:緊縛経験 < EXPLV:4
	LOSEBASE:体力 += 40
	LOSEBASE:気力 += 60
ELSE
	LOSEBASE:体力 += 30
	LOSEBASE:気力 += 50
ENDIF


;ABL:マゾっ気をみる
IF ABL:マゾっ気 == 0
	A = 60
ELSEIF ABL:マゾっ気 == 1
	A = 180
ELSEIF ABL:マゾっ気 == 2
	A = 300
ELSEIF ABL:マゾっ気 == 3
	A = 480
ELSEIF ABL:マゾっ気 == 4
	A = 700
ELSE
	A = 850
ENDIF

;倒錯的
SIF TALENT:倒錯的
	TIMES A , 2.00

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES A , 0.80
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES A , 0.90
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES A , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES A , 1.10
ELSEIF PALAM:欲情 >= PALAMLV:4
	TIMES A , 1.20
ENDIF

SOURCE:痛み += A
SOURCE:液体追加 += A
SOURCE:露出 += A
SOURCE:屈従 += A
SOURCE:逸脱 += A
SOURCE:恭順追加 += A

UP:欲情 += A
UP:恐怖 += SOURCE:逸脱

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	PRINTS EXPNAME:40
	PRINTL +1
	EXP:レズ経験 += 1
ELSEIF TALENT:オトコ == 1 && TALENT:PLAYER:オトコ == 1
	PRINTS EXPNAME:41
	PRINTL +1
	EXP:ＢＬ経験 += 1
ENDIF

SIF ASSIPLAY == 0 && ABL:マゾっ気 >= 2
	TFLAG:30 += 1

W = 1 + RAND:10
TFLAG:68 -= W

IF TFLAG:68 <= 0
	TFLAG:68 = 0
	PRINTL 何度ももがくうちに糸は切れてしまった
	PRINTFORML 糸の断面から粘り気のある液体が滲み出て%CALLNAME:TARGET%の全身に付着した
	STAIN:TARGET:口 |= 1
	STAIN:TARGET:手 |= 1
	STAIN:TARGET:ペニス |= 1
	STAIN:TARGET:ヴァギナ |= 1
	STAIN:TARGET:アナル |= 1
ENDIF

EXP:緊縛経験 += 2
PRINTL 緊縛経験＋２
