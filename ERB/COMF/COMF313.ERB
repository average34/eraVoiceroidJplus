@COM_ABLE313
;願掛け実行判定
;COM_ABLE共通判定
CALL COM_ABLE_COMMON, SELECTCOM
SIF RESULT == 0
	RETURN 0
;ウフフ中は無理
SIF TFLAG:44 == 1
	RETURN 0
;七夕の夜(本来は、夜だけらしい)
SIF TIME:1 == 22 && TIME
	RETURN 1
;デート先に博麗神社、守矢神社を選択
SIF TEQUIP:2 == 2 || (TEQUIP:2 == 26 && TEQUIP:6 == 1)
	RETURN 1
;流星を観測中
SIF TIME:11 > 0
	RETURN 1
RETURN 0

@COM313
;願掛け
;使用変数をAとその配列、L:0,L:1のみに絞る
;とある場所や期間限定のコマンド

;博麗神社、守矢神社且つ元日の場合は、コマンド名が変わる
IF TIME:1 == 11 && (TEQUIP:2 == 2 || (TEQUIP:2 == 26 && TEQUIP:6 == 1))
	PRINTL 初詣
	STR:0 = 初詣
;博麗神社、守矢神社の場合は、コマンド名が変わる
ELSEIF TEQUIP:2 == 2 || (TEQUIP:2 == 26 && TEQUIP:6 == 1)
	PRINTL 参拝
	STR:0 = 参拝
ELSE
	PRINTL 願掛け
	STR:0 = 願掛け
ENDIF
CALL GET_SUCCESS_RATE
A:1 = A
A:2 = 90 + A:1 / 5
SIF A:2 > 99
	A:2 = 99
A:1 = RAND:100

;マイナスの場合は0にする
SIF (A:1+15) <= 15
	A:1 = 0
IF A:1 <= 4
	;5％で大成功
	TFLAG:18 = 1
ELSEIF A:1 >= A:2
	;5～1％で失敗
	TFLAG:18 = -1
ELSE
	;残りは成功
	TFLAG:18 = 0
ENDIF

;元日の博麗神社、守矢神社(初詣)
IF TIME:1 == 11 && (TEQUIP:2 == 2 || (TEQUIP:2 == 26 && TEQUIP:6 == 1))
	TFLAG:17 = 0
;デート先に博麗神社、守矢神社を選択
ELSEIF TEQUIP:2 == 2 || (TEQUIP:2 == 26 && TEQUIP:6 == 1)
	TFLAG:17 = 1
;七夕
ELSEIF TIME:1 == 22
	TFLAG:17 = 2
;流星雨を観測中
ELSEIF TIME:11 == 2
	TFLAG:17 = 3
;流星を観測中
ELSEIF TIME:11 == 1
	TFLAG:17 = 4
ENDIF

CALL TRAIN_MESSAGE_B

LOSEBASE:体力 += 0
LOSEBASE:気力 += 50

;固定で獲得するソース、達成感
SOURCE:達成感 = 10

;初期化
L = 0
;気力の成長限界(奉仕快楽経験、愛情経験、デート経験、キス経験)
L:1 = 3000 + (EXP:奉仕快楽経験 / 3) + (EXP:愛情経験 / 2) + EXP:デート経験 + EXP:キス経験
;9999は超えない
SIF L:1 > 9999
	L:1 = 9999
;元日の博麗神社、守矢神社(初詣)
IF TFLAG:17 == 0
	L = 3 - TFLAG:18
	IF RAND:L == 0
		L = RAND:L+(ABL:親密*3)+1
		;成長限界と比較
		IF MAXBASE:気力 > L:1
			SIF (MAXBASE:気力+L) > L:1
				L = 9999 - MAXBASE:気力
			MAXBASE:気力 += L
			PRINTFORMW 気力の上限値が{L}上がった
		ENDIF
	ENDIF
	SOURCE:達成感 += 100 + L
	SOURCE:歓楽 += 500 + L
;デート先に博麗神社を選択
ELSEIF TFLAG:17 == 1 && TEQUIP:2 == 2
	L = 5 - TFLAG:18
	T = 1
	CALL GET_EXISTANCE
	IF E != 0
		L -= 1
		E -= 1
		;霊夢様の場合、確率が更に下がる
		SIF CFLAG:E:0 != 0
			L -= 1
	ENDIF
	IF RAND:L == 0
		L = RAND:L+(ABL:親密*3)
		;成長限界と比較
		IF MAXBASE:気力 > L:1
			SIF (MAXBASE:気力+L) > L:1
				L = 9999 - MAXBASE:気力
			MAXBASE:気力 += L+1
			PRINTFORMW 気力の上限値が{L}上がった
		ENDIF
	ENDIF
	SOURCE:歓楽 += 10 + L
	SOURCE:達成感 += 100 + L
;守矢神社を選択(TFLAGは初期化されるので判定不可)
ELSEIF TFLAG:17 == 1 && (TEQUIP:2 == 26 && TEQUIP:6 == 1)
	L = 7 - TFLAG:18
	T = 42
	CALL GET_EXISTANCE
	IF E != 0
		L -= 1
		E -= 1
		;奇跡を持っていると確率が更に下がる
		SIF TALENT:E:淫魔 != 0
			L -= 1
	ENDIF
	T = 43
	CALL GET_EXISTANCE
	SIF E != 0
		L -= 1
	T = 44
	CALL GET_EXISTANCE
	SIF E != 0
		L -= 1
	IF RAND:L == 0
		L = RAND:L+(ABL:親密*2)+1
		;成長限界と比較
		IF MAXBASE:気力 > L:1
			SIF (MAXBASE:気力+L) > L:1
				L = 9999 - MAXBASE:気力
			MAXBASE:気力 += L
			PRINTFORMW 気力の上限値が{L}上がった
		ENDIF
	ENDIF
	SOURCE:歓楽 += 20 + L
	SOURCE:達成感 += 100 + L
;七夕
ELSEIF TFLAG:17 == 2
	L = 3 - TFLAG:18
	IF RAND:L == 0
		L = RAND:L+(ABL:親密*2)+1
		;成長限界と比較
		IF MAXBASE:気力 > L:1
			SIF (MAXBASE:気力+L) > L:1
				L = 9999 - MAXBASE:気力
			MAXBASE:気力 += L
			PRINTFORMW 気力の上限値が{L}上がった
		ENDIF
	ENDIF
	SOURCE:達成感 += 100 + L
	SOURCE:歓楽 += 300 + L
;流星雨を観測中
ELSEIF TFLAG:17 == 3
	L = 3 - TFLAG:18
	IF RAND:L == 0
		L = RAND:L+(ABL:親密*3)+1
		;成長限界と比較
		IF MAXBASE:気力 > L:1
			SIF (MAXBASE:気力+L) > L:1
				L = 9999 - MAXBASE:気力
			MAXBASE:気力 += L
			PRINTFORMW 気力の上限値が{L}上がった
		ENDIF
	ENDIF
	SOURCE:達成感 += 200 + L
	SOURCE:歓楽 += 400 + L
;流星を観測中
ELSEIF TFLAG:17 == 4
	L = 5 - TFLAG:18
	IF RAND:L == 0
		L = RAND:L+(ABL:親密*2)+1
		;成長限界と比較
		IF MAXBASE:気力 > L:1
			SIF (MAXBASE:気力+L) > L:1
				L = 9999 - MAXBASE:気力
			MAXBASE:気力 += L
			PRINTFORMW 気力の上限値が{L}上がった
		ENDIF
	ENDIF
	SOURCE:達成感 += 20 + L
	SOURCE:歓楽 += 100 + L
ENDIF

;好感度をみる
IF CFLAG:2 <= 300
	SOURCE:歓楽 += CFLAG:2
ELSEIF CFLAG:2 <= 3000
	SOURCE:歓楽 += 500 + (CFLAG:2 - 300) / 3
ELSE
	SOURCE:歓楽 += 2000 + (CFLAG:2 - 3000) / 5
ENDIF
SIF SOURCE:歓楽 < 0
	SOURCE:歓楽 = 0

;巫女ボーナス(神社でのみ有効)
SIF (NO:MASTER == 1 || NO:MASTER == 42) && TFLAG:17 <= 1
	SOURCE:達成感 += 200
SIF (NO:TARGET == 1 || NO:TARGET == 42) && TFLAG:17 <= 1
	SOURCE:家務 += 200

;好感度ランダム変化
A:1 = RAND:5 + 1

;親密が低いと好感度低下
IF ABL:親密 == 0
	A:1 -= 2
ELSEIF ABL:親密 == 1
	A:1 -= 1
ENDIF
;好感度が低いと好感度低下
IF CFLAG:2 <= 20
	A:1 -= 3
ELSEIF CFLAG:2 <= 80
	A:1 -= 2
ELSEIF CFLAG:2 <= 150
	A:1 -= 1
ENDIF
;反発があると好感度低下
IF MARK:反発感情 >= 3
	A:1 -= 3
ELSEIF MARK:反発感情 >= 2
	A:1 -= 2
ELSEIF MARK:反発感情 >= 1
	A:1 -= 1
ENDIF

;好感度変化
TFLAG:30 += A:1

IF TFLAG:18 == -1
	TIMES SOURCE:歓楽 , 0.10
	TIMES SOURCE:達成感 , 0.50
	TFLAG:30 -= 10
	TFLAG:37 -= 5
ELSEIF TFLAG:18 == 0
	TIMES SOURCE:歓楽 , 1.00
	TIMES SOURCE:達成感 , 1.00
	TFLAG:37 += A:1
ELSE
	TIMES SOURCE:歓楽 , 2.00
	TIMES SOURCE:達成感 , 2.00
	TFLAG:30 += 2
	TFLAG:37 += 5 + A:1
ENDIF

RETURN 1

;願掛け
;懲りすぎ？ そんな馬鹿な…
;願い事の内容はぼかしたので口上に任せます
@TRAIN_MESSAGE_COM313
;元旦の博麗神社、守矢神社(初詣)
IF TFLAG:17 == 0
	PRINTFORMW %CALLNAME:PLAYER%と%CALLNAME:TARGET%と初詣に行った
	IF TFLAG:18 == -1
		PRINTFORML 小銭を切らしており、大きな出費となった…
	ELSE
		PRINTFORML 賽銭を入れ、願いごとを念じた…
	ENDIF
;デート先に博麗神社、守矢神社を選択
ELSEIF TFLAG:17 == 1
	PRINTFORMW %CALLNAME:PLAYER%と%CALLNAME:TARGET%は参拝した
	IF TFLAG:18 == -1
		PRINTFORML 小銭を切らしており、大きな出費となった…
	ELSE
		IF TALENT:妊娠 == 1
			PRINTFORML 賽銭を入れ、安産を祈願した…
		ELSE
			PRINTFORML 賽銭を入れ、願いごとを念じた…
		ENDIF
	ENDIF
;七夕
ELSEIF TFLAG:17 == 2
	;晴れor快晴
	IF TIME:5 < 2
		PRINTFORMW %CALLNAME:PLAYER%と%CALLNAME:TARGET%は短冊に願いを書き、竹に飾りつけた
		IF TFLAG:18 == -1
			PRINTFORML 星々や天の川を前に願いを忘れてしまった…
		ELSE
			PRINTFORML 短冊が風に揺られ、独特の風情を醸し出していた…
		ENDIF
	ELSE
		PRINTFORMW %CALLNAME:PLAYER%と%CALLNAME:TARGET%は雲の向こうに願掛けした
		IF TFLAG:18 == -1
			PRINTFORML 短冊を作る気も起きず、飾りつけは中止となった…
		ELSE
			PRINTFORML 天の川や星々は見えないが、短冊が願いを届けてくれるだろう…
		ENDIF
	ENDIF
;流星雨を観測中
ELSEIF TFLAG:17 == 3
	PRINTFORMW %CALLNAME:PLAYER%と%CALLNAME:TARGET%は流星雨に願い事を唱えた
	IF TFLAG:18 == -1
		PRINTFORML 流れる星々を前に、何を願うか忘れてしまった…
	ELSE
		PRINTFORML 星々は願いを乗せて流れ続けた…
	ENDIF
;流星を観測中
ELSEIF TFLAG:17 == 4
	PRINTFORMW %CALLNAME:PLAYER%と%CALLNAME:TARGET%は流れ星に願い事を唱えた
	IF TFLAG:18 == -1
		PRINTFORML しかし、3回唱える前に見えなくなってしまった…
	ELSE
		PRINTFORML 見えなくなる前に何とか唱えることが出来た…
	ENDIF
ENDIF

