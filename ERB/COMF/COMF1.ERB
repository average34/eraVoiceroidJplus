@COM1

;前回と今回の調教者が同じ
IF (ASSIPLAY && TFLAG:50) || (ASSIPLAY == 0 && TFLAG:50 == 0)
	;縄使用中でなく、前回の調教がフェラチオかクンニ強制かシックスナインだとシックスナインに
	IF (PREVCOM == 31 || PREVCOM == 61 || PREVCOM == 69) && TEQUIP:44 == 0
		CALL COMABLE_69
		SIF RESULT == 1
			JUMP COM69
	ENDIF
ENDIF

;羞恥プレイ中は
IF TEQUIP:57
JUMP COM87
ENDIF

PRINTL クンニ
STR:0 = クンニ
CALL TRAIN_MESSAGE_B

;クンニ
LOSEBASE:体力 += 5
LOSEBASE:気力 += 50

SOURCE:液体追加 = 100
SOURCE:露出 = 220
SOURCE:逸脱 = 50

;ABL:Ｃ感覚をみる
IF ABL:Ｃ感覚 == 0
	SOURCE:快Ｃ = 40
ELSEIF ABL:Ｃ感覚 == 1
	SOURCE:快Ｃ = 160
ELSEIF ABL:Ｃ感覚 == 2
	SOURCE:快Ｃ = 700
ELSEIF ABL:Ｃ感覚 == 3
	SOURCE:快Ｃ = 1500
ELSEIF ABL:Ｃ感覚 == 4
	SOURCE:快Ｃ = 2400
ELSE
	SOURCE:快Ｃ = 1050 + (ABL:Ｃ感覚 * 450)
ENDIF

;クスコ装着中は効果変動
IF TEQUIP:61
	TIMES SOURCE:快Ｃ , 0.30
	SOURCE:露出 += 500
ENDIF

;調教者のTALENT:舌使いをみる
IF TALENT:PLAYER:舌使い
	TIMES SOURCE:快Ｃ , 2.00
	SOURCE:恭順追加 += SOURCE:快Ｃ/20
ENDIF

;奴隷のＶ⇔調教者の口の汚れが移動
STAIN:ヴァギナ |= STAIN:PLAYER:口
STAIN:PLAYER:口 |= STAIN:ヴァギナ

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	PRINTS EXPNAME:40
	PRINTL +3
	EXP:レズ経験 += 3
	EXP:PLAYER:レズ経験 += 3
ENDIF


;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:従順をみる
	IF ABL:親密 <= 1
		SOURCE:受動 += 300 + (ABL:親密 * 50)
	ELSEIF ABL:親密 <= 3
		SOURCE:受動 += 600 + (ABL:親密 * 100)
	ELSEIF ABL:親密 <= 5
		SOURCE:受動 += 800 + (ABL:親密 * 120)
	ELSEIF ABL:親密 <= 8
		SOURCE:受動 += 1000 + (ABL:親密 * 140)
	ELSEIF ABL:親密 <= 11
		SOURCE:受動 += 1200 + (ABL:親密 * 160)
	ELSE
		SOURCE:受動 += 1500 + (ABL:親密 * 180)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:サドっ気 == 0
		SOURCE:征服 = 600
	ELSEIF ABL:サドっ気 == 1
		SOURCE:征服 = 900
	ELSEIF ABL:サドっ気 == 2
		SOURCE:征服 = 1200
	ELSEIF ABL:サドっ気 == 3
		SOURCE:征服 = 1600
	ELSEIF ABL:サドっ気 == 4
		SOURCE:征服 = 2000
	ELSE
		SOURCE:征服 = 1000 + (ABL:サドっ気 * 300)
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:10 + 5
;合意がないと好感度が下がる
IF TALENT:合意 == 0 && TFLAG:48 == 0
	A -= 7
	SOURCE:反感追加 += 200
ENDIF
TFLAG:30 += A
TFLAG:37 += A / 2

RETURN 1
