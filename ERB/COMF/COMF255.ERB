@COM_ABLE255
;挿入Ｇスポ責め実行判定
;COM_ABLE共通判定
CALL COM_ABLE_COMMON, SELECTCOM
SIF RESULT == 0
	RETURN 0
;正常位実行判定流用
;ウフフ中じゃないと無理
SIF TFLAG:44 == 0
	RETURN 0
;バイブ挿入中はダメ
SIF TEQUIP:11
	RETURN 0
;クスコ挿入中はダメ
SIF TEQUIP:61
	RETURN 0
;焼芋入中は不可
SIF TEQUIP:71
	RETURN 0
;オトコだとダメ
SIF TALENT:122
	RETURN 0
;小人体型持ちは、双方が調教者と調教対象ならOK
;それ以外の場合は主人が禁断の知識持ちじゃないとダメ
SIF TALENT:149 && (TALENT:MASTER:125 == 0 && TALENT:PLAYER:149 == 0)
	RETURN 0
;調教者がオトコ、半人半妖、ふたなり、もしくはペニスバンドがないとダメ
SIF TALENT:PLAYER:119 == 0 && TALENT:PLAYER:121 == 0 && TALENT:PLAYER:122 == 0 && ITEM:PBAND == 0
	RETURN 0
;処女で、従順4以下・レズっ気4以下のASSIの場合正常位はできない（サドならＯＫ）
IF EXP:0 == 0 && ASSIPLAY
	SIF (ABL:ASSI:親密 <= 4 || ABL:ASSI:レズっ気 <= 4) && TALENT:ASSI:83 == 0
		RETURN 0
ENDIF
;潤滑不足で、従順3以下・レズっ気3以下のASSIの場合正常位はできない（サドならＯＫ）
IF PALAM:3 < PALAMLV:2 && ASSIPLAY
	SIF (ABL:ASSI:親密 <= 3 || ABL:ASSI:レズっ気 <= 3) && TALENT:ASSI:83 == 0
		RETURN 0
ENDIF
;触手調教中はダメ
SIF TEQUIP:90
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
IF TEQUIP:58
	SIF ITEM:13 == 0 && NOITEM == 0
		RETURN 0
ENDIF
;前回コマンドが正常位・後背位・対面座位・背面座位・騎乗位・乱れ牡丹・挿入Ｇスポ責め・挿入子宮口責めでないとダメ
SIF PREVCOM != 20 && PREVCOM != 21 && PREVCOM != 24 && PREVCOM != 25 && PREVCOM != 34 && PREVCOM != 88 && PREVCOM != 255 && PREVCOM != 256
	RETURN 0
;調教者の技巧3以上かつ前回コマンド実行者が今回も実行していないとダメ
IF ABL:PLAYER:技巧 > 2 && ((ASSIPLAY && TFLAG:50) || (ASSIPLAY == 0 && TFLAG:50 == 0))
	RETURN 1
ELSE
	RETURN 0
ENDIF

RETURN 1

@COM255
;挿入Ｇスポ責め

TFLAG:42 = 0
IF SELECTCOM == 34
	PRINTL 騎乗Ｇスポ責め
	STR:0 = 騎乗Ｇスポ責め
ELSE
	PRINTL 挿入Ｇスポ責め
	STR:0 = 挿入Ｇスポ責め
ENDIF

;派生元コマンドをPREVCOMに登録、SELECTCOMをこのコマンド番号に変更してからCALL TRAIN_MESSAGE_B
SIF SELECTCOM != 255
	PREVCOM = SELECTCOM
SELECTCOM = 255

CALL TRAIN_MESSAGE_B

;V経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:19 = 1
SIF TALENT:85 && ASSIPLAY == 0 && EXP:0 == 0
	TFLAG:20 = 1

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
SIF TALENT:PLAYER:121 || TALENT:PLAYER:122
	BASE:PLAYER:2 += CALC_EJAC(SELECTCOM, PLAYER)

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
IF PREVCOM == 34
	LOSEBASE:0 += 60
	LOSEBASE:1 += 150
	SOURCE:12 = 1300
ELSE
	LOSEBASE:0 += 50
	LOSEBASE:1 += 100
	SOURCE:12 = 900
ENDIF

;ABL:Ｖ感覚をみる
IF ABL:Ｖ感覚 == 0
	SOURCE:1 = 150
	SOURCE:3 = 100
ELSEIF ABL:Ｖ感覚 == 1
	SOURCE:1 = 200
	SOURCE:3 = 180
ELSEIF ABL:Ｖ感覚 == 2
	SOURCE:1 = 600
	SOURCE:3 = 250
ELSEIF ABL:Ｖ感覚 == 3
	SOURCE:1 = 2000
	SOURCE:3 = 380
ELSEIF ABL:Ｖ感覚 == 4
	SOURCE:1 = 2600
	SOURCE:3 = 500
ELSE
	SOURCE:1 = 400 + (ABL:Ｖ感覚 * 600)
	SOURCE:3 = 200 + (ABL:Ｖ感覚 * 120)
ENDIF

;EXP:Ｖ経験をみる

IF EXP:0 < EXPLV:1
	TIMES SOURCE:1 , 0.20
	SOURCE:6 = 300
ELSEIF EXP:0 < EXPLV:2
	TIMES SOURCE:1 , 0.60
	SOURCE:6 = 100
ELSEIF EXP:0 < EXPLV:3
	TIMES SOURCE:1 , 1.00
	SOURCE:6 = 10
ELSEIF EXP:0 < EXPLV:4
	TIMES SOURCE:1 , 1.20
	SOURCE:6 = 0
ELSEIF EXP:0 < EXPLV:5
	TIMES SOURCE:1 , 1.30
	SOURCE:6 = 0
ELSE
	TIMES SOURCE:1 , 1.80
	SOURCE:6 = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:3 < PALAMLV:1
	TIMES SOURCE:1 , 0.10
	SOURCE:6 += 1000
	TIMES SOURCE:6 , 3.00
ELSEIF PALAM:3 < PALAMLV:2
	TIMES SOURCE:1 , 0.40
	SOURCE:6 += 300
	TIMES SOURCE:6 , 1.00
ELSEIF PALAM:3 < PALAMLV:3
	TIMES SOURCE:1 , 1.00
	TIMES SOURCE:6 , 0.50
ELSEIF PALAM:3 < PALAMLV:4
	TIMES SOURCE:1 , 1.40
	TIMES SOURCE:6 , 0.20
ELSEIF PALAM:3 >= PALAMLV:4
	TIMES SOURCE:1 , 1.80
	TIMES SOURCE:6 , 0.10
ENDIF

;対面座位だと効果が低い
IF PREVCOM == 24
	TIMES SOURCE:1 , 0.70
	TIMES SOURCE:3 , 0.90
ENDIF

;背面座位だと効果が高い
IF PREVCOM == 25 || PREVCOM == 88
	TIMES SOURCE:1 , 1.30
	TIMES SOURCE:3 , 1.10
ENDIF

;調教者がふたなり
IF ASSIPLAY
	SIF TALENT:ASSI:121
		TIMES SOURCE:1 , 2.50
ENDIF

;小柄体形
SIF TALENT:100
	TIMES SOURCE:6 , 2.00

;貞操観念
IF TALENT:30
	IF EXP:0 == 0
		TIMES SOURCE:3 , 0.60
		TIMES SOURCE:14, 5.00
		SOURCE:15 = 2000
	ELSE
		TIMES SOURCE:3 , 0.60
		SOURCE:15 = 1000
	ENDIF
;貞操無頓着
ELSEIF TALENT:31
	IF EXP:0 == 0
		TIMES SOURCE:3 , 0.60
		SOURCE:15 = 300
	ENDIF
ELSE
	IF EXP:0 == 0
		SOURCE:15 = 3000
	ENDIF
ENDIF

;PALAM:欲情をみる
IF PALAM:5 < PALAMLV:1
	TIMES SOURCE:1 , 0.60
	TIMES SOURCE:3 , 0.30
ELSEIF PALAM:5 < PALAMLV:2
	TIMES SOURCE:1 , 0.80
	TIMES SOURCE:3 , 0.60
ELSEIF PALAM:5 < PALAMLV:3
	TIMES SOURCE:1 , 1.00
	TIMES SOURCE:3 , 1.00
ELSEIF PALAM:5 < PALAMLV:4
	TIMES SOURCE:1 , 1.20
	TIMES SOURCE:3 , 1.50
ELSEIF PALAM:5 >= PALAMLV:4
	TIMES SOURCE:1 , 1.50
	TIMES SOURCE:3 , 1.80
ENDIF

;ABL:従順をみる
IF ABL:親密 == 0
	TIMES SOURCE:1 , 0.50
	TIMES SOURCE:3 , 0.60
	TIMES SOURCE:15 , 2.00
ELSEIF ABL:親密 == 1
	TIMES SOURCE:1 , 0.80
	TIMES SOURCE:3 , 0.80
	TIMES SOURCE:15 , 1.50
ELSEIF ABL:親密 == 2
	TIMES SOURCE:1 , 1.00
	TIMES SOURCE:3 , 1.00
	TIMES SOURCE:15 , 1.00
ELSEIF ABL:親密 == 3
	TIMES SOURCE:1 , 1.30
	TIMES SOURCE:3 , 1.20
	TIMES SOURCE:15 , 0.80
ELSEIF ABL:親密 == 4
	TIMES SOURCE:1 , 1.60
	TIMES SOURCE:3 , 1.40
	TIMES SOURCE:15 , 0.60
ELSE
	SOURCE:1 = SOURCE:1 * (ABL:親密 * 20 + 100) / 100
	SOURCE:3 = SOURCE:3 * (ABL:親密 * 10 + 110) / 100
	TIMES SOURCE:15 , 0.30
ENDIF

IF PREVCOM == 34
	;ABL:奉仕精神をみる
	IF ABL:奉仕精神 == 0
		SOURCE:4 = 200
		SOURCE:5 = 50
		SOURCE:14 = 300
	ELSEIF ABL:奉仕精神 == 1
		SOURCE:4 = 250
		SOURCE:5 = 200
		SOURCE:14 = 100
	ELSEIF ABL:奉仕精神 == 2
		SOURCE:4 = 350
		SOURCE:5 = 550
		SOURCE:14 = 30
	ELSEIF ABL:奉仕精神 == 3
		SOURCE:4 = 450
		SOURCE:5 = 900
		SOURCE:14 = 0
	ELSEIF ABL:奉仕精神 == 4
		SOURCE:4 = 600
		SOURCE:5 = 1500
		SOURCE:14 = 0
	ELSE
		SOURCE:4 = 375 + (ABL:奉仕精神 * 75)
		SOURCE:5 = 450 + (ABL:奉仕精神 * 350)
		SOURCE:14 = 0
	ENDIF
	;ABL:技巧をみる
	IF ABL:技巧 == 0
		TIMES SOURCE:1 , 0.30
		TIMES SOURCE:3 , 0.30
	ELSEIF ABL:技巧 == 1
		TIMES SOURCE:1 , 0.60
		TIMES SOURCE:3 , 0.60
	ELSEIF ABL:技巧 == 2
		TIMES SOURCE:1 , 1.00
		TIMES SOURCE:3 , 1.00
	ELSEIF ABL:技巧 == 3
		TIMES SOURCE:1 , 1.10
		TIMES SOURCE:3 , 1.10
	ELSEIF ABL:技巧 == 4
		TIMES SOURCE:1 , 1.30
		TIMES SOURCE:3 , 1.20
	ELSE
		SOURCE:1 = SOURCE:1 * (ABL:技巧 * 100 + 100) / 100
		SOURCE:3 = SOURCE:3 * (ABL:技巧 * 10 + 90) / 100
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:12 + 8
SIF TALENT:85 || TALENT:153
	A += 5
SIF TALENT:88
	A += 5
;合意がないと好感度が下がる
IF TALENT:152 == 0 && TFLAG:48 == 0
	A -= 15
	SOURCE:15 += 1200
ENDIF
TFLAG:30 += A
TFLAG:37 += A / 2

EXP:0 += 1
PRINTL Ｖ経験＋１

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK

;射精時の処理
IF E >= 1
	;コマンドごとに固有の処理はこの下に書く
	IF ABL:親密 >= 3 && ABL:欲望 >= 3 && FLAG:6
	PRINTFORML %CALLNAME:TARGET%の膣壁を抉ったまま、%CALLNAME:PLAYER%は精液を放出した…
	PRINTFORML 
	ENDIF

	;射精先タイプの設定
	TFLAG:703 = 1
	;射精先選択の可・不可
	TFLAG:704 = 0
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;射精に関係なく行われる処理はここから
;奴隷のＶ⇔調教者のＰの汚れが移動
STAIN:3 |= STAIN:PLAYER:2
STAIN:PLAYER:2 |= STAIN:3

IF TALENT:122 == 0 && TALENT:PLAYER:122 == 0
	PRINTFORML %EXPNAME:40%＋4
	EXP:40 += 4
ENDIF

;IF ASSIPLAY == 0
;	IF ABL:Ｖ感覚 >= 3
;		TFLAG:30 += 2
;	ELSE
;		TFLAG:30 += 1
;	ENDIF
;ENDIF

RETURN 1

@TRAIN_MESSAGE_COM255
;騎乗位
IF PREVCOM == 34
	PRINTFORMW %CALLNAME:TARGET%は上体を大きく逸らし、膣内の天井にペニスを擦り付けた…
	IF EXP:0 <= 30
		PRINTFORMW %CALLNAME:TARGET%は戸惑いながらもその行為を続けている…
	ELSEIF EXP:0 >= 31 && EXP:0 <= 50
		PRINTFORMW %CALLNAME:TARGET%は掴んだ快感を追って腰をくねらせている…
	ELSEIF EXP:0 >= 51 && EXP:0 <= 80
		PRINTFORMW %CALLNAME:TARGET%は尻をすぼめ、小刻みに腰をしゃくって嬌声を上げている…
	ELSEIF EXP:0 >= 81 && EXP:0 <= 120
		PRINTFORML %CALLNAME:TARGET%は後ろに手を衝いて、腰をガクガク揺らして快楽を貪っている…
		PRINTFORMW 反り返ったペニスの先端が、鋭角にＧスポットを抉っている…
	ELSEIF EXP:0 >= 121
		PRINTFORML %CALLNAME:TARGET%は女の急所を、自らの腰の痙攣で思う存分刺激している…
		PRINTFORMW ぐぽんと重い水音がするたび、鼻に抜けただらしのない声が漏れている…
	ENDIF
ELSE
	;正常位
	SIF PREVCOM == 20
		PRINTFORMW 腰をしゃくり、%CALLNAME:TARGET%の膣内の天井を小刻みに擦りあげた…
	;後背位
	SIF PREVCOM == 21
		PRINTFORMW 上体を屈め、%CALLNAME:TARGET%の膣内の天井を抉るように突き入れた…
	;対面座位
	SIF PREVCOM == 24
		PRINTFORMW 上体を離し、%CALLNAME:TARGET%の下腹を押さえて膣内の天井を擦りあげた…
	;背面座位・乱れ牡丹
	SIF PREVCOM == 25 || PREVCOM == 88
		PRINTFORMW %CALLNAME:TARGET%を持ち上げ、揺するように膣内の天井を執拗に刺激した…
	IF EXP:0 <= 30
		PRINTFORMW %CALLNAME:TARGET%はむずがるように腰を揺すっている…
	ELSEIF EXP:0 >= 31 && EXP:0 <= 50
		PRINTFORMW %CALLNAME:TARGET%は口を結んで不慣れな快楽を堪えている…
	ELSEIF EXP:0 >= 51 && EXP:0 <= 80
		PRINTFORMW %CALLNAME:TARGET%は弱点を責められ、あまりの快感に目をぱちぱちさせている…
	ELSEIF EXP:0 >= 81 && EXP:0 <= 120
		PRINTFORMW %CALLNAME:PLAYER%は腰に力を入れ、膣内でビクビクと震わせてやった…
		PRINTFORMW %CALLNAME:TARGET%は過敏な天井を擦られる感触に熱い吐息を漏らしている…
	ELSEIF EXP:0 >= 121
		PRINTFORML %CALLNAME:TARGET%は喉を鳴らして涎を飲み、足指を丸めて頭を振って悶えている…
		PRINTFORMW まともに呼吸もできず、口をぱくつかせて声にならない喘ぎを繰り返す…
	ENDIF
ENDIF

