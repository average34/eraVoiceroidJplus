@COM_ABLE196
;ペニバン挿入実行判定
;COM_ABLE共通判定
CALL COM_ABLE_COMMON, SELECTCOM
SIF RESULT == 0
	RETURN 0
;ウフフ中じゃないと無理
SIF TFLAG:44 == 0
	RETURN 0
;オトコだとダメ
SIF TALENT:122
	RETURN 0
;潤滑不足で、従順3以下・レズっ気3以下のASSIの場合ペニバン挿入はできない（サドならＯＫ）
IF (PALAM:3 < PALAMLV:2) && ASSIPLAY
	SIF ((ABL:ASSI:0 <= 3) || (ABL:ASSI:9 <= 3)) && (TALENT:ASSI:83 == 0)
		RETURN 0
ENDIF
;バイブ挿入中はダメ
SIF TEQUIP:11
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:90
	RETURN 0
;ペニスバンドが無いとダメ
SIF ITEM:PBAND == 0
	RETURN 0
RETURN 1

@COM196
;ペニバン挿入

;前回の調教がペニバン挿入かつプレイヤの技巧３以上だとＧスポット刺激に
IF PREVCOM == 196 && ABL:PLAYER:2 >= 3
	SELECTCOM = 84
	JUMP COM84
ENDIF

;前回の調教がGスポットならさらにＧスポット刺激に
;IF PREVCOM == 84
;	JUMP COM84
;ENDIF

;;クスコ挿入中はＧスポット刺激に
;IF TEQUIP:61
;	JUMP COM84
;ENDIF

PRINTL ペニバン挿入
STR:0 = ペニバン挿入
CALL TRAIN_MESSAGE_B

;V経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:19 = 1

LOSEBASE:0 += 60
LOSEBASE:1 += 120

SOURCE:3 = 100
SOURCE:12 = 800
SOURCE:14 = 500

;ABL:Ｖ感覚をみる
IF ABL:4 == 0
	SOURCE:1 = 30
	SOURCE:13 = 200
ELSEIF ABL:4 == 1
	SOURCE:1 = 120
	SOURCE:13 = 350
ELSEIF ABL:4 == 2
	SOURCE:1 = 350
	SOURCE:13 = 550
ELSEIF ABL:4 == 3
	SOURCE:1 = 700
	SOURCE:13 = 900
ELSEIF ABL:4 == 4
	SOURCE:1 = 1300
	SOURCE:13 = 1500
ELSE
	SOURCE:1 = 750 + (ABL:4 * 250)
	SOURCE:13 = 1050 + (ABL:4 * 250)
ENDIF

;EXP:Ｖ経験をみる
IF EXP:0 < EXPLV:1
	TIMES SOURCE:1 , 0.20
	TIMES SOURCE:13, 0.20
	SOURCE:6 = 300
ELSEIF EXP:0 < EXPLV:2
	TIMES SOURCE:1 , 0.50
	TIMES SOURCE:13, 0.50
	SOURCE:6 = 180
ELSEIF EXP:0 < EXPLV:3
	TIMES SOURCE:1 , 1.00
	TIMES SOURCE:13, 0.80
	SOURCE:6 = 80
ELSEIF EXP:0 < EXPLV:4
	TIMES SOURCE:1 , 1.20
	TIMES SOURCE:13, 1.00
	SOURCE:6 = 30
ELSEIF EXP:0 < EXPLV:5
	TIMES SOURCE:1 , 1.60
	TIMES SOURCE:13, 1.20
	SOURCE:6 = 0
ELSE
	TIMES SOURCE:1 , 1.80
	TIMES SOURCE:13, 1.50
	SOURCE:6 = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:3 < PALAMLV:1
	TIMES SOURCE:1 , 0.10
	SOURCE:6 += 700
	TIMES SOURCE:6 , 3.00
ELSEIF PALAM:3 < PALAMLV:2
	TIMES SOURCE:1 , 0.20
	SOURCE:6 += 200
	TIMES SOURCE:6 , 1.00
ELSEIF PALAM:3 < PALAMLV:3
	TIMES SOURCE:1 , 0.60
	TIMES SOURCE:6 , 0.80
ELSEIF PALAM:3 < PALAMLV:4
	TIMES SOURCE:1 , 1.00
	TIMES SOURCE:6 , 0.50
ELSEIF PALAM:3 >= PALAMLV:4
	TIMES SOURCE:1 , 2.00
	TIMES SOURCE:6 , 0.10
ENDIF

;PALAM:欲情をみる
IF PALAM:5 < PALAMLV:1
	TIMES SOURCE:1 , 0.50
ELSEIF PALAM:5 < PALAMLV:2
	TIMES SOURCE:1 , 0.80
ELSEIF PALAM:5 < PALAMLV:3
	TIMES SOURCE:1 , 1.20
ELSEIF PALAM:5 < PALAMLV:4
	TIMES SOURCE:1 , 1.50
ELSEIF PALAM:5 >= PALAMLV:4
	TIMES SOURCE:1 , 1.80
ENDIF

;Ｖ敏感、鈍感をみる
;快Ｖ自体のチェックは後でまとめてやる
IF TALENT:103
	TIMES SOURCE:6 , 1.50
	TIMES SOURCE:13 , 1.50
	TIMES SOURCE:14 , 1.50
ELSEIF TALENT:104
	TIMES SOURCE:6 , 0.60
	TIMES SOURCE:13 , 0.60
	TIMES SOURCE:14 , 0.60
ENDIF

;処女で貞操観念
IF EXP:0 == 0 && TALENT:30
	TIMES SOURCE:13 , 2.00
ENDIF

EXP:0 += 1
PRINTL Ｖ経験＋１

;奴隷のＶ⇔調教者の口の汚れが移動
	STAIN:3 |= STAIN:PLAYER:0
	STAIN:PLAYER:0 |= STAIN:3

IF TALENT:122 == 0 && TALENT:PLAYER:122 == 0
	PRINTS EXPNAME:40
	PRINTL +1
	EXP:40 += 1
	EXP:PLAYER:40 += 1
ELSEIF TALENT:122 == 1 && TALENT:PLAYER:122 == 1
	PRINTS EXPNAME:41
	PRINTL +1
	EXP:41 += 1
	EXP:PLAYER:41 += 1
ENDIF

;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:従順をみる
	IF ABL:0 <= 1
		SOURCE:21 += 300 + (ABL:0 * 50)
	ELSEIF ABL:0 <= 3
		SOURCE:21 += 600 + (ABL:0 * 100)
	ELSEIF ABL:0 <= 5
		SOURCE:21 += 800 + (ABL:0 * 120)
	ELSEIF ABL:0 <= 8
		SOURCE:21 += 1000 + (ABL:0 * 140)
	ELSEIF ABL:0 <= 11
		SOURCE:21 += 1200 + (ABL:0 * 160)
	ELSE
		SOURCE:21 += 1500 + (ABL:0 * 180)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:15 == 0
		SOURCE:20 = 600
	ELSEIF ABL:15 == 1
		SOURCE:20 = 900
	ELSEIF ABL:15 == 2
		SOURCE:20 = 1200
	ELSEIF ABL:15 == 3
		SOURCE:20 = 1600
	ELSEIF ABL:15 == 4
		SOURCE:20 = 2000
	ELSE
		SOURCE:20 = 1000 + (ABL:15 * 300)
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:10 + 5
;合意がないと好感度が下がる
IF TALENT:152 == 0 && TFLAG:48 == 0
	A -= 9
	SOURCE:15 += 400
ENDIF
TFLAG:30 += A
TFLAG:37 += A / 2

RETURN 1



;ペニバン挿入
@TRAIN_MESSAGE_COM196
PRINTFORM %CALLNAME:PLAYER%はペニスバンドを口に装着して、%CALLNAME:TARGET%の
	IF EXP:0 == 0
		PRINT 男を知らない
	ELSEIF PALAM:3 >= PALAMLV:4
		PRINT 濡れそぼった
	ENDIF
	IF TALENT:100
		PRINT 幼いワレメ
	ELSE
		PRINT 秘裂
	ENDIF
IF TEQUIP:61
	PRINTFORML のクスコで開かれた内壁に挿入した…
ELSE
	PRINTFORML に挿入した…
	ENDIF
	PRINTFORM %CALLNAME:PLAYER%は
	SIF PALAM:3 >= PALAMLV:4
		PRINT 顔を愛液まみれにしながら、
	IF TALENT:85 && RAND:3 >= 1
		PRINTFORM %CALLNAME:TARGET%の顔をじっと見上げて
	ELSEIF TALENT:PLAYER:83 || (ABL:PLAYER:15 >= 3 && RAND:2 == 0)
		PRINT 激しく
	ELSE
		PRINT ゆっくりと
	ENDIF
PRINTW 頭を振っている…

