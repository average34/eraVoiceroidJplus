@COM323
;デート
;改造するのに一番困ってる。バグ多いかもしれない
;ToDo:ゲームクリア前であり、かつEXTRAでなければ東北邸に行かないようにする
;ToDo:親密10以上でないと行きたそうにしている場所がわからないようにする

;すでにデートしている場合
;TEQUIP:2 - デート中
;TFALG:44 - ウフフ中 
;TFLAG:62 - 聖輦船乗船フラグ
IF TEQUIP:2 && MONEY > 2000 && TFLAG:44 == 0 && TFLAG:62 == 0
	PRINTL [0] デート終了
	PRINTL [1] 場所を変える($2000を消費)
	PRINTL [100] やっぱりやめる
	$INPUT_LOOP2
	INPUT
	IF RESULT != 100 && (RESULT < 0 || RESULT > 1)
		GOTO INPUT_LOOP2
	ELSEIF RESULT == 1
		FLAG:8 = TEQUIP:2
		PRINT デートの場所を変更します。
		GOTO DATE_LOOP4
	ELSEIF RESULT == 0
		TEQUIP:2 = 0
		TEQUIP:6 = 0
		CALL CHECK_DATE
		PRINTW デートから帰ってきました
		STR:0 = デート終了
		STR:4 = 
		CALL TRAIN_MESSAGE_B
		RETURN 1
	ELSEIF RESULT == 100
		RETURN 0
	ENDIF
;デート中だがデート先を変更できない場合
ELSEIF TEQUIP:2
	TEQUIP:2 = 0
	TFLAG:62 = 0
	CALL CHECK_DATE
	PRINTW デートから帰ってきました
	STR:0 = デート終了
	STR:4 = 
	CALL TRAIN_MESSAGE_B
	RETURN 1
ELSE
	PRINTL デート($5000を消費)
ENDIF

CALL GET_SUCCESS_RATE

B = 90 + A / 10
SIF B > 99
	B = 99
A = RAND:100
IF A <= 9
	;10％で大成功
	TFLAG:18 = 1
ELSEIF A >= B
	;10～1％で失敗
	TFLAG:18 = -1
ELSE
	;残りは成功
	TFLAG:18 = 0
ENDIF
;TFLAG:17でデートの種類を判別　通常：0　パートナーが勝手に決定：1　パートナーの行きたいデート先：2　パートナーが行きたいデート先以外：3
TFLAG:17 = 0

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;-------------------------------------------------
;行き先選択
;-------------------------------------------------
$DATE_LOOP4

;強制行き先決定
IF CFLAG:780 && CFLAG:781
	TEQUIP:2 = CFLAG:780
	CFLAG:780 = 0
	SIF TEQUIP:2 == 26
		TEQUIP:6 = CFLAG:781 - 1
	CFLAG:781 = 0
ELSE
	VARSET LOCAL, 0
	;相手が行きたいデート先を決定しLOCALに保存
	CALL PARTNER_SELECT_DATE
	;本体で設定した各キャラ好みのデート先を1/5の確率で出るように
	SIF RAND:5 == 0 && ABL:親密 >= 10
		CALL PARTNER_DATE_PLACE
	;好みのデート先を1/5の確率で出るように
	SIF CFLAG:777 && RAND:5 == 0 && FLAG:8 != CFLAG:777 && ABL:親密 >= 10
		TEQUIP:2 = CFLAG:777
	CALL PLACE_NAME
	LOCAL = TEQUIP:2
	TEQUIP:2 = 0

	;主導権ポイント計算
	T = TARGET
	CALL GET_INITIATIVE_RATE
	;相手の主導権ポイントが極端に高い＆パートナーに主導権があると、たまにデートの行き先を勝手に決定される
	IF R >= 50
		A = R / 10
		;確率に上限アリ
		SIF A > 15
			A = 15
	ENDIF
	IF A > RAND:100 && TFLAG:45
		;相手が握る
		PRINTFORMW デートに誘うと%CALLNAME:TARGET%の行きたいところに連れて行かれた…
		;大成功
		TFLAG:18 = 1
		TEQUIP:2 = LOCAL
		TFLAG:17 = 1
	ELSE
		IF RAND:10 == 0
			;1/10の確率でパートナーに行きたいデート先あり。選んだデート先が同じ場合ボーナス追加
			;パートナーに主導権あり
			IF TFLAG:45
				PRINTFORMW %CALLNAME:TARGET%は%STR:4%に行きたいようだ…
			ELSE
				PRINTFORMW %CALLNAME:TARGET%は%STR:4%に行きたそうにしている…
			ENDIF
		ELSE
			;パートナーに行きたいデート先がない場合LOCALクリア
			LOCAL = 0
		ENDIF
;デート先選択関数
		CALL GENSOUKYO_DATE
	ENDIF
ENDIF

;何処も選択しなかった場合
IF TEQUIP:2 == 0 || FLAG:8 == 100
	FLAG:8 = 0
	TFLAG:62 = 0
	;パートナーに行きたいデート先がある場合好感度減少
	IF LOCAL
		A = -10 + RAND:10
		CFLAG:2 += A
		PRINTFORML %CALLNAME:TARGET%はデートが中止になり残念そうだ…
		PRINTFORML 好感度上昇:{A}
		SIF ((FLAG:23 & 1p26) == 0) && FLAG:2 != 1
			PRINTFORML 好感度合計:{CFLAG:2}
		WAIT
	ENDIF
	RETURN 0
ENDIF

;パートナーに行きたいデート先があり、パートナーの行きたいデート先だった場合大成功扱い。
;違う場合成功・失敗を再判定し成功もしくは失敗扱い。確率は通常の判定より厳しめ
IF LOCAL
	IF TEQUIP:2 == LOCAL
		;大成功
		TFLAG:18 = 1
		TFLAG:17 = 2
	ELSE
		;成功もしくは失敗
		CALL GET_SUCCESS_RATE
		B = 50 + A / 10
		SIF B > 99
			B = 99
		A = RAND:100
		IF A >= B
			;50～1％で失敗
			TFLAG:18 = -1
		ELSE
			;残りは成功
			TFLAG:18 = 0
		ENDIF
	ENDIF
ENDIF

STR:0 = デート
CALL TRAIN_MESSAGE_B

;-------------------------------------------------
;パラメータ変動
;-------------------------------------------------


IF FLAG:8
	;行き先変更場所によっての体力消費
	IF ((FLAG:8 >= 10 && FLAG:8 <= 23) || (FLAG:8 >= 33 && FLAG:8 <= 36)) && (TEQUIP:2 <= 9 || (TEQUIP:2 >= 24 && TEQUIP:2 <= 32) || TEQUIP:2 == 37)
		LOSEBASE:体力 += 25
	ELSEIF ((TEQUIP:2 >= 10 && TEQUIP:2 <= 23) || (TEQUIP:2 >= 33 && TEQUIP:2 <= 36)) && (FLAG:8 <= 9 || (FLAG:8 >= 24 && FLAG:8 <= 32) || FLAG:8 == 37)
		LOSEBASE:体力 += 25
	ELSE
		LOSEBASE:体力 += 15
	ENDIF
	;恋慕or恋人による体力消費軽減（デートとウフフが長続きする、の再現だとでも脳内補完）
	SIF TALENT:85 || TALENT:153
		LOSEBASE:体力 -= 7
	GOTO DATE_LOOP1
ENDIF


IF TFLAG:62
	LOSEBASE:気力 += 50
ELSE
	LOSEBASE:気力 += 220
	;恋慕・恋人・親愛による気力消費軽減（デートが長続きする、の再現だとでも脳内補完）
	SIF TALENT:85
		LOSEBASE:気力 -= 40
	SIF TALENT:153
		LOSEBASE:気力 -= 50
	SIF TALENT:88
		LOSEBASE:気力 -= 90
ENDIF


;固定で獲得するソース
SOURCE:18 = 100
SOURCE:9 = 50

;ABL:従順をみる
IF ABL:親密 <= 1
	SOURCE:18 += (ABL:親密 * 20)
	SOURCE:9 += (ABL:親密 * 10)
ELSEIF ABL:親密 <= 3
	SOURCE:18 += 200 + (ABL:親密 * 40)
	SOURCE:9 += 100 + (ABL:親密 * 15)
ELSEIF ABL:親密 <= 5
	SOURCE:18 += 500 + (ABL:親密 * 50)
	SOURCE:9 += 200 + (ABL:親密 * 20)
ELSEIF ABL:親密 <= 8
	SOURCE:18 += 700 + (ABL:親密 * 80)
	SOURCE:9 += 300 + (ABL:親密 * 30)
ELSEIF ABL:親密 <= 11
	SOURCE:18 += 1200 + (ABL:親密 * 100)
	SOURCE:9 += 400 + (ABL:親密 * 35)
ELSE
	SOURCE:18 += 2000 + (ABL:親密 * 130)
	SOURCE:9 += 800 + (ABL:親密 * 40)
ENDIF

;好感度をみる
IF CFLAG:2 <= 500
	SOURCE:18 += CFLAG:2
ELSEIF CFLAG:2 <= 5000
	SOURCE:18 += 500 + (CFLAG:2 - 500) / 3
ELSE
	SOURCE:18 += 2000 + (CFLAG:2 - 5000) / 5
ENDIF
SIF SOURCE:18 < 0
	SOURCE:18 = 0

;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:従順をみる
	IF ABL:親密 <= 1
		SOURCE:21 += 300 + (ABL:親密 * 50)
	ELSEIF ABL:親密 <= 3
		SOURCE:21 += 600 + (ABL:親密 * 100)
	ELSEIF ABL:親密 <= 5
		SOURCE:21 += 800 + (ABL:親密 * 120)
	ELSEIF ABL:親密 <= 8
		SOURCE:21 += 1000 + (ABL:親密 * 140)
	ELSEIF ABL:親密 <= 11
		SOURCE:21 += 1200 + (ABL:親密 * 160)
	ELSE
		SOURCE:21 += 1500 + (ABL:親密 * 180)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:サドっ気 == 0
		SOURCE:20 = 600
	ELSEIF ABL:サドっ気 == 1
		SOURCE:20 = 900
	ELSEIF ABL:サドっ気 == 2
		SOURCE:20 = 1200
	ELSEIF ABL:サドっ気 == 3
		SOURCE:20 = 1600
	ELSEIF ABL:サドっ気 == 4
		SOURCE:20 = 2000
	ELSE
		SOURCE:20 = 1000 + (ABL:サドっ気 * 300)
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:6 - 1
TFLAG:30 += A
B = 0

IF TFLAG:18 == -1
	TIMES SOURCE:18 , 0.10
	TIMES SOURCE:9 , 0.50
	TIMES SOURCE:20 , 0.50
	TIMES SOURCE:21 , 0.50
	TFLAG:30 -= 10
	TFLAG:37 -= 5
	B = 1
ELSEIF TFLAG:18 == 0
	TIMES SOURCE:18 , 1.00
	TIMES SOURCE:9 , 1.00
	TIMES SOURCE:20 , 1.00
	TIMES SOURCE:21 , 1.00
	TFLAG:37 += 2 + A * 3
	B = 10
ELSE
	TIMES SOURCE:18 , 2.00
	TIMES SOURCE:9 , 2.00
	TIMES SOURCE:20 , 2.00
	TIMES SOURCE:21 , 2.00
	TFLAG:30 += 2
	TFLAG:37 += 5 + A * 4
	B = 25
ENDIF

;恋慕
IF TALENT:85
	TIMES SOURCE:18 , 1.10
	TIMES SOURCE:9 , 1.30
	TIMES SOURCE:20 , 1.10
	TIMES SOURCE:21 , 1.20
ENDIF
;恋人
IF TALENT:153
	TIMES SOURCE:18 , 1.20
	TIMES SOURCE:9 , 1.50
	TIMES SOURCE:20 , 1.20
	TIMES SOURCE:21 , 1.10
ENDIF
;親愛
IF TALENT:88
	TIMES SOURCE:18 , 1.50
	TIMES SOURCE:9 , 2.00
	TIMES SOURCE:20 , 1.50
	TIMES SOURCE:21 , 1.50
ENDIF

PRINTFORML %CALLNAME:TARGET%とデートに出発しました
MONEY -= 5000


IF B > 0
	PRINTFORML %EXPNAME:94%+{B}
	EXP:デート経験 += B
ENDIF





;-------------------------------------------------
;どこへ行くか
;-------------------------------------------------
$DATE_LOOP1
SIF FLAG:8
	MONEY -= 2000
FLAG:8 = 0
SIF TEQUIP:2 != 26 && TEQUIP:2 != 50
	TEQUIP:6 = 0
;地の文
PRINTFORM %CALLNAME:PLAYER%達は
;地名呼び出し
CALL PLACE_NAME
PRINTS STR:4
;近所
IF TEQUIP:2 <= 9 || (TEQUIP:2 >= 24 && TEQUIP:2 <= 32) || TEQUIP:2 == 37
	PRINTW へと向かいました
;少し遠いところ
ELSEIF (TEQUIP:2 >= 10 && TEQUIP:2 <= 17) || (TEQUIP:2 >= 33 && TEQUIP:2 <= 36)
	PRINTW へと行きました
;映画
ELSEIF TEQUIP:2 >= 18 && TEQUIP:2 <= 22
	PRINTW を観ることにしました
;乗り物
ELSEIF TEQUIP:2 == 23 || TEQUIP:2 == 50
	PRINTW に乗ることにしました
ENDIF
;行き先に応じた補正
;神社の場合
IF TEQUIP:2 == 2
	IF MONEY < 1000
		PRINTFORMW そこで%CALLNAME:PLAYER%達はお賽銭を入れようとしましたが、お金が足りませんでした
	ELSE
		PRINTFORML そこで%CALLNAME:PLAYER%達はお賽銭を入れて、神様に色々と願い事をしました
		MONEY -= 1000
		A = RAND:5
		B = RAND:100

		;好感度増減
		IF A <= 1
			;80%で成功
			IF B < 80
				PRINTFORMW ……何だか%CALLNAME:TARGET%と仲良くなれたような気がした
				CFLAG:2 += 30
			ELSE
				PRINTFORMW ……何だか%CALLNAME:TARGET%と仲が悪くなったような気がした
				CFLAG:2 -= 5
			ENDIF
		;ムード増減
		ELSEIF A <= 3
			;80%で成功
			IF B < 80
				PRINTFORMW ……何だかその場のムードが良くなったような気がした
				TFLAG:37 += 35
			ELSE
				PRINTFORMW ……何だかその場のムードが悪くなったような気がした
				TFLAG:37 -= 5
			ENDIF
		;歓楽増減
		ELSE
			;80%で成功
			IF B < 80
				PRINTFORMW ……%CALLNAME:TARGET%はいつもよりこの状況を楽しんでいるようだ
				TIMES SOURCE:18 , 3.00
			ELSE
				PRINTFORMW ……%CALLNAME:TARGET%はいつもよりこの状況が楽しくないようだ
				TIMES SOURCE:18 , 0.70
			ENDIF
		ENDIF
	ENDIF
;湖
ELSEIF TEQUIP:2 == 3
	;素質発覚イベント
	CALL FIND_BUSTSIZE
	;オムツ解除
	TEQUIP:20 = 0
;花畑
ELSEIF TEQUIP:2 == 4

;温泉
ELSEIF TEQUIP:2 == 7
	;オムツ解除
	TEQUIP:20 = 0
;海
ELSEIF TEQUIP:2 == 14
	;素質発覚イベント
	CALL FIND_BUSTSIZE
	;オムツ解除
	TEQUIP:20 = 0
;映画館
ELSEIF TEQUIP:2 >= 18 && TEQUIP:2 <= 22
	MONEY -= N
	N = 0
;遊覧船
ELSEIF TEQUIP:2 == 23
	MONEY -= 10000
;スポーツ観戦
ELSEIF TEQUIP:2 == 35
	MONEY -= 5000
ENDIF

RETURN 1

;---------------------------------------------------------
;デート場所名とCFLAG:778,779に対応する変数を設定
;---------------------------------------------------------
@DATE_NAME
SAVESTR:6 = 
SAVESTR:7 = 
;キャラ固有のデート先取得(1)
;東北邸
IF NO:TARGET == 5 || NO:TARGET == 10 || NO:TARGET == 12
	SAVESTR:7 = 東北邸
ELSE
;そうじゃなければおうち
	SAVESTR:7 = %CALLNAME:TARGET%の家
ENDIF
IF CFLAG:782 & 1
	CALL DATE_DATE
	IF STR:27 != ""
		SAVESTR:7 = %STR:27%
	ENDIF
ENDIF

;キャラ固有のデート先取得(2)
O = 0
;セイカさんは精華町
IF NO:TARGET == 9
	SAVESTR:6 = 精華町
	O = 1
ENDIF

IF CFLAG:782 & 2
	CALL DATE_DATE
	IF STR:26 != ""
		SAVESTR:6 = %STR:26%
	ENDIF
	O = 1
ENDIF

TSTR:0 = 街
TSTR:1 = 神社
TSTR:2 = 湖
TSTR:3 = 花畑
TSTR:4 = 肝試し
TSTR:5 = カラオケ
TSTR:6 = 温泉
TSTR:7 = 図書館
;TSTR:8 = 白玉楼
;TSTR:9 = 外界
;TSTR:10 = 
;TSTR:11 = 月の都
;TSTR:12 = 妖怪の山
;TSTR:13 = 有頂天
;TSTR:14 = 地霊殿
;TSTR:15 = 間欠泉地下センター
;TSTR:16 = 魔法の森
;TSTR:17 = 命蓮寺
;TSTR:18 = 魔界
TSTR:19 = %SAVESTR:7%
;IF R
;	TSTR:20 = 聖輦船
;ELSE
;	TSTR:20 = 行き先の分からない船
;ENDIF

TSTR:30 = 遊園地
TSTR:31 = 水族館
TSTR:32 = 動物園
TSTR:33 = 繁華街
TSTR:34 = 海
TSTR:35 = ゲームセンター
TSTR:36 = カジノ
TSTR:37 = 博物館
TSTR:38 = 映画館
TSTR:39 = 遊覧船
TSTR:40 = 牧場
TSTR:41 = 教会
TSTR:42 = スポーツ観戦
TSTR:43 = %SAVESTR:6%
TSTR:44 = やっぱり戻る

TSTR:50 = コメディ
TSTR:51 = アクション
TSTR:52 = ホラー
TSTR:53 = 恋愛系
TSTR:54 = ポルノ

T:0 = 1
T:1 = 2
T:2 = 4
T:3 = 8
T:4 = 16
T:5 = 32
T:6 = 64
T:7 = 128
T:8 = 256
T:9 = 512
T:10 = 1024
T:11 = 2048
T:12 = 4096
T:13 = 8192
T:14 = 16384
T:15 = 32768
T:16 = 65536
T:17 = 131072
T:18 = 262144
T:19 = 524288
T:20 = 1048576

;---------------------------------------------------------
;デート先表示・選択
;---------------------------------------------------------
@GENSOUKYO_DATE


;映画の金額処理（通常1人$1800）
N = 0
;1日は映画の日で1人$1000
IF DAY:1 == 1
	N = 2000
ELSE
	;PLAYERがオトコの場合
	IF TALENT:PLAYER:122
		;火曜日はメンズディ（紳士$1000）、木曜日はレディースディ（淑女$1000）
		IF DAY:5 == 1 || DAY:5 == 3
			N = 2800
		ELSE
		;それ以外の日
			N = 3600
		ENDIF
	ELSE
	;PLAYERがオトコじゃない場合
		IF DAY:5 == 3
		;木曜日はレディースディ（淑女$1000）
			N = 2000
		ELSE
		;それ以外の日
			N = 3600
		ENDIF
	ENDIF
ENDIF

;外界に行けるかどうかの判定。このバリアントは問答無用で行ける
K = 1
J = 0
H = 0
L = 0
M = 0
R = 0
;禁断の知識があればS=1
S = 0



REPEAT CHARANUM
	;別れているキャラはチェックしない
	SIF CFLAG:COUNT:21 == 1
		CONTINUE
;ここにはキャラを所有していればデート先選択可能フラグを立てる処理が入っていたが、このバリアントでは不必要

REND

;禁断の知識を持っているか
SIF TALENT:MASTER:125 || TALENT:TARGET:125
	S = 1
IF ASSI > 0
	SIF TALENT:ASSI:125
		S = 1
ENDIF

;調合でデート先を表示する場合、制限は解除された状態
IF SELECTCOM == 310
	K = 1
	J = 1
	H = 1
	L = 1
	M = 1
	S = 1
ENDIF

;表示処理の前にデート場所名とCFLAG:778,779に対応する変数を設定
CALL DATE_NAME

;綺麗に並べるためのフラグ
D:1 = 0
;表示処理
PRINTL どこに向かいますか？

;表示判定
REPEAT 43
	;口上側による行き先制限判定。制限されていれば表示しない
	SIF CFLAG:778 & (T:COUNT)
		CONTINUE
	
	;TSTR:COUNTが空白の場合表示しない（主にキャラ固有デート先に関係）
	SIF TSTR:COUNT == ""
		CONTINUE

	;条件のあるデート先はここで判定（IF文が行ける場合の条件で条件を満たしていない場合は表示しない）
	IF (COUNT == 6 || COUNT == 9 || COUNT == 19 || COUNT == 36 || COUNT == 38 || COUNT == 39 || COUNT == 42 || COUNT == 43) && SELECTCOM == 323

		;温泉
		IF COUNT == 6 && (TALENT:153 || (ABL:親密 >= 10 && ((TALENT:122 == 0 && TALENT:MASTER:122 == 0) || (TALENT:122 && TALENT:MASTER:122))))
		
		
		;キャラ固有デート先(1)
		;東北邸の場合はクリア後でないとだめ
		ELSEIF COUNT == 19 && ABL:親密 >= 10 && ( GET_TOHOKUABLE() == 1 || (NO:TARGET != 5 && NO:TARGET != 10 && NO:TARGET != 12))
		
		;カジノ
		ELSEIF COUNT == 36 && MONEY >= 5500
		
		;映画館
		ELSEIF COUNT == 38 && (MONEY > 5000 + N || (FLAG:8 && MONEY > 2000 + N))
		
		;遊覧船
		ELSEIF COUNT == 39 && (MONEY > 15000 || (FLAG:8 && MONEY > 12000))
		
		;スポーツ観戦
		ELSEIF COUNT == 42 && (MONEY > 10000 || ( FLAG:8 && MONEY > 7000))
		
		;キャラ固有デート先(2)
		ELSEIF COUNT == 43 && (ABL:親密 >= 10)
		
		;条件を満たしていないと表示しない
		ELSE
			CONTINUE
		ENDIF
	ENDIF
	

;デート先選択肢出力部分

	;名前が長いデート名は特殊
	IF COUNT == 1
		PRINTFORM  [{COUNT, 2}] %TSTR:COUNT%
		SIF SELECTCOM == 323
			PRINTFORM （お賽銭$1000が更に必要です）
		D:1 += 1
	ELSEIF COUNT == 38
		PRINTFORM  [{COUNT, 2}] %TSTR:38%(${N}が更に必要です)    
		D:1 += 2
	ELSEIF COUNT == 39
		PRINTFORM  [{COUNT, 2}] %TSTR:39%($10000が更に必要です)  
		D:1 += 2
	ELSEIF COUNT == 42
		PRINTFORM  [{COUNT, 2}] %TSTR:42%($5000が更に必要です)   
		D:1 += 2
	ELSEIF COUNT == 35 || COUNT == 41
		PRINTFORM  [{COUNT, 2}] %TSTR:COUNT, 30, LEFT%
		D:1 += 1
	ELSE
		PRINTFORM  [{COUNT, 2}] %TSTR:COUNT, 12, LEFT%
	ENDIF
	
	;改行処理
	D:1 += 1
	IF D:1 > 2
		PRINTL 
		D:1 = 0
	ENDIF
REND

PRINTL [100] やっぱりやめる

$INPUT_LOOP0
INPUT
IF RESULT != 100 && (RESULT < 0 || RESULT > 50)
	GOTO INPUT_LOOP0
ELSEIF RESULT == 100
	;やっぱりやめる
	IF FLAG:8
		PRINTW 場所を変えるのを止めました
		FLAG:8 = 100
		RETURN 0
	ELSE
		PRINTW デートに行くのを止めました
		RETURN 0
	ENDIF
ELSEIF (RESULT + 1 == FLAG:8 && (RESULT >= 0 && RESULT <= 8)) || (RESULT + 14 == FLAG:8 && (RESULT >= 10 && RESULT <= 18)) || (FLAG:8 == 37 && RESULT == 19) || (FLAG:8 == 99 && (RESULT >= 30 && RESULT <= 37))
	PRINTL 同じ場所です
	GOTO INPUT_LOOP0
ELSEIF CFLAG:778 & (T:RESULT)
	;口上側による行き先制限
	GOTO INPUT_LOOP0
ELSEIF TSTR:RESULT == ""
	;TSTRが空白の場合
	GOTO INPUT_LOOP0
ELSEIF (RESULT == 6 || RESULT == 9 || RESULT == 19 || RESULT == 36 || RESULT == 38 || RESULT == 39 || RESULT == 42 || RESULT == 43) && SELECTCOM == 323
	;条件のあるデート先はここで判定
	;温泉
	IF RESULT == 6 && (TALENT:153 || (ABL:親密 >= 10 && ((TALENT:122 == 0 && TALENT:MASTER:122 == 0) || (TALENT:122 && TALENT:MASTER:122))))
		TEQUIP:2 = 7
	ELSEIF RESULT == 6 
		PRINTFORML %CALLNAME:TARGET%は一緒に温泉に入るのを恥ずかしがっているようです
		GOTO INPUT_LOOP0
	;キャラ固有デート先(1)
	ELSEIF RESULT == 19 && ABL:親密 >= 10 && ( GET_TOHOKUABLE() == 1 || (NO:TARGET != 5 && NO:TARGET != 10 && NO:TARGET != 12))
			TEQUIP:2 = 37
			
	ELSEIF RESULT == 19 && ( GET_TOHOKUABLE() == 1 || (NO:TARGET != 5 && NO:TARGET != 10 && NO:TARGET != 12))
		PRINTFORML %CALLNAME:TARGET%の親密が不足しています
		GOTO INPUT_LOOP0
	ELSEIF RESULT == 19 && ( GET_TOHOKUABLE() == 0 && !(NO:TARGET != 5 && NO:TARGET != 10 && NO:TARGET != 12))
		PRINTFORML ストーリーを進めてください
		GOTO INPUT_LOOP0
	;カジノ
	ELSEIF RESULT == 36 && MONEY >= 5500
		TEQUIP:2 = 16
	ELSEIF RESULT == 36 
		PRINTL お金が足りません
		GOTO INPUT_LOOP0
	;映画館
	ELSEIF RESULT == 38 && (MONEY > 5000 + N || (FLAG:8 && MONEY > 2000 + N))
		CALL MOVIE_DATE
		RETURN 1
	ELSEIF RESULT == 38 
		PRINTL お金が足りません
		GOTO INPUT_LOOP0
	;遊覧船
	ELSEIF RESULT == 39 && (MONEY > 15000 || (FLAG:8 && MONEY > 12000))
		TEQUIP:2 = 23
	ELSEIF RESULT == 39
		PRINTL お金が足りません
		GOTO INPUT_LOOP0
	;スポーツ観戦
	ELSEIF RESULT == 42 && (MONEY > 10000 || ( FLAG:8 && MONEY > 7000))
		TEQUIP:2 = 35
	ELSEIF RESULT == 42
		PRINTL お金が足りません
		GOTO INPUT_LOOP0
	;キャラ固有デート先(2)
	ELSEIF RESULT == 43 && ABL:親密 >= 10
		TEQUIP:2 = 36
	ELSEIF RESULT == 43
		PRINTFORML %CALLNAME:TARGET%の親密が不足しています
		GOTO INPUT_LOOP0
	;条件を満たしていないとGOTO INPUT_LOOP0
	ELSE
		GOTO INPUT_LOOP0
	ENDIF
ENDIF

IF 0
ELSEIF RESULT == 100
	;やっぱりやめる
	RETURN 0
ELSEIF RESULT == 0
	;街
	TEQUIP:2 = RESULT + 1
ELSEIF (RESULT >= 1 && RESULT <= 7)
	;神社～図書館
	TEQUIP:2 = RESULT + 1
ELSEIF (RESULT >= 30 && RESULT <= 37)
	;遊園地～博物館
	TEQUIP:2 = RESULT - 20
ELSEIF RESULT == 38
	;映画館
	CALL MOVIE_DATE
	RETURN 1
ELSEIF RESULT == 39
	;遊覧船
	TEQUIP:2 = 23
ELSEIF (RESULT >= 40 && RESULT <= 43)
;牧場、教会、スポーツ観戦
	TEQUIP:2 = RESULT - 7
	ELSEIF RESULT == 19 && ABL:親密 >= 10
ELSE
	GOTO INPUT_LOOP0
ENDIF
L = 0
M = 0

RETURN 1

;---------------------------------------------------------
;外界
;---------------------------------------------------------
@GAIKAI_DATE

;使わないよねたぶん
RETURN 0


;---------------------------------------------------------
;映画
;---------------------------------------------------------
@MOVIE_DATE
;綺麗に並べるために
D:1 = 0
PRINTFORMW %CALLNAME:PLAYER%達は映画を見ることにしました
PRINTL どの映画を見ますか？
REPEAT 5
	;口上側による行き先制限判定。制限されていれば表示しない
	SIF CFLAG:779 & (T:COUNT+8)
		CONTINUE
	
	;条件のあるデート先はここで判定
	;ポルノ
	IF COUNT == 4 && TALENT:153 == 0 && SELECTCOM == 323
		CONTINUE
	ENDIF
	
	PRINTFORM  [{COUNT, 2}] %TSTR:(COUNT+50), 12, LEFT%
	
	;改行処理
	D:1 += 1
	IF D:1 > 2
		PRINTL 
		D:1 = 0
	ENDIF
REND
PRINTL [100] 映画をやめる
$INPUT_LOOP5
INPUT
IF RESULT != 100 && (RESULT < 0 || RESULT > 4)
	GOTO INPUT_LOOP5
ELSEIF RESULT == 100
	PRINT 映画を見るのを止め、
	CALL GENSOUKYO_DATE
ELSEIF RESULT + 18 == FLAG:8
	PRINTL 同じ映画です
	GOTO INPUT_LOOP5
ELSEIF CFLAG:779 & (T:(RESULT+8))
	;口上側による行き先制限判定
	GOTO INPUT_LOOP5
ELSEIF RESULT == 4 && TALENT:153 == 0 && SELECTCOM == 323
	GOTO INPUT_LOOP5
ELSEIF (RESULT >= 0 || RESULT <= 4)
	TEQUIP:2 = RESULT + 18
ELSE
	PRINT 映画を見るのを止め、
	CALL GENSOUKYO_DATE
ENDIF

RETURN 1


@SEIRENSEN_DATE

;使わない
RETURN 0

CALL GENSOUKYO_DATE

RETURN 1

@PLACE_NAME
;デート場所をSTR:4に保存してみる
IF TEQUIP:2 == 1
	STR:4 = 街
ELSEIF TEQUIP:2 == 2
	STR:4 = 神社
ELSEIF TEQUIP:2 == 3
	STR:4 = 湖
ELSEIF TEQUIP:2 == 4
	STR:4 = 花畑
ELSEIF TEQUIP:2 == 5
	STR:4 = 肝試し
ELSEIF TEQUIP:2 == 6
	STR:4 = カラオケ
ELSEIF TEQUIP:2 == 7
	IF (TALENT:122 && TALENT:PLAYER:122 == 0) || (TALENT:122 == 0 && TALENT:PLAYER:122)
		STR:4 = 混浴の温泉
	ELSE
		STR:4 = 温泉
	ENDIF
ELSEIF TEQUIP:2 == 8
	STR:4 = 図書館
ELSEIF TEQUIP:2 == 10
	STR:4 = 遊園地
ELSEIF TEQUIP:2 == 11
	STR:4 = 水族館
ELSEIF TEQUIP:2 == 12
	STR:4 = 動物園
ELSEIF TEQUIP:2 == 13
	STR:4 = 繁華街
ELSEIF TEQUIP:2 == 14
	STR:4 = 海
ELSEIF TEQUIP:2 == 15
	STR:4 = ゲームセンター
ELSEIF TEQUIP:2 == 16
	STR:4 = カジノ
ELSEIF TEQUIP:2 == 17
	STR:4 = 博物館
ELSEIF TEQUIP:2 == 18
	STR:4 = コメディ映画
ELSEIF TEQUIP:2 == 19
	STR:4 = アクション映画
ELSEIF TEQUIP:2 == 20
	STR:4 = ホラー映画
ELSEIF TEQUIP:2 == 21
	STR:4 = 恋愛映画
ELSEIF TEQUIP:2 == 22
	STR:4 = ポルノ映画
ELSEIF TEQUIP:2 == 23
	STR:4 = 遊覧船
ELSEIF TEQUIP:2 == 33
	STR:4 = 牧場
ELSEIF TEQUIP:2 == 34
	STR:4 = 教会
ELSEIF TEQUIP:2 == 35
	STR:4 = スポーツ観戦
ELSEIF TEQUIP:2 == 36
	;キャラ固有デート先(2)
	STR:4 = %SAVESTR:6%
ELSEIF TEQUIP:2 == 37
	;キャラ固有デート先(1)
	STR:4 = %SAVESTR:7%
ENDIF


@PARTNER_SELECT_DATE
LOCAL = RAND:14

IF LOCAL == 0
	;街
	TEQUIP:2 = 1
ELSEIF LOCAL == 1
	;神社
	TEQUIP:2 = 2
ELSEIF LOCAL == 2
	;湖
	TEQUIP:2 = 3
ELSEIF LOCAL == 3
	;花畑
	TEQUIP:2 = 4
ELSEIF LOCAL == 4
	;肝試し
	TEQUIP:2 = 5
ELSEIF LOCAL == 5
	;カラオケ
	TEQUIP:2 = 6
ELSEIF LOCAL == 6
	;図書館
	TEQUIP:2 = 8
ELSEIF LOCAL == 7
	;遊園地
	TEQUIP:2 = 10
ELSEIF LOCAL == 8
	;水族館
	TEQUIP:2 = 11
ELSEIF LOCAL == 9
	;動物園
	TEQUIP:2 = 12
ELSEIF LOCAL == 10
	;繁華街
	TEQUIP:2 = 13
ELSEIF LOCAL == 11
	;海
	TEQUIP:2 = 14
ELSEIF LOCAL == 12
	;ゲームセンター
	TEQUIP:2 = 15
ELSEIF LOCAL == 13
	;カジノ
	TEQUIP:2 = 16
ELSE
	;街
	TEQUIP:2 = 1
ENDIF


RETURN 1

@EQUIP_COM2
;破壊されても良いように地名呼び出し
CALL PLACE_NAME
;デート中
PRINTFORML ＜%STR:4%でデート中＞

LOSEBASE:気力 += 35

;恋慕・恋人・親愛による気力消費軽減（デートが長続きする、の再現だとでも脳内補完）
SIF TALENT:85
	LOSEBASE:気力 -= 5
SIF TALENT:153
	LOSEBASE:気力 -= 5
SIF TALENT:88
	LOSEBASE:気力 -= 10

B = 0
C = 0
D = 0
E = 0


;固定で獲得するソース
B = 100
C = 50

;ABL:従順をみる
IF ABL:親密 <= 1
	B += (ABL:親密 * 20)
	C += (ABL:親密 * 10)
ELSEIF ABL:親密 <= 3
	B += 200 + (ABL:親密 * 40)
	C += 100 + (ABL:親密 * 15)
ELSEIF ABL:親密 <= 5
	B += 500 + (ABL:親密 * 50)
	C += 200 + (ABL:親密 * 20)
ELSEIF ABL:親密 <= 8
	B += 700 + (ABL:親密 * 80)
	C += 300 + (ABL:親密 * 30)
ELSEIF ABL:親密 <= 11
	B += 1200 + (ABL:親密 * 100)
	C += 400 + (ABL:親密 * 35)
ELSE
	B += 2000 + (ABL:親密 * 130)
	C += 800 + (ABL:親密 * 40)
ENDIF

;好感度をみる
IF CFLAG:2 <= 500
	B += CFLAG:2
ELSEIF CFLAG:2 <= 5000
	B += 500 + (CFLAG:2 - 500) / 3
ELSE
	B += 2000 + (CFLAG:2 - 5000) / 5
ENDIF

;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:従順をみる
	IF ABL:親密 <= 1
		E += 300 + (ABL:親密 * 50)
	ELSEIF ABL:親密 <= 3
		E += 600 + (ABL:親密 * 100)
	ELSEIF ABL:親密 <= 5
		E += 800 + (ABL:親密 * 120)
	ELSEIF ABL:親密 <= 8
		E += 1000 + (ABL:親密 * 140)
	ELSEIF ABL:親密 <= 11
		E += 1200 + (ABL:親密 * 160)
	ELSE
		E += 1500 + (ABL:親密 * 180)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:サドっ気 == 0
		D = 600
	ELSEIF ABL:サドっ気 == 1
		D = 900
	ELSEIF ABL:サドっ気 == 2
		D = 1200
	ELSEIF ABL:サドっ気 == 3
		D = 1600
	ELSEIF ABL:サドっ気 == 4
		D = 2000
	ELSE
		D = 1000 + (ABL:サドっ気 * 300)
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:3 + 1
;10%で好感度-3
SIF RAND:10 == 0
	A = -3
TFLAG:30 += A
F = 0

IF A == -3
	TIMES B , 0.10
	TIMES C , 0.50
	TIMES D , 0.50
	TIMES E , 0.50
	TFLAG:37 -= 1
ELSEIF A < 3
	TIMES B , 1.00
	TIMES C , 1.00
	TIMES D , 1.00
	TIMES E , 1.00
	TFLAG:37 += 2 + A * 2
	F = 1
ELSE
	TIMES B , 1.50
	TIMES C , 1.50
	TIMES D , 1.50
	TIMES E , 1.50
	TFLAG:30 += 2
	TFLAG:37 += 5 + A * 2
	F = 2
ENDIF

;恋慕
IF TALENT:85
	TIMES B , 1.10
	TIMES C , 1.30
	TIMES D , 1.10
	TIMES E , 1.20
ENDIF
;恋人
IF TALENT:153
	TIMES B , 1.20
	TIMES C , 1.50
	TIMES D , 1.20
	TIMES E , 1.10
ENDIF
;親愛
IF TALENT:88
	TIMES B , 1.50
	TIMES C , 2.00
	TIMES D , 1.50
	TIMES E , 1.50
ENDIF

;実行中の効果は、実行時の20%
TIMES B , 0.20
TIMES C , 0.20
TIMES D , 0.20
TIMES E , 0.20

;行き先に応じた補正

;街
IF TEQUIP:2 == 1
	;歓楽のソースに+100～400
	SOURCE:18 += RAND:301 + 100
;湖
ELSEIF TEQUIP:2 == 3
	;露出のソースに+200～500
	SOURCE:12 += RAND:301 + 200
	;夏は更にUP
	IF DAY:2 == 1
		SOURCE:12 += RAND:201
	;冬は更にDOWNするかわりに接触のソースに+0～10
	ELSEIF DAY:2 == 3
		SOURCE:12 -= RAND:201
		SOURCE:9 += RAND:11
	ENDIF
;花畑
ELSEIF TEQUIP:2 == 4
	;ムードに更に+1～3
	TFLAG:37 += RAND:3 + 1
;肝試し
ELSEIF TEQUIP:2 == 5
	;不安のソースに+200～500
	SOURCE:23 += RAND:301 + 200
	;夏は更にUP
	IF DAY:2 == 1
		SOURCE:23 += 100
	ENDIF
	;素質発覚イベント
	CALL FIND_BUSTSIZE
;カラオケ
ELSEIF TEQUIP:2 == 6
	;ネチョ中は、歌わない代わりに狭いところなのでよくくっつく
	IF TFLAG:44
		SOURCE:9 += RAND:301 + 200
	ELSE
		IF ABL:歌唱技能 >= 5
			PRINTFORML %EXPNAME:93%＋{ABL:歌唱技能 / 5 +1}
			EXP:歌唱経験 += ABL:歌唱技能 / 5 + 1
		ELSE
			;歌唱経験に＋1
			PRINTFORML %EXPNAME:93%＋1
			EXP:歌唱経験 += 1
		ENDIF
	ENDIF
;温泉
ELSEIF TEQUIP:2 == 7
	;どちらかと言うとこのままウフフ突入する時向け
	;毎ターン汚れリセット
	;合意持ちだと露出に+500～+1000（一緒に入るよ！）

	STAIN:0 = 0
	STAIN:1 = 0
	STAIN:2 = 2
	STAIN:3 = 1
	STAIN:4 = 8
	STAIN:5 = 0

	STAIN:PLAYER:0 = 0
	STAIN:PLAYER:1 = 0
	STAIN:PLAYER:2 = 2
	STAIN:PLAYER:3 = 1
	STAIN:PLAYER:4 = 8
	STAIN:PLAYER:5 = 0

	IF TALENT:152
		SOURCE:12 += RAND:501 + 500
		;冬は更にUP
		IF DAY:2 == 3
			SOURCE:12 += RAND:501 + 500
		ENDIF
	ENDIF

;図書館
ELSEIF TEQUIP:2 == 8
	;習得増加
	UP:7 += 200
	REPEAT ABL:技巧 + 1
		UP:7 += RAND:101
	REND
	;秋は更にUP
	IF DAY:2 == 2
		REPEAT ABL:技巧 + 1
			UP:7 += RAND:51
		REND
	ENDIF
;遊園地
ELSEIF TEQUIP:2 == 10
	;歓楽のソースに+0～200
	SOURCE:18 += RAND:201
	;接触のソースに+0～400
	SOURCE:9 += RAND:401
;水族館
ELSEIF TEQUIP:2 == 11
	;歓楽のソースに+0～200
	SOURCE:18 += RAND:201
	;ムードに更に+0～2
	TFLAG:37 += RAND:5 / 2
;動物園
ELSEIF TEQUIP:2 == 12
	;歓楽のソースに+0～200
	SOURCE:18 += RAND:201
	;不安のソースに+0～300
	SOURCE:23 += RAND:301
;繁華街
ELSEIF TEQUIP:2 == 13
	;好感度に+1～5
	TFLAG:30 += RAND:5 + 1
;海
ELSEIF TEQUIP:2 == 14
	;歓楽のソースに+0～200
	SOURCE:18 += RAND:201
	;露出のソースに+100～400
	SOURCE:12 += RAND:301 + 100
	;夏は更にUP
	IF DAY:2 == 1
		SOURCE:18 += RAND:201
		SOURCE:12 += RAND:201
	;冬は更にDOWNするかわりに接触のソースに+0～10
	ELSEIF DAY:2 == 3
		SOURCE:12 -= RAND:101
		SOURCE:9 += RAND:11
	ENDIF
;ゲームセンター
ELSEIF TEQUIP:2 == 15
	;歓楽のソースに-300～+900
	REPEAT 2
		SOURCE:18 += RAND:601 - 150
	REND
;カジノ
ELSEIF TEQUIP:2 == 16
	;習得増加
	UP:7 += 100
	REPEAT ABL:技巧 + 1
		UP:7 += RAND:51
	REND
	IF TFLAG:44 == 0
		;賭博選択時以外は自動変動
		IF SELECTCOM != 390
			;念のため初期化
			G = 0
			;現金変動
			IF RAND:100 < 33
				;33%でお金が+1000～1990
				G = RAND:991 + 1000
			ELSEIF RAND:67 < 66
				;66%でお金が-500～1000
				G -= 500
				REPEAT 3
					G += RAND:501
				REND
			ELSE
				REPEAT 10
					G += RAND:910
				REND
				;1%でお金が+10000～20000
				G += RAND:911 + 10000
			ENDIF
			IF G < 0
				G = G * -1
				PRINTFORML ＜ギャンブル中＞　　${G}負けました…
				MONEY -= G
			ELSE
				PRINTFORML ＜ギャンブル中＞　　${G}勝ちました！
				MONEY += G
			ENDIF
		ENDIF
	ENDIF
;博物館
ELSEIF TEQUIP:2 == 17
	;習得増加
	UP:7 += 100
	REPEAT ABL:技巧 + 1
		UP:7 += RAND:51
	REND
	;秋は更にUP
	IF DAY:2 == 2
		REPEAT ABL:技巧 + 1
			UP:7 += RAND:26
		REND
	ENDIF
	;接触のソースに+0～10
	SOURCE:9 += RAND:11
;映画（コメディ）
ELSEIF TEQUIP:2 == 18
	;歓楽のソースに+150～450
	SOURCE:18 += RAND:301 + 150
	;接触のソースに+0～50
	SOURCE:9 += RAND:51
;映画（アクション）
ELSEIF TEQUIP:2 == 19
	;歓楽のソースに+200～700
	SOURCE:18 += RAND:501 + 200
	;接触のソースに+0～50
	SOURCE:9 += RAND:51
;映画（ホラー）
ELSEIF TEQUIP:2 == 20
	;不安のソースに+200～600
	SOURCE:23 += RAND:401 + 200
	;接触のソースに+5～55
	SOURCE:9 += RAND:51 + 5
;映画（恋愛）
ELSEIF TEQUIP:2 == 21
	;ムードに更に+3～7
	TFLAG:37 += RAND:5 + 3
	;接触のソースに+10～60
	SOURCE:9 += RAND:51 + 10
;映画（ポルノ）
ELSEIF TEQUIP:2 == 22
	;ネチョしても追い出されない（ポルノだしね！）
	IF TFLAG:44
		;露出のソースに+200～1100
		SOURCE:12 += RAND:901 + 200
		;接触のソースに+200～700
		SOURCE:9 += RAND:501 + 200
	ELSE
		;ムードに更に+1～3
		TFLAG:37 += RAND:3 + 1
		;露出のソースに+200～600
		SOURCE:12 += RAND:401 + 200
		;接触のソースに+100～500
		SOURCE:9 += RAND:401 + 100
	ENDIF
;遊覧船
ELSEIF TEQUIP:2 == 23
	;ムードに更に+1～3
	TFLAG:37 += RAND:3 + 1
	;歓楽のソースに+0～200
	SOURCE:18 += RAND:201
	;接触のソースに+10～110
	SOURCE:9 += RAND:101 + 10
	;夜は更にムード+3
	SIF TIME == 1
		TFLAG:37 += 3
;彼岸
ELSEIF TEQUIP:2 == 24
	;不安のソースに+200～500
	SOURCE:23 += RAND:301 + 200
	;接触のソースに+10～210
	SOURCE:9 += RAND:201 + 10
	;お彼岸はDOWN（幽霊いないよ！）
	IF (DAY:2 == 0 || DAY:2 == 2) && ( DAY:1 > 19 && DAY:1 < 27 )
		SOURCE:23 -= 100
	ENDIF
;月の都
ELSEIF TEQUIP:2 == 25
	;ムードに更に+1～6
	TFLAG:37 += RAND:6 + 1
	;不安のソースに+2～32
	SOURCE:23 += RAND:31 + 2
	;接触のソースに+10～310
	SOURCE:9 += RAND:301 + 10
;妖怪の山
ELSEIF TEQUIP:2 == 26
	;歓楽のソースに+0～200
	SOURCE:18 += RAND:201
	;不安のソースに+0～300
	SOURCE:23 += RAND:301
;有頂天
ELSEIF TEQUIP:2 == 27
	;ムードに更に+1～3
	TFLAG:37 += RAND:3 + 1
	;露出のソースに+200～500
	SOURCE:12 += RAND:301 + 200
;地霊殿
ELSEIF TEQUIP:2 == 28
	;歓楽のソースに+0～20
	SOURCE:18 += RAND:21
	;不安のソースに+0～300
	SOURCE:23 += RAND:301
	;冬は歓楽UP
	IF DAY:2 == 3
		SOURCE:18 += 100
	ENDIF
;間欠泉地下センター
ELSEIF TEQUIP:2 == 29
	;不安のソースに+0～20
	SOURCE:23 += RAND:21
;魔法の森
ELSEIF TEQUIP:2 == 30
	;不安のソースに+0～20
	SOURCE:23 += RAND:21
	;夜は更にUP
	SIF TIME
		SOURCE:23 += RAND:201 + 10
;命蓮寺
ELSEIF TEQUIP:2 == 31
	;習得増加
	UP:7 += 50
	REPEAT ABL:技巧 + 1
		UP:7 += RAND:51
	REND
;魔界
ELSEIF TEQUIP:2 == 32
	;不安のソースに+0～300
	SOURCE:23 += RAND:301
;牧場
ELSEIF TEQUIP:2 == 33
	;歓楽のソースに+50～250
	SOURCE:18 += RAND:201 + 50
	;不安のソースに+100～400
	SOURCE:23 += RAND:301 + 100
;教会
ELSEIF TEQUIP:2 == 34
	;習得増加(やぶ牧師だから命蓮寺より低い？)
	UP:7 += 30
	REPEAT ABL:技巧 + 1
		UP:7 += RAND:11
	REND
;スポーツ観戦
ELSEIF TEQUIP:2 == 35
	;体力気力消費(応援とか雰囲気で消費するよ！)
	LOSEBASE:体力 += RAND:11 + 5
	LOSEBASE:気力 += RAND:20 + 10
	;歓楽のソースに+400～800
	SOURCE:18 += RAND:401 + 400
;固有デート場所
ELSEIF TEQUIP:2 == 36
	;基本的に故郷にいくことが多いので情愛と歓楽に+
	;情愛のソースに+200～500
	SOURCE:3 += RAND:301 + 200
	;歓楽のソースに+50～150
	SOURCE:18 += RAND:101 + 50
;口上デート先
ELSEIF TEQUIP:2 == 37
	;好きなところに行くので情愛と歓楽に+
	;情愛のソースに+200～500
	SOURCE:3 += RAND:301 + 200
	;歓楽のソースに+50～150
	SOURCE:18 += RAND:101 + 50
;聖輦船
ELSEIF TEQUIP:2 == 50
	;ムードに更に+1～3
	TFLAG:37 += RAND:3 + 1
	;歓楽のソースに+0～100
	SOURCE:18 += RAND:101
	;接触のソースに+10～80
	SOURCE:9 += RAND:71 + 10
ENDIF

;これなに？
SIF TFLAG:62 >= 2 && TFLAG:44 == 0
	TFLAG:62 += 1

;これもなに？
$DATE_LOOP6
R = 0
IF TFLAG:62 >= 5
	R = RAND:19 + 1
	IF R <= 9
		;温泉は恋人以上じゃないと行けない
		SIF R == 7 && (TALENT:153 == 0 || (ABL:親密 < 10 && ((TALENT:122 == 0 && TALENT:PLAYER:122) || (TALENT:122 && TALENT:PLAYER:122 == 0))))
			GOTO DATE_LOOP6
		TEQUIP:2 = R
	ELSEIF R >= 10 && R <= 18
		;月の都は変身魔法とかがないと行けない
		SIF R == 11 && H:1 == 0
			GOTO DATE_LOOP6
		TEQUIP:2 = R + 14
	ELSEIF R == 19 && CFLAG:782 & 1
		TEQUIP:2 = R + 18
	ELSE
		GOTO DATE_LOOP6
	ENDIF
	;地名呼び出し
	CALL PLACE_NAME
	PRINTFORMW %STR:4%に到着しました
	;湖、温泉、海ならオムツ解除
	IF TEQUIP:2 == 3 || TEQUIP:2 == 7 || TEQUIP:2 == 14
		TEQUIP:20 = 0
	ENDIF
	TFLAG:62 = 0
ENDIF

SOURCE:18 += B
SIF SOURCE:18 < 0
	SOURCE:18 = 0
SOURCE:9 += C
SOURCE:20 += D
SOURCE:21 += E


IF F > 0
	PRINTFORML %EXPNAME:94%+{F}
	EXP:デート経験 += F
ENDIF

RETURN 1

;------------------------------------
;デート終了時のキスや告白の判定
;------------------------------------
@CHECK_DATE

TFLAG:33 = 1

;告白判定
;最低でも従順5、好感度1000が必要
IF ABL:親密 >= 5 && CFLAG:2 >= 1000 && TALENT:153 == 0 && ASSIPLAY == 0
	A = 0
	;目標難易度は190
	V = 190

	;純愛系コマンド実行値取得
	CALL GET_JUN_VALUE

	;好感度
	A += CFLAG:2 / 50

	;キス未経験
	SIF TALENT:151
		A -= 10

	;恋慕
	SIF TALENT:85
		A += 30

	;臆病
	SIF TALENT:10
		A -= 10

	;恥じらい
	SIF TALENT:35
		A -= 10

	;恥薄い
	SIF TALENT:36
		A += 5

	;悲観的
	SIF TALENT:26
		A -= 5

	;一線越えない
	SIF TALENT:27
		A -= 15

	;Aの値を退避
	Z = A
	;主導権基準値ボーナス
	T = TARGET
	CALL GET_INITIATIVE_RATE
	;退避させた値を戻す
	A = Z
	;従順なキャラには＋ボーナス、強気なキャラには－ボーナス
	IF R < 0
		;負の値の計算がeramakerのバグで怪しいので要注意
		R = R * -1
		A += R / 5 + 5
	ELSEIF R > 0
		A -= R / 5
	ENDIF

	;ムードボーナス この値が本命
	IF TFLAG:37 < 10
		A -= 10
	ELSEIF TFLAG:37 < 30
		A -= 1
	ELSEIF TFLAG:37 < 50
		A += 1
	ELSEIF TFLAG:37 < 100
		A += TFLAG:37 / 6
	ELSEIF TFLAG:37 < 300
		A += TFLAG:37 / 4
	ELSE
		A += TFLAG:37
	ENDIF

	;デート経験 これも割と大きい
	IF EXP:デート経験 < 50
		A -= 5
	ELSEIF EXP:デート経験 < 100
		A += 5
	ELSEIF EXP:デート経験 < 500
		A += EXP:デート経験 / 15
	ELSE
		A += EXP:デート経験 / 10
	ENDIF

	;桃色の秘薬使用中
	IF TEQUIP:28
		A += 5
	ENDIF

	;判定成功
	IF A >= V
		;この辺の描写は口上との兼ね合いが難しいけど、とりあえず暫定処置
		;口上表示
		TFLAG:600 = 26
		TALENT:153 = 1
		CALL KOJO_JUN
		SIF TALENT:153 == 0
			RETURN 0
		IF TALENT:85 == 0
			TALENT:85 = 1
			PRINTFORMW %CALLNAME:TARGET%は【恋慕】を得ました
			;堕とした人数＋１
			FLAG:30 += 1

			;堕とした人数が5人以上で、謎の魅力が付く
			IF FLAG:30 >= 5 && TALENT:MASTER:92 == 0
				PRINTFORMW %NAME:MASTER%は【謎の魅力】を身につけた
				TALENT:MASTER:92 = 1
			ENDIF
		ENDIF
		T = TARGET
		CALL GET_LOVERS
		IF TALENT:PLAYER:151
			PRINTFORMW %CALLNAME:PLAYER%は【キス未経験】を失いました
			TALENT:PLAYER:151 = 0
		ENDIF
		IF TALENT:151
			TALENT:151 = 0
			PRINTFORMW %CALLNAME:TARGET%は【キス未経験】を失いました
			TFLAG:38 = 1
		ENDIF
		EXP:キス経験 += 1
		EXP:PLAYER:キス経験 += 1

		;既成事実２に相当
		TFLAG:200 = 2
		
		TFLAG:33 = 0
	ENDIF
ENDIF

;ファーストキス判定
;最低でも従順4、好感度400が必要
IF ABL:親密 >= 4 && CFLAG:2 >= 400 && TALENT:151 == 1 && ASSIPLAY == 0 && TALENT:153 == 0
	A = 0
	;目標難易度は70
	V = 70

	;純愛系コマンド実行値取得
	CALL GET_JUN_VALUE

	;好感度
	A += CFLAG:2 / 50

	;恋慕
	SIF TALENT:85
		A += 30

	;臆病
	SIF TALENT:10
		A -= 10

	;恥じらい
	SIF TALENT:35
		A -= 10

	;恥薄い
	SIF TALENT:36
		A += 5

	;悲観的
	SIF TALENT:26
		A -= 5

	;一線越えない
	SIF TALENT:27
		A -= 10

	;Aの値を退避
	Z = A
	;主導権基準値ボーナス
	T = TARGET
	CALL GET_INITIATIVE_RATE
	;退避させた値を戻す
	A = Z
	IF R < 0
		A += R / 5 + 5
	ELSEIF R > 0
		A -= R / 5
	ENDIF

	;ムードボーナス この値が本命
	IF TFLAG:37 < 10
		A -= 10
	ELSEIF TFLAG:37 < 30
		A -= 1
	ELSEIF TFLAG:37 < 50
		A += 1
	ELSEIF TFLAG:37 < 100
		A += TFLAG:37 / 5
	ELSEIF TFLAG:37 < 300
		A += TFLAG:37 / 3
	ELSE
		A += TFLAG:37
	ENDIF

	;デート経験 これも割と大きい
	IF EXP:デート経験 < 50
		A -= 5
	ELSEIF EXP:デート経験 < 100
		A += 5
	ELSEIF EXP:デート経験 < 500
		A += EXP:デート経験 / 15
	ELSE
		A += EXP:デート経験 / 10
	ENDIF

	;桃色の秘薬使用中
	IF TEQUIP:28
		A += 5
	ENDIF

	;判定成功
	IF A >= V
		;この辺の描写は口上との兼ね合いが難しいけど、とりあえず暫定処置
		;口上表示
		TFLAG:600 = 25
		CALL KOJO_JUN
		TALENT:151 = 0
		PRINTFORML %EXPNAME:96%＋１
		EXP:キス経験 += 1
		EXP:PLAYER:キス経験 += 1
		IF TALENT:PLAYER:151
			PRINTFORMW %CALLNAME:PLAYER%は【キス未経験】を失いました
			TALENT:PLAYER:151 = 0
		ENDIF
		PRINTFORMW %CALLNAME:TARGET%は【キス未経験】を失いました
		TFLAG:38 = 1
		;既成事実1に相当
		TFLAG:200 = 1
		
		TFLAG:33 = 0
	ENDIF
ENDIF

;アクセサリプレゼント判定
;告白やキスが発生していると口上の兼ね合い的な問題で発生しない(TFLAG:33で判定）
SIF TFLAG:33
	CALL ACCESSORY_CHECK

RETURN 1

;-------------------------------------------------
;映画館から追い出される
;同時に起きるTEQUIP解除と素質発覚イベントもここで設定
;-------------------------------------------------
@GET_OUT_MOVIE
IF TEQUIP:2 >= 18 && TEQUIP:2 <= 21
	PRINTFORML %CALLNAME:PLAYER%達は映画館から追い出されました
	PRINTW 繁華街へ移動します
	TEQUIP:2 = 13
ENDIF
;着衣ローター解除
TEQUIP:36 = 0
;オムツを脱がす
TEQUIP:20 = 0
;炬燵から出る
TEQUIP:7 = 0
;素質発覚イベント
CALL FIND_BUSTSIZE

;-------------------------------------------------
;キャラごとの好みのデート先設定
;-------------------------------------------------
@PARTNER_DATE_PLACE
IF 0
ELSEIF NO:TARGET == 1
	;ショウタ
	;街
	TEQUIP:2 = 1
ELSEIF NO:TARGET == 2
	;アイ
	;街
	TEQUIP:2 = 1
ELSEIF NO:TARGET == 3 && CFLAG:0 == 0
	;マキ
	;遊園地
	TEQUIP:2 = 10
ELSEIF NO:TARGET == 3 && CFLAG:0
	;マキ様
	;映画（ポルノ）
	TEQUIP:2 = 22
ELSEIF NO:TARGET == 4 && CFLAG:0 == 0
	;ゆかり
	;ゲームセンター
	TEQUIP:2 = 15
ELSEIF NO:TARGET == 4 && CFLAG:0
	;ゆかり様
	;カジノ
	TEQUIP:2 = 16
ELSEIF NO:TARGET == 5 || NO:TARGET == 10 || NO:TARGET == 12
	;ずん子、きりたん、イタコ
	;キャラ固有デート先(1)
	TEQUIP:2 = 37
ELSEIF NO:TARGET == 6 || NO:TARGET == 7
	;琴葉姉妹
	;海
	TEQUIP:2 = 14
ELSEIF NO:TARGET == 8
	;コウ
	;繁華街
	TEQUIP:2 = 13
ELSEIF NO:TARGET == 9
	;セイカ
	IF RAND:2 == 0
		;キャラ固有デート先(1)、精華町
		TEQUIP:2 = 37
	ELSE
		;キャラ固有デート先(2)、セイカの家
		TEQUIP:2 = 36
	ENDIF
ELSEIF NO:TARGET == 11
	;あかり
	;繁華街
	TEQUIP:2 = 13
ELSEIF NO:TARGET == 13
	;そら
	;アクション映画
	TEQUIP:2 = 19
ELSEIF NO:TARGET == 14
	;ついな
	;海
	;江ノ島のイメージ
	TEQUIP:2 = 14
	
ELSEIF NO:TARGET == 50 
	;ささら
	;キャラ固有デート先(1)
	TEQUIP:2 = 37
ELSEIF NO:TARGET == 51
	;つづみ
	;図書館
	TEQUIP:2 = 8
ELSEIF NO:TARGET == 52
	;タカハシ
	;こいつラーメン食いたそうだな
	;街
	TEQUIP:2 = 1
ELSEIF NO:TARGET == 53
	;ＯＮＥ
	;カラオケ
	TEQUIP:2 = 6
ELSEIF NO:TARGET == 54
	;ＩＡ
	;カラオケ
	TEQUIP:2 = 6

ELSEIF NO:TARGET == 80
	;ギャラ子
	;中の人女優さんだし
	;コメディ映画
	TEQUIP:2 = 18
ELSEIF NO:TARGET == 81
	;ウナ
	;水族館
	TEQUIP:2 = 11
ELSEIF NO:TARGET == 82
	;ヒメ
	;花畑
	TEQUIP:2 = 4
ELSEIF NO:TARGET == 83
	;ミコト
	;花畑
	TEQUIP:2 = 4
ELSEIF NO:TARGET == 84
	;flower
	;花畑
	TEQUIP:2 = 4
ELSE
	;一応例外設定
	;街
	TEQUIP:2 = 1
ENDIF

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_323
A = 0
S = 0
V = 25

;終了時は判定不要(他の処理で利用するためA > VになるようAを設定)
IF TEQUIP:2
	A = 999
	RETURN 0
ENDIF

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;男嫌い
IF TALENT:82 && TALENT:PLAYER:122
	A -= 7
ENDIF
;恥じらい
IF TALENT:35
	A -= 5
ENDIF
;献身的
IF TALENT:63
	A += 6
ENDIF
;恋慕
IF TALENT:85 && ASSIPLAY == 0
	A += 5
	S = 5
ENDIF

;好感度
IF CFLAG:2 < 400
	B = 0
	IF CFLAG:2 < 50
		B = 40
	ELSEIF CFLAG:2 < 150
		B = 20
	ELSEIF CFLAG:2 < 300
		B = 10
	ELSE
		B = 5
	ENDIF
	A -= B
ENDIF

;好感度
IF CFLAG:2 >= 500
	B = 0
	B = CFLAG:2 / 75
	A += B
ENDIF

IF ((FLAG:23 & 1p27) == 0) && FLAG:2 != 1
	;男嫌い
	IF TALENT:82 && TALENT:PLAYER:122
		PRINT  - 
		PRINTS TALENTNAME:82
		PRINTV '(,7,')
		S = 1
	ENDIF
	;恥じらい
	IF TALENT:35
		PRINT  - 
		PRINTS TALENTNAME:35
		PRINTV '(,5,')
		S = 1
	ENDIF
	;献身的
	IF TALENT:63
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:63
		PRINTV '(,6,')
		S = 1
	ENDIF
	;恋慕
	IF TALENT:85 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:85
		PRINTV '(,5,')
		S = 5
	ENDIF

	;好感度
	IF CFLAG:2 < 400
		B = 0
		PRINT  - 
		IF CFLAG:2 < 50
			B = 40
		ELSEIF CFLAG:2 < 150
			B = 20
		ELSEIF CFLAG:2 < 300
			B = 10
		ELSE
			B = 5
		ENDIF
		PRINT 好感度不足
		PRINTV '(,B,')
		S = 1
	ENDIF

	;好感度
	IF CFLAG:2 >= 500
		B = 0
		SIF S
			PRINT  + 
		B = CFLAG:2 / 75
		PRINT 好感度
		PRINTV '(,B,')
		S = 1
	ENDIF

	;合計を表示(25以上で実行)
	PRINT  = 
	PRINTV A

	IF A < V
		PRINT  < 
		PRINT 実行値
		PRINTV V
	ELSEIF A == V
		PRINT  = 
		PRINT 実行値
		PRINTV V
	ELSEIF A > V
		PRINT  > 
		PRINT 実行値
		PRINTV V
	ENDIF
	WAIT
ENDIF

;-------------------------------------------------
;東北邸に行けるかどうかの関数
;行けるならRETURNF 1 行けないならRETURNF 0
;-------------------------------------------------
@GET_TOHOKUABLE
#FUNCTION

;従順が10以上無いとだめ
IF ABL:TARGET:親密 < 10
	RETURNF 0
;そもそも主人公が東北邸の人だとだめ
ELSEIF NO:MASTER == 5 || NO:MASTER == 10 || NO:MASTER == 12
	RETURNF 0
;難易度がEX,Phでなく、かつクリア前だと無理 攻略しましょう
ELSEIF FLAG:5 < 9 && FLAG:28 == 0
	RETURNF 0
ENDIF

RETURNF 1

