@COM303
;スキンシップ

PRINTL スキンシップ
STR:0 = スキンシップ
CALL GET_SUCCESS_RATE
B = 90 + A / 10
SIF B > 99
	B = 99
A = RAND:100
IF A <= 9
	;10％で大成功
	TFLAG:18 = 1
ELSEIF A >= B
	;10～1％で失敗
	TFLAG:18 = -1
ELSE
	;残りは成功
	TFLAG:18 = 0
ENDIF

TFLAG:17 = 0
;連続実行は強制的に濃厚になる
IF PREVCOM == 303
	TFLAG:17 = 1
;クリスマスデート中にある天気なら濃厚になる
ELSEIF TIME:5 == 8 && TEQUIP:2
	TFLAG:17 = 1
;従順8以上or好感度1000以上or合意or恋人 なら、実行判定を行い、成功でパワーアップ
ELSEIF ABL:親密 >= 8 || CFLAG:2 >= 1000 || TALENT:合意 || TALENT:恋人

A = 0
S = 0

;合計を表示(50以上で実行)
V = 50
;デート中はイチャイチャしやすい
SIF TEQUIP:2
	V -= 10

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;ABL:欲望
IF ABL:欲望
	A += ABL:欲望 * 3
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	A += ABL:奉仕精神 * 3
ENDIF
;男嫌い
IF TALENT:男嫌い && TALENT:PLAYER:オトコ
	A -= 7
ENDIF
;天気：雪
IF TIME:5 == 5 || TIME:5 == 6 || TIME:5 == 8
	A += 10
ENDIF
;好感度
IF CFLAG:2 < 700
	B = 0
	IF CFLAG:2 < 50
		B = 50
	ELSEIF CFLAG:2 < 300
		B = 20
	ELSEIF CFLAG:2 < 500
		B = 10
	ELSE
		B = 5
	ENDIF
	A -= B
ENDIF

;好感度
IF CFLAG:2 >= 700
	B = 0
	IF CFLAG:2 < 1000
		B = 5
	ELSE
		B = CFLAG:2 / 100
	ENDIF
	A += B
ENDIF

IF ((FLAG:23 & 1p27) == 0) && FLAG:2 != 1
	;ABL:欲望
	IF ABL:欲望
		SIF S
			PRINT  + 
		PRINTS ABLNAME:1
		PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 3,')
		S = 1
	ENDIF
	;ABL:奉仕精神
	IF ABL:奉仕精神
		SIF S
			PRINT  + 
		PRINTS ABLNAME:6
		PRINTV 'LV,ABL:奉仕精神,'(,ABL:奉仕精神 * 3,')
		S = 1
	ENDIF
	;男嫌い
	IF TALENT:男嫌い && TALENT:PLAYER:オトコ
		PRINT  - 
		PRINTS TALENTNAME:82
		PRINTV '(,7,')
		S = 1
	ENDIF
	;天気：雪
	IF TIME:5 == 5 || TIME:5 == 6 || TIME:5 == 8
		SIF S
			PRINT  + 
		PRINT 天気:雪
		PRINTV '(,10,')
		S = 1
	ENDIF
	;好感度
	IF CFLAG:2 < 700
		B = 0
		PRINT  - 
		IF CFLAG:2 < 50
			B = 50
		ELSEIF CFLAG:2 < 300
			B = 20
		ELSEIF CFLAG:2 < 500
			B = 10
		ELSE
			B = 5
		ENDIF
		PRINT 好感度不足
		PRINTV '(,B,')
		S = 1
	ENDIF

	;好感度
	IF CFLAG:2 >= 700
		B = 0
		SIF S
			PRINT  + 
		IF CFLAG:2 < 1000
			B = 5
		ELSE
			B = CFLAG:2 / 100
		ENDIF
		PRINT 好感度
		PRINTV '(,B,')
		S = 1
	ENDIF

	PRINT  = 
	PRINTV A

	IF A < V
		PRINT  < 
		PRINT 実行値
		PRINTV V
	ELSEIF A == V
		PRINT  = 
		PRINT 実行値
		PRINTV V
	ELSEIF A > V
		PRINT  > 
		PRINT 実行値
		PRINTV V
	ENDIF
	WAIT
ENDIF

;実行できる場合
IF A >= V
	;パワーアップ
	TFLAG:17 = 1
ENDIF

ENDIF

CALL TRAIN_MESSAGE_B

LOSEBASE:気力 += 70

;固定で獲得するソース
SOURCE:歓楽 = 0
SOURCE:接触 = 500

;ABL:従順をみる
IF ABL:親密 <= 1
	SOURCE:接触 += (ABL:親密 * 30)
ELSEIF ABL:親密 <= 3
	SOURCE:歓楽 += 100 + (ABL:親密 * 40)
	SOURCE:接触 += 200 + (ABL:親密 * 30)
ELSEIF ABL:親密 <= 5
	SOURCE:歓楽 += 400 + (ABL:親密 * 50)
	SOURCE:接触 += 400 + (ABL:親密 * 30)
ELSEIF ABL:親密 <= 8
	SOURCE:歓楽 += 700 + (ABL:親密 * 60)
	SOURCE:接触 += 600 + (ABL:親密 * 40)
ELSEIF ABL:親密 <= 11
	SOURCE:歓楽 += 1200 + (ABL:親密 * 80)
	SOURCE:接触 += 800 + (ABL:親密 * 40)
ELSE
	SOURCE:歓楽 += 2000 + (ABL:親密 * 50)
	SOURCE:接触 += 1800 + (ABL:親密 * 20)
ENDIF

;好感度をみる
IF CFLAG:2 <= 500
	SOURCE:歓楽 += CFLAG:2
ELSEIF CFLAG:2 <= 5000
	SOURCE:歓楽 += 500 + (CFLAG:2 - 500) / 3
ELSE
	SOURCE:歓楽 += 2000 + (CFLAG:2 - 5000) / 5
ENDIF
SIF SOURCE:歓楽 < 0
	SOURCE:歓楽 = 0

;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:従順をみる
	IF ABL:親密 == 0
		SOURCE:受動 = 100
	ELSEIF ABL:親密 == 1
		SOURCE:受動 = 200
	ELSEIF ABL:親密 == 2
		SOURCE:受動 = 350
	ELSEIF ABL:親密 == 3
		SOURCE:受動 = 500
	ELSEIF ABL:親密 == 4
		SOURCE:受動 = 700
	ELSE
		SOURCE:受動 = 1000 + (ABL:親密 * 100)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:サドっ気 == 0
		SOURCE:征服 = 500
	ELSEIF ABL:サドっ気 == 1
		SOURCE:征服 = 700
	ELSEIF ABL:サドっ気 == 2
		SOURCE:征服 = 900
	ELSEIF ABL:サドっ気 == 3
		SOURCE:征服 = 1100
	ELSEIF ABL:サドっ気 == 4
		SOURCE:征服 = 1300
	ELSE
		SOURCE:征服 = 1000 + (ABL:サドっ気 * 150)
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:5 - 1

SOURCE:情愛 = 0
;パワーアップ版ならソースを追加
IF TFLAG:17
	TIMES SOURCE:歓楽 , 1.20
	TIMES SOURCE:接触 , 2.00
	TIMES SOURCE:征服 , 1.50
	TIMES SOURCE:受動 , 1.50
	SOURCE:情愛 = SOURCE:接触 / 10
	A += 2
	IF TALENT:キス未経験 == 0
		PRINTFORML %EXPNAME:96%＋１
		EXP:キス経験 += 1
		EXP:PLAYER:キス経験 += 1
		IF TALENT:PLAYER:キス未経験
			PRINTFORMW %CALLNAME:PLAYER%は【キス未経験】を失いました
			TALENT:PLAYER:キス未経験 = 0
		ENDIF
	ENDIF

	IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
		PRINTS EXPNAME:40
		PRINTL +1
		EXP:レズ経験 += 1
		EXP:PLAYER:レズ経験 += 1
	ELSEIF TALENT:オトコ == 1 && TALENT:PLAYER:オトコ == 1
		PRINTS EXPNAME:41
		PRINTL +1
		EXP:ＢＬ経験 += 1
		EXP:PLAYER:ＢＬ経験 += 1
	ENDIF

ENDIF

;従順が低いと好感度低下
IF ABL:親密 == 0
	A -= 3
ELSEIF ABL:親密 == 1
	A -= 2
ELSEIF ABL:親密 == 2
	A -= 1
ENDIF
;好感度が低いと好感度低下
IF CFLAG:2 <= 50
	A -= 3
ELSEIF CFLAG:2 <= 100
	A -= 2
ELSEIF CFLAG:2 <= 250
	A -= 1
ENDIF


;好感度変化
TFLAG:30 += A

IF TFLAG:18 == -1
	TIMES SOURCE:歓楽 , 0.10
	TIMES SOURCE:接触 , 0.50
	TIMES SOURCE:征服 , 0.50
	TIMES SOURCE:受動 , 0.50
	TIMES SOURCE:情愛 , 0.50
	TFLAG:30 -= 10
	TFLAG:37 -= 5
ELSEIF TFLAG:18 == 0
	TIMES SOURCE:歓楽 , 1.00
	TIMES SOURCE:接触 , 1.00
	TIMES SOURCE:征服 , 1.00
	TIMES SOURCE:受動 , 1.00
	TIMES SOURCE:情愛 , 1.00
	TFLAG:37 += A
ELSE
	TIMES SOURCE:歓楽 , 2.00
	TIMES SOURCE:接触 , 2.00
	TIMES SOURCE:征服 , 2.00
	TIMES SOURCE:受動 , 2.00
	TIMES SOURCE:情愛 , 2.00
	TFLAG:30 += 2
	TFLAG:37 += 5 + A
ENDIF

;デート中は更にムードアップ
SIF TEQUIP:2
	TFLAG:37 += 1 + TFLAG:17 * 2

RETURN 1

