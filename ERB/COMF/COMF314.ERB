@COM_ABLE314
;炬燵にあたる実行判定
;COM_ABLE共通判定
CALL COM_ABLE_COMMON, SELECTCOM
SIF RESULT == 0
	RETURN 0
;ウフフ中は無理
SIF TFLAG:44 == 1
	RETURN 0
;冬以外は仕舞ってある
SIF DAY:2 != 3
	RETURN 0
;デート中は無理
SIF TEQUIP:2 > 0
	RETURN 0
;着衣ローター中は無理(併用されると色々崩壊するので)
SIF TEQUIP:36 == 1
	RETURN 0
;炬燵を買ってないと実行できない
SIF ITEM:52 == 0
	RETURN 0
RETURN 1

@COM314
;炬燵にあたる

IF TEQUIP:7 == 0
	PRINTL 炬燵にあたる
	STR:0 = 炬燵にあたる
ELSE
	PRINTL 炬燵から出る
	STR:0 = 炬燵から出る
ENDIF

CALL TRAIN_MESSAGE_B

LOSEBASE:体力 += 30
LOSEBASE:気力 += 50

;炬燵を出す
IF TEQUIP:7 == 0
	;ABL:親密をみる
	IF ABL:親密 <= 1
		SOURCE:18 += (ABL:親密 * 60)
	ELSEIF ABL:親密 <= 3
		SOURCE:18 += 50 + (ABL:親密 * 60)
	ELSEIF ABL:親密 <= 5
		SOURCE:18 += 250 + (ABL:親密 * 60)
	ELSEIF ABL:親密 <= 8
		SOURCE:18 += 300 + (ABL:親密 * 75)
	ELSEIF ABL:親密 <= 11
		SOURCE:18 += 400 + (ABL:親密 * 75)
	ELSE
		SOURCE:18 += 1000 + (ABL:親密 * 30)
	ENDIF

	;ABL:奉仕精神をみる
	IF ABL:奉仕精神 == 0
		TIMES SOURCE:18 , 0.80
	ELSEIF ABL:奉仕精神 == 1
		TIMES SOURCE:18 , 0.90
	ELSEIF ABL:奉仕精神 == 2
		TIMES SOURCE:18 , 1.00
	ELSEIF ABL:奉仕精神 == 3
		TIMES SOURCE:18 , 1.10
	ELSEIF ABL:奉仕精神 == 4
		TIMES SOURCE:18 , 1.20
	ELSE
		TIMES SOURCE:18 , 1.30
	ENDIF

	;主導権をチェック
	IF TFLAG:45 == 0
		;主導権が主人公側
		;ABL:親密をみる
		IF ABL:親密 <= 1
			SOURCE:21 = 100
		ELSEIF ABL:親密 <= 2
			SOURCE:21 = 150
		ELSEIF ABL:親密 <= 4
			SOURCE:21 = 200
		ELSEIF ABL:親密 <= 6
			SOURCE:21 = 250
		ELSEIF ABL:親密 <= 8
			SOURCE:21 = 500
		ELSE
			SOURCE:21 = 250 + (ABL:親密 * 50)
		ENDIF
	ELSE
		;主導権が相手側
		;ABL:奉仕精神をみる
		IF ABL:Ｖ感覚 == 0
			SOURCE:20 = 100
		ELSEIF ABL:Ｖ感覚 <= 1
			SOURCE:20 = 200
		ELSEIF ABL:Ｖ感覚 <= 3
			SOURCE:20 = 300
		ELSEIF ABL:Ｖ感覚 <= 5
			SOURCE:20 = 400
		ELSEIF ABL:Ｖ感覚 <= 7
			SOURCE:20 = 500
		ELSE
			SOURCE:20 = 100 + (ABL:Ｖ感覚 * 100)
		ENDIF
	ENDIF

	;好感度ランダム変化(雪、吹雪、細氷だと+3)
	A:1 = RAND:10 + 5 + (TIME:5 >= 5 && TIME:5 <= 8 && TIME:5 != 7) * 3
	;好感度が30以下だと好感度が下がる(一緒に入るなって事です)
	IF CFLAG:2 <= 30
		A:1 -= 9
		SOURCE:15 += 1
	ENDIF

	B = 0
	T = 40
	CALL GET_EXISTANCE
	SIF E
		B += 1
	T = 71
	CALL GET_EXISTANCE
	SIF E
		B += 1
	;にとりor夢美が居ると熱源が電気式が動くようになります(普通は練炭、木炭、豆炭等)
	;修理したものを使ってもらえたので効果アップ
	IF B > 0
		;にとり、夢美がパートナーなら効果アップ
		SIF NO:TARGET == 40 || NO:TARGET == 71
			A:1 += RAND:3+1
		TFLAG:18 = 1
	ELSE
		;性能が高いので凶悪なトラップ付き
		;火傷の可能性があったり火力調整が面倒だったりで効果は落ちます
		TFLAG:18 = 0
	ENDIF

	TFLAG:30 += A:1
	TFLAG:37 += A:1 / 2
	TEQUIP:7 = 1
;炬燵を片付ける
ELSE
	TEQUIP:7 = 0
	TFLAG:37 += 1
ENDIF

RETURN 1


@EQUIP_COM314
;炬燵でぬくぬく中
PRINTL 
PRINTL ＜炬燵でぬくぬく中＞
PRINTL 
;4分の1の確率で舟を漕ぐ(チルノとレティは除く)
;炬燵かがりor炬燵隠れだと眠らない
IF RAND:4 == 0 && NO:TARGET != 5 && NO:TARGET != 12 && SELECTCOM != 368 && SELECTCOM != 369
	TFLAG:13 = 100
	IF RAND:2 == 0
		TFLAG:17 = 1
		BASE:体力 = MAXBASE:体力
		BASE:耐力 = MAXBASE:耐力
		BASE:PLAYER:体力 = MAXBASE:PLAYER:体力
		BASE:PLAYER:耐力 = MAXBASE:PLAYER:耐力
	ELSE
		TFLAG:17 = 0
	ENDIF
	;地の文表示
	IF FLAG:6 == 1 && (CFLAG:99 & 1) == 0
		PRINTL 
		PRINTL ―――――――――――――――――――――――
		PRINTL         CAUTION!!             CAUTION!!
		PRINTL ―――――――――――――――――――――――
		PRINTL 
		PRINTFORML %CALLNAME:TARGET%は余りの心地良さに船を漕ぎ始めた…
		PRINTL 
		PRINTL ―――――――――――――――――――――――
		PRINTL         CAUTION!!             CAUTION!!
		PRINTL ―――――――――――――――――――――――
		PRINTL 
		WAIT
		;2分の1の確率でおやすみなさい
		IF TFLAG:17 == 1
			PRINTFORML %CALLNAME:TARGET%は炬燵の魔力に囚われ夢の世界に
			PRINT 旅立ってしまったので
			IF TIME
				PRINT 朝
			ELSE
				PRINT 夜
			ENDIF
			PRINTL になります…
			PRINTL 
			CALL SELF_KOJO
			BEGIN AFTERTRAIN
		ELSEIF TFLAG:17 == 0
			CALL SELF_KOJO
			PRINTFORML %CALLNAME:TARGET%は何とか現実世界に踏み止まることに成功した！
			PRINTL 
		ENDIF
	ELSE
		;2分の1の確率でおやすみなさい
		IF TFLAG:17 == 1
			CALL SELF_KOJO
			BEGIN AFTERTRAIN
		ELSEIF TFLAG:17 == 0
			CALL SELF_KOJO
		ENDIF
	ENDIF
ENDIF

;地の文表示
IF FLAG:6 == 1 && CFLAG:99 == 0
	;チルノとレティの場合、苦しみます
	IF NO:TARGET == 5 && NO:TARGET == 12
		PRINTFORML %CALLNAME:TARGET%は熱さに負けて逃げ出した…
	;反発感情が1以上
	ELSEIF MARK:反発感情 > 0
		PRINTFORML %CALLNAME:TARGET%一人が炬燵でぬくぬくしてます…
	ELSE
		PRINTFORML %CALLNAME:TARGET%と炬燵でぬくぬくしてます…
	ENDIF
ENDIF

;チルノとレティの場合、苦しみます
IF NO:TARGET == 5 || NO:TARGET == 12
	LOSEBASE:体力 += 55
	LOSEBASE:気力 += 100

	;ABL:マゾっ気をみる
	IF ABL:マゾっ気 == 0
		A:1 = 40
	ELSEIF ABL:マゾっ気 <= 1
		A:1 = 120
	ELSEIF ABL:マゾっ気 <= 2
		A:1 = 300
	ELSEIF ABL:マゾっ気 <= 3
		A:1 = 500
	ELSEIF ABL:マゾっ気 <= 4
		A:1 = 650
	ELSE
		A:1 = 350 + (ABL:Ｖ感覚 * 100)
	ENDIF

	;熱に弱い娘は痛みと不安を追加(にとり、夢美が居ても強化しない)
	SOURCE:6 += A:1
	SOURCE:23 += A:1
ELSE
	;ABL:親密をみる
	IF ABL:親密 == 0
		A:1 = 40
	ELSEIF ABL:親密 <= 1
		A:1 = 120
	ELSEIF ABL:親密 <= 2
		A:1 = 300
	ELSEIF ABL:親密 <= 3
		A:1 = 500
	ELSEIF ABL:親密 <= 4
		A:1 = 650
	ELSE
		A:1 = 350 + (ABL:Ｖ感覚 * 100)
	ENDIF
	
	B = 0
	T = 40
	CALL GET_EXISTANCE
	SIF E
		B += 1
	T = 71
	CALL GET_EXISTANCE
	SIF E
		B += 1
	;にとりor夢美が居ると熱源が電気式が動くようになります(普通は練炭、木炭、豆炭等)
	;発電方法は、色々あるので口上作者様の発想に依存
	IF B > 0
		;衣玖、空が実行者なら効果アップ
		IF NO:PLAYER == 62 || NO:PLAYER == 83
				TIMES A:1 , 1.25
		;衣玖、空がパートナーなら効果アップ
		ELSEIF NO:TARGET == 62 || NO:TARGET == 83
				TIMES A:1 , 1.20
		ELSEIF (ASSI+1) != 0
			;衣玖、空が助手に居ると効果アップ
			IF NO:ASSI == 62 || NO:ASSI == 83
				TIMES A:1 , 1.10
			ENDIF
		ENDIF

		;性能が高いので凶悪なトラップ付き
		LOSEBASE:体力 += 3
		LOSEBASE:気力 -= 60
		TFLAG:18 = 1
	ELSE
		;性能が高いので凶悪なトラップ付き
		;火傷の可能性があったり火力調整が面倒だったりで効果は落ちます
		LOSEBASE:体力 += 10
		LOSEBASE:気力 -= 50
		TFLAG:18 = 0
	ENDIF
	
	;猫舌なら効果アップ(猫科ボーナス)
	SIF TALENT:TARGET:猫舌
		TIMES A:1 , 1.3
	;雪、吹雪、細氷だと効果アップ(極寒ボーナス)
	SIF TIME:5 >= 5 && TIME:5 <= 8 && TIME:5 != 7
		TIMES A:1 , 1.5

	;通常は歓楽追加
	SOURCE:18 += A:1
ENDIF

;恋慕且つ恋人
IF TALENT:恋慕 && TALENT:恋人 && ASSIPLAY == 0
	SOURCE:9 = (RAND:5+1) * ABL:親密 + 500

	;猫舌なら効果アップ(猫科ボーナス)
	SIF TALENT:TARGET:猫舌
		TIMES SOURCE:9 , 1.3
ENDIF

TFLAG:30 += 1
TFLAG:37 += 1

RETURN 1

;炬燵にあたる
@TRAIN_MESSAGE_COM314
;設置
IF TEQUIP:7 == 0
	;主導権を持ってる方が設置する(一緒にあたろうと誘う)
	IF TFLAG:45 == 0
		PRINTFORMW %CALLNAME:PLAYER%は炬燵を設置した
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は炬燵を設置した
	ENDIF
	IF MARK:反発感情 > 0
		PRINTFORMW %CALLNAME:TARGET%は一人で炬燵を占拠していた…
	;狭いし密着度アップさ
	ELSEIF TALENT:恋慕 == 1 || TALENT:恋人 == 1
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%と同じ箇所から潜り込み擦り寄ってきた…
	ELSEIF CFLAG:2 > 100
		PRINTFORMW %CALLNAME:TARGET%は向かいに潜り込んだ…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は隣に潜り込んだ…
	ENDIF
;撤去
ELSE
	;主導権を持ってる方が撤去する
	IF TFLAG:45 == 0
		PRINTFORMW %CALLNAME:PLAYER%は炬燵を撤去した…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は炬燵を撤去した…
	ENDIF
ENDIF

