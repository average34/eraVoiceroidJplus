;5ターン継続、使用中奉仕効果増大、体力減少大
;TEQUIP:23を使わせてもらいました
;奉仕行動以外はほぼ体力消費するだけの悲しい効果
@COM_ABLE400
;COM_ABLE共通判定
CALL COM_ABLE_COMMON, SELECTCOM
SIF RESULT == 0
	RETURN 0
;幽明の苦輪（妖夢専用）

SIF NO:TARGET != 19
	RETURN 0
IF ASSI > 0
	SIF NO:TARGET == 19 && NO:ASSI == 87
		RETURN 0
ENDIF
;親密10以上、技巧5以上、奉仕精神5以上
SIF ABL:親密 < 10 || ABL:技巧 <5 || ABL:奉仕精神 < 5
	RETURN 0
RETURN 1

@COM400
;幽明の苦輪
PRINTFORML 幽明の苦輪
STR:0 = 幽明の苦輪

CALL TRAIN_MESSAGE_B

;幽明の苦輪発動もしくは終了
IF TEQUIP:23 > 0
	LOSEBASE:体力 += 0
	LOSEBASE:気力 += 30
	TEQUIP:23 = 0
ELSE
	LOSEBASE:体力 += 0
	LOSEBASE:気力 += 40
	TEQUIP:23 = 6
ENDIF


;好感度ランダム変化
A = RAND:5 + 5
;合意がないと好感度が下がる、おまけ程度の反感
IF TALENT:合意 == 0 && TFLAG:48 == 0
	A -= 5
	SOURCE:反感追加 += 50
ENDIF
TFLAG:30 += A
TFLAG:37 += A / 2

;口上の呼び出し
CALL SELF_KOJO

RETURN 1

@TRAIN_MESSAGE_COM400
;幽明の苦輪発動もしくは終了
IF TEQUIP:23 > 0
	IF TFLAG:45 == 0
		PRINTFORMW %CALLNAME:TARGET%に幽明の苦輪をやめさせた。
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は幽明の苦輪をやめた。
	ENDIF
ELSE
	IF TFLAG:45 == 0
		PRINTFORMW %CALLNAME:TARGET%に幽明の苦輪を使わせた。
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は幽明の苦輪を使った。
	ENDIF
ENDIF

@EQUIP_COM400
;幽明の苦輪発動中

TEQUIP:23 -= 1
 PRINTFORML ＜幽明の苦輪発動中＞


LOSEBASE:体力 += LOSEBASE:気力 * 4 / 5
LOSEBASE:気力 += 20

SOURCE:達成感 += 300
SOURCE:逸脱 += 200
IF (SELECTCOM >= 30 && SELECTCOM <= 37) || (SELECTCOM >= 60 && SELECTCOM <= 69) || SELECTCOM == 201
	PRINTFORMW 妖夢は半霊と一緒に奉仕をしている…
	SOURCE:快Ｃ *= 8
	SOURCE:快Ｃ /= 5
	SOURCE:情愛 *= 9
	SOURCE:情愛 /= 5
	SOURCE:性行動 *= 9
	SOURCE:性行動 /= 5
	SOURCE:達成感 *= 2
	SOURCE:痛み *= 6
	SOURCE:痛み /= 5
	SOURCE:中毒充足 *= 9
	SOURCE:中毒充足 /= 5
	SOURCE:不潔 *= 6
	SOURCE:不潔 /= 5
	SOURCE:接触 *= 9
	SOURCE:接触 /= 5
	SOURCE:液体追加 *= 3
	SOURCE:液体追加 /= 2
	SOURCE:逸脱 *= 9
	SOURCE:逸脱 /= 5
	SOURCE:恭順追加 *= 7
	SOURCE:恭順追加 /= 5
	SOURCE:快Ｂ *= 9
	SOURCE:快Ｂ /= 5
	SOURCE:征服 *= 8
	SOURCE:征服 /= 5
	SOURCE:不安 *= 6
	SOURCE:不安 /= 5
ELSEIF SELECTCOM == 300
	PRINTFORML 妖夢は半霊と一緒に攻撃してきた
	PRINTFORMW 戦闘経験+2
		EXP:戦闘経験 += 2
	SOURCE:達成感 *= 2
	SOURCE:歓楽 *= 8
	SOURCE:歓楽 /= 5
	SOURCE:征服 *= 2
	SOURCE:受動 *= 9
	SOURCE:受動 /= 5
ELSEIF SELECTCOM == 308
	PRINTFORML 妖夢は半霊と一緒に家事をこなしている…
	PRINTFORMW %EXPNAME:90%+2
		EXP:家事経験 += 2
	SOURCE:達成感 *= 2
	SOURCE:歓楽 *= 6
	SOURCE:歓楽 /= 5
	SOURCE:家務 *= 2
	SOURCE:征服 *= 2
	SOURCE:受動 *= 9
	SOURCE:受動 /= 5
ELSE
	SOURCE:懐疑 += 200
	SOURCE:懐疑 *= 3
	SOURCE:懐疑 /= 2
ENDIF

SIF TEQUIP:23 == 0
	PRINTFORMW ＜幽明の苦輪が終了しました＞

RETURN 1
