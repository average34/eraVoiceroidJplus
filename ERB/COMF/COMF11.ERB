@COM11
;バイブ
IF TEQUIP:90
	PRINTL 触手挿入
ELSE
	;実行者が[淫具知識]を持っているなら効果増
	SIF TALENT:PLAYER:58
		PRINT 高性能
	PRINTL バイブ
ENDIF

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

IF TEQUIP:90
	STR:0 = 触手挿入
ELSE
	STR:0 = バイブ
ENDIF
CALL TRAIN_MESSAGE_B

;-------------------------------------------------
;実行決定
;-------------------------------------------------
LOSEBASE:0 += 30
LOSEBASE:1 += 100

;ABL:Ｖ感覚をみる
IF ABL:4 == 0
	SOURCE:1 = 80
ELSEIF ABL:4 == 1
	SOURCE:1 = 250
ELSEIF ABL:4 == 2
	SOURCE:1 = 600
ELSEIF ABL:4 == 3
	SOURCE:1 = 1000
ELSEIF ABL:4 == 4
	SOURCE:1 = 1300
ELSE
	SOURCE:1 = 700 + (ABL:4 * 200)
ENDIF

;EXP:Ｖ経験をみる
IF EXP:0 < EXPLV:1
	TIMES SOURCE:1 , 0.20
	SOURCE:6 = 5500

	EXP:50 += 1
	PRINTFORML %EXPNAME:50%＋１
ELSEIF EXP:0 < EXPLV:2
	TIMES SOURCE:1 , 0.60
	SOURCE:6 = 300
ELSEIF EXP:0 < EXPLV:3
	TIMES SOURCE:1 , 1.00
	SOURCE:6 = 50
ELSEIF EXP:0 < EXPLV:4
	TIMES SOURCE:1 , 1.20
	SOURCE:6 = 10
ELSEIF EXP:0 < EXPLV:5
	TIMES SOURCE:1 , 1.40
	SOURCE:6 = 0
ELSE
	TIMES SOURCE:1 , 1.60
	SOURCE:6 = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:3 < PALAMLV:1
	TIMES SOURCE:1 , 0.10
	SOURCE:6 += 1000
	TIMES SOURCE:6 , 3.00
ELSEIF PALAM:3 < PALAMLV:2
	TIMES SOURCE:1 , 0.40
	SOURCE:6 += 400
	TIMES SOURCE:6 , 1.00
ELSEIF PALAM:3 < PALAMLV:3
	TIMES SOURCE:1 , 1.00
	TIMES SOURCE:6 , 0.50
ELSEIF PALAM:3 < PALAMLV:4
	TIMES SOURCE:1 , 1.40
	TIMES SOURCE:6 , 0.20
ELSEIF PALAM:3 >= PALAMLV:4
	TIMES SOURCE:1 , 1.80
	TIMES SOURCE:6 , 0.10
ENDIF

;PALAM:欲情をみる
GETPALAMLV PALAM:5, 36
SOURCE:1 = SOURCE:1 * (8 + RESULT) / 10

;ABL:従順をみる
IF ABL:0 == 0
	TIMES SOURCE:1 , 0.80
ELSEIF ABL:0 == 1
	TIMES SOURCE:1 , 0.90
ELSEIF ABL:0 == 2
	TIMES SOURCE:1 , 1.00
ELSEIF ABL:0 == 3
	TIMES SOURCE:1 , 1.10
ELSEIF ABL:0 == 4
	TIMES SOURCE:1 , 1.20
ELSE
	IF SOURCE:1 < 100000
		SOURCE:1 = SOURCE:1 * (ABL:0 * 5 + 105) / 100
	ELSEIF SOURCE:1 < 100000000
		SOURCE:1 = SOURCE:1 / 100 * (ABL:0 * 5 + 105)
	ENDIF
ENDIF

;小柄体型
SIF TALENT:100
	TIMES SOURCE:6 , 2.00

;実行者が[淫具知識]を持っているなら効果増
SIF TALENT:PLAYER:58
	TIMES SOURCE:1 , 1.20

EXP:0 += 1
PRINTL Ｖ経験＋１

;バイブの着脱
IF TEQUIP:90
	IF TEQUIP:11
		TEQUIP:11 = 0
	ELSE
		TEQUIP:11 = 1
		STAIN:3 |= 2
		STAIN:3 |= 4
	ENDIF
	T = 0
ELSE
	IF TEQUIP:11
		TEQUIP:11 = 0
	ELSE
		TEQUIP:11 = 1
	ENDIF
ENDIF

;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:従順をみる
	IF ABL:0 <= 1
		SOURCE:21 += 300 + (ABL:0 * 50)
	ELSEIF ABL:0 <= 3
		SOURCE:21 += 600 + (ABL:0 * 100)
	ELSEIF ABL:0 <= 5
		SOURCE:21 += 800 + (ABL:0 * 120)
	ELSEIF ABL:0 <= 8
		SOURCE:21 += 1000 + (ABL:0 * 140)
	ELSEIF ABL:0 <= 11
		SOURCE:21 += 1200 + (ABL:0 * 160)
	ELSE
		SOURCE:21 += 1500 + (ABL:0 * 180)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:15 == 0
		SOURCE:20 = 600
	ELSEIF ABL:15 == 1
		SOURCE:20 = 900
	ELSEIF ABL:15 == 2
		SOURCE:20 = 1200
	ELSEIF ABL:15 == 3
		SOURCE:20 = 1600
	ELSEIF ABL:15 == 4
		SOURCE:20 = 2000
	ELSE
		SOURCE:20 = 1000 + (ABL:15 * 300)
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:10 + 5
;合意がないと好感度が下がる
IF TALENT:152 == 0 && TFLAG:48 == 0
	A -= 9
	SOURCE:15 += 400
ENDIF
TFLAG:30 += A
TFLAG:37 += A / 2

RETURN 1

@TRAIN_MESSAGE_COM11
;バイブの着脱
IF TEQUIP:90
	IF TEQUIP:11
		PRINTFORML 触手が%CALLNAME:TARGET%の性器から引き抜かれた
	ELSE
		PRINTFORML 細かい突起に覆われた触手が%CALLNAME:TARGET%の性器に潜り込んでいった
	ENDIF
ELSE
	IF TEQUIP:11
		PRINTFORML %CALLNAME:TARGET%から蠢くバイブをゆっくりと引き抜いた。
		IF EXP:0 >= 201 && TALENT:85
			PRINTFORM %CALLNAME:TARGET%は、膣内の喪失感に耐えかねて涙と涎をこぼし
		ELSEIF EXP:0 >= 151 && EXP:0 <= 200 && TALENT:85
			PRINTFORM %CALLNAME:TARGET%の膣内に溜まっていた粘液が掻き出されて滴り、水溜りを作っ
		ELSEIF EXP:0 >= 121 && EXP:0 <= 150 && TALENT:85
			PRINTFORM バイブが抜かれた%CALLNAME:TARGET%の肉襞からボタボタと愛液が滴っ
		ELSEIF EXP:0 >= 71 && EXP:0 <= 120 && TALENT:85
			PRINTFORM %CALLNAME:TARGET%はもじもじと股を擦り合わせて%CALLNAME:PLAYER%の方を見
		ELSEIF EXP:0 >= 41 && EXP:0 <= 70 && TALENT:85
			PRINTFORM %CALLNAME:TARGET%は熱のこもった吐息を吐い
		ELSEIF EXP:0 >= 21
			PRINTFORM %CALLNAME:TARGET%はゆるんだ膣口をひくつかせ
		ELSEIF EXP:0 >= 1 && EXP:0 <= 20
			PRINTFORM %CALLNAME:TARGET%はホッとしたような表情をし
		ENDIF
		PRINTW ている…
	ELSE
		PRINTFORM %CALLNAME:TARGET%の
		SIF PALAM:3 >= PALAMLV:5
			PRINT たっぷりと
		SIF PALAM:3 >= PALAMLV:3
			PRINT 粘液をたたえた
		IF TALENT:100
			PRINT 幼いワレメに
		ELSE
			PRINT 秘裂に
		ENDIF
		PRINTL バイブの先端をあてがい馴染ませていく…

		PRINTFORM %CALLNAME:TARGET%は
		IF TALENT:0 && TALENT:85 && TALENT:76
			PRINT 自分が処女である事を忘れているかのような笑みを見せ
		ELSEIF TALENT:0 && TALENT:85
			PRINTFORM 少し寂しそうな目で%CALLNAME:PLAYER%を見
		ELSEIF TALENT:0 && TALENT:76
			PRINT 処女である事もお構いなしに、腰を揺らしておねだりをし
		ELSEIF TALENT:0 && TALENT:27
			PRINT 何かを諦めた表情で目にうっすら涙を浮かべ
		ELSEIF TALENT:0
			PRINT ガタガタと震え、喪失の恐怖に怯え

		ELSEIF EXP:0 >= 201 && TALENT:85
			PRINT 挿入への期待だけでガクガクと腰を震わせてイきそうになっ
		ELSEIF EXP:0 >= 151 && EXP:0 <= 200 && TALENT:85
			PRINTFORM とろけきった目で%CALLNAME:PLAYER%を見つめ、自分から陰唇を擦りつけ
		ELSEIF EXP:0 >= 121 && EXP:0 <= 150 && TALENT:85
			PRINT 恍惚とした表情で待ちきれないように腰をくねらせ
		ELSEIF EXP:0 >= 71 && EXP:0 <= 120 && TALENT:85
			PRINTFORM もの欲しそうに目を潤ませ%CALLNAME:PLAYER%の方を見
		ELSEIF EXP:0 >= 41 && EXP:0 <= 70 && TALENT:85
			PRINT バイブの擦れる感触に陶酔しきった吐息をつい
		ELSEIF EXP:0 >= 1 && EXP:0 <= 40 && TALENT:85
			PRINT バイブの擦れる感触に身をよじらせ愛液を滲ませて
		ELSEIF EXP:0 >= 1
			PRINT バイブの擦れる感触に肉襞をひくつかせ
		ENDIF
		PRINTW ている…

		PRINTFORML 　
			PRINTFORM %CALLNAME:TARGET%の
		SIF PALAM:3 >= PALAMLV:4
			PRINT 濡れそぼった
		PRINTFORML 性器にバイブを挿し込んだ
		PRINTFORML 　
	ENDIF
ENDIF

@EQUIP_COM11
;バイブ挿入中

;V経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:19 = 1

IF TEQUIP:90
	PRINTL ＜触手挿入中＞
ELSE
	PRINTL ＜バイブ挿入中＞
ENDIF

LOSEBASE:0 += 10
LOSEBASE:1 += 50

;ABL:Ｖ感覚をみる
IF ABL:4 == 0
	A = 40
ELSEIF ABL:4 == 1
	A = 120
ELSEIF ABL:4 == 2
	A = 300
ELSEIF ABL:4 == 3
	A = 500
ELSEIF ABL:4 == 4
	A = 650
ELSE
	A = 350 + (ABL:4 * 100)
ENDIF

;EXP:Ｖ経験をみる
;処女はありえない
IF EXP:0 < EXPLV:2
	TIMES A , 0.60
	B = 150
ELSEIF EXP:0 < EXPLV:3
	TIMES A , 1.00
	B = 20
ELSEIF EXP:0 < EXPLV:4
	TIMES A , 1.20
	B = 0
ELSEIF EXP:0 < EXPLV:5
	TIMES A , 1.40
	B = 0
ELSE
	TIMES A , 1.60
	B = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:3 < PALAMLV:1
	TIMES A , 0.10
	B += 400
	TIMES B , 3.00
	C += 1000
ELSEIF PALAM:3 < PALAMLV:2
	TIMES A , 0.40
	B += 150
	TIMES B , 1.00
	C += 200
ELSEIF PALAM:3 < PALAMLV:3
	TIMES A , 1.00
	TIMES B , 0.50
	C += 80
ELSEIF PALAM:3 < PALAMLV:4
	TIMES A , 1.40
	TIMES B , 0.20
ELSEIF PALAM:3 >= PALAMLV:4
	TIMES A , 1.80
	TIMES B , 0.10
ENDIF

;PALAM:欲情をみる
GETPALAMLV PALAM:5, 36
A = A * (8 + RESULT) / 10

;ABL:従順をみる
IF ABL:0 == 0
	TIMES A , 0.80
ELSEIF ABL:0 == 1
	TIMES A , 0.90
ELSEIF ABL:0 == 2
	TIMES A , 1.00
ELSEIF ABL:0 == 3
	TIMES A , 1.10
ELSEIF ABL:0 == 4
	TIMES A , 1.20
ELSE
	A = A * (ABL:0 * 5 + 105) / 100
ENDIF

;実行者が[淫具知識]を持っているなら効果増
SIF TALENT:PLAYER:58
	TIMES A , 1.20

;小柄体型
SIF TALENT:100
	TIMES B , 2.00

;貞操観念
IF TALENT:30
	;処女はありえない
	TIMES B, 3.00
ENDIF

SOURCE:1 += A
SOURCE:6 += B

;自慰実行時は経験＋１
IF SELECTCOM == 3
	EXP:0 += 3
	PRINTL Ｖ経験＋３
ELSE
	EXP:0 += 2
	PRINTL Ｖ経験＋２
ENDIF

IF TALENT:122 == 0 && TALENT:PLAYER:122 == 0
	PRINTS EXPNAME:40
	PRINTL +1
	EXP:40 += 1
	EXP:PLAYER:40 += 1
ELSEIF TALENT:122 == 1 && TALENT:PLAYER:122 == 1
	PRINTS EXPNAME:41
	PRINTL +1
	EXP:41 += 1
	EXP:PLAYER:41 += 1
ENDIF

SIF TEQUIP:90
	T += 1

RETURN 1

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_11
A = 0
S = 0
V = 110

;抜く時は判定不要(他の処理で利用するためA > VになるようAを設定)
IF TEQUIP:11
	A = 999
	RETURN 0
ENDIF

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;ABL:欲望
IF ABL:1
	A += ABL:1 * 3
ENDIF
;ABL:Ｖ感覚
IF ABL:4
	A += ABL:4 * 2
ENDIF
;ABL:奉仕精神
IF ABL:6
	A += ABL:6 * 4
ENDIF

IF MARK:1
	A += MARK:1 * 3
ENDIF

;PALAM:潤滑（の不足）
IF PALAM:3 < PALAMLV:3
	A -= 5
ENDIF
;PALAM:欲情
IF PALAM:5 < PALAMLV:1
	L = 0
ELSEIF PALAM:5 < PALAMLV:2
	L = 1
ELSEIF PALAM:5 < PALAMLV:3
	L = 2
ELSEIF PALAM:5 < PALAMLV:4
	L = 3
ELSEIF PALAM:5 < PALAMLV:5
	L = 4
ELSE
	L = 5
ENDIF
IF L
	A += L * 3
ENDIF

;恥じらい
IF TALENT:35
	A -= 2
	S = 1
ENDIF
;快感の否定
IF TALENT:71
	A -= 5
ENDIF
;男嫌い
IF TALENT:82 && TALENT:PLAYER:122
	A -= 12
ENDIF
;恋慕
IF TALENT:85 && ASSIPLAY == 0
	A += 5
ENDIF

;処女
IF TALENT:0
	A -= 20
ELSEIF EXP:0 < EXPLV:2
	A -= 5
ENDIF

;媚薬
IF TEQUIP:21
	A += 6
ENDIF

Y = 0
;愛液の汚れ
SIF STAIN:PLAYER:2 & 1
	Y += 1
;精液の汚れ
SIF STAIN:PLAYER:2 & 4
	Y += 3
;アナルの汚れ
SIF STAIN:PLAYER:2 & 8
	Y += 7
SIF STAIN:PLAYER:2 & 16
	Y += 15

SIF TALENT:61
	Y /= 3
SIF TALENT:62
	Y *= 2

;汚れあり
IF Y
	A -= Y
ENDIF

;好感度
IF CFLAG:2 < 1500
	B = 0
	IF CFLAG:2 < 500
		B = 50
	ELSEIF CFLAG:2 < 1000
		B = 20
	ELSEIF CFLAG:2 < 1300
		B = 10
	ELSE
		B = 5
	ENDIF
	A -= B
ENDIF
;好感度
IF CFLAG:2 >= 2000
	B = 0
	IF CFLAG:2 < 3000
		B = 5
	ELSE
		B = CFLAG:2 / 200
	ENDIF
	A += B
	S = 1
ENDIF

IF ((FLAG:23 & 1p27) == 0) && FLAG:2 != 1
	;ABL:欲望
	IF ABL:1
		SIF S
			PRINT  + 
		PRINTS ABLNAME:1
		PRINTV 'LV,ABL:1,'(,ABL:1 * 3,')
		S = 1
	ENDIF
	;ABL:Ｖ感覚
	IF ABL:4
		SIF S
			PRINT  + 
		PRINTS ABLNAME:4
		PRINTV 'LV,ABL:4,'(,ABL:4 * 2,')
		S = 1
	ENDIF
	;ABL:奉仕精神
	IF ABL:6
		SIF S
			PRINT  + 
		PRINTS ABLNAME:6
		PRINTV 'LV,ABL:6,'(,ABL:6 * 4,')
		S = 1
	ENDIF

	IF MARK:1
		SIF S
			PRINT  + 
		PRINTS MARKNAME:1
		PRINTV 'LV,MARK:1,'(,MARK:1 * 3,')
		S = 1
	ENDIF

	;PALAM:潤滑（の不足）
	IF PALAM:3 < PALAMLV:3
		PRINT  - 
		PRINT 潤滑不足
		PRINTV '(,5,')
		S = 1
	ENDIF
	;PALAM:欲情
	IF L
		SIF S
			PRINT  + 
		PRINTS PALAMNAME:5
		PRINTV 'LV,L,'(,L * 3,')
		S = 1
	ENDIF

	;恥じらい
	IF TALENT:35
		PRINT  - 
		PRINTS TALENTNAME:35
		PRINTV '(,2,')
		S = 1
	ENDIF
	;快感の否定
	IF TALENT:71
		PRINT  - 
		PRINTS TALENTNAME:71
		PRINTV '(,5,')
		S = 1
	ENDIF
	;男嫌い
	IF TALENT:82 && TALENT:PLAYER:122
		PRINT  - 
		PRINTS TALENTNAME:82
		PRINTV '(,12,')
		S = 1
	ENDIF
	;恋慕
	IF TALENT:85 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:85
		PRINTV '(,5,')
		S = 5
	ENDIF

	;処女
	IF TALENT:0
		PRINT  - 
		PRINTS TALENTNAME:0
		PRINTV '(,20,')
		S = 1
	ELSEIF EXP:0 < EXPLV:2
		PRINT  - 
		PRINTS EXPNAME:0
		PRINT 不足
		PRINTV '(,5,')
		S = 1
	ENDIF

	;媚薬
	IF TEQUIP:21
		SIF S
			PRINT  + 
		PRINTS ITEMNAME:26
		PRINTV '(,6,')
		S = 1
	ENDIF

	Y = 0
	;愛液の汚れ
	SIF STAIN:PLAYER:2 & 1
		Y += 1
	;精液の汚れ
	SIF STAIN:PLAYER:2 & 4
		Y += 3
	;アナルの汚れ
	SIF STAIN:PLAYER:2 & 8
		Y += 7
	SIF STAIN:PLAYER:2 & 16
		Y += 15

	SIF TALENT:61
		Y /= 3
	SIF TALENT:62
		Y *= 2

	;汚れあり
	IF Y
		PRINT  - 
		A -= Y
		;汚臭鈍感
		IF TALENT:61
			PRINT 汚れあり、
			PRINTS TALENTNAME:61
		ELSEIF TALENT:62
			PRINT 汚れあり、
			PRINTS TALENTNAME:62
		ELSE
			PRINT 汚れあり
		ENDIF
		PRINTV '(,Y,')
		S = 1
	ENDIF

	;好感度
	IF CFLAG:2 < 1500
		B = 0
		PRINT  - 
		IF CFLAG:2 < 500
			B = 50
		ELSEIF CFLAG:2 < 1000
			B = 20
		ELSEIF CFLAG:2 < 1300
			B = 10
		ELSE
			B = 5
		ENDIF
		PRINT 好感度不足
		PRINTV '(,B,')
		S = 1
	ENDIF
	;好感度
	IF CFLAG:2 >= 2000
		B = 0
		SIF S
			PRINT  + 
		IF CFLAG:2 < 3000
			B = 5
		ELSE
			B = CFLAG:2 / 200
		ENDIF
		PRINT 好感度
		PRINTV '(,B,')
		S = 1
	ENDIF

	;合計を表示(110以上で実行)
	PRINT  = 
	PRINTV A

	SIF A < V
		PRINT  < 
	SIF A == V
		PRINT  = 
	SIF A > V
		PRINT  > 
	PRINT 実行値
	PRINTV V
	WAIT
ENDIF

