@COM11
;バイブ
IF TEQUIP:90
	PRINTL 触手挿入
ELSE
	;実行者が[淫具知識]を持っているなら効果増
	SIF TALENT:PLAYER:淫具知識
		PRINT 高性能
	PRINTL バイブ
ENDIF

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

IF TEQUIP:90
	STR:0 = 触手挿入
ELSE
	STR:0 = バイブ
ENDIF
CALL TRAIN_MESSAGE_B

;-------------------------------------------------
;実行決定
;-------------------------------------------------
LOSEBASE:体力 += 30
LOSEBASE:気力 += 100

;ABL:Ｖ感覚をみる
IF ABL:Ｖ感覚 == 0
	SOURCE:快Ｖ = 80
ELSEIF ABL:Ｖ感覚 == 1
	SOURCE:快Ｖ = 250
ELSEIF ABL:Ｖ感覚 == 2
	SOURCE:快Ｖ = 600
ELSEIF ABL:Ｖ感覚 == 3
	SOURCE:快Ｖ = 1000
ELSEIF ABL:Ｖ感覚 == 4
	SOURCE:快Ｖ = 1300
ELSE
	SOURCE:快Ｖ = 700 + (ABL:Ｖ感覚 * 200)
ENDIF

;EXP:Ｖ経験をみる
IF EXP:Ｖ経験 < EXPLV:1
	TIMES SOURCE:快Ｖ , 0.20
	SOURCE:痛み = 5500

	EXP:異常経験 += 1
	PRINTFORML %EXPNAME:50%＋１
ELSEIF EXP:Ｖ経験 < EXPLV:2
	TIMES SOURCE:快Ｖ , 0.60
	SOURCE:痛み = 300
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES SOURCE:快Ｖ , 1.00
	SOURCE:痛み = 50
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES SOURCE:快Ｖ , 1.20
	SOURCE:痛み = 10
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES SOURCE:快Ｖ , 1.40
	SOURCE:痛み = 0
ELSE
	TIMES SOURCE:快Ｖ , 1.60
	SOURCE:痛み = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:快Ｖ , 0.10
	SOURCE:痛み += 1000
	TIMES SOURCE:痛み , 3.00
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:快Ｖ , 0.40
	SOURCE:痛み += 400
	TIMES SOURCE:痛み , 1.00
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:痛み , 0.50
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.40
	TIMES SOURCE:痛み , 0.20
ELSEIF PALAM:潤滑 >= PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.80
	TIMES SOURCE:痛み , 0.10
ENDIF

;PALAM:欲情をみる
GETPALAMLV PALAM:欲情, 36
SOURCE:快Ｖ = SOURCE:快Ｖ * (8 + RESULT) / 10

;ABL:従順をみる
IF ABL:親密 == 0
	TIMES SOURCE:快Ｖ , 0.80
ELSEIF ABL:親密 == 1
	TIMES SOURCE:快Ｖ , 0.90
ELSEIF ABL:親密 == 2
	TIMES SOURCE:快Ｖ , 1.00
ELSEIF ABL:親密 == 3
	TIMES SOURCE:快Ｖ , 1.10
ELSEIF ABL:親密 == 4
	TIMES SOURCE:快Ｖ , 1.20
ELSE
	IF SOURCE:快Ｖ < 100000
		SOURCE:快Ｖ = SOURCE:快Ｖ * (ABL:親密 * 5 + 105) / 100
	ELSEIF SOURCE:快Ｖ < 100000000
		SOURCE:快Ｖ = SOURCE:快Ｖ / 100 * (ABL:親密 * 5 + 105)
	ENDIF
ENDIF

;小柄体型
SIF TALENT:小柄体型
	TIMES SOURCE:痛み , 2.00

;実行者が[淫具知識]を持っているなら効果増
SIF TALENT:PLAYER:淫具知識
	TIMES SOURCE:快Ｖ , 1.20

EXP:Ｖ経験 += 1
PRINTL Ｖ経験＋１

;バイブの着脱
IF TEQUIP:90
	IF TEQUIP:11
		TEQUIP:11 = 0
	ELSE
		TEQUIP:11 = 1
		STAIN:ヴァギナ |= 2
		STAIN:ヴァギナ |= 4
	ENDIF
	T = 0
ELSE
	IF TEQUIP:11
		TEQUIP:11 = 0
	ELSE
		TEQUIP:11 = 1
	ENDIF
ENDIF

;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:従順をみる
	IF ABL:親密 <= 1
		SOURCE:受動 += 300 + (ABL:親密 * 50)
	ELSEIF ABL:親密 <= 3
		SOURCE:受動 += 600 + (ABL:親密 * 100)
	ELSEIF ABL:親密 <= 5
		SOURCE:受動 += 800 + (ABL:親密 * 120)
	ELSEIF ABL:親密 <= 8
		SOURCE:受動 += 1000 + (ABL:親密 * 140)
	ELSEIF ABL:親密 <= 11
		SOURCE:受動 += 1200 + (ABL:親密 * 160)
	ELSE
		SOURCE:受動 += 1500 + (ABL:親密 * 180)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:サドっ気 == 0
		SOURCE:征服 = 600
	ELSEIF ABL:サドっ気 == 1
		SOURCE:征服 = 900
	ELSEIF ABL:サドっ気 == 2
		SOURCE:征服 = 1200
	ELSEIF ABL:サドっ気 == 3
		SOURCE:征服 = 1600
	ELSEIF ABL:サドっ気 == 4
		SOURCE:征服 = 2000
	ELSE
		SOURCE:征服 = 1000 + (ABL:サドっ気 * 300)
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:10 + 5
;合意がないと好感度が下がる
IF TALENT:合意 == 0 && TFLAG:48 == 0
	A -= 9
	SOURCE:反感追加 += 400
ENDIF
TFLAG:30 += A
TFLAG:37 += A / 2

RETURN 1

@TRAIN_MESSAGE_COM11
;バイブの着脱
IF TEQUIP:90
	IF TEQUIP:11
		PRINTFORML 触手が%CALLNAME:TARGET%の性器から引き抜かれた
	ELSE
		PRINTFORML 細かい突起に覆われた触手が%CALLNAME:TARGET%の性器に潜り込んでいった
	ENDIF
ELSE
	IF TEQUIP:11
		PRINTFORML %CALLNAME:TARGET%から蠢くバイブをゆっくりと引き抜いた。
		IF EXP:Ｖ経験 >= 201 && TALENT:恋慕
			PRINTFORM %CALLNAME:TARGET%は、膣内の喪失感に耐えかねて涙と涎をこぼし
		ELSEIF EXP:Ｖ経験 >= 151 && EXP:Ｖ経験 <= 200 && TALENT:恋慕
			PRINTFORM %CALLNAME:TARGET%の膣内に溜まっていた粘液が掻き出されて滴り、水溜りを作っ
		ELSEIF EXP:Ｖ経験 >= 121 && EXP:Ｖ経験 <= 150 && TALENT:恋慕
			PRINTFORM バイブが抜かれた%CALLNAME:TARGET%の肉襞からボタボタと愛液が滴っ
		ELSEIF EXP:Ｖ経験 >= 71 && EXP:Ｖ経験 <= 120 && TALENT:恋慕
			PRINTFORM %CALLNAME:TARGET%はもじもじと股を擦り合わせて%CALLNAME:PLAYER%の方を見
		ELSEIF EXP:Ｖ経験 >= 41 && EXP:Ｖ経験 <= 70 && TALENT:恋慕
			PRINTFORM %CALLNAME:TARGET%は熱のこもった吐息を吐い
		ELSEIF EXP:Ｖ経験 >= 21
			PRINTFORM %CALLNAME:TARGET%はゆるんだ膣口をひくつかせ
		ELSEIF EXP:Ｖ経験 >= 1 && EXP:Ｖ経験 <= 20
			PRINTFORM %CALLNAME:TARGET%はホッとしたような表情をし
		ENDIF
		PRINTW ている…
	ELSE
		PRINTFORM %CALLNAME:TARGET%の
		SIF PALAM:潤滑 >= PALAMLV:5
			PRINT たっぷりと
		SIF PALAM:潤滑 >= PALAMLV:3
			PRINT 粘液をたたえた
		IF TALENT:小柄体型
			PRINT 幼いワレメに
		ELSE
			PRINT 秘裂に
		ENDIF
		PRINTL バイブの先端をあてがい馴染ませていく…

		PRINTFORM %CALLNAME:TARGET%は
		IF TALENT:処女 && TALENT:恋慕 && TALENT:淫乱
			PRINT 自分が処女である事を忘れているかのような笑みを見せ
		ELSEIF TALENT:処女 && TALENT:恋慕
			PRINTFORM 少し寂しそうな目で%CALLNAME:PLAYER%を見
		ELSEIF TALENT:処女 && TALENT:淫乱
			PRINT 処女である事もお構いなしに、腰を揺らしておねだりをし
		ELSEIF TALENT:処女 && TALENT:一線越えない
			PRINT 何かを諦めた表情で目にうっすら涙を浮かべ
		ELSEIF TALENT:処女
			PRINT ガタガタと震え、喪失の恐怖に怯え

		ELSEIF EXP:Ｖ経験 >= 201 && TALENT:恋慕
			PRINT 挿入への期待だけでガクガクと腰を震わせてイきそうになっ
		ELSEIF EXP:Ｖ経験 >= 151 && EXP:Ｖ経験 <= 200 && TALENT:恋慕
			PRINTFORM とろけきった目で%CALLNAME:PLAYER%を見つめ、自分から陰唇を擦りつけ
		ELSEIF EXP:Ｖ経験 >= 121 && EXP:Ｖ経験 <= 150 && TALENT:恋慕
			PRINT 恍惚とした表情で待ちきれないように腰をくねらせ
		ELSEIF EXP:Ｖ経験 >= 71 && EXP:Ｖ経験 <= 120 && TALENT:恋慕
			PRINTFORM もの欲しそうに目を潤ませ%CALLNAME:PLAYER%の方を見
		ELSEIF EXP:Ｖ経験 >= 41 && EXP:Ｖ経験 <= 70 && TALENT:恋慕
			PRINT バイブの擦れる感触に陶酔しきった吐息をつい
		ELSEIF EXP:Ｖ経験 >= 1 && EXP:Ｖ経験 <= 40 && TALENT:恋慕
			PRINT バイブの擦れる感触に身をよじらせ愛液を滲ませて
		ELSEIF EXP:Ｖ経験 >= 1
			PRINT バイブの擦れる感触に肉襞をひくつかせ
		ENDIF
		PRINTW ている…

		PRINTFORML 　
			PRINTFORM %CALLNAME:TARGET%の
		SIF PALAM:潤滑 >= PALAMLV:4
			PRINT 濡れそぼった
		PRINTFORML 性器にバイブを挿し込んだ
		PRINTFORML 　
	ENDIF
ENDIF

@EQUIP_COM11
;バイブ挿入中

;V経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:19 = 1

IF TEQUIP:90
	PRINTL ＜触手挿入中＞
ELSE
	PRINTL ＜バイブ挿入中＞
ENDIF

LOSEBASE:体力 += 10
LOSEBASE:気力 += 50

;ABL:Ｖ感覚をみる
IF ABL:Ｖ感覚 == 0
	A = 40
ELSEIF ABL:Ｖ感覚 == 1
	A = 120
ELSEIF ABL:Ｖ感覚 == 2
	A = 300
ELSEIF ABL:Ｖ感覚 == 3
	A = 500
ELSEIF ABL:Ｖ感覚 == 4
	A = 650
ELSE
	A = 350 + (ABL:Ｖ感覚 * 100)
ENDIF

;EXP:Ｖ経験をみる
;処女はありえない
IF EXP:Ｖ経験 < EXPLV:2
	TIMES A , 0.60
	B = 150
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES A , 1.00
	B = 20
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES A , 1.20
	B = 0
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES A , 1.40
	B = 0
ELSE
	TIMES A , 1.60
	B = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES A , 0.10
	B += 400
	TIMES B , 3.00
	C += 1000
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES A , 0.40
	B += 150
	TIMES B , 1.00
	C += 200
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES A , 1.00
	TIMES B , 0.50
	C += 80
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES A , 1.40
	TIMES B , 0.20
ELSEIF PALAM:潤滑 >= PALAMLV:4
	TIMES A , 1.80
	TIMES B , 0.10
ENDIF

;PALAM:欲情をみる
GETPALAMLV PALAM:欲情, 36
A = A * (8 + RESULT) / 10

;ABL:従順をみる
IF ABL:親密 == 0
	TIMES A , 0.80
ELSEIF ABL:親密 == 1
	TIMES A , 0.90
ELSEIF ABL:親密 == 2
	TIMES A , 1.00
ELSEIF ABL:親密 == 3
	TIMES A , 1.10
ELSEIF ABL:親密 == 4
	TIMES A , 1.20
ELSE
	A = A * (ABL:親密 * 5 + 105) / 100
ENDIF

;実行者が[淫具知識]を持っているなら効果増
SIF TALENT:PLAYER:淫具知識
	TIMES A , 1.20

;小柄体型
SIF TALENT:小柄体型
	TIMES B , 2.00

;貞操観念
IF TALENT:貞操観念
	;処女はありえない
	TIMES B, 3.00
ENDIF

SOURCE:快Ｖ += A
SOURCE:痛み += B

;自慰実行時は経験＋１
IF SELECTCOM == 3
	EXP:Ｖ経験 += 3
	PRINTL Ｖ経験＋３
ELSE
	EXP:Ｖ経験 += 2
	PRINTL Ｖ経験＋２
ENDIF

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	PRINTS EXPNAME:40
	PRINTL +1
	EXP:レズ経験 += 1
	EXP:PLAYER:レズ経験 += 1
ELSEIF TALENT:オトコ == 1 && TALENT:PLAYER:オトコ == 1
	PRINTS EXPNAME:41
	PRINTL +1
	EXP:ＢＬ経験 += 1
	EXP:PLAYER:ＢＬ経験 += 1
ENDIF

SIF TEQUIP:90
	T += 1

RETURN 1

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_11
A = 0
S = 0
V = 110

;抜く時は判定不要(他の処理で利用するためA > VになるようAを設定)
IF TEQUIP:11
	A = 999
	RETURN 0
ENDIF

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;ABL:欲望
IF ABL:欲望
	A += ABL:欲望 * 3
ENDIF
;ABL:Ｖ感覚
IF ABL:Ｖ感覚
	A += ABL:Ｖ感覚 * 2
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	A += ABL:奉仕精神 * 4
ENDIF

IF MARK:快楽依存
	A += MARK:快楽依存 * 3
ENDIF

;PALAM:潤滑（の不足）
IF PALAM:潤滑 < PALAMLV:3
	A -= 5
ENDIF
;PALAM:欲情
IF PALAM:欲情 < PALAMLV:1
	L = 0
ELSEIF PALAM:欲情 < PALAMLV:2
	L = 1
ELSEIF PALAM:欲情 < PALAMLV:3
	L = 2
ELSEIF PALAM:欲情 < PALAMLV:4
	L = 3
ELSEIF PALAM:欲情 < PALAMLV:5
	L = 4
ELSE
	L = 5
ENDIF
IF L
	A += L * 3
ENDIF

;恥じらい
IF TALENT:恥じらい
	A -= 2
	S = 1
ENDIF
;快感の否定
IF TALENT:快感の否定
	A -= 5
ENDIF
;男嫌い
IF TALENT:男嫌い && TALENT:PLAYER:オトコ
	A -= 12
ENDIF
;恋慕
IF TALENT:恋慕 && ASSIPLAY == 0
	A += 5
ENDIF

;処女
IF TALENT:処女
	A -= 20
ELSEIF EXP:Ｖ経験 < EXPLV:2
	A -= 5
ENDIF

;媚薬
IF TEQUIP:21
	A += 6
ENDIF

Y = 0
;愛液の汚れ
SIF STAIN:PLAYER:ペニス & 1
	Y += 1
;精液の汚れ
SIF STAIN:PLAYER:ペニス & 4
	Y += 3
;アナルの汚れ
SIF STAIN:PLAYER:ペニス & 8
	Y += 7
SIF STAIN:PLAYER:ペニス & 16
	Y += 15

SIF TALENT:汚臭鈍感
	Y /= 3
SIF TALENT:汚臭敏感
	Y *= 2

;汚れあり
IF Y
	A -= Y
ENDIF

;好感度
IF CFLAG:2 < 1500
	B = 0
	IF CFLAG:2 < 500
		B = 50
	ELSEIF CFLAG:2 < 1000
		B = 20
	ELSEIF CFLAG:2 < 1300
		B = 10
	ELSE
		B = 5
	ENDIF
	A -= B
ENDIF
;好感度
IF CFLAG:2 >= 2000
	B = 0
	IF CFLAG:2 < 3000
		B = 5
	ELSE
		B = CFLAG:2 / 200
	ENDIF
	A += B
	S = 1
ENDIF

IF ((FLAG:23 & 1p27) == 0) && FLAG:2 != 1
	;ABL:欲望
	IF ABL:欲望
		SIF S
			PRINT  + 
		PRINTS ABLNAME:1
		PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 3,')
		S = 1
	ENDIF
	;ABL:Ｖ感覚
	IF ABL:Ｖ感覚
		SIF S
			PRINT  + 
		PRINTS ABLNAME:4
		PRINTV 'LV,ABL:Ｖ感覚,'(,ABL:Ｖ感覚 * 2,')
		S = 1
	ENDIF
	;ABL:奉仕精神
	IF ABL:奉仕精神
		SIF S
			PRINT  + 
		PRINTS ABLNAME:6
		PRINTV 'LV,ABL:奉仕精神,'(,ABL:奉仕精神 * 4,')
		S = 1
	ENDIF

	IF MARK:快楽依存
		SIF S
			PRINT  + 
		PRINTS MARKNAME:1
		PRINTV 'LV,MARK:快楽依存,'(,MARK:快楽依存 * 3,')
		S = 1
	ENDIF

	;PALAM:潤滑（の不足）
	IF PALAM:潤滑 < PALAMLV:3
		PRINT  - 
		PRINT 潤滑不足
		PRINTV '(,5,')
		S = 1
	ENDIF
	;PALAM:欲情
	IF L
		SIF S
			PRINT  + 
		PRINTS PALAMNAME:5
		PRINTV 'LV,L,'(,L * 3,')
		S = 1
	ENDIF

	;恥じらい
	IF TALENT:恥じらい
		PRINT  - 
		PRINTS TALENTNAME:35
		PRINTV '(,2,')
		S = 1
	ENDIF
	;快感の否定
	IF TALENT:快感の否定
		PRINT  - 
		PRINTS TALENTNAME:71
		PRINTV '(,5,')
		S = 1
	ENDIF
	;男嫌い
	IF TALENT:男嫌い && TALENT:PLAYER:オトコ
		PRINT  - 
		PRINTS TALENTNAME:82
		PRINTV '(,12,')
		S = 1
	ENDIF
	;恋慕
	IF TALENT:恋慕 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:85
		PRINTV '(,5,')
		S = 5
	ENDIF

	;処女
	IF TALENT:処女
		PRINT  - 
		PRINTS TALENTNAME:0
		PRINTV '(,20,')
		S = 1
	ELSEIF EXP:Ｖ経験 < EXPLV:2
		PRINT  - 
		PRINTS EXPNAME:0
		PRINT 不足
		PRINTV '(,5,')
		S = 1
	ENDIF

	;媚薬
	IF TEQUIP:21
		SIF S
			PRINT  + 
		PRINTS ITEMNAME:26
		PRINTV '(,6,')
		S = 1
	ENDIF

	Y = 0
	;愛液の汚れ
	SIF STAIN:PLAYER:ペニス & 1
		Y += 1
	;精液の汚れ
	SIF STAIN:PLAYER:ペニス & 4
		Y += 3
	;アナルの汚れ
	SIF STAIN:PLAYER:ペニス & 8
		Y += 7
	SIF STAIN:PLAYER:ペニス & 16
		Y += 15

	SIF TALENT:汚臭鈍感
		Y /= 3
	SIF TALENT:汚臭敏感
		Y *= 2

	;汚れあり
	IF Y
		PRINT  - 
		A -= Y
		;汚臭鈍感
		IF TALENT:汚臭鈍感
			PRINT 汚れあり、
			PRINTS TALENTNAME:61
		ELSEIF TALENT:汚臭敏感
			PRINT 汚れあり、
			PRINTS TALENTNAME:62
		ELSE
			PRINT 汚れあり
		ENDIF
		PRINTV '(,Y,')
		S = 1
	ENDIF

	;好感度
	IF CFLAG:2 < 1500
		B = 0
		PRINT  - 
		IF CFLAG:2 < 500
			B = 50
		ELSEIF CFLAG:2 < 1000
			B = 20
		ELSEIF CFLAG:2 < 1300
			B = 10
		ELSE
			B = 5
		ENDIF
		PRINT 好感度不足
		PRINTV '(,B,')
		S = 1
	ENDIF
	;好感度
	IF CFLAG:2 >= 2000
		B = 0
		SIF S
			PRINT  + 
		IF CFLAG:2 < 3000
			B = 5
		ELSE
			B = CFLAG:2 / 200
		ENDIF
		PRINT 好感度
		PRINTV '(,B,')
		S = 1
	ENDIF

	;合計を表示(110以上で実行)
	PRINT  = 
	PRINTV A

	SIF A < V
		PRINT  < 
	SIF A == V
		PRINT  = 
	SIF A > V
		PRINT  > 
	PRINT 実行値
	PRINTV V
	WAIT
ENDIF

