@COM305
;家事

PRINTL 家事
CALL GET_SUCCESS_RATE
B = 90 + A / 10
SIF B > 99
	B = 99
A = RAND:100
IF A <= 9
	;10％で大成功
	TFLAG:18 = 1
ELSEIF A >= B
	;10～1％で失敗
	TFLAG:18 = -1
ELSE
	;残りは成功
	TFLAG:18 = 0
ENDIF


;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

STR:0 = 家事
CALL TRAIN_MESSAGE_B

LOSEBASE:1 += 150

;固定で獲得するソース
SOURCE:18 = 50
SOURCE:19 = 500

;ABL:従順をみる
IF ABL:0 <= 1
	SOURCE:18 += (ABL:0 * 20)
	SOURCE:19 += (ABL:0 * 65)
ELSEIF ABL:0 <= 3
	SOURCE:18 += 100 + (ABL:0 * 20)
	SOURCE:19 += 200 + (ABL:0 * 70)
ELSEIF ABL:0 <= 5
	SOURCE:18 += 200 + (ABL:0 * 20)
	SOURCE:19 += 500 + (ABL:0 * 70)
ELSEIF ABL:0 <= 8
	SOURCE:18 += 300 + (ABL:0 * 30)
	SOURCE:19 += 700 + (ABL:0 * 80)
ELSEIF ABL:0 <= 11
	SOURCE:18 += 400 + (ABL:0 * 30)
	SOURCE:19 += 1200 + (ABL:0 * 85)
ELSE
	SOURCE:18 += 800 + (ABL:0 * 20)
	SOURCE:19 += 2000 + (ABL:0 * 100)
ENDIF

;好感度をみる
IF CFLAG:2 <= 500
	SOURCE:18 += CFLAG:2
ELSEIF CFLAG:2 <= 5000
	SOURCE:18 += 500 + (CFLAG:2 - 500) / 3
ELSE
	SOURCE:18 += 2000 + (CFLAG:2 - 5000) / 5
ENDIF
SIF SOURCE:18 < 0
	SOURCE:18 = 0


;ABL:技巧をみる
SOURCE:19 = SOURCE:19 * (ABL:2 * 7 + 100) / 100

;ABL:金銭感覚をみる
SOURCE:19 = SOURCE:19 * (ABL:89 * 10 + 100) / 100

;ABL:料理技術をみる
SOURCE:18 += ABL:90 * 50
SOURCE:19 = SOURCE:19 * (ABL:90 * 15 + 100) / 100


;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:従順をみる
	IF ABL:0 <= 1
		SOURCE:21 += 100 + (ABL:0 * 20)
	ELSEIF ABL:0 <= 3
		SOURCE:21 += 200 + (ABL:0 * 30)
	ELSEIF ABL:0 <= 5
		SOURCE:21 += 300 + (ABL:0 * 40)
	ELSEIF ABL:0 <= 8
		SOURCE:21 += 400 + (ABL:0 * 50)
	ELSEIF ABL:0 <= 11
		SOURCE:21 += 500 + (ABL:0 * 60)
	ELSE
		SOURCE:21 += 600 + (ABL:0 * 70)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:15 == 0
		SOURCE:20 = 200
	ELSEIF ABL:15 == 1
		SOURCE:20 = 300
	ELSEIF ABL:15 == 2
		SOURCE:20 = 400
	ELSEIF ABL:15 == 3
		SOURCE:20 = 550
	ELSEIF ABL:15 == 4
		SOURCE:20 = 700
	ELSE
		SOURCE:20 = 300 + (ABL:15 * 100)
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:5 - 2
TFLAG:30 += A
B = 1

IF TFLAG:18 == -1
	TIMES SOURCE:18 , 0.10
	TIMES SOURCE:19 , 0.50
	TIMES SOURCE:20 , 0.50
	TIMES SOURCE:21 , 0.50
	TFLAG:30 -= 10
	TFLAG:37 -= 5
ELSEIF TFLAG:18 == 0
	TIMES SOURCE:18 , 1.00
	TIMES SOURCE:19 , 1.00
	TIMES SOURCE:20 , 1.00
	TIMES SOURCE:21 , 1.00
	TFLAG:37 += A
ELSE
	TIMES SOURCE:18 , 2.00
	TIMES SOURCE:19 , 2.00
	TIMES SOURCE:20 , 2.00
	TIMES SOURCE:21 , 2.00
	TFLAG:30 += 2
	TFLAG:37 += 5 + A
	B += 1
ENDIF

PRINTFORMW %EXPNAME:90%+{B}
EXP:90 += B

RETURN 1

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_305

A = 0
S = 0
V = 20

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;男嫌い
IF TALENT:82 && TALENT:PLAYER:122
	A -= 7
ENDIF
;献身的
IF TALENT:63
	A += 6
ENDIF
;恋慕
IF TALENT:85 && ASSIPLAY == 0
	A += 5
	S = 5
ENDIF

;好感度
IF CFLAG:2 < 300
	B = 0
	IF CFLAG:2 < 50
		B = 20
	ELSEIF CFLAG:2 < 150
		B = 10
	ELSE
		B = 5
	ENDIF
	A -= B
ENDIF

;好感度
IF CFLAG:2 >= 400
	B = 0
	B = CFLAG:2 / 50
	A += B
ENDIF

IF ((FLAG:23 & 1p27) == 0) && FLAG:2 != 1
	;男嫌い
	IF TALENT:82 && TALENT:PLAYER:122
		PRINT  - 
		PRINTS TALENTNAME:82
		PRINTV '(,7,')
		S = 1
	ENDIF
	;献身的
	IF TALENT:63
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:63
		PRINTV '(,6,')
		S = 1
	ENDIF
	;恋慕
	IF TALENT:85 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:85
		PRINTV '(,5,')
		S = 5
	ENDIF

	;好感度
	IF CFLAG:2 < 300
		B = 0
		PRINT  - 
		IF CFLAG:2 < 50
			B = 20
		ELSEIF CFLAG:2 < 150
			B = 10
		ELSE
			B = 5
		ENDIF
		PRINT 好感度不足
		PRINTV '(,B,')
		S = 1
	ENDIF

	;好感度
	IF CFLAG:2 >= 400
		B = 0
		SIF S
			PRINT  + 
		B = CFLAG:2 / 50
		PRINT 好感度
		PRINTV '(,B,')
		S = 1
	ENDIF

	;合計を表示(20以上で実行)
	PRINT  = 
	PRINTV A

	IF A < V
		PRINT  < 
		PRINT 実行値
		PRINTV V
	ELSEIF A == V
		PRINT  = 
		PRINT 実行値
		PRINTV V
	ELSEIF A > V
		PRINT  > 
		PRINT 実行値
		PRINTV V
	ENDIF
	WAIT
ENDIF



