@COM305
;家事

PRINTL 家事
CALL GET_SUCCESS_RATE
B = 90 + A / 10
SIF B > 99
	B = 99
A = RAND:100
IF A <= 9
	;10％で大成功
	TFLAG:18 = 1
ELSEIF A >= B
	;10～1％で失敗
	TFLAG:18 = -1
ELSE
	;残りは成功
	TFLAG:18 = 0
ENDIF


;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

STR:0 = 家事
CALL TRAIN_MESSAGE_B

LOSEBASE:気力 += 150

;固定で獲得するソース
SOURCE:歓楽 = 50
SOURCE:家務 = 500

;ABL:従順をみる
IF ABL:親密 <= 1
	SOURCE:歓楽 += (ABL:親密 * 20)
	SOURCE:家務 += (ABL:親密 * 65)
ELSEIF ABL:親密 <= 3
	SOURCE:歓楽 += 100 + (ABL:親密 * 20)
	SOURCE:家務 += 200 + (ABL:親密 * 70)
ELSEIF ABL:親密 <= 5
	SOURCE:歓楽 += 200 + (ABL:親密 * 20)
	SOURCE:家務 += 500 + (ABL:親密 * 70)
ELSEIF ABL:親密 <= 8
	SOURCE:歓楽 += 300 + (ABL:親密 * 30)
	SOURCE:家務 += 700 + (ABL:親密 * 80)
ELSEIF ABL:親密 <= 11
	SOURCE:歓楽 += 400 + (ABL:親密 * 30)
	SOURCE:家務 += 1200 + (ABL:親密 * 85)
ELSE
	SOURCE:歓楽 += 800 + (ABL:親密 * 20)
	SOURCE:家務 += 2000 + (ABL:親密 * 100)
ENDIF

;好感度をみる
IF CFLAG:2 <= 500
	SOURCE:歓楽 += CFLAG:2
ELSEIF CFLAG:2 <= 5000
	SOURCE:歓楽 += 500 + (CFLAG:2 - 500) / 3
ELSE
	SOURCE:歓楽 += 2000 + (CFLAG:2 - 5000) / 5
ENDIF
SIF SOURCE:歓楽 < 0
	SOURCE:歓楽 = 0


;ABL:技巧をみる
SOURCE:家務 = SOURCE:家務 * (ABL:技巧 * 7 + 100) / 100

;ABL:金銭感覚をみる
SOURCE:家務 = SOURCE:家務 * (ABL:金銭感覚 * 10 + 100) / 100

;ABL:料理技術をみる
SOURCE:歓楽 += ABL:料理技能 * 50
SOURCE:家務 = SOURCE:家務 * (ABL:料理技能 * 15 + 100) / 100


;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:従順をみる
	IF ABL:親密 <= 1
		SOURCE:受動 += 100 + (ABL:親密 * 20)
	ELSEIF ABL:親密 <= 3
		SOURCE:受動 += 200 + (ABL:親密 * 30)
	ELSEIF ABL:親密 <= 5
		SOURCE:受動 += 300 + (ABL:親密 * 40)
	ELSEIF ABL:親密 <= 8
		SOURCE:受動 += 400 + (ABL:親密 * 50)
	ELSEIF ABL:親密 <= 11
		SOURCE:受動 += 500 + (ABL:親密 * 60)
	ELSE
		SOURCE:受動 += 600 + (ABL:親密 * 70)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:サドっ気 == 0
		SOURCE:征服 = 200
	ELSEIF ABL:サドっ気 == 1
		SOURCE:征服 = 300
	ELSEIF ABL:サドっ気 == 2
		SOURCE:征服 = 400
	ELSEIF ABL:サドっ気 == 3
		SOURCE:征服 = 550
	ELSEIF ABL:サドっ気 == 4
		SOURCE:征服 = 700
	ELSE
		SOURCE:征服 = 300 + (ABL:サドっ気 * 100)
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:5 - 2
TFLAG:30 += A
B = 1

IF TFLAG:18 == -1
	TIMES SOURCE:歓楽 , 0.10
	TIMES SOURCE:家務 , 0.50
	TIMES SOURCE:征服 , 0.50
	TIMES SOURCE:受動 , 0.50
	TFLAG:30 -= 10
	TFLAG:37 -= 5
ELSEIF TFLAG:18 == 0
	TIMES SOURCE:歓楽 , 1.00
	TIMES SOURCE:家務 , 1.00
	TIMES SOURCE:征服 , 1.00
	TIMES SOURCE:受動 , 1.00
	TFLAG:37 += A
ELSE
	TIMES SOURCE:歓楽 , 2.00
	TIMES SOURCE:家務 , 2.00
	TIMES SOURCE:征服 , 2.00
	TIMES SOURCE:受動 , 2.00
	TFLAG:30 += 2
	TFLAG:37 += 5 + A
	B += 1
ENDIF

PRINTFORMW %EXPNAME:90%+{B}
EXP:家事経験 += B

RETURN 1

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_305

A = 0
S = 0
V = 20

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;男嫌い
IF TALENT:男嫌い && TALENT:PLAYER:オトコ
	A -= 7
ENDIF
;献身的
IF TALENT:献身的
	A += 6
ENDIF
;恋慕
IF TALENT:恋慕 && ASSIPLAY == 0
	A += 5
	S = 5
ENDIF

;好感度
IF CFLAG:2 < 300
	B = 0
	IF CFLAG:2 < 50
		B = 20
	ELSEIF CFLAG:2 < 150
		B = 10
	ELSE
		B = 5
	ENDIF
	A -= B
ENDIF

;好感度
IF CFLAG:2 >= 400
	B = 0
	B = CFLAG:2 / 50
	A += B
ENDIF

IF ((FLAG:23 & 1p27) == 0) && FLAG:2 != 1
	;男嫌い
	IF TALENT:男嫌い && TALENT:PLAYER:オトコ
		PRINT  - 
		PRINTS TALENTNAME:82
		PRINTV '(,7,')
		S = 1
	ENDIF
	;献身的
	IF TALENT:献身的
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:63
		PRINTV '(,6,')
		S = 1
	ENDIF
	;恋慕
	IF TALENT:恋慕 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:85
		PRINTV '(,5,')
		S = 5
	ENDIF

	;好感度
	IF CFLAG:2 < 300
		B = 0
		PRINT  - 
		IF CFLAG:2 < 50
			B = 20
		ELSEIF CFLAG:2 < 150
			B = 10
		ELSE
			B = 5
		ENDIF
		PRINT 好感度不足
		PRINTV '(,B,')
		S = 1
	ENDIF

	;好感度
	IF CFLAG:2 >= 400
		B = 0
		SIF S
			PRINT  + 
		B = CFLAG:2 / 50
		PRINT 好感度
		PRINTV '(,B,')
		S = 1
	ENDIF

	;合計を表示(20以上で実行)
	PRINT  = 
	PRINTV A

	IF A < V
		PRINT  < 
		PRINT 実行値
		PRINTV V
	ELSEIF A == V
		PRINT  = 
		PRINT 実行値
		PRINTV V
	ELSEIF A > V
		PRINT  > 
		PRINT 実行値
		PRINTV V
	ENDIF
	WAIT
ENDIF



