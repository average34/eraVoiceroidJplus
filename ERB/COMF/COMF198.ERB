@COM_ABLE198
;お外で楽しむ実行判定
;COM_ABLE共通判定
CALL COM_ABLE_COMMON, SELECTCOM
SIF RESULT == 0
	RETURN 0
;ウフフ中じゃないと無理
SIF TFLAG:44 == 0
	RETURN 0
;野外プレイ中じゃないとダメ
SIF TEQUIP:54 == 0
	RETURN 0
RETURN 1

@COM198
;お外を楽しむ

PRINTL お外を楽しむ
STR:0 = お外を楽しむ

;地の文カットのために先にTFLAG:17を取得
IF RAND:4 == 0
	TFLAG:17 = 0
ELSEIF RAND:3 == 0
	TFLAG:17 = 1
ELSEIF RAND:2 == 0
	TFLAG:17 = 2
ELSE
	TFLAG:17 = 3
ENDIF

CALL TRAIN_MESSAGE_B


;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOSEBASE:0 += 0
LOSEBASE:1 += 30

SOURCE:14 = 100
SOURCE:12 = 120

;奉仕精神をみる
IF ABL:奉仕精神 == 0
	SOURCE:3 = 10
ELSEIF ABL:奉仕精神 == 1
	SOURCE:3 = 30
ELSEIF ABL:奉仕精神 == 2
	SOURCE:3 = 80
ELSEIF ABL:奉仕精神 == 3
	SOURCE:3 = 150
ELSEIF ABL:奉仕精神 == 4
	SOURCE:3 = 230
ELSE
	SOURCE:3 = 200 + (ABL:奉仕精神 * 20)
ENDIF

;ABL:マゾっ気をみる
IF ABL:マゾっ気 == 0
	TIMES SOURCE:3 , 0.80
	TIMES SOURCE:12 , 0.80
	SOURCE:10 = 10
	SOURCE:11 = 10
ELSEIF ABL:マゾっ気 == 1
	TIMES SOURCE:3 , 1.00
	TIMES SOURCE:12 , 1.00
	SOURCE:10 = 40
	SOURCE:11 = 60
ELSEIF ABL:マゾっ気 == 2
	TIMES SOURCE:3 , 1.30
	TIMES SOURCE:12 , 1.20
	SOURCE:10 = 80
	SOURCE:11 = 120
ELSEIF ABL:マゾっ気 == 3
	TIMES SOURCE:3 , 1.40
	TIMES SOURCE:12 , 1.40
	SOURCE:10 = 140
	SOURCE:11 = 240
ELSEIF ABL:マゾっ気 == 4
	TIMES SOURCE:3 , 1.70
	TIMES SOURCE:12 , 1.50
	SOURCE:10 = 220
	SOURCE:11 = 360
ELSE
	SOURCE:3 = SOURCE:3 * (ABL:マゾっ気 * 15 + 125) / 100
	SOURCE:12 = SOURCE:12 * (ABL:マゾっ気 * 10 + 120) / 100
	SOURCE:10 = 100 + (ABL:マゾっ気 * 20)
	SOURCE:11 = 150 + (ABL:マゾっ気 * 35)
ENDIF

SIF PALAM:5 >= PALAMLV:3
	TFLAG:200 = 1

;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:従順をみる
	IF ABL:親密 <= 1
		SOURCE:21 += 300 + (ABL:親密 * 50)
	ELSEIF ABL:親密 <= 3
		SOURCE:21 += 600 + (ABL:親密 * 100)
	ELSEIF ABL:親密 <= 5
		SOURCE:21 += 800 + (ABL:親密 * 120)
	ELSEIF ABL:親密 <= 8
		SOURCE:21 += 1000 + (ABL:親密 * 140)
	ELSEIF ABL:親密 <= 11
		SOURCE:21 += 1200 + (ABL:親密 * 160)
	ELSE
		SOURCE:21 += 1500 + (ABL:親密 * 180)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:サドっ気 == 0
		SOURCE:20 = 600
	ELSEIF ABL:サドっ気 == 1
		SOURCE:20 = 900
	ELSEIF ABL:サドっ気 == 2
		SOURCE:20 = 1200
	ELSEIF ABL:サドっ気 == 3
		SOURCE:20 = 1600
	ELSEIF ABL:サドっ気 == 4
		SOURCE:20 = 2000
	ELSE
		SOURCE:20 = 1000 + (ABL:サドっ気 * 300)
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:10 + 5
SIF TALENT:85 || TALENT:153
	A += 2
SIF TALENT:88
	A += 3
;合意がないと好感度が下がる
IF TALENT:152 == 0 && TFLAG:48 == 0
	A -= 8
	SOURCE:15 += 300
ENDIF
TFLAG:30 += A
TFLAG:37 += A

RETURN 1

;お外で楽しむ
@TRAIN_MESSAGE_COM198
PRINTFORM %CALLNAME:PLAYER%は
IF TFLAG:17 == 0
	PRINTFORMW %CALLNAME:TARGET%を景色のいい場所に連れていった…
	IF TALENT:85 && RAND:3 >= 1
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:MASTER%に抱きついたまま、うっとりしている…
	ELSEIF TALENT:153
		PRINTFORMW %CALLNAME:TARGET%は目の前の景色に心を奪われているようだ…
	ELSEIF TALENT:152
		PRINTFORMW %CALLNAME:TARGET%はどうしても景色を楽しむ気にならないようだ…
	ELSE
		PRINTFORMW 今の%CALLNAME:TARGET%には景色など目に入らないようだ…
	ENDIF
ELSEIF TFLAG:17 == 1
	PRINTFORMW お弁当を取りだした…
	IF TALENT:85 && RAND:3 >= 1
		PRINTFORMW %CALLNAME:TARGET%もこっそりお弁当を作ってきたらしく、二人で食べさせっこをして楽しんだ…
	ELSEIF TALENT:153
		PRINTFORMW %CALLNAME:PLAYER%があ～んと言うと、%CALLNAME:TARGET%は顔を真っ赤にしておずおずと口を開けた…
	ELSEIF TALENT:152
		PRINTFORMW %CALLNAME:TARGET%は全く食欲がないようだ…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%はお弁当をはねのけた…
	ENDIF
ELSEIF TFLAG:17 == 2
	PRINTFORMW %CALLNAME:TARGET%と散歩をした…
	IF TALENT:85 && RAND:3 >= 1
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:MASTER%と腕を組み、人気の多い方に向かった…
	ELSEIF TALENT:153
		PRINTFORMW %CALLNAME:TARGET%は裸で外出することにだいぶ慣れてきたようだ…
	ELSEIF TALENT:152
		PRINTFORMW %CALLNAME:TARGET%はさりげなく人通りの少ないところへ行こうとする…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は泣きそうな顔で抵抗した…
	ENDIF
ELSEIF TFLAG:17 == 3
	PRINTFORMW %CALLNAME:TARGET%と日向ぼっこをした…
	IF TALENT:85 && RAND:3 >= 1
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:MASTER%の腕をしっかりと抱きしめている…
	ELSEIF TALENT:153
		PRINTFORMW %CALLNAME:TARGET%は恥ずかしそうにしているが、いくらかリラックスしてもいるようだ…
	ELSEIF TALENT:152
		PRINTFORMW %CALLNAME:TARGET%はできる限り体を隠そうとしている…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は隙を見つけて逃げようとしていた…
	ENDIF
ENDIF

