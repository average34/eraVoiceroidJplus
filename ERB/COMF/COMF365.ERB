@COM_ABLE365
;着衣後背位実行判定
;COM_ABLE共通判定
CALL COM_ABLE_COMMON, SELECTCOM
SIF RESULT == 0
	RETURN 0
;ウフフ中は無理
SIF TFLAG:44 == 1
	RETURN 0
;デート中は無理
SIF TEQUIP:2
	RETURN 0
;オムツ着用中は無理
SIF TEQUIP:20
	RETURN 0
;オトコだとダメ
SIF TALENT:オトコ
	RETURN 0
;調教者がオトコ、半人半妖、ふたなり、もしくはペニスバンドがないとダメ
SIF TALENT:PLAYER:半人半妖 == 0 && TALENT:PLAYER:ふたなり == 0 && TALENT:PLAYER:オトコ == 0 && ITEM:PBAND == 0
	RETURN 0
;小人体型持ちは、双方が調教者と調教対象ならOK
;それ以外の場合は主人が禁断の知識持ちじゃないとダメ
SIF TALENT:小人体型 && (TALENT:MASTER:禁断の知識 == 0 && TALENT:PLAYER:小人体型 == 0)
	RETURN 0
;潤滑10000以上かつ、欲情10000以上が必要
SIF PALAM:3 < 10000 || PALAM:5 < 10000
	RETURN 0
;欲望9以上が必要
SIF ABL:欲望 < 9
	RETURN 0
;親密13以上or好感度6000以上が必要
SIF ABL:親密 < 13 && CFLAG:2 < 6000
	RETURN 0
;合意が必要
SIF TALENT:合意 == 0
	RETURN 0
;淫乱が必要
SIF TALENT:淫乱 == 0
	RETURN 0
;ムードか酒の勢いが必要
SIF TFLAG:37 < 50 && TFLAG:47 < 30
	RETURN 0
;着衣挿入コマンド拡張コンフィグがONじゃないと無理
SIF FLAG:61 & 1p6 == 0
	RETURN 0
RETURN 1

;着衣後背位
@TRAIN_MESSAGE_COM365
;前回と今回の調教者が同じで、前回の調教が同一コマンドであり、抜かないフラグがある場合
IF TFLAG:60 && PREVCOM == 365 && ((ASSIPLAY && TFLAG:50) || (ASSIPLAY == 0 && TFLAG:50 == 0))
	;調教者がオトコか半人半妖かふたなりである
	IF TALENT:PLAYER:半人半妖 || TALENT:PLAYER:ふたなり || TALENT:PLAYER:オトコ
		PRINTFORMW %CALLNAME:TARGET%の膣内の感触を味わいながら、そのまま再び腰を動かし始めた…
	ELSE
	;ペニスバンドを使用している場合
		PRINTFORMW %CALLNAME:TARGET%の膣内から抜かずに、そのまま再び腰を動かし始めた…
	ENDIF
;同様に、前回の調教が着衣正常位である場合
ELSEIF TFLAG:60 && PREVCOM == 363 && ((ASSIPLAY && TFLAG:50) || (ASSIPLAY == 0 && TFLAG:50 == 0))
	PRINTFORMW 膣内から抜き去ることなく%CALLNAME:TARGET%の後ろに回り、再び腰を動かし始めた…
;同様に、前回の調教が着衣騎乗位である場合
ELSEIF TFLAG:60 && PREVCOM == 366 && ((ASSIPLAY && TFLAG:50) || (ASSIPLAY == 0 && TFLAG:50 == 0))
	PRINTFORMW 挿入したまま%CALLNAME:TARGET%を寝かせて四つん這いにし、再び腰を動かし始めた…
ELSE
	PRINTFORM %CALLNAME:TARGET%の
	IF PALAM:3 > 5000
		PRINTFORM ぐしょぐしょに濡れた
	ELSE
		PRINTFORM 湿り気を帯びた
	ENDIF
	IF PREVCOM == 362 || PREVCOM == 363 || PREVCOM == 364 || PREVCOM == 365 || PREVCOM == 366
		PRINTFORM 下着をずらしたまま
	ELSE
		PRINTFORM 下着をずらし
	ENDIF
	PRINTFORM 、後ろから
	IF EXP:Ｖ経験 == 0
		PRINT 男を知らない
	ELSEIF PALAM:3 >= PALAMLV:4
		PRINT 濡れそぼった
	ENDIF
	SIF EXP:Ｖ経験 > 0 && EXP:Ｖ経験 <= 10
		PRINTFORM 、まだあまり開発されていない
	IF TALENT:小柄体型
		PRINT 幼いワレメ
	ELSE
		PRINT 秘裂
	ENDIF
	PRINTFORMW の最奥まで突き入れた…
ENDIF

@COM365
;着衣後背位

PRINTL 着衣後背位

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL COM_ORDER_COMMON
IF RESULT == 0
	;目標に届かない場合は白い目で見られる
	PRINTFORML %CALLNAME:TARGET%は信じられないものを見るような目でこちらを見ている……
	PRINTFORMW %CALLNAME:TARGET%の好感度が50下がりました。
	CFLAG:2 -= 50
	RETURN 0
ENDIF

;-------------------------------------------------
;実行決定
;-------------------------------------------------
STR:0 = 着衣後背位
CALL TRAIN_MESSAGE_B

;前回と今回の調教者が同じで、前回の調教が同一コマンドであり、抜かないフラグがある場合
IF TFLAG:60 && PREVCOM == 365 && ((ASSIPLAY && TFLAG:50) || (ASSIPLAY == 0 && TFLAG:50 == 0))
	SIF TFLAG:31
		TFLAG:31 = 2
;同様に、前回の調教が着衣正常位である場合
ELSEIF TFLAG:60 && PREVCOM == 363 && ((ASSIPLAY && TFLAG:50) || (ASSIPLAY == 0 && TFLAG:50 == 0))
	SIF TFLAG:31
		TFLAG:31 = 2
;同様に、前回の調教が着衣騎乗位である場合
ELSEIF TFLAG:60 && PREVCOM == 366 && ((ASSIPLAY && TFLAG:50) || (ASSIPLAY == 0 && TFLAG:50 == 0))
	SIF TFLAG:31
		TFLAG:31 = 2
ENDIF

;V経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:19 = 1
SIF TALENT:恋慕 && ASSIPLAY == 0 && EXP:Ｖ経験 == 0
	TFLAG:20 = 1

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
SIF TALENT:PLAYER:ふたなり || TALENT:PLAYER:オトコ
	BASE:PLAYER:射精 += CALC_EJAC(SELECTCOM, PLAYER)

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOSEBASE:体力 += 60
LOSEBASE:気力 += 130

SOURCE:12 = 800
SOURCE:14 = 400
SOURCE:21 = 200
SOURCE:22 = 400


;ABL:Ｖ感覚をみる
IF ABL:Ｖ感覚 == 0
	SOURCE:1 = 40
	SOURCE:3 = 50

ELSEIF ABL:Ｖ感覚 == 1
	SOURCE:1 = 150
	SOURCE:3 = 150
ELSEIF ABL:Ｖ感覚 == 2
	SOURCE:1 = 400
	SOURCE:3 = 250
ELSEIF ABL:Ｖ感覚 == 3
	SOURCE:1 = 1000
	SOURCE:3 = 350
ELSEIF ABL:Ｖ感覚 == 4
	SOURCE:1 = 1700
	SOURCE:3 = 600
ELSE
	SOURCE:1 = 950 + (ABL:Ｖ感覚 * 250)
	SOURCE:3 = 225 + (ABL:Ｖ感覚 * 125)
ENDIF

;EXP:Ｖ経験をみる
IF EXP:Ｖ経験 < EXPLV:1
	TIMES SOURCE:1 , 0.20
	SOURCE:6 = 5000

	IF ASSIPLAY && TALENT:PLAYER:オトコ == 0
		EXP:異常経験 += 3
		PRINTFORML %EXPNAME:50%＋３
	ENDIF
ELSEIF EXP:Ｖ経験 < EXPLV:2
	TIMES SOURCE:1 , 0.60
	SOURCE:6 = 220
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES SOURCE:1 , 1.00
	SOURCE:6 = 30
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES SOURCE:1 , 1.20
	SOURCE:6 = 5
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES SOURCE:1 , 1.30
	SOURCE:6 = 0
ELSE
	TIMES SOURCE:1 , 1.80
	SOURCE:6 = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:3 < PALAMLV:1
	TIMES SOURCE:1 , 0.10
	SOURCE:6 += 900
	TIMES SOURCE:6 , 3.00
ELSEIF PALAM:3 < PALAMLV:2
	TIMES SOURCE:1 , 0.40
	SOURCE:6 += 250
	TIMES SOURCE:6 , 1.00
ELSEIF PALAM:3 < PALAMLV:3
	TIMES SOURCE:1 , 1.00
	TIMES SOURCE:6 , 0.50
ELSEIF PALAM:3 < PALAMLV:4
	TIMES SOURCE:1 , 1.40
	TIMES SOURCE:6 , 0.20
ELSEIF PALAM:3 >= PALAMLV:4
	TIMES SOURCE:1 , 1.80
	TIMES SOURCE:6 , 0.10
ENDIF

;調教者がふたなり
SIF TALENT:PLAYER:ふたなり
	TIMES SOURCE:1 , 2.50


;小柄体形
SIF TALENT:小柄体型
	TIMES SOURCE:6 , 2.00

;貞操観念
IF TALENT:貞操観念 && TALENT:恋慕 == 0
	IF EXP:Ｖ経験 == 0
		TIMES SOURCE:3 , 0.60
		SOURCE:15 = 13000
	ELSE
		TIMES SOURCE:3 , 0.60
		SOURCE:15 = 1300
	ENDIF
;貞操無頓着
ELSEIF TALENT:貞操無頓着 || TALENT:親愛 && ASSIPLAY == 0
	IF EXP:Ｖ経験 == 0
		TIMES SOURCE:3 , 0.60
		SOURCE:15 = 350
	ENDIF
ELSE
	IF EXP:Ｖ経験 == 0
		SOURCE:15 = 3500
	ENDIF
ENDIF

;PALAM:欲情をみる
IF PALAM:5 < PALAMLV:1
	TIMES SOURCE:1 , 0.60
	TIMES SOURCE:3 , 0.30
ELSEIF PALAM:5 < PALAMLV:2
	TIMES SOURCE:1 , 0.80
	TIMES SOURCE:3 , 0.60
ELSEIF PALAM:5 < PALAMLV:3
	TIMES SOURCE:1 , 1.00
	TIMES SOURCE:3 , 1.00
ELSEIF PALAM:5 < PALAMLV:4
	TIMES SOURCE:1 , 1.20
	TIMES SOURCE:3 , 1.50
ELSEIF PALAM:5 >= PALAMLV:4
	TIMES SOURCE:1 , 1.50
	TIMES SOURCE:3 , 1.80
ENDIF

;ABL:従順をみる
IF ABL:親密 == 0
	TIMES SOURCE:1 , 0.50
	TIMES SOURCE:3 , 0.60
	TIMES SOURCE:15 , 2.00
ELSEIF ABL:親密 == 1
	TIMES SOURCE:1 , 0.80
	TIMES SOURCE:3 , 0.80
	TIMES SOURCE:15 , 1.50
ELSEIF ABL:親密 == 2
	TIMES SOURCE:1 , 1.00
	TIMES SOURCE:3 , 1.00
	TIMES SOURCE:15 , 1.00
ELSEIF ABL:親密 == 3
	TIMES SOURCE:1 , 1.30
	TIMES SOURCE:3 , 1.20
	TIMES SOURCE:15 , 0.80
ELSEIF ABL:親密 == 4
	TIMES SOURCE:1 , 1.60
	TIMES SOURCE:3 , 1.40
	TIMES SOURCE:15 , 0.60
ELSE
	SOURCE:1 = SOURCE:1 * (ABL:親密 * 20 + 100) / 100
	SOURCE:3 = SOURCE:3 * (ABL:親密 * 10 + 110) / 100
	TIMES SOURCE:15 , 0.30
ENDIF


;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:従順をみる
	IF ABL:親密 <= 1
		SOURCE:21 += 300 + (ABL:親密 * 50)
	ELSEIF ABL:親密 <= 3
		SOURCE:21 += 600 + (ABL:親密 * 100)
	ELSEIF ABL:親密 <= 5
		SOURCE:21 += 800 + (ABL:親密 * 120)
	ELSEIF ABL:親密 <= 8
		SOURCE:21 += 1000 + (ABL:親密 * 140)
	ELSEIF ABL:親密 <= 11
		SOURCE:21 += 1200 + (ABL:親密 * 160)
	ELSE
		SOURCE:21 += 1500 + (ABL:親密 * 180)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:サドっ気 == 0
		SOURCE:20 = 600
	ELSEIF ABL:サドっ気 == 1
		SOURCE:20 = 900
	ELSEIF ABL:サドっ気 == 2
		SOURCE:20 = 1200
	ELSEIF ABL:サドっ気 == 3
		SOURCE:20 = 1600
	ELSEIF ABL:サドっ気 == 4
		SOURCE:20 = 2000
	ELSE
		SOURCE:20 = 1000 + (ABL:サドっ気 * 300)
	ENDIF
ENDIF

;Ｖ敏感、鈍感をみる
;快Ｖ自体のチェックは後でまとめてやる
IF TALENT:Ｖ鈍感
	TIMES SOURCE:6 , 1.50
	TIMES SOURCE:13 , 1.50
	TIMES SOURCE:14 , 1.50
ELSEIF TALENT:Ｖ敏感
	TIMES SOURCE:6 , 0.60
	TIMES SOURCE:13 , 0.60
	TIMES SOURCE:14 , 0.60
ENDIF

EXP:Ｖ経験 += 1
PRINTL Ｖ経験＋１

;好感度ランダム変化
A = RAND:12 + 3
SIF TALENT:恋慕 || TALENT:恋人
	A += 5
SIF TALENT:親愛
	A += 5
;合意がないと好感度が下がる
IF TALENT:合意 == 0 && TFLAG:48 == 0
	A -= 9
;恋慕でも反感
	IF TALENT:恋慕
		SOURCE:15 += 200
	ELSE
;恋慕なしだと反感増加
		SOURCE:15 += 600
	ENDIF
ENDIF
TFLAG:30 += A
TFLAG:37 += A / 2



;既成事実3に相当
TFLAG:200 = 3

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK
;;オーバーフロー対策に、値が大きすぎる場合と、負の場合は調整を加える
;IF BASE:PLAYER:射精 > 100000000 || BASE:PLAYER:射精 < 0
;	BASE:PLAYER:射精 = 100000000
;ENDIF
;S = BASE:PLAYER:射精
;EJAC = MAXBASE:PLAYER:射精
;
;IF  S > EJAC * 2
;	E = 2
;ELSEIF S > EJAC
;	E = 1
;ELSE
;	E = 0
;ENDIF

IF E >= 1
	;射精先タイプの設定
	TFLAG:703 = 1
	;射精先選択の可・不可
	;TFLAG:704 = 0
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;大量射精
;IF E == 2
;	EXP:PLAYER:射精経験 += 2
;	EXP:精液経験 += 20
;	PRINTL 大量射精
;	PRINTL 精液経験＋20
;	;Ｐに精液汚れ
;	STAIN:PLAYER:ペニス |= 4
;
;	BASE:PLAYER:射精 -= EJAC*2
;	SIF BASE:PLAYER:射精 >= EJAC
;		BASE:PLAYER:射精 = EJAC-1
;
;	;セックスで射精フラグ
;	TFLAG:2 = 2
;;通常の射精
;ELSEIF E == 1
;	EXP:PLAYER:射精経験 += 1
;	;Ｐに精液汚れ
;	EXP:精液経験 += 10
;	PRINTL 射精
;	PRINTL 精液経験＋10
;	STAIN:PLAYER:ペニス |= 4
;
;	BASE:PLAYER:射精 -= EJAC
;	SIF BASE:PLAYER:射精 >= EJAC
;		BASE:PLAYER:射精 = EJAC-1
;
;	;セックスで射精フラグ
;	TFLAG:2 = 1
;ENDIF

;奴隷のＶ⇔調教者のＰの汚れが移動
STAIN:ヴァギナ |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:ヴァギナ

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	PRINTS EXPNAME:40
	PRINTL +4
	EXP:レズ経験 += 4
	EXP:PLAYER:レズ経験 += 4
ENDIF

;IF ASSIPLAY == 0
;	IF ABL:Ｖ感覚 >= 3
;		TFLAG:30 += 2
;	ELSE
;		TFLAG:30 += 1
;	ENDIF
;ENDIF

RETURN 1

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_365
A = 0
S = 0
V = 155
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;ABL:欲望
IF ABL:欲望
	A += ABL:欲望 * 3
ENDIF
;ABL:Ｖ感覚
IF ABL:Ｖ感覚
	A += ABL:Ｖ感覚 * 2
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	A += ABL:奉仕精神 * 4
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	A += ABL:精液中毒 * 1
ENDIF

IF MARK:快楽依存
	A += MARK:快楽依存 * 3
ENDIF

;PALAM:潤滑（の不足）
IF PALAM:3 < PALAMLV:3
	A -= 5
ENDIF
;PALAM:欲情
IF PALAM:5 < PALAMLV:1
	L = 0
ELSEIF PALAM:5 < PALAMLV:2
	L = 1
ELSEIF PALAM:5 < PALAMLV:3
	L = 2
ELSEIF PALAM:5 < PALAMLV:4
	L = 3
ELSEIF PALAM:5 < PALAMLV:5
	L = 4
ELSE
	L = 5
ENDIF
IF L
	A += L * 3
ENDIF

;恥じらい
IF TALENT:恥じらい
	A -= 5
ENDIF
;快感の否定
IF TALENT:快感の否定
	A -= 5
ENDIF
;男嫌い
IF TALENT:男嫌い && TALENT:PLAYER:オトコ
	A -= 12
ENDIF
;恋慕
IF TALENT:恋慕 && ASSIPLAY == 0
	A += 8
	S = 5
ENDIF

;処女
IF TALENT:処女
	A -= 30
ELSEIF EXP:Ｖ経験 < EXPLV:2
	A -= 10
ENDIF

;一線越えない（処女のみ）
IF TALENT:一線越えない && TALENT:処女
	A -= 20
ENDIF

;媚薬
IF TEQUIP:21
	A += 6
ENDIF

;童貞
IF TALENT:PLAYER:童貞
	A += 3
ENDIF

;ムードボーナス
B = 0
IF TFLAG:37 < 10
	B -= 12
ELSEIF TFLAG:37 < 30
	B -= 3
ELSEIF TFLAG:37 < 50
	B -= 1
ELSEIF TFLAG:37 < 100
	B += TFLAG:37 / 10
ELSEIF TFLAG:37 < 300
	B += TFLAG:37 / 5
ELSE
	B += TFLAG:37 / 4
ENDIF
A += B
IF B < 0
	B = B * -1
ENDIF


;好感度
IF CFLAG:2 < 2000
	B = 0
	IF CFLAG:2 < 1000
		B = 50
	ELSEIF CFLAG:2 < 1300
		B = 20
	ELSEIF CFLAG:2 < 1700
		B = 10
	ELSE
		B = 5
	ENDIF
	A -= B
ENDIF
;好感度
IF CFLAG:2 >= 2500
	B = 0
	IF CFLAG:2 < 3000
		B = 5
	ELSE
		B = CFLAG:2 / 250
	ENDIF
	A += B
ENDIF

;合意
IF TALENT:合意 == 0
	A -= 35
ENDIF

IF ((FLAG:23 & 1p27) == 0) && FLAG:2 != 1
	;ABL:欲望
	IF ABL:欲望
		SIF S
			PRINT  + 
		PRINTS ABLNAME:1
		PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 3,')
		S = 1
	ENDIF
	;ABL:Ｖ感覚
	IF ABL:Ｖ感覚
		SIF S
			PRINT  + 
		PRINTS ABLNAME:4
		PRINTV 'LV,ABL:Ｖ感覚,'(,ABL:Ｖ感覚 * 2,')
		S = 1
	ENDIF
	;ABL:奉仕精神
	IF ABL:奉仕精神
		SIF S
			PRINT  + 
		PRINTS ABLNAME:6
		PRINTV 'LV,ABL:奉仕精神,'(,ABL:奉仕精神 * 4,')
		S = 1
	ENDIF
	;ABL:精液中毒
	IF ABL:精液中毒
		SIF S
			PRINT  + 
		PRINTS ABLNAME:11
		PRINTV 'LV,ABL:精液中毒,'(,ABL:精液中毒 * 1,')
		S = 1
	ENDIF

	IF MARK:快楽依存
		SIF S
			PRINT  + 
		PRINTS MARKNAME:1
		PRINTV 'LV,MARK:快楽依存,'(,MARK:快楽依存 * 3,')
		S = 1
	ENDIF

	;PALAM:潤滑（の不足）
	IF PALAM:3 < PALAMLV:3
		PRINT  - 
		PRINT 潤滑不足
		PRINTV '(,5,')
		S = 1
	ENDIF
	;PALAM:欲情
	IF PALAM:5 < PALAMLV:1
		L = 0
	ELSEIF PALAM:5 < PALAMLV:2
		L = 1
	ELSEIF PALAM:5 < PALAMLV:3
		L = 2
	ELSEIF PALAM:5 < PALAMLV:4
		L = 3
	ELSEIF PALAM:5 < PALAMLV:5
		L = 4
	ELSE
		L = 5
	ENDIF
	IF L
		SIF S
			PRINT  + 
		PRINTS PALAMNAME:5
		PRINTV 'LV,L,'(,L * 3,')
		S = 1
	ENDIF

	;恥じらい
	IF TALENT:恥じらい
		PRINT  - 
		PRINTS TALENTNAME:35
		PRINTV '(,5,')
		S = 1
	ENDIF
	;快感の否定
	IF TALENT:快感の否定
		PRINT  - 
		PRINTS TALENTNAME:71
		PRINTV '(,5,')
		S = 1
	ENDIF
	;男嫌い
	IF TALENT:男嫌い && TALENT:PLAYER:オトコ
		PRINT  - 
		PRINTS TALENTNAME:82
		PRINTV '(,12,')
		S = 1
	ENDIF
	;恋慕
	IF TALENT:恋慕 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:85
		PRINTV '(,8,')
		S = 5
	ENDIF

	;処女
	IF TALENT:処女
		PRINT  - 
		PRINTS TALENTNAME:0
		PRINTV '(,30,')
		S = 1
	ELSEIF EXP:Ｖ経験 < EXPLV:2
		PRINT  - 
		PRINTS EXPNAME:0
		PRINT 不足
		PRINTV '(,10,')
		S = 1
	ENDIF

	;一線越えない（処女のみ）
	IF TALENT:一線越えない && TALENT:処女
		PRINT  - 
		PRINTS TALENTNAME:27
		PRINTV '(,20,')
		S = 1
	ENDIF

	;媚薬
	IF TEQUIP:21
		SIF S
			PRINT  + 
		PRINTS ITEMNAME:26
		PRINTV '(,6,')
		S = 1
	ENDIF

	;童貞
	IF TALENT:PLAYER:童貞
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:154
		PRINTV '(,3,')
		S = 1
	ENDIF

	;ムードボーナス
	B = 0
	IF TFLAG:37 < 10
		B -= 12
	ELSEIF TFLAG:37 < 30
		B -= 3
	ELSEIF TFLAG:37 < 50
		B -= 1
	ELSEIF TFLAG:37 < 100
		B += TFLAG:37 / 10
	ELSEIF TFLAG:37 < 300
		B += TFLAG:37 / 5
	ELSE
		B += TFLAG:37 / 4
	ENDIF
	IF B < 0
		B = B * -1
		PRINT  - 
		PRINT ムード悪い
		PRINTV '(,B,')
		S = 1
	ELSE
		PRINT  + 
		PRINT ムード良い
		PRINTV '(,B,')
		S = 1
	ENDIF


	;好感度
	IF CFLAG:2 < 2000
		B = 0
		PRINT  - 
		IF CFLAG:2 < 1000
			B = 50
		ELSEIF CFLAG:2 < 1300
			B = 20
		ELSEIF CFLAG:2 < 1700
			B = 10
		ELSE
			B = 5
		ENDIF
		PRINT 好感度不足
		PRINTV '(,B,')
		S = 1
	ENDIF
	;好感度
	IF CFLAG:2 >= 2500
		B = 0
		SIF S
			PRINT  + 
		IF CFLAG:2 < 3000
			B = 5
		ELSE
			B = CFLAG:2 / 250
		ENDIF
		PRINT 好感度
		PRINTV '(,B,')
		S = 1
	ENDIF

	;合意
	IF TALENT:合意 == 0
		PRINT  - 
		PRINTFORM %TALENTNAME:152%無し
		PRINTV '(,35,')
		S = 1
	ENDIF

	;合計を表示(155以上で実行)
	PRINT  = 
	PRINTV A

	SIF A < V
		PRINT  < 
	SIF A == V
		PRINT  = 
	SIF A > V
		PRINT  > 
	PRINT 実行値
	PRINTV V
	WAIT
ENDIF

