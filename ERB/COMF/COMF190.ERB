@COM_ABLE190
;優しくする実行判定
;COM_ABLE共通判定
CALL COM_ABLE_COMMON, SELECTCOM
SIF RESULT == 0
	RETURN 0
;ウフフ中じゃないと無理
SIF TFLAG:44 == 0
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:90
	RETURN 0
RETURN 1

@COM190
;優しくする
PRINTL 優しくする
STR:0 = 優しくする

TFLAG:17 = 0
TFLAG:18 = 0
;連続実行は強制的に濃厚になる
SIF PREVCOM == 190
	TFLAG:18 = 1

;地の文カットのために先にTFLAG:17を取得
IF RAND:16 == 0
	TFLAG:17 = 0
ELSEIF RAND:15 == 0 && TEQUIP:57 == 1
	TFLAG:17 = 2
ELSEIF RAND:14 == 0 && TEQUIP:57 == 1
	TFLAG:17 = 3
ELSEIF RAND:13 == 0 && TEQUIP:57 == 1
	TFLAG:17 = 4
ELSEIF RAND:12 == 0
	TFLAG:17 = 5
ELSEIF RAND:11 == 0
	TFLAG:17 = 6
ELSEIF RAND:10 == 0
	TFLAG:17 = 7
ELSEIF RAND:9 == 0
	TFLAG:17 = 8
ELSEIF RAND:8 == 0
	TFLAG:17 = 9
ELSEIF RAND:7 == 0
	TFLAG:17 = 10
ELSEIF RAND:6 == 0
	TFLAG:17 = 11
ELSEIF RAND:5 == 0
	TFLAG:17 = 12
ELSEIF RAND:4 == 0
	TFLAG:17 = 13
ELSEIF RAND:3 == 0
	TFLAG:17 = 14
	IF PALAM:潤滑 >= PALAMLV:4 && ITEM:ローション > 0
		ITEM:ローション -= 1
		SOURCE:液体追加 += 10000
	ENDIF
ELSEIF RAND:2 == 0
	TFLAG:17 = 15
	IF PALAM:潤滑 >= PALAMLV:4 && ITEM:ローション > 0
		ITEM:ローション -= 1
		SOURCE:液体追加 += 10000
	ENDIF
ELSE
	TFLAG:17 = 16
ENDIF

CALL TRAIN_MESSAGE_B

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOSEBASE:気力 += 70

;固定で獲得するソース
SOURCE:歓楽 = 0
SOURCE:接触 = 500


;奉仕精神をみる
IF ABL:奉仕精神 == 0
	SOURCE:情愛 = 10
ELSEIF ABL:奉仕精神 == 1
	SOURCE:情愛 = 30
ELSEIF ABL:奉仕精神 == 2
	SOURCE:情愛 = 80
ELSEIF ABL:奉仕精神 == 3
	SOURCE:情愛 = 150
ELSEIF ABL:奉仕精神 == 4
	SOURCE:情愛 = 230
ELSE
	SOURCE:情愛 = 200 + (ABL:奉仕精神 * 20)
ENDIF

;ABL:従順をみる
IF ABL:親密 <= 1
	SOURCE:接触 += (ABL:親密 * 30)
ELSEIF ABL:親密 <= 3
	SOURCE:歓楽 += 100 + (ABL:親密 * 40)
	SOURCE:接触 += 200 + (ABL:親密 * 30)
ELSEIF ABL:親密 <= 5
	SOURCE:歓楽 += 400 + (ABL:親密 * 50)
	SOURCE:接触 += 400 + (ABL:親密 * 30)
ELSEIF ABL:親密 <= 8
	SOURCE:歓楽 += 700 + (ABL:親密 * 60)
	SOURCE:接触 += 600 + (ABL:親密 * 40)
ELSEIF ABL:親密 <= 11
	SOURCE:歓楽 += 1200 + (ABL:親密 * 80)
	SOURCE:接触 += 800 + (ABL:親密 * 40)
ELSE
	SOURCE:歓楽 += 2000 + (ABL:親密 * 50)
	SOURCE:接触 += 1800 + (ABL:親密 * 20)
ENDIF

;好感度をみる
IF CFLAG:2 <= 500
	SOURCE:歓楽 += CFLAG:2
ELSEIF CFLAG:2 <= 5000
	SOURCE:歓楽 += 500 + (CFLAG:2 - 500) / 3
ELSE
	SOURCE:歓楽 += 2000 + (CFLAG:2 - 5000) / 5
ENDIF
SIF SOURCE:歓楽 < 0
	SOURCE:歓楽 = 0

;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:従順をみる
	IF ABL:親密 == 0
		SOURCE:受動 = 100
	ELSEIF ABL:親密 == 1
		SOURCE:受動 = 200
	ELSEIF ABL:親密 == 2
		SOURCE:受動 = 350
	ELSEIF ABL:親密 == 3
		SOURCE:受動 = 500
	ELSEIF ABL:親密 == 4
		SOURCE:受動 = 700
	ELSE
		SOURCE:受動 = 1000 + (ABL:親密 * 100)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:サドっ気 == 0
		SOURCE:征服 = 500
	ELSEIF ABL:サドっ気 == 1
		SOURCE:征服 = 700
	ELSEIF ABL:サドっ気 == 2
		SOURCE:征服 = 900
	ELSEIF ABL:サドっ気 == 3
		SOURCE:征服 = 1100
	ELSEIF ABL:サドっ気 == 4
		SOURCE:征服 = 1300
	ELSE
		SOURCE:征服 = 1000 + (ABL:サドっ気 * 150)
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:5 - 1

;従順が低いと好感度低下
IF ABL:親密 == 0
	A -= 3
ELSEIF ABL:親密 == 1
	A -= 2
ELSEIF ABL:親密 == 2
	A -= 1
ENDIF
;好感度が低いと好感度低下
IF CFLAG:2 <= 50
	A -= 3
ELSEIF CFLAG:2 <= 100
	A -= 2
ELSEIF CFLAG:2 <= 250
	A -= 1
ENDIF

;連続実行ならパワーうｐでソースを追加
IF TFLAG:18
	TIMES SOURCE:歓楽 , 1.20
	TIMES SOURCE:接触 , 2.00
	TIMES SOURCE:征服 , 1.50
	TIMES SOURCE:受動 , 1.50
	SOURCE:情愛 += SOURCE:接触 / 10
	A += 2
ENDIF


;好感度変化
TFLAG:30 += A


TFLAG:37 += A

RETURN 1



;優しくする
@TRAIN_MESSAGE_COM190
IF PREVCOM == 190
	PRINTFORMW %CALLNAME:PLAYER%は%CALLNAME:TARGET%にもっと優しくした
	PRINTL  
ENDIF
PRINTFORM %CALLNAME:PLAYER%は
IF TFLAG:17 == 0
	PRINTFORMW %CALLNAME:TARGET%の頭をなでた…
	IF TALENT:恋慕 && RAND:3 >= 1
		PRINTFORMW %CALLNAME:TARGET%は上目づかいに%CALLNAME:PLAYER%を見つめ、熱い息を吐いている…
	ELSEIF TALENT:恋人
		PRINTFORMW %CALLNAME:TARGET%は少し嬉しそうに身を委ねている…
	ELSEIF TALENT:合意
		PRINTFORMW %CALLNAME:TARGET%は顔を真っ赤にしてうつむいている…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%から離れた…
	ENDIF
;	ELSEIF RAND:16 == 0
;		TFLAG:17 = 1
;		PRINTFORMW %CALLNAME:TARGET%の部屋を片付け始めた…
;		IF TALENT:恋慕 && RAND:3 >= 1
;			PRINTFORMW %CALLNAME:TARGET%は自分を責める器具にわずかに興奮しているようだ…
;		ELSEIF ABL:親密 >= 4 && EXP:奉仕快楽経験 >= 250
;			PRINTFORMW %CALLNAME:TARGET%は慌てた様子で%CALLNAME:PLAYER%を手伝った
;		ELSEIF ABL:親密 >= 2 && EXP:奉仕快楽経験 <= 150
;			PRINTFORMW %CALLNAME:TARGET%は仕方なさそうに%CALLNAME:PLAYER%を手伝った…
;		ELSE
;			PRINTFORMW %CALLNAME:TARGET%は何もしないでいる…
;		ENDIF
ELSEIF TFLAG:17 == 2
	PRINTFORMW %CALLNAME:TARGET%の目を手で覆ってあげた…
	IF TALENT:恋慕 && RAND:3 >= 1
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の手をそっと外し、鏡に向かってほほ笑んだ…
	ELSEIF MARK:反発感情 >= 1
		PRINTFORMW %CALLNAME:TARGET%は鏡を見ずに済んで少し安心しているようだ…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%はされるがままになっている…
	ENDIF
ELSEIF TFLAG:17 == 3
	PRINTFORMW 鏡の中の%CALLNAME:TARGET%をじっと見つめた…
	IF TALENT:恋慕 && RAND:3 >= 1
		PRINTFORMW %CALLNAME:TARGET%は少し拗ねたように%CALLNAME:PLAYER%の顔を自分の方に向けた…
	ELSEIF ABL:親密 >= 3
		PRINTFORMW %CALLNAME:TARGET%は恥ずかしそうに目をそらした…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は睨みつけてきた…
	ENDIF
ELSEIF TFLAG:17 == 4
	PRINTFORMW %CALLNAME:TARGET%を後ろから抱きしめた…
	IF TALENT:恋慕 && RAND:3 >= 1
		PRINTFORMW %CALLNAME:TARGET%は鏡に映った自分と%CALLNAME:PLAYER%の姿を幸せそうに眺めている…
	ELSEIF ABL:親密 >= 3
		PRINTFORMW %CALLNAME:TARGET%は顔を真っ赤にしながらも、薄眼で鏡を見ている…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は鏡から目をそらしている…
	ENDIF
ELSEIF TFLAG:17 == 5
	PRINTFORMW %CALLNAME:TARGET%の目元を舐めた…
	IF TALENT:恋慕 && RAND:3 >= 1
		PRINTFORMW お返しとばかりに%CALLNAME:TARGET%も%CALLNAME:PLAYER%の目元にキスをしてきた…
	ELSEIF TALENT:恋人
		PRINTFORMW %CALLNAME:TARGET%は舐められた目元をそっと撫でた…
	ELSEIF TALENT:合意
		PRINTFORMW %CALLNAME:TARGET%は小さく悲鳴をあげた…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%はのけぞるように%CALLNAME:PLAYER%から離れた…
	ENDIF
ELSEIF TFLAG:17 == 6
	PRINTFORMW %CALLNAME:TARGET%のお臍を舌でねぶった…
	IF TALENT:恋慕 && RAND:3 >= 1
		PRINTFORMW %CALLNAME:TARGET%は優しく%CALLNAME:PLAYER%の頭をなでている…
	ELSEIF TALENT:恋人
		PRINTFORMW %CALLNAME:TARGET%はお腹まで真っ赤にして照れている…
	ELSEIF TALENT:合意
		PRINTFORMW %CALLNAME:TARGET%は慣れない感覚に呻き声をあげている…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%をなんとか引き離そうとしている…
	ENDIF
ELSEIF TFLAG:17 == 7
	PRINTFORMW %CALLNAME:TARGET%の背筋を舐めあげた…
	IF TALENT:恋慕 && RAND:3 >= 1
		PRINTFORMW %CALLNAME:TARGET%は体をのけぞらせると、%CALLNAME:PLAYER%にもたれかかってきた…
	ELSEIF TALENT:恋人
		PRINTFORMW %CALLNAME:TARGET%の口から艶めかしい吐息が漏れた…
	ELSEIF TALENT:合意
		PRINTFORMW %CALLNAME:TARGET%は体を縮こまらせ、歯を食いしばっている…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は暴れだし、%CALLNAME:PLAYER%から離れた…
	ENDIF
ELSEIF TFLAG:17 == 8
	PRINTFORMW %CALLNAME:TARGET%の腋に舌を這わせた…
	IF TALENT:恋慕 && RAND:3 >= 1
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の髪に顔を埋めた…
	ELSEIF TALENT:恋人
		PRINTFORMW %CALLNAME:TARGET%は恥ずかしそうにしているが、同時に悦びも感じているようだ…
	ELSEIF TALENT:合意
		PRINTFORMW %CALLNAME:TARGET%は必死で%CALLNAME:PLAYER%から逃げようとしている…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は肘を振り落とし、%CALLNAME:PLAYER%を引き剥がした…
	ENDIF
ELSEIF TFLAG:17 == 9
	PRINTFORMW %CALLNAME:TARGET%に膝枕をしてあげた…
	IF TALENT:恋慕 && RAND:3 >= 1
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%を見上げると、にっこりとほほ笑んだ…
	ELSEIF TALENT:恋人
		PRINTFORMW %CALLNAME:TARGET%は太ももの感覚を確かめるように、すりすりと頭を動かしている…
	ELSEIF TALENT:合意
		PRINTFORMW %CALLNAME:TARGET%はおとなしくしている…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の顔を見ないようにしている…
	ENDIF
ELSEIF TFLAG:17 == 10
	PRINTFORMW %CALLNAME:TARGET%の首筋やうなじに舌を這わせた…
	IF TALENT:恋慕 && RAND:3 >= 1
		PRINTFORMW %CALLNAME:TARGET%は後ろに手をまわし、%CALLNAME:PLAYER%の頭を抱き寄せた…
	ELSEIF TALENT:恋人
		PRINTFORMW %CALLNAME:TARGET%は体を%CALLNAME:PLAYER%に預け、されるがままになっている…
	ELSEIF TALENT:合意
		PRINTFORMW %CALLNAME:TARGET%は顔を真っ赤にして、いやいやをするように首を振った…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は逃げる機会をうかがっているようだ…
	ENDIF
ELSEIF TFLAG:17 == 11
	PRINTFORMW %CALLNAME:TARGET%を後ろから抱き締め、髪の香りを堪能した…
	IF TALENT:恋慕 && RAND:3 >= 1
		PRINTFORMW %CALLNAME:TARGET%は前に回された%CALLNAME:PLAYER%の手を握りしめている…
	ELSEIF TALENT:恋人
		PRINTFORMW %CALLNAME:PLAYER%に抱きしめられて、%CALLNAME:TARGET%が安心しているように見えた…
	ELSEIF TALENT:合意
		PRINTFORMW %CALLNAME:TARGET%は少し震えている…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は嫌そうに顔をしかめている…
	ENDIF
ELSEIF TFLAG:17 == 12
	PRINTFORMW %CALLNAME:TARGET%の耳元で愛をささやいた…
	IF TALENT:恋慕 && RAND:3 >= 1
		PRINTFORMW %CALLNAME:TARGET%は目をとろんとさせ、うっとりと%CALLNAME:PLAYER%にしなだれかかった…
	ELSEIF TALENT:恋人
		PRINTFORMW %CALLNAME:TARGET%は何も言わず、%CALLNAME:PLAYER%に抱きついた…
	ELSEIF TALENT:合意
		PRINTFORMW %CALLNAME:TARGET%は顔を真っ赤にしている。まんざらでもなさそうだ…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は白けたような顔で%CALLNAME:PLAYER%の話を聞き流している…
	ENDIF
ELSEIF TFLAG:17 == 13
	PRINTFORMW %CALLNAME:TARGET%の耳に息を吹きかけ、軽く舐めてから耳たぶを甘噛みした…
	IF TALENT:恋慕 && RAND:3 >= 1
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の頭をなでると、そのままキスをしてきた…
	ELSEIF TALENT:恋人
		PRINTFORMW %CALLNAME:TARGET%にとって、その感覚は不快ではないようだ…
	ELSEIF TALENT:合意
		PRINTFORMW %CALLNAME:PLAYER%の舌が動くたび、%CALLNAME:TARGET%は小さく身を震わせた…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は不愉快だと言わんばかりに%CALLNAME:PLAYER%を睨んでいる…
	ENDIF
ELSEIF TFLAG:17 == 14
	PRINTFORM %CALLNAME:TARGET%の全身を
	IF PALAM:潤滑 >= PALAMLV:4 && ITEM:ローション > 0
		PRINT ローションまみれにして、
	ENDIF
	PRINTFORMW 優しく撫でさすった…
	IF TALENT:恋慕 && RAND:3 >= 1
		PRINTFORMW %CALLNAME:TARGET%は慈しむような表情で、%CALLNAME:PLAYER%に抱きついてきた…
	ELSEIF TALENT:恋人
		PRINTFORMW %CALLNAME:TARGET%は気持ち良さそうにしている…
	ELSEIF TALENT:合意
		PRINTFORMW %CALLNAME:TARGET%は怯えたように自分の体を抱きしめている…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の手を払った…
	ENDIF
ELSEIF TFLAG:17 == 15
	PRINTFORM %CALLNAME:TARGET%の体に
	IF PALAM:潤滑 >= PALAMLV:4 && ITEM:ローション > 0
		PRINT ローションを垂らすと、
	ENDIF
	PRINTFORM 自分の体を擦りつけ
	SIF PALAM:潤滑 >= PALAMLV:4
		PRINT 、一緒にヌルヌルになっ
	PRINTFORMW た…
	IF TALENT:恋慕 && RAND:3 >= 1
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の首に手を回すと、そのまま唇を奪った…
	ELSEIF TALENT:恋人
		PRINTFORMW %CALLNAME:TARGET%も積極的に体を擦りつけている…
	ELSEIF TALENT:合意
		PRINTFORMW %CALLNAME:TARGET%は石のように硬直していたが、やがておずおずと%CALLNAME:PLAYER%の体に触れてきた…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%はじたばたと暴れ、%CALLNAME:PLAYER%を振り払った…
	ENDIF
ELSEIF TFLAG:17 == 16
	PRINTFORMW %CALLNAME:TARGET%をくすぐった…
	IF TALENT:恋慕 && RAND:3 >= 1
		PRINTFORMW %CALLNAME:TARGET%は楽しそうにキャッキャッと笑っている…
	ELSEIF TALENT:恋人
		PRINTFORMW %CALLNAME:TARGET%もわずかにやり返してきた…
	ELSEIF TALENT:合意
		PRINTFORMW %CALLNAME:TARGET%は必死に笑いをこらえているのか、顔を赤くしたり青くしたりしている…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は露骨に嫌そうな顔をして、%CALLNAME:PLAYER%から距離を取った…
	ENDIF
ENDIF



