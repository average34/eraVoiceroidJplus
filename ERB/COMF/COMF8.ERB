@COM8
;指挿入れ

;前回の調教が指挿入れかつプレイヤの技巧３以上だとＧスポット刺激に
SIF PREVCOM == 8 && ABL:PLAYER:2 >= 3
	JUMP COM84
;前回の調教がGスポットならさらにＧスポット刺激に
SIF PREVCOM == 84
	JUMP COM84

PRINTL 指挿入れ
STR:0 = 指挿入れ
CALL TRAIN_MESSAGE_B

;V経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:19 = 1

LOSEBASE:0 += 30
LOSEBASE:1 += 80

SOURCE:12 = 300
SOURCE:14 = 200


;ABL:Ｖ感覚をみる
IF ABL:4 == 0
	SOURCE:1 = 10
	SOURCE:13 = 150
ELSEIF ABL:4 == 1
	SOURCE:1 = 50
	SOURCE:13 = 250
ELSEIF ABL:4 == 2
	SOURCE:1 = 250
	SOURCE:13 = 400
ELSEIF ABL:4 == 3
	SOURCE:1 = 600
	SOURCE:13 = 700
ELSEIF ABL:4 == 4
	SOURCE:1 = 1200
	SOURCE:13 = 1300
ELSE
	SOURCE:1 = 300 + (ABL:4 * 300)
	SOURCE:13 = 250 + (ABL:4 * 350)
ENDIF

;EXP:Ｖ経験をみる
IF EXP:0 < EXPLV:1
	TIMES SOURCE:1 , 0.20
	TIMES SOURCE:13, 0.20
	SOURCE:6 = 300
ELSEIF EXP:0 < EXPLV:2
	TIMES SOURCE:1 , 0.50
	TIMES SOURCE:13, 0.50
	SOURCE:6 = 180
ELSEIF EXP:0 < EXPLV:3
	TIMES SOURCE:1 , 1.00
	TIMES SOURCE:13, 0.80
	SOURCE:6 = 80
ELSEIF EXP:0 < EXPLV:4
	TIMES SOURCE:1 , 1.20
	TIMES SOURCE:13, 1.00
	SOURCE:6 = 30
ELSEIF EXP:0 < EXPLV:5
	TIMES SOURCE:1 , 1.60
	TIMES SOURCE:13, 1.20
	SOURCE:6 = 0
ELSE
	TIMES SOURCE:2 , 1.80
	TIMES SOURCE:13, 1.50
	SOURCE:6 = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:3 < PALAMLV:1
	TIMES SOURCE:1 , 0.10
	SOURCE:6 += 700
	TIMES SOURCE:6 , 3.00
ELSEIF PALAM:3 < PALAMLV:2
	TIMES SOURCE:1 , 0.20
	SOURCE:6 += 200
	TIMES SOURCE:6 , 1.00
ELSEIF PALAM:3 < PALAMLV:3
	TIMES SOURCE:1 , 0.60
	TIMES SOURCE:6 , 0.80
ELSEIF PALAM:3 < PALAMLV:4
	TIMES SOURCE:1 , 1.00
	TIMES SOURCE:6 , 0.50
ELSEIF PALAM:3 >= PALAMLV:4
	TIMES SOURCE:1 , 2.00
	TIMES SOURCE:6 , 0.10
ENDIF

;PALAM:欲情をみる
IF PALAM:5 < PALAMLV:1
	TIMES SOURCE:1 , 0.50
ELSEIF PALAM:5 < PALAMLV:2
	TIMES SOURCE:1 , 0.80
ELSEIF PALAM:5 < PALAMLV:3
	TIMES SOURCE:1 , 1.20
ELSEIF PALAM:5 < PALAMLV:4
	TIMES SOURCE:1 , 1.50
ELSEIF PALAM:5 >= PALAMLV:4
	TIMES SOURCE:1 , 1.80
ENDIF


;Ｖ敏感、鈍感をみる
;快Ｖ自体のチェックは後でまとめてやる
IF TALENT:103
	TIMES SOURCE:6 , 1.50
	TIMES SOURCE:13 , 1.50
	TIMES SOURCE:14 , 1.50
ELSEIF TALENT:104
	TIMES SOURCE:6 , 0.60
	TIMES SOURCE:13 , 0.60
	TIMES SOURCE:14 , 0.60
ENDIF

;処女で貞操観念
IF EXP:0 == 0 && TALENT:30 && TALENT:85 == 0
	TIMES SOURCE:13 , 2.00
ENDIF

EXP:0 += 1
PRINTL Ｖ経験＋１

IF TEQUIP:90
	;奴隷のＶに触手の汚れがつく
	STAIN:3 |= 2
	STAIN:3 |= 4
ELSE
	;奴隷のＶ⇔調教者の指の汚れが移動
	STAIN:3 |= STAIN:PLAYER:1
	STAIN:PLAYER:1 |= STAIN:3
ENDIF

IF TALENT:122 == 0 && TALENT:PLAYER:122 == 0
	PRINTS EXPNAME:40
	PRINTL +1
	EXP:40 += 1
	EXP:PLAYER:40 += 1
ELSEIF TALENT:122 == 1 && TALENT:PLAYER:122 == 1
	PRINTS EXPNAME:41
	PRINTL +1
	EXP:41 += 1
	EXP:PLAYER:41 += 1
ENDIF


;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:従順をみる
	IF ABL:0 <= 1
		SOURCE:21 += 300 + (ABL:0 * 50)
	ELSEIF ABL:0 <= 3
		SOURCE:21 += 600 + (ABL:0 * 100)
	ELSEIF ABL:0 <= 5
		SOURCE:21 += 800 + (ABL:0 * 120)
	ELSEIF ABL:0 <= 8
		SOURCE:21 += 1000 + (ABL:0 * 140)
	ELSEIF ABL:0 <= 11
		SOURCE:21 += 1200 + (ABL:0 * 160)
	ELSE
		SOURCE:21 += 1500 + (ABL:0 * 180)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:15 == 0
		SOURCE:20 = 600
	ELSEIF ABL:15 == 1
		SOURCE:20 = 900
	ELSEIF ABL:15 == 2
		SOURCE:20 = 1200
	ELSEIF ABL:15 == 3
		SOURCE:20 = 1600
	ELSEIF ABL:15 == 4
		SOURCE:20 = 2000
	ELSE
		SOURCE:20 = 1000 + (ABL:15 * 300)
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:10 + 5
;合意がないと好感度が下がる
IF TALENT:152 == 0 && TFLAG:48 == 0
	A -= 9
	SOURCE:15 += 400
ENDIF
TFLAG:30 += A
TFLAG:37 += A / 2


RETURN 1


