@COM8
;指挿入れ

;前回の調教が指挿入れかつプレイヤの技巧３以上だとＧスポット刺激に
SIF PREVCOM == 8 && ABL:PLAYER:技巧 >= 3
	JUMP COM84
;前回の調教がGスポットならさらにＧスポット刺激に
SIF PREVCOM == 84
	JUMP COM84

PRINTL 指挿入れ
STR:0 = 指挿入れ
CALL TRAIN_MESSAGE_B

;V経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:19 = 1

LOSEBASE:体力 += 30
LOSEBASE:気力 += 80

SOURCE:露出 = 300
SOURCE:逸脱 = 200


;ABL:Ｖ感覚をみる
IF ABL:Ｖ感覚 == 0
	SOURCE:快Ｖ = 10
	SOURCE:屈従 = 150
ELSEIF ABL:Ｖ感覚 == 1
	SOURCE:快Ｖ = 50
	SOURCE:屈従 = 250
ELSEIF ABL:Ｖ感覚 == 2
	SOURCE:快Ｖ = 250
	SOURCE:屈従 = 400
ELSEIF ABL:Ｖ感覚 == 3
	SOURCE:快Ｖ = 600
	SOURCE:屈従 = 700
ELSEIF ABL:Ｖ感覚 == 4
	SOURCE:快Ｖ = 1200
	SOURCE:屈従 = 1300
ELSE
	SOURCE:快Ｖ = 300 + (ABL:Ｖ感覚 * 300)
	SOURCE:屈従 = 250 + (ABL:Ｖ感覚 * 350)
ENDIF

;EXP:Ｖ経験をみる
IF EXP:Ｖ経験 < EXPLV:1
	TIMES SOURCE:快Ｖ , 0.20
	TIMES SOURCE:屈従, 0.20
	SOURCE:痛み = 300
ELSEIF EXP:Ｖ経験 < EXPLV:2
	TIMES SOURCE:快Ｖ , 0.50
	TIMES SOURCE:屈従, 0.50
	SOURCE:痛み = 180
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:屈従, 0.80
	SOURCE:痛み = 80
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES SOURCE:快Ｖ , 1.20
	TIMES SOURCE:屈従, 1.00
	SOURCE:痛み = 30
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES SOURCE:快Ｖ , 1.60
	TIMES SOURCE:屈従, 1.20
	SOURCE:痛み = 0
ELSE
	TIMES SOURCE:快Ａ , 1.80
	TIMES SOURCE:屈従, 1.50
	SOURCE:痛み = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:3 < PALAMLV:1
	TIMES SOURCE:快Ｖ , 0.10
	SOURCE:痛み += 700
	TIMES SOURCE:痛み , 3.00
ELSEIF PALAM:3 < PALAMLV:2
	TIMES SOURCE:快Ｖ , 0.20
	SOURCE:痛み += 200
	TIMES SOURCE:痛み , 1.00
ELSEIF PALAM:3 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 0.60
	TIMES SOURCE:痛み , 0.80
ELSEIF PALAM:3 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:痛み , 0.50
ELSEIF PALAM:3 >= PALAMLV:4
	TIMES SOURCE:快Ｖ , 2.00
	TIMES SOURCE:痛み , 0.10
ENDIF

;PALAM:欲情をみる
IF PALAM:5 < PALAMLV:1
	TIMES SOURCE:快Ｖ , 0.50
ELSEIF PALAM:5 < PALAMLV:2
	TIMES SOURCE:快Ｖ , 0.80
ELSEIF PALAM:5 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 1.20
ELSEIF PALAM:5 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.50
ELSEIF PALAM:5 >= PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.80
ENDIF


;Ｖ敏感、鈍感をみる
;快Ｖ自体のチェックは後でまとめてやる
IF TALENT:Ｖ鈍感
	TIMES SOURCE:痛み , 1.50
	TIMES SOURCE:屈従 , 1.50
	TIMES SOURCE:逸脱 , 1.50
ELSEIF TALENT:Ｖ敏感
	TIMES SOURCE:痛み , 0.60
	TIMES SOURCE:屈従 , 0.60
	TIMES SOURCE:逸脱 , 0.60
ENDIF

;処女で貞操観念
IF EXP:Ｖ経験 == 0 && TALENT:貞操観念 && TALENT:恋慕 == 0
	TIMES SOURCE:屈従 , 2.00
ENDIF

EXP:Ｖ経験 += 1
PRINTL Ｖ経験＋１

IF TEQUIP:90
	;奴隷のＶに触手の汚れがつく
	STAIN:ヴァギナ |= 2
	STAIN:ヴァギナ |= 4
ELSE
	;奴隷のＶ⇔調教者の指の汚れが移動
	STAIN:ヴァギナ |= STAIN:PLAYER:手
	STAIN:PLAYER:手 |= STAIN:ヴァギナ
ENDIF

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	PRINTS EXPNAME:40
	PRINTL +1
	EXP:レズ経験 += 1
	EXP:PLAYER:レズ経験 += 1
ELSEIF TALENT:オトコ == 1 && TALENT:PLAYER:オトコ == 1
	PRINTS EXPNAME:41
	PRINTL +1
	EXP:ＢＬ経験 += 1
	EXP:PLAYER:ＢＬ経験 += 1
ENDIF


;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:従順をみる
	IF ABL:親密 <= 1
		SOURCE:受動 += 300 + (ABL:親密 * 50)
	ELSEIF ABL:親密 <= 3
		SOURCE:受動 += 600 + (ABL:親密 * 100)
	ELSEIF ABL:親密 <= 5
		SOURCE:受動 += 800 + (ABL:親密 * 120)
	ELSEIF ABL:親密 <= 8
		SOURCE:受動 += 1000 + (ABL:親密 * 140)
	ELSEIF ABL:親密 <= 11
		SOURCE:受動 += 1200 + (ABL:親密 * 160)
	ELSE
		SOURCE:受動 += 1500 + (ABL:親密 * 180)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:サドっ気 == 0
		SOURCE:征服 = 600
	ELSEIF ABL:サドっ気 == 1
		SOURCE:征服 = 900
	ELSEIF ABL:サドっ気 == 2
		SOURCE:征服 = 1200
	ELSEIF ABL:サドっ気 == 3
		SOURCE:征服 = 1600
	ELSEIF ABL:サドっ気 == 4
		SOURCE:征服 = 2000
	ELSE
		SOURCE:征服 = 1000 + (ABL:サドっ気 * 300)
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:10 + 5
;合意がないと好感度が下がる
IF TALENT:合意 == 0 && TFLAG:48 == 0
	A -= 9
	SOURCE:反感追加 += 400
ENDIF
TFLAG:30 += A
TFLAG:37 += A / 2


RETURN 1


