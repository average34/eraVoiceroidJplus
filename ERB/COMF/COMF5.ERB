@COM5
;胸愛撫

SIF TEQUIP:65
	PRINT 着衣
PRINTL 胸愛撫
STR:0 = 胸愛撫
CALL TRAIN_MESSAGE_B

LOSEBASE:体力 += 5
LOSEBASE:気力 += 50

SOURCE:性行動 = 60
SOURCE:不潔 = 20
SOURCE:露出 = 100

;ABL:Ｂ感覚をみる
IF ABL:Ｂ感覚 == 0
	SOURCE:快Ｂ = 20
	SOURCE:情愛 = 50
ELSEIF ABL:Ｂ感覚 == 1
	SOURCE:快Ｂ = 100
	SOURCE:情愛 = 100
ELSEIF ABL:Ｂ感覚 == 2
	SOURCE:快Ｂ = 500
	SOURCE:情愛 = 160
ELSEIF ABL:Ｂ感覚 == 3
	SOURCE:快Ｂ = 1200
	SOURCE:情愛 = 200
ELSEIF ABL:Ｂ感覚 == 4
	SOURCE:快Ｂ = 2000
	SOURCE:情愛 = 230
ELSE
	SOURCE:快Ｂ = 800 + (ABL:Ｂ感覚 * 400)
	SOURCE:情愛 = 200 + (ABL:Ｂ感覚 * 10)
ENDIF

;調教者が［幼児退行］
IF TALENT:PLAYER:幼児
	TIMES SOURCE:快Ｂ , 1.20
	TIMES SOURCE:情愛 , 1.20
ENDIF

;調教者が［幼稚］
IF TALENT:PLAYER:幼稚
	TIMES SOURCE:快Ｂ , 1.20
	TIMES SOURCE:情愛 , 1.20
ENDIF

IF TEQUIP:90
	;奴隷のＢに触手の汚れがつく
	STAIN:胸 |= 2
	STAIN:胸 |= 4
ELSE
	;着衣中でかつコンフィグがONだと性能低下
	SIF TEQUIP:65
		TIMES SOURCE:快Ｂ , 0.80
	;奴隷の胸の汚れがなければ口をつかう（愛液と母乳よごれはＯＫ、汚れ無視か助手調教時は問題なし）
	IF STAIN:胸 < 2 || STAIN:胸 == 16 || STAIN:胸 == 17 || ASSIPLAY == 1 || TALENT:MASTER:汚れ無視 == 1
		;調教者のTALENT:舌使いをみる
		IF TALENT:PLAYER:舌使い
			TIMES SOURCE:快Ｂ , 1.40
			SOURCE:恭順追加 += SOURCE:快Ｂ/20
		ENDIF

		;奴隷のＢ⇔調教者の口の汚れが移動
		STAIN:胸 |= STAIN:PLAYER:口
		STAIN:PLAYER:口 |= STAIN:胸
	ENDIF

	;奴隷のＢ⇔調教者の指の汚れが移動
	STAIN:胸 |= STAIN:PLAYER:手
	STAIN:PLAYER:手 |= STAIN:胸
ENDIF

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	PRINTS EXPNAME:40
	PRINTL +5
	EXP:レズ経験 += 5
	EXP:PLAYER:レズ経験 += 5
ELSEIF TALENT:オトコ == 1 && TALENT:PLAYER:オトコ == 1
	PRINTS EXPNAME:41
	PRINTL +5
	EXP:ＢＬ経験 += 5
	EXP:PLAYER:ＢＬ経験 += 5
ENDIF


;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:従順をみる
	IF ABL:親密 <= 1
		SOURCE:受動 += 300 + (ABL:親密 * 50)
	ELSEIF ABL:親密 <= 3
		SOURCE:受動 += 600 + (ABL:親密 * 100)
	ELSEIF ABL:親密 <= 5
		SOURCE:受動 += 800 + (ABL:親密 * 120)
	ELSEIF ABL:親密 <= 8
		SOURCE:受動 += 1000 + (ABL:親密 * 140)
	ELSEIF ABL:親密 <= 11
		SOURCE:受動 += 1200 + (ABL:親密 * 160)
	ELSE
		SOURCE:受動 += 1500 + (ABL:親密 * 180)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:サドっ気 == 0
		SOURCE:征服 = 600
	ELSEIF ABL:サドっ気 == 1
		SOURCE:征服 = 900
	ELSEIF ABL:サドっ気 == 2
		SOURCE:征服 = 1200
	ELSEIF ABL:サドっ気 == 3
		SOURCE:征服 = 1600
	ELSEIF ABL:サドっ気 == 4
		SOURCE:征服 = 2000
	ELSE
		SOURCE:征服 = 1000 + (ABL:サドっ気 * 300)
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:10 + 5
;合意がないと好感度が下がる
IF TALENT:合意 == 0 && TFLAG:48 == 0
	A -= 7
	SOURCE:反感追加 += 200
ENDIF
TFLAG:30 += A
TFLAG:37 += A


;［Ｂ感度］３以下では発生しない
;［母乳体質］無しかつ［小柄体系］で［貧乳］無しかつ［オトコ］無し
;触手調教は発生しない
SIF ABL:TARGET:Ｂ感覚 <= 3 || TEQUIP:90
	RETURN 1
IF TALENT:母乳体質 == 0 && (TALENT:小柄体型 == 0 || TALENT:貧乳 == 0 ) && TALENT:オトコ == 0 
	CALL YOUJI
ENDIF

RETURN 1

