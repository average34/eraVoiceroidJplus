@COM340
;バードキス

PRINTL バードキス
CALL GET_SUCCESS_RATE
B = 90 + A / 10
SIF B > 99
	B = 99
A = RAND:100
IF A <= 9
	;10％で大成功
	TFLAG:18 = 1
ELSEIF A >= B
	;10～1％で失敗
	TFLAG:18 = -1
ELSE
	;残りは成功
	TFLAG:18 = 0
ENDIF


;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

STR:0 = バードキス
CALL TRAIN_MESSAGE_B

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
;キス経験で気力消費軽減（何度もキスしたがる、の再現だとでも脳内補完）
IF EXP:96 < 100
	LOSEBASE:1 += 50
ELSEIF EXP:96 < 500
	LOSEBASE:1 += 40
ELSE
	LOSEBASE:1 += 25
ENDIF

;Ｍ感覚を設定している場合快Ｍソース等取得
SIF FLAG:62 & 4194304
	CALL SOURCE_M_COM340

;固定で獲得するソース
SOURCE:18 = 50
SOURCE:9 = 500

;ABL:従順をみる
IF ABL:0 <= 1
	SOURCE:18 += (ABL:0 * 20)
	SOURCE:9 += (ABL:0 * 65)
ELSEIF ABL:0 <= 3
	SOURCE:18 += 100 + (ABL:0 * 20)
	SOURCE:9 += 200 + (ABL:0 * 70)
ELSEIF ABL:0 <= 5
	SOURCE:18 += 200 + (ABL:0 * 20)
	SOURCE:9 += 500 + (ABL:0 * 70)
ELSEIF ABL:0 <= 8
	SOURCE:18 += 300 + (ABL:0 * 30)
	SOURCE:9 += 700 + (ABL:0 * 80)
ELSEIF ABL:0 <= 11
	SOURCE:18 += 400 + (ABL:0 * 30)
	SOURCE:9 += 1200 + (ABL:0 * 85)
ELSE
	SOURCE:18 += 800 + (ABL:0 * 15)
	SOURCE:9 += 2000 + (ABL:0 * 40)
ENDIF

;好感度をみる
IF CFLAG:2 <= 500
	SOURCE:18 += CFLAG:2
ELSEIF CFLAG:2 <= 5000
	SOURCE:18 += 500 + (CFLAG:2 - 500) / 3
ELSE
	SOURCE:18 += 2000 + (CFLAG:2 - 5000) / 5
ENDIF
SIF SOURCE:18 < 0
	SOURCE:18 = 0


;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:従順をみる
	IF ABL:0 <= 1
		SOURCE:21 += 300 + (ABL:0 * 50)
	ELSEIF ABL:0 <= 3
		SOURCE:21 += 600 + (ABL:0 * 100)
	ELSEIF ABL:0 <= 5
		SOURCE:21 += 800 + (ABL:0 * 120)
	ELSEIF ABL:0 <= 8
		SOURCE:21 += 1000 + (ABL:0 * 140)
	ELSEIF ABL:0 <= 11
		SOURCE:21 += 1200 + (ABL:0 * 160)
	ELSE
		SOURCE:21 += 1500 + (ABL:0 * 180)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:15 == 0
		SOURCE:20 = 600
	ELSEIF ABL:15 == 1
		SOURCE:20 = 900
	ELSEIF ABL:15 == 2
		SOURCE:20 = 1200
	ELSEIF ABL:15 == 3
		SOURCE:20 = 1600
	ELSEIF ABL:15 == 4
		SOURCE:20 = 2000
	ELSE
		SOURCE:20 = 1000 + (ABL:15 * 300)
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:6 - 1
TFLAG:30 += A

IF TFLAG:18 == -1
	TIMES SOURCE:18 , 0.10
	TIMES SOURCE:9 , 0.50
	TIMES SOURCE:20 , 0.50
	TIMES SOURCE:21 , 0.50
	TFLAG:30 -= 10
	TFLAG:37 -= 5
ELSEIF TFLAG:18 == 0
	TIMES SOURCE:18 , 1.00
	TIMES SOURCE:9 , 1.00
	TIMES SOURCE:20 , 1.00
	TIMES SOURCE:21 , 1.00
	TFLAG:37 += A
ELSE
	TIMES SOURCE:18 , 2.00
	TIMES SOURCE:9 , 2.00
	TIMES SOURCE:20 , 2.00
	TIMES SOURCE:21 , 2.00
	TFLAG:30 += 2
	TFLAG:37 += 5 + A
ENDIF

;デート中は更にムードアップ
SIF TEQUIP:2
	TFLAG:37 += 3

PRINTFORML %EXPNAME:96%＋１
EXP:96 += 1
EXP:PLAYER:96 += 1
IF TALENT:PLAYER:151
	PRINTFORMW %CALLNAME:PLAYER%は【キス未経験】を失いました
	TALENT:PLAYER:151 = 0
ENDIF

IF TALENT:122 == 0 && TALENT:PLAYER:122 == 0
	PRINTS EXPNAME:40
	PRINTL +1
	EXP:40 += 1
	EXP:PLAYER:40 += 1
ELSEIF TALENT:122 == 1 && TALENT:PLAYER:122 == 1
	PRINTS EXPNAME:41
	PRINTL +1
	EXP:41 += 1
	EXP:PLAYER:41 += 1
ENDIF

IF TALENT:151
	TFLAG:600 = 1
	CALL KOJO_JUN
	TALENT:151 = 0
	PRINTFORMW %CALLNAME:TARGET%は【キス未経験】を失いました
	TFLAG:38 = 1

	;既成事実１に相当
	TFLAG:200 = 1
ENDIF

RETURN 1

;Ｍ感覚を設定している場合快Ｍソース等取得
@SOURCE_M_COM340
VARSET LOCAL, 0

;屈従・反感ソース
LOCAL:13 += 100
LOCAL:14 += 10

;ABL:Ｍ感覚をみる
IF ABL:17 == 0
	LOCAL:24 += 1
ELSEIF ABL:17 == 1
	LOCAL:24 += 10
ELSEIF ABL:17 == 2
	LOCAL:24 += 40
ELSEIF ABL:17 == 3
	LOCAL:24 += 100
ELSEIF ABL:17 == 4
	LOCAL:24 += 200
ELSE
	LOCAL:24 += 100 + (ABL:17 * 40)
ENDIF

;PLAYERの技巧を見る
SIF ABL:PLAYER:2 >= 4
	LOCAL:24 = LOCAL:24 * (100 + (ABL:PLAYER:2 / 4)) / 100

;技巧を見る
SIF ABL:TARGET:2 >= 4
	LOCAL:24 = LOCAL:24 * (100 + (ABL:TARGET:2 / 4)) / 100

;EXP:キス経験をみる
IF EXP:96 < EXPLV:1
	TIMES LOCAL:24 , 1.00
ELSEIF EXP:96 < EXPLV:2
	TIMES LOCAL:14 , 0.95
	TIMES LOCAL:24 , 1.05
ELSEIF EXP:96 < EXPLV:3
	TIMES LOCAL:14 , 0.90
	TIMES LOCAL:24 , 1.10
ELSEIF EXP:96 < EXPLV:4
	TIMES LOCAL:14 , 0.85
	TIMES LOCAL:24 , 1.15
ELSEIF EXP:96 < EXPLV:5
	TIMES LOCAL:14 , 0.80
	TIMES LOCAL:24 , 1.20
ELSE
	TIMES LOCAL:14 , 0.75
	TIMES LOCAL:24 , 1.25
ENDIF

;TALENT:恋慕をみる
SIF TALENT:85
	TIMES LOCAL:24 , 1.50
;TALENT:恋人をみる
SIF TALENT:153
	TIMES LOCAL:24 , 1.50
;TALENT:親愛をみる
SIF TALENT:88
	TIMES LOCAL:24 , 2.00
;TALENT:相愛をみる
SIF TALENT:89
	TIMES LOCAL:24 , 2.00

;反発感情をみる
IF MARK:3 == 1
	TIMES LOCAL:24 , 0.50
	TIMES LOCAL:13 , 1.15
ELSEIF MARK:3 == 2
	TIMES LOCAL:24 , 0.25
	TIMES LOCAL:13 , 1.45
ELSEIF MARK:3 == 3
	TIMES LOCAL:24 , 0.10
	TIMES LOCAL:13 , 1.90
ENDIF

;成功をみる
IF TFLAG:18 == -1
	TIMES LOCAL:24 , 0.50
ELSEIF TFLAG:18 == 0
	TIMES LOCAL:24 , 1.00
ELSE
	TIMES LOCAL:24 , 2.00
ENDIF

SOURCE:13 += LOCAL:13
SOURCE:14 += LOCAL:14
SOURCE:24 += LOCAL:24

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_340

A = 0
S = 0
V = 40

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;男嫌い
IF TALENT:82 && TALENT:PLAYER:122
	A -= 7
ENDIF
;ABL:奉仕精神
IF ABL:6
	A += ABL:6 * 1
ENDIF
;恥じらい
IF TALENT:35
	A -= 5
ENDIF
;献身的
IF TALENT:63
	A += 6
ENDIF
;快感の否定
IF TALENT:71
	A -= 1
ENDIF
;恋慕
IF TALENT:85 && ASSIPLAY == 0
	A += 5
	S = 5
ENDIF

;キス未経験
IF TALENT:151
	A -= 10
ENDIF

;一線越えない（キス未経験時のみ）
IF TALENT:151 && TALENT:27
	A -= 10
ENDIF

;デート中
IF TEQUIP:2
	A += 3
ENDIF

;ムードボーナス
B = 0
IF TFLAG:37 < 10
	B -= 10
ELSEIF TFLAG:37 < 30
	B -= 1
ELSEIF TFLAG:37 < 50
	B += 1
ELSEIF TFLAG:37 < 100
	B += TFLAG:37 / 10
ELSEIF TFLAG:37 < 300
	B += TFLAG:37 / 5
ELSE
	B += TFLAG:37 / 4
ENDIF
A += B
IF B < 0
	B = B * -1
ELSE
ENDIF


;好感度
IF CFLAG:2 < 400
	B = 0
	IF CFLAG:2 < 100
		B = 20
	ELSEIF CFLAG:2 < 250
		B = 10
	ELSE
		B = 5
	ENDIF
	A -= B
ENDIF

;好感度
IF CFLAG:2 >= 500
	B = 0
	B = CFLAG:2 / 60
	A += B
ENDIF

IF ((FLAG:23 & 1p27) == 0) && FLAG:2 != 1
	;男嫌い
	IF TALENT:82 && TALENT:PLAYER:122
		PRINT  - 
		PRINTS TALENTNAME:82
		PRINTV '(,7,')
		S = 1
	ENDIF
	;ABL:奉仕精神
	IF ABL:6
		SIF S
			PRINT  + 
		PRINTS ABLNAME:6
		PRINTV 'LV,ABL:6,'(,ABL:6 * 1,')
		S = 1
	ENDIF
	;恥じらい
	IF TALENT:35
		PRINT  - 
		PRINTS TALENTNAME:35
		PRINTV '(,5,')
		S = 1
	ENDIF
	;献身的
	IF TALENT:63
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:63
		PRINTV '(,6,')
		S = 1
	ENDIF
	;快感の否定
	IF TALENT:71
		PRINT  - 
		PRINTS TALENTNAME:71
		PRINTV '(,1,')
		S = 1
	ENDIF
	;恋慕
	IF TALENT:85 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:85
		PRINTV '(,5,')
		S = 5
	ENDIF

	;キス未経験
	IF TALENT:151
		PRINT  - 
		PRINTS TALENTNAME:151
		PRINTV '(,10,')
		S = 1
	ENDIF

	;一線越えない（キス未経験時のみ）
	IF TALENT:151 && TALENT:27
		PRINT  - 
		PRINTS TALENTNAME:27
		PRINTV '(,10,')
		S = 1
	ENDIF

	;デート中
	IF TEQUIP:2
		PRINT  + 
		PRINT デート中
		PRINTV '(,3,')
		S = 1
	ENDIF

	;ムードボーナス
	B = 0
	IF TFLAG:37 < 10
		B -= 10
	ELSEIF TFLAG:37 < 30
		B -= 1
	ELSEIF TFLAG:37 < 50
		B += 1
	ELSEIF TFLAG:37 < 100
		B += TFLAG:37 / 10
	ELSEIF TFLAG:37 < 300
		B += TFLAG:37 / 5
	ELSE
		B += TFLAG:37 / 4
	ENDIF
	IF B < 0
		B = B * -1
		PRINT  - 
		PRINT ムード悪い
		PRINTV '(,B,')
		S = 1
	ELSE
		PRINT  + 
		PRINT ムード良い
		PRINTV '(,B,')
		S = 1
	ENDIF


	;好感度
	IF CFLAG:2 < 400
		B = 0
		PRINT  - 
		IF CFLAG:2 < 100
			B = 20
		ELSEIF CFLAG:2 < 250
			B = 10
		ELSE
			B = 5
		ENDIF
		PRINT 好感度不足
		PRINTV '(,B,')
		S = 1
	ENDIF

	;好感度
	IF CFLAG:2 >= 500
		B = 0
		SIF S
			PRINT  + 
		B = CFLAG:2 / 60
		PRINT 好感度
		PRINTV '(,B,')
		S = 1
	ENDIF

	;合計を表示(40以上で実行)
	PRINT  = 
	PRINTV A

	IF A < V
		PRINT  < 
		PRINT 実行値
		PRINTV V
	ELSEIF A == V
		PRINT  = 
		PRINT 実行値
		PRINTV V
	ELSEIF A > V
		PRINT  > 
		PRINT 実行値
		PRINTV V
	ENDIF
	WAIT
ENDIF



