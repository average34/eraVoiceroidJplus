@COM_ABLE167
;角挿入実行判定
;COM_ABLE共通判定
CALL COM_ABLE_COMMON, SELECTCOM
SIF RESULT == 0
	RETURN 0
;ウフフ中じゃないと無理
SIF TFLAG:44 == 0
	RETURN 0
;助手じゃないとダメ
SIF PLAYER != ASSI
	RETURN 0
;異常経験フィルタ
SIF ((FLAG:61 & 1) == 0) && (EXP:Ｖ経験 < EXPLV:1) && (TALENT:PLAYER:122 == 0)
	RETURN 0
;バイブ挿入中はダメ
SIF TEQUIP:11
	RETURN 0
;クスコ挿入中はダメ
SIF TEQUIP:61
	RETURN 0
;焼芋入中は不可
SIF TEQUIP:71
	RETURN 0
;オトコだとダメ
SIF TALENT:122
	RETURN 0
;調教対象が小人体型の場合は主人が禁断の知識持ちじゃないとダメ
SIF TALENT:149 && TALENT:MASTER:125 == 0
	RETURN 0
;調教対象に角がないとダメ
SIF TALENT:214 == 0
	RETURN 0
;処女で、従順4以下・レズっ気4以下のASSIの場合角挿入はできない（サドならＯＫ）
IF EXP:Ｖ経験 == 0 && ASSIPLAY
	SIF (ABL:ASSI:親密 <= 4 || ABL:ASSI:レズっ気 <= 4) && TALENT:ASSI:83 == 0
		RETURN 0
ENDIF
;潤滑不足で、従順3以下・レズっ気3以下のASSIの場合角挿入はできない（サドならＯＫ）
IF PALAM:3 < PALAMLV:2 && ASSIPLAY
	SIF (ABL:ASSI:親密 <= 3 || ABL:ASSI:レズっ気 <= 3) && TALENT:ASSI:83 == 0
		RETURN 0
ENDIF
;触手調教中はダメ
SIF TEQUIP:90
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
IF TEQUIP:58
SIF ITEM:13 == 0 && NOITEM == 0
	RETURN 0
ENDIF
;caved!!!!が発動しているとダメ
SIF TFLAG:69 > 0
	RETURN 0
RETURN 1

@COM167
;角挿入

;調教対象が緊縛していない反発感情3のキモけーねだった場合、逆襲される可能性大
IF NAME_CSV(NO:TARGET, 0) == "慧音" && TEQUIP:1 && MARK:3 >= 3 && TEQUIP:44 == 0
	IF RAND:4 > 0 && TFLAG:69 == 0
		TFLAG:69 = 2
		JUMP CAVED
	ENDIF
ENDIF

PRINTL 角挿入
STR:0 = 角挿入
CALL TRAIN_MESSAGE_B

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
;V経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:19 = 1

LOSEBASE:体力 += 700
LOSEBASE:気力 += 350

SOURCE:3 = 200
SOURCE:6 = 2500
SOURCE:12 = 1200
SOURCE:13 = 1800
SOURCE:14 = 2500


;ABL:V感覚をみる
IF ABL:Ｖ感覚 == 0
	SOURCE:1 = 1700
ELSEIF ABL:Ｖ感覚 == 1
	SOURCE:1 = 2000
ELSEIF ABL:Ｖ感覚 == 2
	SOURCE:1 = 2500
ELSEIF ABL:Ｖ感覚 == 3
	SOURCE:1 = 3000
ELSEIF ABL:Ｖ感覚 == 4
	SOURCE:1 = 3500
ELSE
	SOURCE:1 = 4000
ENDIF

;EXP:Ｖ経験をみる
IF EXP:Ｖ経験 < EXPLV:1
	TIMES SOURCE:1 , 0.20
	SOURCE:6 += 5000

	IF ASSIPLAY && TALENT:PLAYER:122 == 0
		EXP:異常経験 += 2
		PRINTFORML %EXPNAME:50%＋2
	ENDIF
ELSEIF EXP:Ｖ経験 < EXPLV:2
	TIMES SOURCE:1 , 0.60
	SOURCE:6 += 600
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES SOURCE:1 , 1.00
	SOURCE:6 = 300
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES SOURCE:1 , 1.20
	SOURCE:6 = 30
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES SOURCE:1 , 1.30
	SOURCE:6 = 3
ELSE
	TIMES SOURCE:1 , 1.80
	SOURCE:6 = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:3 < PALAMLV:1
	TIMES SOURCE:1 , 0.10
	SOURCE:6 += 900
	TIMES SOURCE:6 , 3.00
ELSEIF PALAM:3 < PALAMLV:2
	TIMES SOURCE:1 , 0.40
	SOURCE:6 += 250
	TIMES SOURCE:6 , 1.00
ELSEIF PALAM:3 < PALAMLV:3
	TIMES SOURCE:1 , 1.00
	TIMES SOURCE:6 , 0.50
ELSEIF PALAM:3 < PALAMLV:4
	TIMES SOURCE:1 , 1.40
	TIMES SOURCE:6 , 0.20
ELSEIF PALAM:3 >= PALAMLV:4
	TIMES SOURCE:1 , 1.80
	TIMES SOURCE:6 , 0.10
ENDIF

;小柄体形
SIF TALENT:100
	TIMES SOURCE:6 , 2.00

;貞操観念
IF TALENT:30
	IF EXP:Ｖ経験 == 0
		TIMES SOURCE:3 , 0.60
		SOURCE:15 = 10000
	ELSE
		TIMES SOURCE:3 , 0.60
		SOURCE:15 = 1000
	ENDIF
;貞操無頓着
ELSEIF TALENT:31
	IF EXP:Ｖ経験 == 0
		TIMES SOURCE:3 , 0.60
		SOURCE:15 = 300
	ENDIF
ELSE
	IF EXP:Ｖ経験 == 0
		SOURCE:15 = 3000
	ENDIF
ENDIF

;PALAM:欲情をみる
IF PALAM:5 < PALAMLV:1
	TIMES SOURCE:1 , 0.60
ELSEIF PALAM:5 < PALAMLV:2
	TIMES SOURCE:1 , 0.80
ELSEIF PALAM:5 < PALAMLV:3
	TIMES SOURCE:1 , 1.00
ELSEIF PALAM:5 < PALAMLV:4
	TIMES SOURCE:1 , 1.20
ELSEIF PALAM:5 >= PALAMLV:4
	TIMES SOURCE:1 , 1.50
ENDIF

;ABL:従順をみる
IF ABL:親密 == 0
	TIMES SOURCE:1 , 0.50
	TIMES SOURCE:15 , 2.00
ELSEIF ABL:親密 == 1
	TIMES SOURCE:1 , 0.80
	TIMES SOURCE:15 , 1.50
ELSEIF ABL:親密 == 2
	TIMES SOURCE:1 , 1.00
	TIMES SOURCE:15 , 1.00
ELSEIF ABL:親密 == 3
	TIMES SOURCE:1 , 1.30
	TIMES SOURCE:15 , 0.80
ELSEIF ABL:親密 == 4
	TIMES SOURCE:1 , 1.60
	TIMES SOURCE:15 , 0.60
ELSE
	TIMES SOURCE:1 , 2.00
	TIMES SOURCE:15 , 0.30
ENDIF

EXP:Ｖ経験 += 20
PRINTL Ｖ経験＋２０
EXP:Ｖ拡張経験 += 1
PRINTL Ｖ拡張経験＋１

IF TALENT:122 == 0 && TALENT:PLAYER:122 == 0
	PRINTS EXPNAME:40
	PRINTL +4
	EXP:レズ経験 += 4
ENDIF


RETURN 1

@TRAIN_MESSAGE_COM167
PRINTFORM %CALLNAME:PLAYER%は自分の角で%CALLNAME:TARGET%の
IF EXP:Ｖ経験 == 0
	PRINT 男を知らない
ELSEIF PALAM:3 >= PALAMLV:4
	PRINT 濡れそぼった
ENDIF
IF TALENT:100
	PRINT 幼いワレメを
ELSE
	PRINT 秘裂を
ENDIF
PRINTW 貫いた…

