@COM_ABLE316
;髪を梳いて貰う実行判定
;COM_ABLE共通判定
CALL COM_ABLE_COMMON, SELECTCOM
SIF RESULT == 0
	RETURN 0
;ウフフ中は無理
SIF TFLAG:44 == 1
	RETURN 0
;親密3以上or好感度300以上or恋人が必要
SIF ABL:親密 < 3 && CFLAG:2 < 300 && TALENT:恋人 == 0
	RETURN 0
RETURN 1

@COM316
;髪を梳いて貰う

PRINTL 髪を梳いて貰う
CALL GET_SUCCESS_RATE
B = 95 + A / 10
SIF B > 99
	B = 99
A = RAND:100
IF A <= 9
	;10％で大成功
	TFLAG:18 = 1
ELSEIF A >= B
	;10～1％で失敗
	TFLAG:18 = -1
ELSE
	;残りは成功
	TFLAG:18 = 0
ENDIF

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

STR:0 = 髪を梳いて貰う
CALL TRAIN_MESSAGE_B

LOSEBASE:気力 += 60

;固定で獲得するソース
SOURCE:歓楽 = 0
SOURCE:接触 = 0

;ABL:従順をみる
IF ABL:親密 <= 1
	SOURCE:歓楽 += (ABL:親密 * 25)
ELSEIF ABL:親密 <= 3
	SOURCE:歓楽 += 150 + (ABL:親密 * 25)
ELSEIF ABL:親密 <= 5
	SOURCE:歓楽 += 300 + (ABL:親密 * 30)
ELSEIF ABL:親密 <= 8
	SOURCE:歓楽 += 600 + (ABL:親密 * 30)
ELSEIF ABL:親密 <= 11
	SOURCE:歓楽 += 1000 + (ABL:親密 * 35)
ELSE
	SOURCE:歓楽 += 1600 + (ABL:親密 * 25)
ENDIF


;好感度をみる
IF CFLAG:2 <= 500
	SOURCE:歓楽 += CFLAG:2
ELSEIF CFLAG:2 <= 5000
	SOURCE:歓楽 += 500 + (CFLAG:2 - 500) / 3
ELSE
	SOURCE:歓楽 += 2000 + (CFLAG:2 - 5000) / 5
ENDIF
SIF SOURCE:歓楽 < 0
	SOURCE:歓楽 = 0

;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:従順をみる
	IF ABL:親密 <= 1
		SOURCE:受動 += 300 + (ABL:親密 * 20)
	ELSEIF ABL:親密 <= 3
		SOURCE:受動 += 300 + (ABL:親密 * 30)
	ELSEIF ABL:親密 <= 5
		SOURCE:受動 += 500 + (ABL:親密 * 40)
	ELSEIF ABL:親密 <= 8
		SOURCE:受動 += 600 + (ABL:親密 * 50)
	ELSEIF ABL:親密 <= 11
		SOURCE:受動 += 800 + (ABL:親密 * 60)
	ELSE
		SOURCE:受動 += 1000 + (ABL:親密 * 70)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:サドっ気 == 0
		SOURCE:征服 = 600
	ELSEIF ABL:サドっ気 == 1
		SOURCE:征服 = 800
	ELSEIF ABL:サドっ気 == 2
		SOURCE:征服 = 1200
	ELSEIF ABL:サドっ気 == 3
		SOURCE:征服 = 1600
	ELSEIF ABL:サドっ気 == 4
		SOURCE:征服 = 2000
	ELSE
		SOURCE:征服 = 1500 + (ABL:サドっ気 * 300)
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:6 - 1
TFLAG:30 += A

IF TFLAG:18 == -1
	TIMES SOURCE:歓楽 , 0.10
	TIMES SOURCE:恭順追加 , 0.10
	TIMES SOURCE:征服 , 0.50
	TIMES SOURCE:受動 , 0.50
	TFLAG:30 -= 10
	TFLAG:37 -= 5
ELSEIF TFLAG:18 == 0
	TIMES SOURCE:歓楽 , 1.00
	TIMES SOURCE:恭順追加 , 1.00
	TIMES SOURCE:征服 , 1.00
	TIMES SOURCE:受動 , 1.00
	TFLAG:37 += A
	;恋慕または恋人ありで愛情経験追加
	IF TALENT:恋慕 || TALENT:恋人
		EXP:愛情経験 += 2
		PRINTFORML %EXPNAME:23%+2
	ENDIF
ELSE
	TIMES SOURCE:歓楽 , 2.00
	TIMES SOURCE:恭順追加 , 2.00
	TIMES SOURCE:征服 , 2.00
	TIMES SOURCE:受動 , 2.00
	TFLAG:30 += 2
	TFLAG:37 += 5 + A
	;愛情経験追加
	IF TALENT:恋慕 || TALENT:恋人
		EXP:愛情経験 += 6
		PRINTFORML %EXPNAME:23%+6
	ELSE
		EXP:愛情経験 += 2
		PRINTFORML %EXPNAME:23%+2
	ENDIF
ENDIF

;手櫛補正
IF TFLAG:17 == 0
	SOURCE:接触 += 400
	TIMES SOURCE:歓楽 , 1.20
	TIMES SOURCE:恭順追加 , 1.20
	TIMES SOURCE:征服 , 1.20
	TIMES SOURCE:受動 , 1.20
	TFLAG:30 += 5
	TFLAG:37 += 7
ENDIF

RETURN 1

@TRAIN_MESSAGE_COM316
;髪を梳いて貰う
;手櫛
IF TFLAG:17 == 0
	;主導権で分岐
	IF TFLAG:45
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%に手櫛を入れた…
	ELSE
		PRINTFORMW %CALLNAME:PLAYER%は%CALLNAME:TARGET%に手櫛を入れさせた…
	ENDIF
;櫛
ELSE
	;主導権で分岐
	IF TFLAG:45
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の髪を櫛で梳かした…
	ELSE
		PRINTFORMW %CALLNAME:PLAYER%は自分の髪を%CALLNAME:TARGET%に櫛で梳かさせた…
	ENDIF
ENDIF
IF TFLAG:18 == 1
	PRINTFORMW %CALLNAME:TARGET%は楽しそうだ…
ELSEIF TFLAG:18 == -1
	PRINTFORMW %CALLNAME:TARGET%の顔には飽きの色が見える…
ENDIF

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_316

A = 0
S = 0

;櫛が無くアイテム有り設定なら手櫛オンリー
V = 25
SIF ITEM:櫛 == 0 && NOITEM == 0
	GOTO SKIP
;有れば手櫛と櫛を選択
$INPUT_LOOP
PRINTL [0] 手櫛で梳いて貰う
PRINTL [1] 櫛で梳いて貰う
INPUT
IF RESULT == 0
	V = 25
ELSEIF RESULT == 1
	V = 20
	TFLAG:17 = 1
ELSE
	GOTO INPUT_LOOP
ENDIF
$SKIP


;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;男嫌い
IF TALENT:男嫌い && TALENT:PLAYER:オトコ
	A -= 7
ENDIF

;恋慕
IF TALENT:恋慕 && ASSIPLAY == 0
	A += 5
	S = 5
ENDIF

;デート中
IF TEQUIP:2
	A += 3
ENDIF

;ムードボーナス
B = 0
IF TFLAG:37 < 10
	B -= 10
ELSEIF TFLAG:37 < 30
	B -= 1
ELSEIF TFLAG:37 < 50
	B += 1
ELSEIF TFLAG:37 < 100
	B += TFLAG:37 / 10
ELSEIF TFLAG:37 < 300
	B += TFLAG:37 / 5
ELSE
	B += TFLAG:37 / 4
ENDIF
A += B
IF B < 0
	B = B * -1
ELSE
ENDIF

;好感度
IF CFLAG:2 < 300
	B = 0
	IF CFLAG:2 < 0
		B = 20
	ELSEIF CFLAG:2 < 100
		B = 10
	ELSE
		B = 5
	ENDIF
	A -= B
ENDIF

;好感度
IF CFLAG:2 >= 400
	B = 0
	B = CFLAG:2 / 30
	A += B
ENDIF

IF ((FLAG:23 & 1p27) == 0) && FLAG:2 != 1
	;男嫌い
	IF TALENT:男嫌い && TALENT:PLAYER:オトコ
		PRINT  - 
		PRINTS TALENTNAME:82
		PRINTV '(,7,')
		S = 1
	ENDIF

	;恋慕
	IF TALENT:恋慕 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:85
		PRINTV '(,5,')
		S = 5
	ENDIF

	;デート中
	IF TEQUIP:2
		PRINT  + 
		PRINT デート中
		PRINTV '(,3,')
		S = 1
	ENDIF

	;ムードボーナス
	B = 0
	IF TFLAG:37 < 10
		B -= 10
	ELSEIF TFLAG:37 < 30
		B -= 1
	ELSEIF TFLAG:37 < 50
		B += 1
	ELSEIF TFLAG:37 < 100
		B += TFLAG:37 / 10
	ELSEIF TFLAG:37 < 300
		B += TFLAG:37 / 5
	ELSE
		B += TFLAG:37 / 4
	ENDIF
	IF B < 0
		B = B * -1
		PRINT  - 
		PRINT ムード悪い
		PRINTV '(,B,')
		S = 1
	ELSE
		PRINT  + 
		PRINT ムード良い
		PRINTV '(,B,')
		S = 1
	ENDIF

	;好感度
	IF CFLAG:2 < 300
		B = 0
		PRINT  - 
		IF CFLAG:2 < 0
			B = 20
		ELSEIF CFLAG:2 < 100
			B = 10
		ELSE
			B = 5
		ENDIF
		PRINT 好感度不足
		PRINTV '(,B,')
		S = 1
	ENDIF

	;好感度
	IF CFLAG:2 >= 400
		B = 0
		SIF S
			PRINT  + 
		B = CFLAG:2 / 30
		PRINT 好感度
		PRINTV '(,B,')
		S = 1
	ENDIF

	;合計を表示(30以上で実行)
	PRINT  = 
	PRINTV A

	IF A < V
		PRINT  < 
		PRINT 実行値
		PRINTV V
	ELSEIF A == V
		PRINT  = 
		PRINT 実行値
		PRINTV V
	ELSEIF A > V
		PRINT  > 
		PRINT 実行値
		PRINTV V
	ENDIF
	WAIT
ENDIF

