@COM321
;遊びに行く

PRINTL 遊びに行く($1500を消費)
CALL GET_SUCCESS_RATE
B = 90 + A / 10
SIF B > 99
	B = 99
A = RAND:100
IF A <= 9
	;10％で大成功
	TFLAG:18 = 1
ELSEIF A >= B
	;10～1％で失敗
	TFLAG:18 = -1
ELSE
	;残りは成功
	TFLAG:18 = 0
ENDIF

;15%で戦闘訓練が発生
IF RAND:100 < 15
	TFLAG:17 = 1
;戦闘訓練が発生しなかった場合、20%で幽霊屋敷行きが発生
ELSEIF RAND:100 < 20
	TFLAG:17 = 2
ELSE
	TFLAG:17 = 0
ENDIF

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

STR:0 = 遊びに行く
CALL TRAIN_MESSAGE_B

LOSEBASE:気力 += 150
MONEY -= 1500

;固定で獲得するソース
SOURCE:18 = 300
SOURCE:9 = 0

;ABL:従順をみる
IF ABL:親密 <= 1
	SOURCE:18 += (ABL:親密 * 70)
	SOURCE:9 += (ABL:親密 * 20)
ELSEIF ABL:親密 <= 3
	SOURCE:18 += 200 + (ABL:親密 * 75)
	SOURCE:9 += 100 + (ABL:親密 * 20)
ELSEIF ABL:親密 <= 5
	SOURCE:18 += 500 + (ABL:親密 * 75)
	SOURCE:9 += 200 + (ABL:親密 * 20)
ELSEIF ABL:親密 <= 8
	SOURCE:18 += 700 + (ABL:親密 * 85)
	SOURCE:9 += 300 + (ABL:親密 * 30)
ELSEIF ABL:親密 <= 11
	SOURCE:18 += 1200 + (ABL:親密 * 90)
	SOURCE:9 += 400 + (ABL:親密 * 30)
ELSE
	SOURCE:18 += 2000 + (ABL:親密 * 45)
	SOURCE:9 += 800 + (ABL:親密 * 15)
ENDIF

;好感度をみる
IF CFLAG:2 <= 500
	SOURCE:18 += CFLAG:2
ELSEIF CFLAG:2 <= 5000
	SOURCE:18 += 500 + (CFLAG:2 - 500) / 3
ELSE
	SOURCE:18 += 2000 + (CFLAG:2 - 5000) / 5
ENDIF
SIF SOURCE:18 < 0
	SOURCE:18 = 0

;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:従順をみる
	IF ABL:親密 <= 1
		SOURCE:21 += 300 + (ABL:親密 * 50)
	ELSEIF ABL:親密 <= 3
		SOURCE:21 += 600 + (ABL:親密 * 100)
	ELSEIF ABL:親密 <= 5
		SOURCE:21 += 800 + (ABL:親密 * 120)
	ELSEIF ABL:親密 <= 8
		SOURCE:21 += 1000 + (ABL:親密 * 140)
	ELSEIF ABL:親密 <= 11
		SOURCE:21 += 1200 + (ABL:親密 * 160)
	ELSE
		SOURCE:21 += 1500 + (ABL:親密 * 180)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:サドっ気 == 0
		SOURCE:20 = 600
	ELSEIF ABL:サドっ気 == 1
		SOURCE:20 = 900
	ELSEIF ABL:サドっ気 == 2
		SOURCE:20 = 1200
	ELSEIF ABL:サドっ気 == 3
		SOURCE:20 = 1600
	ELSEIF ABL:サドっ気 == 4
		SOURCE:20 = 2000
	ELSE
		SOURCE:20 = 1000 + (ABL:サドっ気 * 300)
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:6 - 1
B = 0
;パワーアップ版ならソースを追加
IF TFLAG:17 == 1
	;戦闘訓練
	IF A < 0
		B = 5
	ELSEIF A < 3
		B = 3
	ELSE
		B = 2
	ENDIF
	TIMES SOURCE:18 , 1.20
	TIMES SOURCE:9 , 2.00
	TIMES SOURCE:20 , 1.50
	TIMES SOURCE:21 , 1.50
	A += 2
ELSEIF TFLAG:17 == 2
	;幽霊屋敷行き
	TIMES SOURCE:9 , 2.50
	SOURCE:23 = RAND:501 + 1000
	A += 2
ENDIF


;好感度変化
TFLAG:30 += A

IF TFLAG:18 == -1
	TIMES SOURCE:18 , 0.10
	TIMES SOURCE:9 , 0.50
	TIMES SOURCE:20 , 0.50
	TIMES SOURCE:21 , 0.50
	TFLAG:30 -= 10
	TFLAG:37 -= 5
ELSEIF TFLAG:18 == 0
	TIMES SOURCE:18 , 1.00
	TIMES SOURCE:9 , 1.00
	TIMES SOURCE:20 , 1.00
	TIMES SOURCE:21 , 1.00
	TFLAG:37 += A
ELSE
	TIMES SOURCE:18 , 2.00
	TIMES SOURCE:9 , 2.00
	TIMES SOURCE:20 , 2.00
	TIMES SOURCE:21 , 2.00
	TFLAG:30 += 2
	TFLAG:37 += 5 + A
	SIF B > 0
		B += 1
ENDIF

IF B > 0
	PRINTFORML 遊びに行った先で、少し運動することにした
	PRINTFORML %EXPNAME:95%+{B}
	PRINTFORMW %EXPNAME:95%（%CALLNAME:PLAYER%）+{B}
	EXP:戦闘経験 += B
	EXP:PLAYER:戦闘経験 += B
ELSEIF TFLAG:17 == 2
	PRINTFORMW 何故か幽霊屋敷に遊びに行く事になってしまった
ENDIF

RETURN 1

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_321

A = 0
S = 0
V = 10
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;男嫌い
IF TALENT:82 && TALENT:PLAYER:122
	A -= 7
ENDIF

;好感度
IF CFLAG:2 < 100
	B = 0
	IF CFLAG:2 < 0
		B = 20
	ELSEIF CFLAG:2 < 50
		B = 10
	ELSE
		B = 5
	ENDIF
	A -= B
ENDIF

;好感度
IF CFLAG:2 >= 200
	B = 0
	B = CFLAG:2 / 30
	A += B
ENDIF

IF ((FLAG:23 & 1p27) == 0) && FLAG:2 != 1
	;男嫌い
	IF TALENT:82 && TALENT:PLAYER:122
		PRINT  - 
		PRINTS TALENTNAME:82
		PRINTV '(,7,')
		S = 1
	ENDIF

	;好感度
	IF CFLAG:2 < 100
		B = 0
		PRINT  - 
		IF CFLAG:2 < 0
			B = 20
		ELSEIF CFLAG:2 < 50
			B = 10
		ELSE
			B = 5
		ENDIF
		PRINT 好感度不足
		PRINTV '(,B,')
		S = 1
	ENDIF

	;好感度
	IF CFLAG:2 >= 200
		B = 0
		SIF S
			PRINT  + 
		B = CFLAG:2 / 30
		PRINT 好感度
		PRINTV '(,B,')
		S = 1
	ENDIF

	;合計を表示(10以上で実行)
	PRINT  = 
	PRINTV A

	IF A < V
		PRINT  < 
		PRINT 実行値
		PRINTV V
	ELSEIF A == V
		PRINT  = 
		PRINT 実行値
		PRINTV V
	ELSEIF A > V
		PRINT  > 
		PRINT 実行値
		PRINTV V
	ENDIF
	WAIT
ENDIF

