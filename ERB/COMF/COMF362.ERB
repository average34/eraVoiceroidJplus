;着衣愛撫系のV版くらいの位置付け。でも実行難易度は非常に高い
;処女の場合、C快感＋僅かなV快感で処女維持（V経験増えず）
;処女で無い場合、僅かなC快感＋V快感で、やはりV経験は増やさない
;処女のままV快感とかファンタジーすぎるだと？あー、あー、聞こえなーい
;まぁどうせ処女＋V感覚０だと補正が大きすぎて快感殆ど無いだろうけど

@COM_ABLE362
;着衣指挿入れ実行判定
;COM_ABLE共通判定
CALL COM_ABLE_COMMON, SELECTCOM
SIF RESULT == 0
	RETURN 0
;ウフフ中は無理
SIF TFLAG:44 == 1
	RETURN 0
;デート中は無理
SIF TEQUIP:2
	RETURN 0
;オムツ着用中は無理
SIF TEQUIP:20
	RETURN 0
;オトコだとダメ
SIF TALENT:オトコ
	RETURN 0
;潤滑10000以上かつ、欲情10000以上が必要
SIF PALAM:3 < 10000 || PALAM:5 < 10000
	RETURN 0
;欲望6以上が必要
SIF ABL:欲望 < 6
	RETURN 0
;親密10以上or好感度2000以上or恋人が必要
SIF ABL:親密 < 10 && CFLAG:2 < 2000 && TALENT:恋人 == 0
	RETURN 0
;合意が必要
SIF TALENT:合意 == 0
	RETURN 0
;ムードか酒の勢いが必要
SIF TFLAG:37 < 30 && TFLAG:47 < 20
	RETURN 0
RETURN 1

@COM362
;着衣指挿入れ
PRINTL 指挿入れ
;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL COM_ORDER_COMMON
IF RESULT == 0
	;目標の半分にすら届かない場合白い目で見られる
	IF A < V / 2
		PRINTFORML %CALLNAME:TARGET%は白い目でこちらを見ている……
		PRINTFORMW %CALLNAME:TARGET%の好感度が30下がりました。
		CFLAG:2 -= 30
	ENDIF
	RETURN 0
ENDIF

STR:0 = 指挿入れ
CALL TRAIN_MESSAGE_B

IF TALENT:処女
	LOSEBASE:体力 += 10
	LOSEBASE:気力 += 100
ELSE
	LOSEBASE:体力 += 30
	LOSEBASE:気力 += 100
ENDIF

SOURCE:12 = 300
SOURCE:14 = 250
SOURCE:22 = 200


;ABL:Ｃ感覚をみる
IF ABL:Ｃ感覚 == 0
	SOURCE:0 = 10
	SOURCE:3 = 50
ELSEIF ABL:Ｃ感覚 == 1
	SOURCE:0 = 50
	SOURCE:3 = 100
ELSEIF ABL:Ｃ感覚 == 2
	SOURCE:0 = 200
	SOURCE:3 = 160
ELSEIF ABL:Ｃ感覚 == 3
	SOURCE:0 = 400
	SOURCE:3 = 200
ELSEIF ABL:Ｃ感覚 == 4
	SOURCE:0 = 700
	SOURCE:3 = 230
ELSE
	SOURCE:0 = 100 + (ABL:Ｃ感覚 * 200)
	SOURCE:3 = 200 + (ABL:Ｃ感覚 * 10)
ENDIF

;ABL:Ｖ感覚をみる
IF ABL:Ｖ感覚 == 0
	SOURCE:1 = 20
	SOURCE:13 = 150
ELSEIF ABL:Ｖ感覚 == 1
	SOURCE:1 = 50
	SOURCE:13 = 250
ELSEIF ABL:Ｖ感覚 == 2
	SOURCE:1 = 250
	SOURCE:13 = 400
ELSEIF ABL:Ｖ感覚 == 3
	SOURCE:1 = 600
	SOURCE:13 = 700
ELSEIF ABL:Ｖ感覚 == 4
	SOURCE:1 = 1200
	SOURCE:13 = 1300
ELSE
	SOURCE:1 = 300 + (ABL:Ｖ感覚 * 300)
	SOURCE:13 = 250 + (ABL:Ｖ感覚 * 350)
ENDIF

;着衣補正
TIMES SOURCE:3 , 0.80
IF TALENT:処女
	;処女ならV快感6割減
	TIMES SOURCE:1 , 0.33
ELSE
	;処女でないならC快感半減＋着衣補正でV快感3割減
	TIMES SOURCE:0 , 0.50
	TIMES SOURCE:1 , 0.66
ENDIF

;EXP:Ｖ経験をみる
IF EXP:Ｖ経験 < EXPLV:1
	TIMES SOURCE:1 , 0.20
	TIMES SOURCE:13, 0.20
	SOURCE:6 = 300
ELSEIF EXP:Ｖ経験 < EXPLV:2
	TIMES SOURCE:1 , 0.50
	TIMES SOURCE:13, 0.50
	SOURCE:6 = 180
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES SOURCE:1 , 1.00
	TIMES SOURCE:13, 0.80
	SOURCE:6 = 80
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES SOURCE:1 , 1.20
	TIMES SOURCE:13, 1.00
	SOURCE:6 = 30
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES SOURCE:1 , 1.60
	TIMES SOURCE:13, 1.20
	SOURCE:6 = 0
ELSE
	TIMES SOURCE:2 , 1.80
	TIMES SOURCE:13, 1.50
	SOURCE:6 = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:3 < PALAMLV:1
	TIMES SOURCE:1 , 0.10
	SOURCE:6 += 700
	TIMES SOURCE:6 , 3.00
ELSEIF PALAM:3 < PALAMLV:2
	TIMES SOURCE:1 , 0.20
	SOURCE:6 += 200
	TIMES SOURCE:6 , 1.00
ELSEIF PALAM:3 < PALAMLV:3
	TIMES SOURCE:1 , 0.60
	TIMES SOURCE:6 , 0.80
ELSEIF PALAM:3 < PALAMLV:4
	TIMES SOURCE:1 , 1.00
	TIMES SOURCE:6 , 0.50
ELSEIF PALAM:3 >= PALAMLV:4
	TIMES SOURCE:1 , 2.00
	TIMES SOURCE:6 , 0.10
ENDIF

;PALAM:欲情をみる
IF PALAM:5 < PALAMLV:1
	TIMES SOURCE:1 , 0.50
ELSEIF PALAM:5 < PALAMLV:2
	TIMES SOURCE:1 , 0.80
ELSEIF PALAM:5 < PALAMLV:3
	TIMES SOURCE:1 , 1.20
ELSEIF PALAM:5 < PALAMLV:4
	TIMES SOURCE:1 , 1.50
ELSEIF PALAM:5 >= PALAMLV:4
	TIMES SOURCE:1 , 1.80
ENDIF


;Ｖ敏感、鈍感をみる
;快Ｖ自体のチェックは後でまとめてやる
IF TALENT:Ｖ鈍感
	TIMES SOURCE:6 , 1.50
	TIMES SOURCE:13 , 1.50
	TIMES SOURCE:14 , 1.50
ELSEIF TALENT:Ｖ敏感
	TIMES SOURCE:6 , 0.60
	TIMES SOURCE:13 , 0.60
	TIMES SOURCE:14 , 0.60
ENDIF

;処女で貞操観念
IF EXP:Ｖ経験 == 0 && TALENT:貞操観念 && TALENT:恋慕 == 0
	TIMES SOURCE:13 , 2.00
ENDIF

;奴隷のＶ⇔調教者の指の汚れが移動
STAIN:3 |= STAIN:PLAYER:1
STAIN:PLAYER:1 |= STAIN:3

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	PRINTS EXPNAME:40
	PRINTL +1
	EXP:レズ経験 += 1
	EXP:PLAYER:レズ経験 += 1
ELSEIF TALENT:オトコ == 1 && TALENT:PLAYER:オトコ == 1
	PRINTS EXPNAME:41
	PRINTL +1
	EXP:ＢＬ経験 += 1
	EXP:PLAYER:ＢＬ経験 += 1
ENDIF


;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:従順をみる
	IF ABL:親密 <= 1
		SOURCE:21 += 300 + (ABL:親密 * 50)
	ELSEIF ABL:親密 <= 3
		SOURCE:21 += 600 + (ABL:親密 * 100)
	ELSEIF ABL:親密 <= 5
		SOURCE:21 += 800 + (ABL:親密 * 120)
	ELSEIF ABL:親密 <= 8
		SOURCE:21 += 1000 + (ABL:親密 * 140)
	ELSEIF ABL:親密 <= 11
		SOURCE:21 += 1200 + (ABL:親密 * 160)
	ELSE
		SOURCE:21 += 1500 + (ABL:親密 * 180)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:サドっ気 == 0
		SOURCE:20 = 600
	ELSEIF ABL:サドっ気 == 1
		SOURCE:20 = 900
	ELSEIF ABL:サドっ気 == 2
		SOURCE:20 = 1200
	ELSEIF ABL:サドっ気 == 3
		SOURCE:20 = 1600
	ELSEIF ABL:サドっ気 == 4
		SOURCE:20 = 2000
	ELSE
		SOURCE:20 = 1000 + (ABL:サドっ気 * 300)
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:13 + 5
;欲望9以上か好感度5000以上でないと好感度が下がる
IF ABL:欲望 < 9 && CFLAG:2 < 5000
	A -= 12
	;恋慕で少し反感
	IF TALENT:恋慕
		SOURCE:15 += 200
	ELSE
	;恋慕なしだと反感増加
		SOURCE:15 += 600
	ENDIF
ENDIF
TFLAG:30 += A
TFLAG:37 += A / 2


RETURN 1

@TRAIN_MESSAGE_COM362
;着衣指挿入れ
IF TFLAG:45
	PRINTFORM %CALLNAME:TARGET%は自身の
ELSE
	PRINTFORM %CALLNAME:TARGET%の
ENDIF
IF PALAM:3 > 20000
	PRINTFORM ぐしょぐしょに濡れた
ELSE
	PRINTFORM すっかり濡れてしまった
ENDIF
IF PREVCOM == 362 || PREVCOM == 363 || PREVCOM == 364 || PREVCOM == 365 || PREVCOM == 366
	PRINTFORM 下着をずらしたまま
ELSE
	PRINTFORM 下着をずらし
ENDIF
SIF EXP:Ｖ経験 > 0 && EXP:Ｖ経験 <= 10
	PRINTFORM 、まだあまり開発されていない
IF TALENT:小柄体型
	PRINT 幼いワレメ
ELSE
	PRINT 秘裂
ENDIF
	PRINTFORM に
	SIF TFLAG:45
		PRINTFORM %CALLNAME:PLAYER%の
	PRINTFORM 指を
IF TALENT:処女 == 1
	IF TFLAG:45
		PRINTFORML 挿入れさせ、ゆっくり動かさせた…
	ELSE
		PRINTFORML 挿入れて優しく動かした…
	ENDIF
ELSE
	PRINTFORM ゆっくりと挿入
	IF TFLAG:45
		PRINTFORMW させた…
	ELSE
		PRINTFORMW した…
	ENDIF
ENDIF

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_362
A = 0
S = 0
V = 120
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;ABL:欲望
IF ABL:欲望
	A += ABL:欲望 * 3
	S = 1
ENDIF
;ABL:Ｖ感覚
IF ABL:Ｖ感覚
	A += ABL:Ｖ感覚 * 2
	S = 1
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	A += ABL:奉仕精神 * 4
	S = 1
ENDIF

IF MARK:1
	A += MARK:1 * 3
	S = 1
ENDIF

;PALAM:欲情
IF PALAM:5 < 15000
	L = 0
ELSEIF PALAM:5 < 20000
	L = 1
ELSEIF PALAM:5 < 30000
	L = 2
ELSEIF PALAM:5 < 40000
	L = 3
ELSEIF PALAM:5 < 60000
	L = 4
ELSE
	L = 5
ENDIF
IF L
	A += L * 3
	S = 1
ENDIF

;恥じらい
IF TALENT:恥じらい
	A -= 5
	S = 1
ENDIF
;快感の否定
IF TALENT:快感の否定
	A -= 5
	S = 1
ENDIF
;男嫌い
IF TALENT:男嫌い && TALENT:PLAYER:オトコ
	A -= 12
	S = 1
ENDIF
;恋慕
IF TALENT:恋慕 && ASSIPLAY == 0
	A += 8
	S = 5
ENDIF

;処女
IF TALENT:処女
	A -= 20
	S = 1
ENDIF

;一線越えない（処女のみ）
IF TALENT:一線越えない && TALENT:処女
	A -= 10
	S = 1
ENDIF

;媚薬
IF TEQUIP:21
	A += 6
	S = 1
ENDIF

;ムードボーナス
B = 0
IF TFLAG:37 < 10
	B -= 30
ELSEIF TFLAG:37 < 30
	B -= 15
ELSEIF TFLAG:37 < 50
	B -= 5
ELSEIF TFLAG:37 < 100
	B += TFLAG:37 / 10
ELSEIF TFLAG:37 < 300
	B += TFLAG:37 / 5
ELSE
	B += TFLAG:37 / 4
ENDIF
A += B
IF B < 0
	B = B * -1
	S = 1
ELSE
	S = 1
ENDIF


;好感度
IF CFLAG:2 < 2000
	B = 0
	IF CFLAG:2 < 1000
		B = 50
	ELSEIF CFLAG:2 < 1300
		B = 20
	ELSEIF CFLAG:2 < 1700
		B = 10
	ELSE
		B = 5
	ENDIF
	A -= B
	S = 1
ENDIF
;好感度
IF CFLAG:2 >= 2500
	B = 0
	IF CFLAG:2 < 4000
		B = 5
	ELSE
		B = CFLAG:2 / 300
	ENDIF
	A += B
	S = 1
ENDIF

IF ((FLAG:23 & 1p27) == 0) && FLAG:2 != 1
	;ABL:欲望
	IF ABL:欲望
		SIF S
			PRINT  + 
		PRINTS ABLNAME:1
		PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 3,')
		S = 1
	ENDIF
	;ABL:Ｖ感覚
	IF ABL:Ｖ感覚
		SIF S
			PRINT  + 
		PRINTS ABLNAME:4
		PRINTV 'LV,ABL:Ｖ感覚,'(,ABL:Ｖ感覚 * 2,')
		S = 1
	ENDIF
	;ABL:奉仕精神
	IF ABL:奉仕精神
		SIF S
			PRINT  + 
		PRINTS ABLNAME:6
		PRINTV 'LV,ABL:奉仕精神,'(,ABL:奉仕精神 * 4,')
		S = 1
	ENDIF

	IF MARK:1
		SIF S
			PRINT  + 
		PRINTS MARKNAME:1
		PRINTV 'LV,MARK:1,'(,MARK:1 * 3,')
		S = 1
	ENDIF

	;PALAM:欲情
	IF PALAM:5 < 15000
		L = 0
	ELSEIF PALAM:5 < 20000
		L = 1
	ELSEIF PALAM:5 < 30000
		L = 2
	ELSEIF PALAM:5 < 40000
		L = 3
	ELSEIF PALAM:5 < 60000
		L = 4
	ELSE
		L = 5
	ENDIF
	IF L
		SIF S
			PRINT  + 
		PRINTS PALAMNAME:5
		PRINTV 'LV,L,'(,L * 3,')
		S = 1
	ENDIF

	;恥じらい
	IF TALENT:恥じらい
		PRINT  - 
		PRINTS TALENTNAME:35
		PRINTV '(,5,')
		S = 1
	ENDIF
	;快感の否定
	IF TALENT:快感の否定
		PRINT  - 
		PRINTS TALENTNAME:71
		PRINTV '(,5,')
		S = 1
	ENDIF
	;男嫌い
	IF TALENT:男嫌い && TALENT:PLAYER:オトコ
		PRINT  - 
		PRINTS TALENTNAME:82
		PRINTV '(,12,')
		S = 1
	ENDIF
	;恋慕
	IF TALENT:恋慕 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:85
		PRINTV '(,8,')
		S = 5
	ENDIF

	;処女
	IF TALENT:処女
		PRINT  - 
		PRINTS TALENTNAME:0
		PRINTV '(,20,')
		S = 1
	ENDIF

	;一線越えない（処女のみ）
	IF TALENT:一線越えない && TALENT:処女
		PRINT  - 
		PRINTS TALENTNAME:27
		PRINTV '(,10,')
		S = 1
	ENDIF

	;媚薬
	IF TEQUIP:21
		SIF S
			PRINT  + 
		PRINTS ITEMNAME:26
		PRINTV '(,6,')
		S = 1
	ENDIF

	;ムードボーナス
	B = 0
	IF TFLAG:37 < 10
		B -= 30
	ELSEIF TFLAG:37 < 30
		B -= 15
	ELSEIF TFLAG:37 < 50
		B -= 5
	ELSEIF TFLAG:37 < 100
		B += TFLAG:37 / 10
	ELSEIF TFLAG:37 < 300
		B += TFLAG:37 / 5
	ELSE
		B += TFLAG:37 / 4
	ENDIF
	IF B < 0
		B = B * -1
		PRINT  - 
		PRINT ムード悪い
		PRINTV '(,B,')
		S = 1
	ELSE
		PRINT  + 
		PRINT ムード良い
		PRINTV '(,B,')
		S = 1
	ENDIF


	;好感度
	IF CFLAG:2 < 2000
		B = 0
		PRINT  - 
		IF CFLAG:2 < 1000
			B = 50
		ELSEIF CFLAG:2 < 1300
			B = 20
		ELSEIF CFLAG:2 < 1700
			B = 10
		ELSE
			B = 5
		ENDIF
		PRINT 好感度不足
		PRINTV '(,B,')
		S = 1
	ENDIF
	;好感度
	IF CFLAG:2 >= 2500
		B = 0
		SIF S
			PRINT  + 
		IF CFLAG:2 < 4000
			B = 5
		ELSE
			B = CFLAG:2 / 300
		ENDIF
		PRINT 好感度
		PRINTV '(,B,')
		S = 1
	ENDIF


	;合計を表示(120以上で実行)
	PRINT  = 
	PRINTV A

	SIF A < V
		PRINT  < 
	SIF A == V
		PRINT  = 
	SIF A > V
		PRINT  > 
	PRINT 実行値
	PRINTV V
	WAIT
ENDIF



