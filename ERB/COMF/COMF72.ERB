@COM72
;サラシ

PRINTL サラシ
STR:0 = サラシ

LOSEBASE:体力 += 20
LOSEBASE:気力 += 70

SOURCE:12 = 100
SOURCE:14 = 90

;ABL:Ｂ感覚をみる
IF ABL:Ｂ感覚 == 0
	A = 50
ELSEIF ABL:Ｂ感覚 == 1
	A = 100
ELSEIF ABL:Ｂ感覚== 2
	A = 300
ELSEIF ABL:Ｂ感覚 == 3
	A = 600
ELSEIF ABL:Ｂ感覚 == 4
	A = 1200
ELSE
	A = 2100
ENDIF

;貧乳
IF TALENT:貧乳
	TIMES A , 1.50
	SOURCE:12 += 700
ENDIF

;巨乳
IF TALENT:巨乳
	TIMES A , 0.80
	SOURCE:6 = 300
ENDIF

IF TALENT:Ｂ敏感
	TIMES A , 1.50
ELSEIF TALENT:Ｂ鈍感
	TIMES A , 0.60
ENDIF

SOURCE:17 += A

CALL TRAIN_MESSAGE_B

;サラシの着脱
IF TEQUIP:76
	TEQUIP:76 = 0
ELSE
	TEQUIP:76 = 1
ENDIF

RETURN 1

@TRAIN_MESSAGE_COM72
;サラシの着脱
IF TEQUIP:76
	PRINTFORML %CALLNAME:TARGET%に巻いてあるサラシを外した
ELSE
	PRINTFORML %CALLNAME:TARGET%の胸にサラシを巻き付けた
	PRINTFORM %CALLNAME:TARGET%は
	;サラシが似合うボイロっていないね
	IF 0
		PRINTL これが一番落ち着くと感じているのかほっとした表情を浮かべている
	ELSEIF TALENT:貧乳
		PRINTL 改めて自分の胸が小さいことを気にし始めたのか、顔が真っ赤に染まっている
	ELSEIF TALENT:巨乳
		PRINTL 少し窮屈そうな表情を浮かべ、白い布によって締め付けられた自分の豊かな胸を見下ろしている
	ELSE
		PRINTL 胸に巻かれた物に違和感を感じるのか、困惑した表情でしきりに布をくいくいと引っ張っている……
	ENDIF
	PRINTL 　
ENDIF

@EQUIP_COM76
;サラシ装備中
PRINTL ＜サラシ装着中＞

IF TALENT:巨乳
	LOSEBASE:体力 += 10
ELSE
	LOSEBASE:体力 += 5
ENDIF
LOSEBASE:気力 += 20

;ABL:Ｂ感覚をみる
IF ABL:Ｂ感覚 == 0
	A = 20
ELSEIF ABL:Ｂ感覚 == 1
	A = 40
ELSEIF ABL:Ｂ感覚 == 2
	A = 80
ELSEIF ABL:Ｂ感覚 == 3
	A = 160
ELSEIF ABL:Ｂ感覚 == 4
	A = 320
ELSE
	A = 640
ENDIF

;PALAM:欲情をみる
IF PALAM:5 < PALAMLV:1
	TIMES A , 0.80
ELSEIF PALAM:5 < PALAMLV:2
	TIMES A , 0.90
ELSEIF PALAM:5 < PALAMLV:3
	TIMES A , 1.00
ELSEIF PALAM:5 < PALAMLV:4
	TIMES A , 1.10
ELSEIF PALAM:5 >= PALAMLV:4
	TIMES A , 1.20
ENDIF

;ABL:従順をみる
IF ABL:親密 == 0
	TIMES A , 0.80
ELSEIF ABL:親密 == 1
	TIMES A , 0.90
ELSEIF ABL:親密 == 2
	TIMES A , 1.00
ELSEIF ABL:親密 == 3
	TIMES A , 1.10
ELSEIF ABL:親密 == 4
	TIMES A , 1.20
ELSE
	TIMES A , 1.30
ENDIF

;貧乳
IF TALENT:貧乳
	TIMES A , 1.10
	SOURCE:12 += 700
;巨乳
ELSEIF TALENT:巨乳
	TIMES A , 0.90
	SOURCE:6 += 100
ENDIF

SOURCE:17 += A
SOURCE:11 += 10
SOURCE:12 += 50

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	PRINTS EXPNAME:40
	PRINTL +1
	EXP:レズ経験 += 1
	EXP:PLAYER:レズ経験 += 1
ELSEIF TALENT:オトコ == 1 && TALENT:PLAYER:オトコ == 1
	PRINTS EXPNAME:41
	PRINTL +1
	EXP:ＢＬ経験 += 1
	EXP:PLAYER:ＢＬ経験 += 1
ENDIF

RETURN 1
