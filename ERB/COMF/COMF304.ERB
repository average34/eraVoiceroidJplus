@COM304
;まったりする

PRINTL まったりする
CALL GET_SUCCESS_RATE
B = 90 + A / 10
SIF B > 99
	B = 99
A = RAND:100
IF A <= 9
	;10％で大成功
	TFLAG:18 = 1
ELSEIF A >= B
	;10～1％で失敗
	TFLAG:18 = -1
ELSE
	;残りは成功
	TFLAG:18 = 0
ENDIF

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

STR:0 = まったりする
CALL TRAIN_MESSAGE_B

LOSEBASE:気力 += 100

;固定で獲得するソース
SOURCE:歓楽 = 300
SOURCE:家務 = 50

;ABL:従順をみる
IF ABL:親密 <= 1
	SOURCE:歓楽 += (ABL:親密 * 65)
	SOURCE:家務 += (ABL:親密 * 20)
ELSEIF ABL:親密 <= 3
	SOURCE:歓楽 += 200 + (ABL:親密 * 70)
	SOURCE:家務 += 100 + (ABL:親密 * 20)
ELSEIF ABL:親密 <= 5
	SOURCE:歓楽 += 500 + (ABL:親密 * 70)
	SOURCE:家務 += 200 + (ABL:親密 * 20)
ELSEIF ABL:親密 <= 8
	SOURCE:歓楽 += 700 + (ABL:親密 * 80)
	SOURCE:家務 += 300 + (ABL:親密 * 30)
ELSEIF ABL:親密 <= 11
	SOURCE:歓楽 += 1200 + (ABL:親密 * 85)
	SOURCE:家務 += 400 + (ABL:親密 * 30)
ELSE
	SOURCE:歓楽 += 2000 + (ABL:親密 * 40)
	SOURCE:家務 += 800 + (ABL:親密 * 15)
ENDIF

;好感度をみる
IF CFLAG:2 <= 500
	SOURCE:歓楽 += CFLAG:2
ELSEIF CFLAG:2 <= 5000
	SOURCE:歓楽 += 500 + (CFLAG:2 - 500) / 3
ELSE
	SOURCE:歓楽 += 2000 + (CFLAG:2 - 5000) / 5
ENDIF
SIF SOURCE:歓楽 < 0
	SOURCE:歓楽 = 0

;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:従順をみる
	IF ABL:親密 <= 1
		SOURCE:受動 += 100 + (ABL:親密 * 20)
	ELSEIF ABL:親密 <= 3
		SOURCE:受動 += 200 + (ABL:親密 * 30)
	ELSEIF ABL:親密 <= 5
		SOURCE:受動 += 300 + (ABL:親密 * 40)
	ELSEIF ABL:親密 <= 8
		SOURCE:受動 += 400 + (ABL:親密 * 50)
	ELSEIF ABL:親密 <= 11
		SOURCE:受動 += 500 + (ABL:親密 * 60)
	ELSE
		SOURCE:受動 += 600 + (ABL:親密 * 70)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:サドっ気 == 0
		SOURCE:征服 = 200
	ELSEIF ABL:サドっ気 == 1
		SOURCE:征服 = 300
	ELSEIF ABL:サドっ気 == 2
		SOURCE:征服 = 400
	ELSEIF ABL:サドっ気 == 3
		SOURCE:征服 = 550
	ELSEIF ABL:サドっ気 == 4
		SOURCE:征服 = 700
	ELSE
		SOURCE:征服 = 300 + (ABL:サドっ気 * 100)
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:6 - 1
TFLAG:30 += A

IF TFLAG:18 == -1
	TIMES SOURCE:歓楽 , 0.10
	TIMES SOURCE:家務 , 0.50
	TIMES SOURCE:征服 , 0.50
	TIMES SOURCE:受動 , 0.50
	TFLAG:30 -= 10
	TFLAG:37 -= 5
ELSEIF TFLAG:18 == 0
	TIMES SOURCE:歓楽 , 1.00
	TIMES SOURCE:家務 , 1.00
	TIMES SOURCE:征服 , 1.00
	TIMES SOURCE:受動 , 1.00
	TFLAG:37 += A
ELSE
	TIMES SOURCE:歓楽 , 2.00
	TIMES SOURCE:家務 , 2.00
	TIMES SOURCE:征服 , 2.00
	TIMES SOURCE:受動 , 2.00
	TFLAG:30 += 2
	TFLAG:37 += 5 + A
ENDIF


RETURN 1

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_304

A = 0
S = 0
V = 10
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;男嫌い
IF TALENT:男嫌い && TALENT:PLAYER:オトコ
	A -= 7
ENDIF

;好感度
IF CFLAG:2 < 100
	B = 0
	IF CFLAG:2 < 0
		B = 20
	ELSEIF CFLAG:2 < 50
		B = 10
	ELSE
		B = 5
	ENDIF
	A -= B
ENDIF

;好感度
IF CFLAG:2 >= 200
	B = 0
	B = CFLAG:2 / 30
	A += B
ENDIF

IF ((FLAG:23 & 1p27) == 0) && FLAG:2 != 1
	;男嫌い
	IF TALENT:男嫌い && TALENT:PLAYER:オトコ
		PRINT  - 
		PRINTS TALENTNAME:82
		PRINTV '(,7,')
		S = 1
	ENDIF

	;好感度
	IF CFLAG:2 < 100
		B = 0
		PRINT  - 
		IF CFLAG:2 < 0
			B = 20
		ELSEIF CFLAG:2 < 50
			B = 10
		ELSE
			B = 5
		ENDIF
		PRINT 好感度不足
		PRINTV '(,B,')
		S = 1
	ENDIF

	;好感度
	IF CFLAG:2 >= 200
		B = 0
		SIF S
			PRINT  + 
		B = CFLAG:2 / 30
		PRINT 好感度
		PRINTV '(,B,')
		S = 1
	ENDIF


	PRINT  = 
	PRINTV A
	;合計を表示(10以上で実行)

	IF A < V
		PRINT  < 
		PRINT 実行値
		PRINTV V
	ELSEIF A == V
		PRINT  = 
		PRINT 実行値
		PRINTV V
	ELSEIF A > V
		PRINT  > 
		PRINT 実行値
		PRINTV V
	ENDIF
	WAIT
ENDIF



