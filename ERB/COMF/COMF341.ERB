@COM341
;ソフトキス

PRINTL ソフトキス
CALL GET_SUCCESS_RATE
B = 90 + A / 10
SIF B > 99
	B = 99
A = RAND:100
IF A <= 9
	;10％で大成功
	TFLAG:18 = 1
ELSEIF A >= B
	;10～1％で失敗
	TFLAG:18 = -1
ELSE
	;残りは成功
	TFLAG:18 = 0
ENDIF


;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

STR:0 = ソフトキス
CALL TRAIN_MESSAGE_B

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
;キス経験で気力消費軽減（何度もキスしたがる、の再現だとでも脳内補完）
IF EXP:キス経験 < 100
	LOSEBASE:気力 += 70
ELSEIF EXP:キス経験 < 500
	LOSEBASE:気力 += 55
ELSE
	LOSEBASE:気力 += 35
ENDIF

;Ｍ感覚を設定している場合快Ｍソース等取得
SIF FLAG:62 & 4194304
	CALL SOURCE_M_COM341

;固定で獲得するソース
SOURCE:歓楽 = 50
SOURCE:接触 = 700

;ABL:従順をみる
IF ABL:親密 <= 1
	SOURCE:歓楽 += (ABL:親密 * 20)
	SOURCE:接触 += (ABL:親密 * 80)
ELSEIF ABL:親密 <= 3
	SOURCE:歓楽 += 100 + (ABL:親密 * 20)
	SOURCE:接触 += 200 + (ABL:親密 * 85)
ELSEIF ABL:親密 <= 5
	SOURCE:歓楽 += 200 + (ABL:親密 * 20)
	SOURCE:接触 += 500 + (ABL:親密 * 90)
ELSEIF ABL:親密 <= 8
	SOURCE:歓楽 += 300 + (ABL:親密 * 30)
	SOURCE:接触 += 700 + (ABL:親密 * 100)
ELSEIF ABL:親密 <= 11
	SOURCE:歓楽 += 400 + (ABL:親密 * 30)
	SOURCE:接触 += 1200 + (ABL:親密 * 110)
ELSE
	SOURCE:歓楽 += 800 + (ABL:親密 * 15)
	SOURCE:接触 += 2000 + (ABL:親密 * 70)
ENDIF

;好感度をみる
IF CFLAG:2 <= 500
	SOURCE:歓楽 += CFLAG:2
ELSEIF CFLAG:2 <= 5000
	SOURCE:歓楽 += 500 + (CFLAG:2 - 500) / 3
ELSE
	SOURCE:歓楽 += 2000 + (CFLAG:2 - 5000) / 5
ENDIF
SIF SOURCE:歓楽 < 0
	SOURCE:歓楽 = 0

;ABL:奉仕精神をみる
IF ABL:奉仕精神 == 0
	SOURCE:性行動 = 50
	SOURCE:達成感 = 10
ELSEIF ABL:奉仕精神 == 1
	SOURCE:性行動 = 150
	SOURCE:達成感 = 50
ELSEIF ABL:奉仕精神 == 2
	SOURCE:性行動 = 200
	SOURCE:達成感 = 100
ELSEIF ABL:奉仕精神 == 3
	SOURCE:性行動 = 250
	SOURCE:達成感 = 180
ELSEIF ABL:奉仕精神 == 4
	SOURCE:性行動 = 300
	SOURCE:達成感 = 300
ELSE
	SOURCE:性行動 = 225 + (ABL:奉仕精神 * 25)
	SOURCE:達成感 = ABL:奉仕精神 * 100
ENDIF

;ABL:技巧をみる
IF ABL:技巧 == 0
	TIMES SOURCE:性行動 , 0.50
	TIMES SOURCE:達成感 , 0.50
ELSEIF ABL:技巧 == 1
	TIMES SOURCE:性行動 , 0.80
	TIMES SOURCE:達成感 , 0.80
ELSEIF ABL:技巧 == 2
	TIMES SOURCE:性行動 , 1.00
	TIMES SOURCE:達成感 , 1.00
ELSEIF ABL:技巧 == 3
	TIMES SOURCE:性行動 , 1.50
	TIMES SOURCE:達成感 , 1.50
ELSEIF ABL:技巧 == 4
	TIMES SOURCE:性行動 , 2.50
	TIMES SOURCE:達成感 , 2.50
ELSE
	SOURCE:性行動 = SOURCE:性行動 * (ABL:技巧 * 75 + 25) / 100
	SOURCE:達成感 = SOURCE:達成感 * (ABL:技巧 * 75 + 25) / 100
ENDIF

;プレイヤーのABL:技巧をみる
IF ABL:PLAYER:技巧 == 0
	SOURCE:情愛 = 100
	SOURCE:液体追加 = 0
ELSEIF ABL:PLAYER:技巧 == 1
	SOURCE:情愛 = 150
	SOURCE:液体追加 = 0
ELSEIF ABL:PLAYER:技巧 == 2
	SOURCE:情愛 = 200
	SOURCE:液体追加 = 0
ELSEIF ABL:PLAYER:技巧 == 3
	SOURCE:情愛 = 300
	SOURCE:液体追加 = 50
ELSEIF ABL:PLAYER:技巧 == 4
	SOURCE:情愛 = 500
	SOURCE:液体追加 = 100
ELSE
	SOURCE:情愛 = 50 + (ABL:PLAYER:技巧 * 150)
	SOURCE:液体追加 = (ABL:PLAYER:技巧 * 50) - 50
ENDIF

;TALENT:恋慕をみる
SIF TALENT:恋慕
	TIMES SOURCE:情愛 , 1.50
;TALENT:恋人をみる
SIF TALENT:恋人
	TIMES SOURCE:情愛 , 1.50
;TALENT:親愛をみる
SIF TALENT:親愛
	TIMES SOURCE:情愛 , 2.00


;キスの種類補正		面倒なんで、殆どのソースをコピペして、ここだけ弄ってみる
TIMES SOURCE:情愛 , 0.60
TIMES SOURCE:性行動 , 0.60
TIMES SOURCE:達成感 , 0.60
TIMES SOURCE:液体追加 , 0.60



;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:従順をみる
	IF ABL:親密 <= 1
		SOURCE:受動 += 300 + (ABL:親密 * 50)
	ELSEIF ABL:親密 <= 3
		SOURCE:受動 += 600 + (ABL:親密 * 100)
	ELSEIF ABL:親密 <= 5
		SOURCE:受動 += 800 + (ABL:親密 * 120)
	ELSEIF ABL:親密 <= 8
		SOURCE:受動 += 1000 + (ABL:親密 * 140)
	ELSEIF ABL:親密 <= 11
		SOURCE:受動 += 1200 + (ABL:親密 * 160)
	ELSE
		SOURCE:受動 += 1500 + (ABL:親密 * 180)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:サドっ気 == 0
		SOURCE:征服 = 600
	ELSEIF ABL:サドっ気 == 1
		SOURCE:征服 = 900
	ELSEIF ABL:サドっ気 == 2
		SOURCE:征服 = 1200
	ELSEIF ABL:サドっ気 == 3
		SOURCE:征服 = 1600
	ELSEIF ABL:サドっ気 == 4
		SOURCE:征服 = 2000
	ELSE
		SOURCE:征服 = 1000 + (ABL:サドっ気 * 300)
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:6 - 1
TFLAG:30 += A

IF TFLAG:18 == -1
	TIMES SOURCE:歓楽 , 0.10
	TIMES SOURCE:接触 , 0.50
	TIMES SOURCE:征服 , 0.50
	TIMES SOURCE:受動 , 0.50
	TFLAG:30 -= 10
	TFLAG:37 -= 5
ELSEIF TFLAG:18 == 0
	TIMES SOURCE:歓楽 , 1.00
	TIMES SOURCE:接触 , 1.00
	TIMES SOURCE:征服 , 1.00
	TIMES SOURCE:受動 , 1.00
	TFLAG:37 += A
ELSE
	TIMES SOURCE:歓楽 , 2.00
	TIMES SOURCE:接触 , 2.00
	TIMES SOURCE:征服 , 2.00
	TIMES SOURCE:受動 , 2.00
	TFLAG:30 += 2
	TFLAG:37 += 5 + A
ENDIF

;デート中は更にムードアップ
SIF TEQUIP:2
	TFLAG:37 += 11

PRINTFORML %EXPNAME:96%＋１
EXP:キス経験 += 1
EXP:PLAYER:キス経験 += 1
IF TALENT:PLAYER:キス未経験
	PRINTFORMW %CALLNAME:PLAYER%は【キス未経験】を失いました
	TALENT:PLAYER:キス未経験 = 0
ENDIF

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	PRINTS EXPNAME:40
	PRINTL +2
	EXP:レズ経験 += 2
	EXP:PLAYER:レズ経験 += 2
ELSEIF TALENT:オトコ == 1 && TALENT:PLAYER:オトコ == 1
	PRINTS EXPNAME:41
	PRINTL +2
	EXP:ＢＬ経験 += 2
	EXP:PLAYER:ＢＬ経験 += 2
ENDIF

IF TALENT:キス未経験
	TFLAG:600 = 1
	CALL KOJO_JUN
	TALENT:キス未経験 = 0
	PRINTFORMW %CALLNAME:TARGET%は【キス未経験】を失いました
	TFLAG:38 = 1

	;既成事実１に相当
	TFLAG:200 = 1
ENDIF


RETURN 1

;Ｍ感覚を設定している場合快Ｍソース等取得
@SOURCE_M_COM341
VARSET LOCAL, 0

;屈従・反感ソース
LOCAL:13 += 100
LOCAL:14 += 10

;ABL:Ｍ感覚をみる
IF ABL:Ｍ感覚 == 0
	LOCAL:24 += 5
ELSEIF ABL:Ｍ感覚 == 1
	LOCAL:24 += 25
ELSEIF ABL:Ｍ感覚 == 2
	LOCAL:24 += 100
ELSEIF ABL:Ｍ感覚 == 3
	LOCAL:24 += 300
ELSEIF ABL:Ｍ感覚 == 4
	LOCAL:24 += 500
ELSE
	LOCAL:24 += 200 + (ABL:Ｍ感覚 * 100)
ENDIF

;PLAYERの技巧を見る
SIF ABL:PLAYER:技巧 >= 3
	LOCAL:24 = LOCAL:24 * (100 + (ABL:PLAYER:技巧 / 3)) / 100

;技巧を見る
SIF ABL:TARGET:技巧 >= 3
	LOCAL:24 = LOCAL:24 * (100 + (ABL:TARGET:技巧 / 3)) / 100

;EXP:キス経験をみる
IF EXP:キス経験 < EXPLV:1
	TIMES LOCAL:24 , 1.00
ELSEIF EXP:キス経験 < EXPLV:2
	TIMES LOCAL:14 , 0.95
	TIMES LOCAL:24 , 1.05
ELSEIF EXP:キス経験 < EXPLV:3
	TIMES LOCAL:14 , 0.90
	TIMES LOCAL:24 , 1.10
ELSEIF EXP:キス経験 < EXPLV:4
	TIMES LOCAL:14 , 0.85
	TIMES LOCAL:24 , 1.15
ELSEIF EXP:キス経験 < EXPLV:5
	TIMES LOCAL:14 , 0.80
	TIMES LOCAL:24 , 1.20
ELSE
	TIMES LOCAL:14 , 0.75
	TIMES LOCAL:24 , 1.25
ENDIF

;TALENT:恋慕をみる
SIF TALENT:恋慕
	TIMES LOCAL:24 , 1.50
;TALENT:恋人をみる
SIF TALENT:恋人
	TIMES LOCAL:24 , 1.50
;TALENT:親愛をみる
SIF TALENT:親愛
	TIMES LOCAL:24 , 2.00
;TALENT:相愛をみる
SIF TALENT:相愛
	TIMES LOCAL:24 , 2.00

;反発感情をみる
IF MARK:反発感情 == 1
	TIMES LOCAL:24 , 0.50
	TIMES LOCAL:13 , 1.15
ELSEIF MARK:反発感情 == 2
	TIMES LOCAL:24 , 0.25
	TIMES LOCAL:13 , 1.45
ELSEIF MARK:反発感情 == 3
	TIMES LOCAL:24 , 0.10
	TIMES LOCAL:13 , 1.90
ENDIF

;成功をみる
IF TFLAG:18 == -1
	TIMES LOCAL:24 , 0.50
ELSEIF TFLAG:18 == 0
	TIMES LOCAL:24 , 1.00
ELSE
	TIMES LOCAL:24 , 2.00
ENDIF

SOURCE:屈従 += LOCAL:13
SOURCE:逸脱 += LOCAL:14
SOURCE:快Ｍ += LOCAL:24

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_341

A = 0
S = 0
V = 50
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;男嫌い
IF TALENT:男嫌い && TALENT:PLAYER:オトコ
	A -= 7
ENDIF

;ABL:欲望
IF ABL:欲望
	A += ABL:欲望 * 1
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	A += ABL:奉仕精神 * 2
ENDIF
;PALAM:欲情
IF PALAM:欲情 < PALAMLV:1
	L = 0
ELSEIF PALAM:欲情 < PALAMLV:2
	L = 1
ELSEIF PALAM:欲情 < PALAMLV:3
	L = 2
ELSEIF PALAM:欲情 < PALAMLV:4
	L = 3
ELSEIF PALAM:欲情 < PALAMLV:5
	L = 4
ELSE
	L = 5
ENDIF
IF L
	A += L * 1
ENDIF

;恥じらい
IF TALENT:恥じらい
	A -= 5
ENDIF
;献身的
IF TALENT:献身的
	A += 6
ENDIF
;快感の否定
IF TALENT:快感の否定
	A -= 1
ENDIF
;恋慕
IF TALENT:恋慕 && ASSIPLAY == 0
	A += 5
	S = 5
ENDIF


;キス未経験
IF TALENT:キス未経験
	A -= 15
ENDIF

;一線越えない（キス未経験時のみ）
IF TALENT:キス未経験 && TALENT:一線越えない
	A -= 10
ENDIF

;デート中
IF TEQUIP:2
	A += 3
ENDIF

;ムードボーナス
B = 0
IF TFLAG:37 < 10
	B -= 10
ELSEIF TFLAG:37 < 30
	B -= 1
ELSEIF TFLAG:37 < 50
	B += 1
ELSEIF TFLAG:37 < 100
	B += TFLAG:37 / 10
ELSEIF TFLAG:37 < 300
	B += TFLAG:37 / 5
ELSE
	B += TFLAG:37 / 4
ENDIF
A += B
IF B < 0
	B = B * -1
ELSE
ENDIF


;好感度
IF CFLAG:2 < 400
	B = 0
	IF CFLAG:2 < 100
		B = 20
	ELSEIF CFLAG:2 < 250
		B = 10
	ELSE
		B = 5
	ENDIF
	A -= B
ENDIF

;好感度
IF CFLAG:2 >= 500
	B = 0
	B = CFLAG:2 / 70
	A += B
ENDIF

IF ((FLAG:23 & 1p27) == 0) && FLAG:2 != 1
	;男嫌い
	IF TALENT:男嫌い && TALENT:PLAYER:オトコ
		PRINT  - 
		PRINTS TALENTNAME:82
		PRINTV '(,7,')
		S = 1
	ENDIF

	;ABL:欲望
	IF ABL:欲望
		SIF S
			PRINT  + 
		PRINTS ABLNAME:1
		PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 1,')
		S = 1
	ENDIF
	;ABL:奉仕精神
	IF ABL:奉仕精神
		SIF S
			PRINT  + 
		PRINTS ABLNAME:6
		PRINTV 'LV,ABL:奉仕精神,'(,ABL:奉仕精神 * 2,')
		S = 1
	ENDIF
	;PALAM:欲情
	IF PALAM:欲情 < PALAMLV:1
		L = 0
	ELSEIF PALAM:欲情 < PALAMLV:2
		L = 1
	ELSEIF PALAM:欲情 < PALAMLV:3
		L = 2
	ELSEIF PALAM:欲情 < PALAMLV:4
		L = 3
	ELSEIF PALAM:欲情 < PALAMLV:5
		L = 4
	ELSE
		L = 5
	ENDIF
	IF L
		SIF S
			PRINT  + 
		PRINTS PALAMNAME:5
		PRINTV 'LV,L,'(,L * 1,')
		S = 1
	ENDIF

	;恥じらい
	IF TALENT:恥じらい
		PRINT  - 
		PRINTS TALENTNAME:35
		PRINTV '(,5,')
		S = 1
	ENDIF
	;献身的
	IF TALENT:献身的
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:63
		PRINTV '(,6,')
		S = 1
	ENDIF
	;快感の否定
	IF TALENT:快感の否定
		PRINT  - 
		PRINTS TALENTNAME:71
		PRINTV '(,1,')
		S = 1
	ENDIF
	;恋慕
	IF TALENT:恋慕 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:85
		PRINTV '(,5,')
		S = 5
	ENDIF


	;キス未経験
	IF TALENT:キス未経験
		PRINT  - 
		PRINTS TALENTNAME:151
		PRINTV '(,15,')
		S = 1
	ENDIF

	;一線越えない（キス未経験時のみ）
	IF TALENT:キス未経験 && TALENT:一線越えない
		PRINT  - 
		PRINTS TALENTNAME:27
		PRINTV '(,10,')
		S = 1
	ENDIF

	;デート中
	IF TEQUIP:2
		PRINT  + 
		PRINT デート中
		PRINTV '(,3,')
		S = 1
	ENDIF

	;ムードボーナス
	B = 0
	IF TFLAG:37 < 10
		B -= 10
	ELSEIF TFLAG:37 < 30
		B -= 1
	ELSEIF TFLAG:37 < 50
		B += 1
	ELSEIF TFLAG:37 < 100
		B += TFLAG:37 / 10
	ELSEIF TFLAG:37 < 300
		B += TFLAG:37 / 5
	ELSE
		B += TFLAG:37 / 4
	ENDIF
	IF B < 0
		B = B * -1
		PRINT  - 
		PRINT ムード悪い
		PRINTV '(,B,')
		S = 1
	ELSE
		PRINT  + 
		PRINT ムード良い
		PRINTV '(,B,')
		S = 1
	ENDIF


	;好感度
	IF CFLAG:2 < 400
		B = 0
		PRINT  - 
		IF CFLAG:2 < 100
			B = 20
		ELSEIF CFLAG:2 < 250
			B = 10
		ELSE
			B = 5
		ENDIF
		PRINT 好感度不足
		PRINTV '(,B,')
		S = 1
	ENDIF

	;好感度
	IF CFLAG:2 >= 500
		B = 0
		SIF S
			PRINT  + 
		B = CFLAG:2 / 70
		PRINT 好感度
		PRINTV '(,B,')
		S = 1
	ENDIF

	;合計を表示(50以上で実行)
	PRINT  = 
	PRINTV A

	IF A < V
		PRINT  < 
		PRINT 実行値
		PRINTV V
	ELSEIF A == V
		PRINT  = 
		PRINT 実行値
		PRINTV V
	ELSEIF A > V
		PRINT  > 
		PRINT 実行値
		PRINTV V
	ENDIF
	WAIT
ENDIF



