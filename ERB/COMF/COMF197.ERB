@COM_ABLE197
;お風呂で楽しむ実行判定
;COM_ABLE共通判定
CALL COM_ABLE_COMMON, SELECTCOM
SIF RESULT == 0
	RETURN 0
;ウフフ中じゃないと無理
SIF TFLAG:44 == 0
	RETURN 0
;お風呂中じゃないとダメ
SIF TEQUIP:58 == 0
	RETURN 0
;クリキャップ使用中は不可
SIF TEQUIP:14
	RETURN 0
;クリップローター使用中は不可
SIF TEQUIP:15
	RETURN 0
;搾乳器使用中は不可
SIF TEQUIP:16
	RETURN 0
;;乳房電極装着中はダメ
;SIF TEQUIP:50
;	RETURN 0
;吸血鬼や動物耳には従順４がないと使用不可（プレイヤーがサドならＯＫ）
IF TALENT:114 || TALENT:124
SIF (ABL:親密 < 4) && (TALENT:PLAYER:83 == 0)
	RETURN 0
ENDIF
RETURN 1

@COM197
;お風呂を楽しむ

PRINTL お風呂を楽しむ
STR:0 = お風呂を楽しむ

;地の文カットのために先にTFLAG:17を取得
IF RAND:4 == 0
	TFLAG:17 = 0
ELSEIF RAND:3 == 0
	TFLAG:17 = 1
ELSEIF RAND:2 == 0
	TFLAG:17 = 2
ELSE
	TFLAG:17 = 3
ENDIF

CALL TRAIN_MESSAGE_B

LOSEBASE:0 += 0
LOSEBASE:1 += 20

SOURCE:9 = 50
SOURCE:11 = 50

;PALAM:欲情をみる
IF PALAM:5 < PALAMLV:1
	SOURCE:12 = 30
ELSEIF PALAM:5 < PALAMLV:2
	SOURCE:12 = 50
ELSEIF PALAM:5 < PALAMLV:3
	SOURCE:12 = 80
ELSEIF PALAM:5 < PALAMLV:4
	SOURCE:12 = 120
ELSEIF PALAM:5 >= PALAMLV:4
	SOURCE:12 = 200
ENDIF

;奉仕精神をみる
IF ABL:奉仕精神 == 0
	SOURCE:3 = 10
ELSEIF ABL:奉仕精神 == 1
	SOURCE:3 = 30
ELSEIF ABL:奉仕精神 == 2
	SOURCE:3 = 60
ELSEIF ABL:奉仕精神 == 3
	SOURCE:3 = 100
ELSEIF ABL:奉仕精神 == 4
	SOURCE:3 = 160
ELSE
	SOURCE:3 = 160 + (ABL:奉仕精神 * 15)
ENDIF

;ABL:従順をみる
IF ABL:親密 == 0
	TIMES SOURCE:5 , 0.80
ELSEIF ABL:親密 == 1
	TIMES SOURCE:5 , 0.90
ELSEIF ABL:親密 == 2
	TIMES SOURCE:5 , 1.00
ELSEIF ABL:親密 == 3
	TIMES SOURCE:5 , 1.10
ELSEIF ABL:親密 == 4
	TIMES SOURCE:5 , 1.20
ELSE
	;TIMES SOURCE:5 , 1.30
	SOURCE:5 *= (ABL:親密 * 5 + 105) / 100
ENDIF

;吸血鬼の場合
IF TALENT:114
	SOURCE:6 = 100
	TIMES SOURCE:6 , 1.60
	TIMES SOURCE:13 , 2.00
	TIMES SOURCE:15 , 1.40
ENDIF


;動物耳の場合
IF TALENT:124
	TIMES SOURCE:14 , 1.60
	TIMES SOURCE:13 , 1.50
	TIMES SOURCE:15 , 2.00
ENDIF

IF TALENT:122 == 0 && TALENT:PLAYER:122 == 0
	PRINTS EXPNAME:40
	PRINTL +1
	EXP:40 += 1
	EXP:PLAYER:40 += 1
ELSEIF TALENT:122 == 1 && TALENT:PLAYER:122 == 1
	PRINTS EXPNAME:41
	PRINTL +1
	EXP:41 += 1
	EXP:PLAYER:41 += 1
ENDIF

;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:従順をみる
	IF ABL:親密 <= 1
		SOURCE:21 += 300 + (ABL:親密 * 50)
	ELSEIF ABL:親密 <= 3
		SOURCE:21 += 600 + (ABL:親密 * 100)
	ELSEIF ABL:親密 <= 5
		SOURCE:21 += 800 + (ABL:親密 * 120)
	ELSEIF ABL:親密 <= 8
		SOURCE:21 += 1000 + (ABL:親密 * 140)
	ELSEIF ABL:親密 <= 11
		SOURCE:21 += 1200 + (ABL:親密 * 160)
	ELSE
		SOURCE:21 += 1500 + (ABL:親密 * 180)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:サドっ気 == 0
		SOURCE:20 = 600
	ELSEIF ABL:サドっ気 == 1
		SOURCE:20 = 900
	ELSEIF ABL:サドっ気 == 2
		SOURCE:20 = 1200
	ELSEIF ABL:サドっ気 == 3
		SOURCE:20 = 1600
	ELSEIF ABL:サドっ気 == 4
		SOURCE:20 = 2000
	ELSE
		SOURCE:20 = 1000 + (ABL:サドっ気 * 300)
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:10 + 5
SIF TALENT:85 || TALENT:153
	A += 2
SIF TALENT:88
	A += 3
;合意がないと好感度が下がる
IF TALENT:152 == 0 && TFLAG:48 == 0
	A -= 8
	SOURCE:15 += 300
ENDIF
TFLAG:30 += A
TFLAG:37 += A

;シャワーで汚れがリセット
STAIN:0 = 0
STAIN:1 = 0
STAIN:2 = 2
STAIN:3 = 1
STAIN:4 = 8
STAIN:5 = 0
STAIN:PLAYER:0 = 0
STAIN:PLAYER:1 = 0
STAIN:PLAYER:2 = 2
STAIN:PLAYER:3 = 1
STAIN:PLAYER:4 = 8
STAIN:PLAYER:5 = 0

;（できれば）潤滑が半分に
PALAM:3 /= 2
PALAM:12 /= 2

RETURN 1

;お風呂で楽しむ
@TRAIN_MESSAGE_COM197
PRINTFORM %CALLNAME:PLAYER%は
IF TFLAG:17 == 0
	PRINTFORMW お湯を掬って%CALLNAME:TARGET%にかけた…
	IF TALENT:85 && RAND:3 >= 1
		PRINTFORMW %CALLNAME:TARGET%は楽しそうにお湯をかけ返してきた…
	ELSEIF TALENT:153
		PRINTFORMW %CALLNAME:TARGET%の口元が楽しげに緩んでいるのを%CALLNAME:PLAYER%は見逃さなかった…
	ELSEIF TALENT:152
		PRINTFORMW %CALLNAME:TARGET%は呆れたように髪を撫でつけた…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%はまったく相手にしてくれない…
	ENDIF
ELSEIF TFLAG:17 == 1
	PRINTFORMW %CALLNAME:TARGET%の体を洗ってあげた…
	IF TALENT:85 && RAND:3 >= 1
		PRINTFORMW いつの間にか%CALLNAME:TARGET%との洗いっこになっていた…
	ELSEIF TALENT:153
		PRINTFORMW %CALLNAME:TARGET%は綺麗になるのが嬉しいようだ…
	ELSEIF TALENT:152
		PRINTFORMW %CALLNAME:TARGET%はくすぐったそうにしている…
	ELSE
		PRINTFORMW しかし、%CALLNAME:TARGET%はすぐにシャワーを浴びてしまった…
	ENDIF
ELSEIF TFLAG:17 == 2
	PRINTFORMW %CALLNAME:TARGET%と一緒に湯船に入った…
	IF TALENT:85 && RAND:3 >= 1
		PRINTFORMW %CALLNAME:TARGET%はぴったりと体を密着させ、%CALLNAME:PLAYER%の顔を見つめている…
		PRINTW …ちょっとのぼせてしまった…
	ELSEIF TALENT:153
		PRINTFORMW %CALLNAME:TARGET%と%CALLNAME:PLAYER%の距離が日に日に近づいてきているようだ…
	ELSEIF TALENT:152
		PRINTFORMW %CALLNAME:TARGET%はちらちらとこちらの体を見ている…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は早々に湯船から出ていった…
	ENDIF
ELSEIF TFLAG:17 == 3
	PRINTFORMW 体を石鹸まみれにして、%CALLNAME:TARGET%に抱きついた…
	IF TALENT:85 && RAND:3 >= 1
		PRINTFORMW %CALLNAME:PLAYER%の体の感触を%CALLNAME:TARGET%は大層気に入ったようだ…
	ELSEIF TALENT:153
		PRINTFORMW %CALLNAME:TARGET%は恥ずかしそうにしながらも、黙って%CALLNAME:PLAYER%に抱かれていた…
	ELSEIF TALENT:152
		PRINTFORMW %CALLNAME:TARGET%は複雑な表情を浮かべている…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%を押しのけようとしたが、滑ってしまい思い通りにいかない…
	ENDIF
ENDIF

