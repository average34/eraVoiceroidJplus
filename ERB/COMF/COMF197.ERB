@COM_ABLE197
;お風呂で楽しむ実行判定
;COM_ABLE共通判定
CALL COM_ABLE_COMMON, SELECTCOM
SIF RESULT == 0
	RETURN 0
;ウフフ中じゃないと無理
SIF TFLAG:44 == 0
	RETURN 0
;お風呂中じゃないとダメ
SIF TEQUIP:58 == 0
	RETURN 0
;クリキャップ使用中は不可
SIF TEQUIP:14
	RETURN 0
;クリップローター使用中は不可
SIF TEQUIP:15
	RETURN 0
;搾乳器使用中は不可
SIF TEQUIP:16
	RETURN 0
;;乳房電極装着中はダメ
;SIF TEQUIP:50
;	RETURN 0
;吸血鬼や動物耳には従順４がないと使用不可（プレイヤーがサドならＯＫ）
IF TALENT:吸血鬼 || TALENT:動物耳
SIF (ABL:親密 < 4) && (TALENT:PLAYER:サド == 0)
	RETURN 0
ENDIF
RETURN 1

@COM197
;お風呂を楽しむ

PRINTL お風呂を楽しむ
STR:0 = お風呂を楽しむ

;地の文カットのために先にTFLAG:17を取得
IF RAND:4 == 0
	TFLAG:17 = 0
ELSEIF RAND:3 == 0
	TFLAG:17 = 1
ELSEIF RAND:2 == 0
	TFLAG:17 = 2
ELSE
	TFLAG:17 = 3
ENDIF

CALL TRAIN_MESSAGE_B

LOSEBASE:体力 += 0
LOSEBASE:気力 += 20

SOURCE:接触 = 50
SOURCE:欲情追加 = 50

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	SOURCE:露出 = 30
ELSEIF PALAM:欲情 < PALAMLV:2
	SOURCE:露出 = 50
ELSEIF PALAM:欲情 < PALAMLV:3
	SOURCE:露出 = 80
ELSEIF PALAM:欲情 < PALAMLV:4
	SOURCE:露出 = 120
ELSEIF PALAM:欲情 >= PALAMLV:4
	SOURCE:露出 = 200
ENDIF

;奉仕精神をみる
IF ABL:奉仕精神 == 0
	SOURCE:情愛 = 10
ELSEIF ABL:奉仕精神 == 1
	SOURCE:情愛 = 30
ELSEIF ABL:奉仕精神 == 2
	SOURCE:情愛 = 60
ELSEIF ABL:奉仕精神 == 3
	SOURCE:情愛 = 100
ELSEIF ABL:奉仕精神 == 4
	SOURCE:情愛 = 160
ELSE
	SOURCE:情愛 = 160 + (ABL:奉仕精神 * 15)
ENDIF

;ABL:従順をみる
IF ABL:親密 == 0
	TIMES SOURCE:達成感 , 0.80
ELSEIF ABL:親密 == 1
	TIMES SOURCE:達成感 , 0.90
ELSEIF ABL:親密 == 2
	TIMES SOURCE:達成感 , 1.00
ELSEIF ABL:親密 == 3
	TIMES SOURCE:達成感 , 1.10
ELSEIF ABL:親密 == 4
	TIMES SOURCE:達成感 , 1.20
ELSE
	;TIMES SOURCE:達成感 , 1.30
	SOURCE:達成感 *= (ABL:親密 * 5 + 105) / 100
ENDIF

;吸血鬼の場合
IF TALENT:吸血鬼
	SOURCE:痛み = 100
	TIMES SOURCE:痛み , 1.60
	TIMES SOURCE:屈従 , 2.00
	TIMES SOURCE:反感追加 , 1.40
ENDIF


;動物耳の場合
IF TALENT:動物耳
	TIMES SOURCE:逸脱 , 1.60
	TIMES SOURCE:屈従 , 1.50
	TIMES SOURCE:反感追加 , 2.00
ENDIF

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	PRINTS EXPNAME:40
	PRINTL +1
	EXP:レズ経験 += 1
	EXP:PLAYER:レズ経験 += 1
ELSEIF TALENT:オトコ == 1 && TALENT:PLAYER:オトコ == 1
	PRINTS EXPNAME:41
	PRINTL +1
	EXP:ＢＬ経験 += 1
	EXP:PLAYER:ＢＬ経験 += 1
ENDIF

;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:従順をみる
	IF ABL:親密 <= 1
		SOURCE:受動 += 300 + (ABL:親密 * 50)
	ELSEIF ABL:親密 <= 3
		SOURCE:受動 += 600 + (ABL:親密 * 100)
	ELSEIF ABL:親密 <= 5
		SOURCE:受動 += 800 + (ABL:親密 * 120)
	ELSEIF ABL:親密 <= 8
		SOURCE:受動 += 1000 + (ABL:親密 * 140)
	ELSEIF ABL:親密 <= 11
		SOURCE:受動 += 1200 + (ABL:親密 * 160)
	ELSE
		SOURCE:受動 += 1500 + (ABL:親密 * 180)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:サドっ気 == 0
		SOURCE:征服 = 600
	ELSEIF ABL:サドっ気 == 1
		SOURCE:征服 = 900
	ELSEIF ABL:サドっ気 == 2
		SOURCE:征服 = 1200
	ELSEIF ABL:サドっ気 == 3
		SOURCE:征服 = 1600
	ELSEIF ABL:サドっ気 == 4
		SOURCE:征服 = 2000
	ELSE
		SOURCE:征服 = 1000 + (ABL:サドっ気 * 300)
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:10 + 5
SIF TALENT:恋慕 || TALENT:恋人
	A += 2
SIF TALENT:親愛
	A += 3
;合意がないと好感度が下がる
IF TALENT:合意 == 0 && TFLAG:48 == 0
	A -= 8
	SOURCE:反感追加 += 300
ENDIF
TFLAG:30 += A
TFLAG:37 += A

;シャワーで汚れがリセット
STAIN:口 = 0
STAIN:手 = 0
STAIN:ペニス = 2
STAIN:ヴァギナ = 1
STAIN:アナル = 8
STAIN:胸 = 0
STAIN:PLAYER:口 = 0
STAIN:PLAYER:手 = 0
STAIN:PLAYER:ペニス = 2
STAIN:PLAYER:ヴァギナ = 1
STAIN:PLAYER:アナル = 8
STAIN:PLAYER:胸 = 0

;（できれば）潤滑が半分に
PALAM:潤滑 /= 2
PALAM:不快 /= 2

RETURN 1

;お風呂で楽しむ
@TRAIN_MESSAGE_COM197
PRINTFORM %CALLNAME:PLAYER%は
IF TFLAG:17 == 0
	PRINTFORMW お湯を掬って%CALLNAME:TARGET%にかけた…
	IF TALENT:恋慕 && RAND:3 >= 1
		PRINTFORMW %CALLNAME:TARGET%は楽しそうにお湯をかけ返してきた…
	ELSEIF TALENT:恋人
		PRINTFORMW %CALLNAME:TARGET%の口元が楽しげに緩んでいるのを%CALLNAME:PLAYER%は見逃さなかった…
	ELSEIF TALENT:合意
		PRINTFORMW %CALLNAME:TARGET%は呆れたように髪を撫でつけた…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%はまったく相手にしてくれない…
	ENDIF
ELSEIF TFLAG:17 == 1
	PRINTFORMW %CALLNAME:TARGET%の体を洗ってあげた…
	IF TALENT:恋慕 && RAND:3 >= 1
		PRINTFORMW いつの間にか%CALLNAME:TARGET%との洗いっこになっていた…
	ELSEIF TALENT:恋人
		PRINTFORMW %CALLNAME:TARGET%は綺麗になるのが嬉しいようだ…
	ELSEIF TALENT:合意
		PRINTFORMW %CALLNAME:TARGET%はくすぐったそうにしている…
	ELSE
		PRINTFORMW しかし、%CALLNAME:TARGET%はすぐにシャワーを浴びてしまった…
	ENDIF
ELSEIF TFLAG:17 == 2
	PRINTFORMW %CALLNAME:TARGET%と一緒に湯船に入った…
	IF TALENT:恋慕 && RAND:3 >= 1
		PRINTFORMW %CALLNAME:TARGET%はぴったりと体を密着させ、%CALLNAME:PLAYER%の顔を見つめている…
		PRINTW …ちょっとのぼせてしまった…
	ELSEIF TALENT:恋人
		PRINTFORMW %CALLNAME:TARGET%と%CALLNAME:PLAYER%の距離が日に日に近づいてきているようだ…
	ELSEIF TALENT:合意
		PRINTFORMW %CALLNAME:TARGET%はちらちらとこちらの体を見ている…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は早々に湯船から出ていった…
	ENDIF
ELSEIF TFLAG:17 == 3
	PRINTFORMW 体を石鹸まみれにして、%CALLNAME:TARGET%に抱きついた…
	IF TALENT:恋慕 && RAND:3 >= 1
		PRINTFORMW %CALLNAME:PLAYER%の体の感触を%CALLNAME:TARGET%は大層気に入ったようだ…
	ELSEIF TALENT:恋人
		PRINTFORMW %CALLNAME:TARGET%は恥ずかしそうにしながらも、黙って%CALLNAME:PLAYER%に抱かれていた…
	ELSEIF TALENT:合意
		PRINTFORMW %CALLNAME:TARGET%は複雑な表情を浮かべている…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%を押しのけようとしたが、滑ってしまい思い通りにいかない…
	ENDIF
ENDIF

