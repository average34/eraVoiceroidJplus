@COM2
;アナル愛撫
PRINTL アナル愛撫

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

STR:0 = アナル愛撫
CALL TRAIN_MESSAGE_B

LOSEBASE:0 += 20
LOSEBASE:1 += 100

SOURCE:12 = 850
SOURCE:14 = 400

;ABL:Ａ感覚をみる
IF ABL:5 == 0
	SOURCE:2 = 20
	SOURCE:13 = 300
ELSEIF ABL:5 == 1
	SOURCE:2 = 75
	SOURCE:13 = 350
ELSEIF ABL:5 == 2
	SOURCE:2 = 300
	SOURCE:13 = 400
ELSEIF ABL:5 == 3
	SOURCE:2 = 700
	SOURCE:13 = 650
ELSEIF ABL:5 == 4
	SOURCE:2 = 1100
	SOURCE:13 = 1000
ELSE
	SOURCE:2 = 500 + (ABL:5 * 200)
	SOURCE:13 = 250 + (ABL:5 * 250)
ENDIF

;EXP:Ａ経験をみる
IF EXP:1 < EXPLV:1
	TIMES SOURCE:2 , 0.20
	TIMES SOURCE:13, 0.20
	SOURCE:6 = 500
ELSEIF EXP:1 < EXPLV:2
	TIMES SOURCE:2 , 0.50
	TIMES SOURCE:13, 0.50
	SOURCE:6 = 400
ELSEIF EXP:1 < EXPLV:3
	TIMES SOURCE:2 , 1.00
	TIMES SOURCE:13, 1.00
	SOURCE:6 = 300
ELSEIF EXP:1 < EXPLV:4
	TIMES SOURCE:2 , 1.20
	TIMES SOURCE:13, 1.20
	SOURCE:6 = 200
ELSEIF EXP:1 < EXPLV:5
	TIMES SOURCE:2 , 1.60
	TIMES SOURCE:13, 1.60
	SOURCE:6 = 100
ELSE
	TIMES SOURCE:2 , 1.80
	TIMES SOURCE:13, 1.80
	SOURCE:6 = 50
ENDIF

;PALAM:潤滑をみる
IF PALAM:3 < PALAMLV:1
	TIMES SOURCE:2 , 0.10
	TIMES SOURCE:13, 0.10
	TIMES SOURCE:6 , 3.00
ELSEIF PALAM:3 < PALAMLV:2
	TIMES SOURCE:2 , 0.20
	TIMES SOURCE:13, 0.20
	TIMES SOURCE:6 , 2.00
ELSEIF PALAM:3 < PALAMLV:3
	TIMES SOURCE:2 , 0.60
	TIMES SOURCE:13, 0.60
	TIMES SOURCE:6 , 1.00
ELSEIF PALAM:3 < PALAMLV:4
	TIMES SOURCE:2 , 1.00
	TIMES SOURCE:13, 1.00
	TIMES SOURCE:6 , 0.50
ELSEIF PALAM:3 >= PALAMLV:4
	TIMES SOURCE:2 , 2.00
	TIMES SOURCE:13, 2.00
	TIMES SOURCE:6 , 0.10
ENDIF

;PALAM:欲情をみる
IF PALAM:5 < PALAMLV:1
	TIMES SOURCE:2 , 0.30
	TIMES SOURCE:13, 0.30
ELSEIF PALAM:5 < PALAMLV:2
	TIMES SOURCE:2 , 0.60
	TIMES SOURCE:13, 0.60
ELSEIF PALAM:5 < PALAMLV:3
	TIMES SOURCE:2 , 1.00
	TIMES SOURCE:13, 1.00
ELSEIF PALAM:5 < PALAMLV:4
	TIMES SOURCE:2 , 1.30
	TIMES SOURCE:13, 1.30
ELSEIF PALAM:5 >= PALAMLV:4
	TIMES SOURCE:2 , 1.60
	TIMES SOURCE:13, 1.60
ENDIF


;Ａ敏感、鈍感をみる
;快Ａ自体のチェックは後でまとめてやる
IF TALENT:105
	TIMES SOURCE:6 , 1.50
	TIMES SOURCE:13 , 1.50
	TIMES SOURCE:14 , 1.50
ELSEIF TALENT:106
	TIMES SOURCE:6 , 0.60
	TIMES SOURCE:13 , 0.60
	TIMES SOURCE:14 , 0.60
ENDIF

;処女で貞操観念
IF EXP:0 == 0 && TALENT:30
	SOURCE:13 /= 3
ENDIF

EXP:1 += 1
PRINTL Ａ経験＋１

IF TEQUIP:90
	;奴隷のＡに触手の汚れがつく
	STAIN:4 |= 2
	STAIN:4 |= 4
ELSE
	;奴隷のＡ⇔調教者の指の汚れが移動
	STAIN:4 |= STAIN:PLAYER:1
	STAIN:PLAYER:1 |= STAIN:4
ENDIF

IF TALENT:122 == 0 && TALENT:PLAYER:122 == 0
	PRINTS EXPNAME:40
	PRINTL +2
	EXP:40 += 2
	EXP:PLAYER:40 += 2
ELSEIF TALENT:122 == 1 && TALENT:PLAYER:122 == 1
	PRINTS EXPNAME:41
	PRINTL +5
	EXP:41 += 5
	EXP:PLAYER:41 += 5
ENDIF


;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:従順をみる
	IF ABL:0 <= 1
		SOURCE:21 += 300 + (ABL:0 * 50)
	ELSEIF ABL:0 <= 3
		SOURCE:21 += 600 + (ABL:0 * 100)
	ELSEIF ABL:0 <= 5
		SOURCE:21 += 800 + (ABL:0 * 120)
	ELSEIF ABL:0 <= 8
		SOURCE:21 += 1000 + (ABL:0 * 140)
	ELSEIF ABL:0 <= 11
		SOURCE:21 += 1200 + (ABL:0 * 160)
	ELSE
		SOURCE:21 += 1500 + (ABL:0 * 180)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:15 == 0
		SOURCE:20 = 600
	ELSEIF ABL:15 == 1
		SOURCE:20 = 900
	ELSEIF ABL:15 == 2
		SOURCE:20 = 1200
	ELSEIF ABL:15 == 3
		SOURCE:20 = 1600
	ELSEIF ABL:15 == 4
		SOURCE:20 = 2000
	ELSE
		SOURCE:20 = 1000 + (ABL:15 * 300)
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:10 + 5
;合意がないと好感度が下がる
IF TALENT:152 == 0 && TFLAG:48 == 0
	A -= 10
	SOURCE:15 += 500
ENDIF
TFLAG:30 += A
TFLAG:37 += A / 2

RETURN 1

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_2
A = 0
S = 0
V = 110

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;ABL:欲望
IF ABL:1
	A += ABL:1 * 3
ENDIF
;ABL:A感覚
IF ABL:5
	A += ABL:5 * 2
ENDIF
;ABL:奉仕精神
IF ABL:6
	A += ABL:6 * 4
ENDIF
;ABL:精液中毒
IF ABL:11
	A += ABL:11 * 1
ENDIF
IF MARK:1
	A += MARK:1 * 3
ENDIF

;PALAM:潤滑（の不足）
IF PALAM:3 < PALAMLV:3
	A -= 5
ENDIF
;PALAM:欲情
IF PALAM:5 < PALAMLV:1
	L = 0
ELSEIF PALAM:5 < PALAMLV:2
	L = 1
ELSEIF PALAM:5 < PALAMLV:3
	L = 2
ELSEIF PALAM:5 < PALAMLV:4
	L = 3
ELSEIF PALAM:5 < PALAMLV:5
	L = 4
ELSE
	L = 5
ENDIF
IF L
	A += L * 3
ENDIF

;恥じらい
IF TALENT:35
	A -= 2
ENDIF
;快感の否定
IF TALENT:71
	A -= 5
ENDIF
;男嫌い
IF TALENT:82 && TALENT:PLAYER:122
	A -= 12
ENDIF
;恋慕
IF TALENT:85 && ASSIPLAY == 0
	A += 5
	S = 5
ENDIF
;調教者がふたなり
IF TALENT:PLAYER:121
	A += 4
ENDIF


;媚薬
IF TEQUIP:21
	A += 6
ENDIF

;好感度
IF CFLAG:2 < 3000
	B = 0
	IF CFLAG:2 < 1000
		B = 50
	ELSEIF CFLAG:2 < 2000
		B = 20
	ELSEIF CFLAG:2 < 2500
		B = 10
	ELSE
		B = 5
	ENDIF
	A -= B
ENDIF
;好感度
IF CFLAG:2 >= 4000
	B = 0
	IF CFLAG:2 < 5000
		B = 5
	ELSE
		B = CFLAG:2 / 300
	ENDIF
	A += B
ENDIF

IF ((FLAG:23 & 1p27) == 0) && FLAG:2 != 1
	;ABL:欲望
	IF ABL:1
		SIF S
			PRINT  + 
		PRINTS ABLNAME:1
		PRINTV 'LV,ABL:1,'(,ABL:1 * 3,')
		S = 1
	ENDIF
	;ABL:A感覚
	IF ABL:5
		SIF S
			PRINT  + 
		PRINTS ABLNAME:5
		PRINTV 'LV,ABL:5,'(,ABL:5 * 2,')
		S = 1
	ENDIF
	;ABL:奉仕精神
	IF ABL:6
		SIF S
			PRINT  + 
		PRINTS ABLNAME:6
		PRINTV 'LV,ABL:6,'(,ABL:6 * 4,')
		S = 1
	ENDIF
	;ABL:精液中毒
	IF ABL:11
		SIF S
			PRINT  + 
		PRINTS ABLNAME:11
		PRINTV 'LV,ABL:11,'(,ABL:11 * 1,')
		S = 1
	ENDIF
	IF MARK:1
		SIF S
			PRINT  + 
		PRINTS MARKNAME:1
		PRINTV 'LV,MARK:1,'(,MARK:1 * 3,')
		S = 1
	ENDIF

	;PALAM:潤滑（の不足）
	IF PALAM:3 < PALAMLV:3
		PRINT  - 
		PRINT 潤滑不足
		PRINTV '(,5,')
		S = 1
	ENDIF
	;PALAM:欲情
	IF PALAM:5 < PALAMLV:1
		L = 0
	ELSEIF PALAM:5 < PALAMLV:2
		L = 1
	ELSEIF PALAM:5 < PALAMLV:3
		L = 2
	ELSEIF PALAM:5 < PALAMLV:4
		L = 3
	ELSEIF PALAM:5 < PALAMLV:5
		L = 4
	ELSE
		L = 5
	ENDIF
	IF L
		SIF S
			PRINT  + 
		PRINTS PALAMNAME:5
		PRINTV 'LV,L,'(,L * 3,')
		S = 1
	ENDIF

	;恥じらい
	IF TALENT:35
		PRINT  - 
		PRINTS TALENTNAME:35
		PRINTV '(,2,')
		S = 1
	ENDIF
	;快感の否定
	IF TALENT:71
		PRINT  - 
		PRINTS TALENTNAME:71
		PRINTV '(,5,')
		S = 1
	ENDIF
	;男嫌い
	IF TALENT:82 && TALENT:PLAYER:122
		PRINT  - 
		PRINTS TALENTNAME:82
		PRINTV '(,12,')
		S = 1
	ENDIF
	;恋慕
	IF TALENT:85 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:85
		PRINTV '(,5,')
		S = 5
	ENDIF
	;調教者がふたなり
	IF TALENT:PLAYER:121
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:121
		PRINTV '(,4,')
		S = 1
	ENDIF


	;媚薬
	IF TEQUIP:21
		SIF S
			PRINT  + 
		PRINTS ITEMNAME:26
		PRINTV '(,6,')
		S = 1
	ENDIF

	;好感度
	IF CFLAG:2 < 3000
		B = 0
		PRINT  - 
		IF CFLAG:2 < 1000
			B = 50
		ELSEIF CFLAG:2 < 2000
			B = 20
		ELSEIF CFLAG:2 < 2500
			B = 10
		ELSE
			B = 5
		ENDIF
		PRINT 好感度不足
		PRINTV '(,B,')
		S = 1
	ENDIF
	;好感度
	IF CFLAG:2 >= 4000
		B = 0
		SIF S
			PRINT  + 
		IF CFLAG:2 < 5000
			B = 5
		ELSE
			B = CFLAG:2 / 300
		ENDIF
		PRINT 好感度
		PRINTV '(,B,')
		S = 1
	ENDIF

	;合計を表示(110以上で実行)
	PRINT  = 
	PRINTV A

	SIF A < V
		PRINT  < 
	SIF A == V
		PRINT  = 
	SIF A > V
		PRINT  > 
	PRINT 実行値
	PRINTV V
	WAIT
ENDIF

