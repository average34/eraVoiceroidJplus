@COM0
;愛撫
SIF TEQUIP:65
	PRINT 着衣
PRINTL 愛撫
STR:0 = 愛撫
CALL TRAIN_MESSAGE_B

LOSEBASE:体力 += 5
LOSEBASE:気力 += 50

SOURCE:4 = 60
SOURCE:8 = 30
SOURCE:12 = 100

;ABL:Ｃ感覚をみる
IF ABL:Ｃ感覚 == 0
	SOURCE:0 = 20
	SOURCE:3 = 50
ELSEIF ABL:Ｃ感覚 == 1
	SOURCE:0 = 100
	SOURCE:3 = 100
ELSEIF ABL:Ｃ感覚 == 2
	SOURCE:0 = 500
	SOURCE:3 = 160
ELSEIF ABL:Ｃ感覚 == 3
	SOURCE:0 = 1200
	SOURCE:3 = 200
ELSEIF ABL:Ｃ感覚 == 4
	SOURCE:0 = 2000
	SOURCE:3 = 230
ELSE
	SOURCE:0 = 800 + (ABL:Ｃ感覚 * 400)
	SOURCE:3 = 200 + (ABL:Ｃ感覚 * 10)
ENDIF

;ABL:Ｂ感覚をみる
IF ABL:Ｂ感覚 == 0
	SOURCE:17 = 15
ELSEIF ABL:Ｂ感覚 == 1
	SOURCE:17 = 50
ELSEIF ABL:Ｂ感覚 == 2
	SOURCE:17 = 300
ELSEIF ABL:Ｂ感覚 == 3
	SOURCE:17 = 700
ELSEIF ABL:Ｂ感覚 == 4
	SOURCE:17 = 1100
ELSE
	SOURCE:17 = 350 + (ABL:Ｂ感覚 * 250)
ENDIF

;着衣中でかつコンフィグがONだと性能低下
IF TEQUIP:65
	TIMES SOURCE:0 , 0.80
	TIMES SOURCE:17 , 0.80
ENDIF

IF (STAIN:口 >= 2 && (STAIN:口 != 16 || STAIN:口 != 17) && ASSIPLAY == 0 && TALENT:MASTER:汚れ無視 == 0) || TEQUIP:45 || TALENT:キス未経験
;奴隷の口の汚れがあるときキスはない（愛液と母乳よごれはＯＫ、汚れ無視か助手調教時は問題なし）
;ボールギャグ使用中もなし
;キス未経験でもなし
	SOURCE:8 = 0
	SOURCE:0 /= 2
	SOURCE:3 /= 4
	SOURCE:10 /= 2
ELSE
	;Ｍ感覚を設定している場合快Ｍソース等取得
	SIF FLAG:62 & 4194304
		CALL SOURCE_M_COM0
	
	;汚臭鈍感
	SIF TALENT:汚臭鈍感
		SOURCE:8 /= 4
	;汚臭敏感
	SIF TALENT:汚臭敏感
		SOURCE:8 *= 3
	;自尊心
	SIF TALENT:プライド高い
		SOURCE:8 *= 2
	;恋慕
	IF TALENT:恋慕 && ASSIPLAY == 0
		SOURCE:3 *= 2
		SOURCE:8 /= 10
	ENDIF
	IF STAIN:PLAYER:口
	;主人の口の汚れがあると不潔150%
		SOURCE:8 *= 3
		SOURCE:8 /= 2
	ENDIF

	;奴隷の口⇔調教者の口の汚れが移動
	STAIN:口 |= STAIN:PLAYER:口
	STAIN:PLAYER:口 |= STAIN:口

	PRINTFORML %EXPNAME:96%＋１
	EXP:キス経験 += 1
	EXP:PLAYER:キス経験 += 1
	IF TALENT:PLAYER:キス未経験
		PRINTFORMW %CALLNAME:PLAYER%は【キス未経験】を失いました
		TALENT:PLAYER:キス未経験 = 0
	ENDIF
ENDIF

IF TEQUIP:90
	;奴隷に触手の汚れがつく
	STAIN:手 |= 2
	STAIN:手 |= 4
	STAIN:胸 |= 2
	STAIN:胸 |= 4
ELSE
	;奴隷のＶ⇔調教者の指の汚れが移動
	STAIN:ヴァギナ |= STAIN:PLAYER:手
	STAIN:PLAYER:手 |= STAIN:ヴァギナ

	;奴隷のＢ⇔調教者の指の汚れが移動
	STAIN:胸 |= STAIN:PLAYER:手
	STAIN:PLAYER:手 |= STAIN:胸
ENDIF

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	PRINTS EXPNAME:40
	PRINTL +5
	EXP:レズ経験 += 5
	EXP:PLAYER:レズ経験 += 5
ELSEIF TALENT:オトコ == 1 && TALENT:PLAYER:オトコ == 1
	PRINTS EXPNAME:41
	PRINTL +5
	EXP:ＢＬ経験 += 5
	EXP:PLAYER:ＢＬ経験 += 5
ENDIF

;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:従順をみる
	IF ABL:親密 <= 1
		SOURCE:21 += 300 + (ABL:親密 * 50)
	ELSEIF ABL:親密 <= 3
		SOURCE:21 += 600 + (ABL:親密 * 100)
	ELSEIF ABL:親密 <= 5
		SOURCE:21 += 800 + (ABL:親密 * 120)
	ELSEIF ABL:親密 <= 8
		SOURCE:21 += 1000 + (ABL:親密 * 140)
	ELSEIF ABL:親密 <= 11
		SOURCE:21 += 1200 + (ABL:親密 * 160)
	ELSE
		SOURCE:21 += 1500 + (ABL:親密 * 180)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:サドっ気 == 0
		SOURCE:20 = 600
	ELSEIF ABL:サドっ気 == 1
		SOURCE:20 = 900
	ELSEIF ABL:サドっ気 == 2
		SOURCE:20 = 1200
	ELSEIF ABL:サドっ気 == 3
		SOURCE:20 = 1600
	ELSEIF ABL:サドっ気 == 4
		SOURCE:20 = 2000
	ELSE
		SOURCE:20 = 1000 + (ABL:サドっ気 * 300)
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:10 + 5
;合意がないと好感度が下がる
IF TALENT:合意 == 0 && TFLAG:48 == 0
	A -= 7
	SOURCE:15 += 200
ENDIF
TFLAG:30 += A
TFLAG:37 += A

RETURN 1

;Ｍ感覚を設定している場合快Ｍソース等取得
@SOURCE_M_COM0
;ABL:Ｍ感覚をみる
IF ABL:Ｍ感覚 == 0
	SOURCE:24 = 5
ELSEIF ABL:Ｍ感覚 == 1
	SOURCE:24 = 25
ELSEIF ABL:Ｍ感覚 == 2
	SOURCE:24 = 100
ELSEIF ABL:Ｍ感覚 == 3
	SOURCE:24 = 300
ELSEIF ABL:Ｍ感覚 == 4
	SOURCE:24 = 500
ELSE
	SOURCE:24 = 200 + (ABL:Ｍ感覚 * 200)
ENDIF

;TALENT:恋慕をみる
SIF TALENT:恋慕
	TIMES SOURCE:24 , 1.50
;TALENT:恋人をみる
SIF TALENT:恋人
	TIMES SOURCE:24 , 1.50
;TALENT:親愛をみる
SIF TALENT:親愛
	TIMES SOURCE:24 , 2.00
;TALENT:相愛をみる
SIF TALENT:相愛
	TIMES SOURCE:24 , 2.00

