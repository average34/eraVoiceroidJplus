@COM0
;愛撫
SIF TEQUIP:65
	PRINT 着衣
PRINTL 愛撫
STR:0 = 愛撫
CALL TRAIN_MESSAGE_B

LOSEBASE:0 += 5
LOSEBASE:1 += 50

SOURCE:4 = 60
SOURCE:8 = 30
SOURCE:12 = 100

;ABL:Ｃ感覚をみる
IF ABL:3 == 0
	SOURCE:0 = 20
	SOURCE:3 = 50
ELSEIF ABL:3 == 1
	SOURCE:0 = 100
	SOURCE:3 = 100
ELSEIF ABL:3 == 2
	SOURCE:0 = 500
	SOURCE:3 = 160
ELSEIF ABL:3 == 3
	SOURCE:0 = 1200
	SOURCE:3 = 200
ELSEIF ABL:3 == 4
	SOURCE:0 = 2000
	SOURCE:3 = 230
ELSE
	SOURCE:0 = 800 + (ABL:3 * 400)
	SOURCE:3 = 200 + (ABL:3 * 10)
ENDIF

;ABL:Ｂ感覚をみる
IF ABL:14 == 0
	SOURCE:17 = 15
ELSEIF ABL:14 == 1
	SOURCE:17 = 50
ELSEIF ABL:14 == 2
	SOURCE:17 = 300
ELSEIF ABL:14 == 3
	SOURCE:17 = 700
ELSEIF ABL:14 == 4
	SOURCE:17 = 1100
ELSE
	SOURCE:17 = 350 + (ABL:14 * 250)
ENDIF

;着衣中でかつコンフィグがONだと性能低下
IF TEQUIP:65
	TIMES SOURCE:0 , 0.80
	TIMES SOURCE:17 , 0.80
ENDIF

IF (STAIN:0 >= 2 && (STAIN:0 != 16 || STAIN:0 != 17) && ASSIPLAY == 0 && TALENT:MASTER:64 == 0) || TEQUIP:45 || TALENT:151
;奴隷の口の汚れがあるときキスはない（愛液と母乳よごれはＯＫ、汚れ無視か助手調教時は問題なし）
;ボールギャグ使用中もなし
;キス未経験でもなし
	SOURCE:8 = 0
	SOURCE:0 /= 2
	SOURCE:3 /= 4
	SOURCE:10 /= 2
ELSE
	;Ｍ感覚を設定している場合快Ｍソース等取得
	SIF FLAG:62 & 4194304
		CALL SOURCE_M_COM0
	
	;汚臭鈍感
	SIF TALENT:61
		SOURCE:8 /= 4
	;汚臭敏感
	SIF TALENT:62
		SOURCE:8 *= 3
	;自尊心
	SIF TALENT:15
		SOURCE:8 *= 2
	;恋慕
	IF TALENT:85 && ASSIPLAY == 0
		SOURCE:3 *= 2
		SOURCE:8 /= 10
	ENDIF
	IF STAIN:PLAYER:0
	;主人の口の汚れがあると不潔150%
		SOURCE:8 *= 3
		SOURCE:8 /= 2
	ENDIF

	;奴隷の口⇔調教者の口の汚れが移動
	STAIN:0 |= STAIN:PLAYER:0
	STAIN:PLAYER:0 |= STAIN:0

	PRINTFORML %EXPNAME:96%＋１
	EXP:96 += 1
	EXP:PLAYER:96 += 1
	IF TALENT:PLAYER:151
		PRINTFORMW %CALLNAME:PLAYER%は【キス未経験】を失いました
		TALENT:PLAYER:151 = 0
	ENDIF
ENDIF

IF TEQUIP:90
	;奴隷に触手の汚れがつく
	STAIN:1 |= 2
	STAIN:1 |= 4
	STAIN:5 |= 2
	STAIN:5 |= 4
ELSE
	;奴隷のＶ⇔調教者の指の汚れが移動
	STAIN:3 |= STAIN:PLAYER:1
	STAIN:PLAYER:1 |= STAIN:3

	;奴隷のＢ⇔調教者の指の汚れが移動
	STAIN:5 |= STAIN:PLAYER:1
	STAIN:PLAYER:1 |= STAIN:5
ENDIF

IF TALENT:122 == 0 && TALENT:PLAYER:122 == 0
	PRINTS EXPNAME:40
	PRINTL +5
	EXP:40 += 5
	EXP:PLAYER:40 += 5
ELSEIF TALENT:122 == 1 && TALENT:PLAYER:122 == 1
	PRINTS EXPNAME:41
	PRINTL +5
	EXP:41 += 5
	EXP:PLAYER:41 += 5
ENDIF

;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:従順をみる
	IF ABL:0 <= 1
		SOURCE:21 += 300 + (ABL:0 * 50)
	ELSEIF ABL:0 <= 3
		SOURCE:21 += 600 + (ABL:0 * 100)
	ELSEIF ABL:0 <= 5
		SOURCE:21 += 800 + (ABL:0 * 120)
	ELSEIF ABL:0 <= 8
		SOURCE:21 += 1000 + (ABL:0 * 140)
	ELSEIF ABL:0 <= 11
		SOURCE:21 += 1200 + (ABL:0 * 160)
	ELSE
		SOURCE:21 += 1500 + (ABL:0 * 180)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:15 == 0
		SOURCE:20 = 600
	ELSEIF ABL:15 == 1
		SOURCE:20 = 900
	ELSEIF ABL:15 == 2
		SOURCE:20 = 1200
	ELSEIF ABL:15 == 3
		SOURCE:20 = 1600
	ELSEIF ABL:15 == 4
		SOURCE:20 = 2000
	ELSE
		SOURCE:20 = 1000 + (ABL:15 * 300)
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:10 + 5
;合意がないと好感度が下がる
IF TALENT:152 == 0 && TFLAG:48 == 0
	A -= 7
	SOURCE:15 += 200
ENDIF
TFLAG:30 += A
TFLAG:37 += A

RETURN 1

;Ｍ感覚を設定している場合快Ｍソース等取得
@SOURCE_M_COM0
;ABL:Ｍ感覚をみる
IF ABL:17 == 0
	SOURCE:24 = 5
ELSEIF ABL:17 == 1
	SOURCE:24 = 25
ELSEIF ABL:17 == 2
	SOURCE:24 = 100
ELSEIF ABL:17 == 3
	SOURCE:24 = 300
ELSEIF ABL:17 == 4
	SOURCE:24 = 500
ELSE
	SOURCE:24 = 200 + (ABL:17 * 200)
ENDIF

;TALENT:恋慕をみる
SIF TALENT:85
	TIMES SOURCE:24 , 1.50
;TALENT:恋人をみる
SIF TALENT:153
	TIMES SOURCE:24 , 1.50
;TALENT:親愛をみる
SIF TALENT:88
	TIMES SOURCE:24 , 2.00
;TALENT:相愛をみる
SIF TALENT:89
	TIMES SOURCE:24 , 2.00

