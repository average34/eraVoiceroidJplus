@_COMF257_PUBES_GLOW
#FUNCTION
RETURNF 200

@_COMF257_ENABLE_SHAVING
#FUNCTION
RETURNF 150

@COM_ABLE257
;剃毛プレイ実行判定
;YM Mk3改7.1を参考に

;COM_ABLE共通判定
CALL COM_ABLE_COMMON, SELECTCOM
SIF RESULT == 0
	RETURN 0

;恋慕か合意＆恋人が必要
SIF TALENT:恋慕 == 0 && (TALENT:合意 == 0 || TALENT:恋人 == 0)
	RETURN 0

;反発は不可。刃物扱うし。
SIF MARK:反発感情
	RETURN 0

;ウフフ中/お風呂場プレイ中じゃないと無理
SIF TFLAG:44 == 0 || TEQUIP:58 == 0
	RETURN 0

;プレイマットが必要
SIF ITEM:プレイマット == 0
	RETURN 0

;触手調教中・暗闇プレイはダメ
SIF TEQUIP:90 || TEQUIP:80
	RETURN 0

;縄で緊縛はダメ
SIF TEQUIP:44
	RETURN 0

;PLAYER主導権時の実行判定
CALL COM_ABLE257_PLAYER_MAIN
LOCAL:0 = RESULT

;TARGET主導権時の実行判定
CALL COM_ABLE257_TARGET_MAIN
LOCAL:1 = RESULT

;PLAYER主導権時、TARGET主導権時のどちらも実行できない場合はダメ
SIF LOCAL:0 == 0 && LOCAL:1 == 0
	RETURN 0

RETURN 1

@COM_ABLE257_PLAYER_MAIN
;PLAYER主導権時の実行判定
;バイブ・クスコ・焼き芋・オナホ・オムツはダメ
SIF TEQUIP:11 || TEQUIP:61 || TEQUIP:71 || TEQUIP:17 || TEQUIP:20
	RETURN 0
;小人体型だとダメ
SIF TALENT:TARGET:小人体型
	RETURN 0
;一度剃った後、ある程度伸びないうちはダメ
SIF BASE:TARGET:陰毛 < _COMF257_ENABLE_SHAVING()
	RETURN 0
;技巧 3 + 酩酊度 / 20 以上
;[酒豪]持ってると技巧 3 + 酩酊度 / 25
SIF ABL:PLAYER:技巧 < (3 + TFLAG:47 / (TALENT:PLAYER:酒豪 ? 25 # 20))
	RETURN 0
RETURN 1

@COM_ABLE257_TARGET_MAIN
;TARGET主導権時の実行判定
;プレイヤーが小人体型だとダメ
SIF TALENT:PLAYER:小人体型
	RETURN 0
;プレイヤーが一度剃った後、ある程度伸びないうちはダメ
SIF BASE:PLAYER:陰毛 < _COMF257_ENABLE_SHAVING()
	RETURN 0
;技巧 3 + 酩酊度 / 20 以上
;[酒豪]持ってると技巧 3 + 酩酊度 / 25
SIF ABL:TARGET:技巧 < (3 + TFLAG:47 / (TALENT:TARGET:酒豪 ? 25 # 20))
	RETURN 0
RETURN 1

@COM257
;PLAYER主導権時の実行判定
CALL COM_ABLE257_PLAYER_MAIN
LOCAL:0 = RESULT

;TARGET主導権時の実行判定
CALL COM_ABLE257_TARGET_MAIN
LOCAL:1 = RESULT

;プレイヤーに主導権がある時にPLAYER主導権時の実行ができない場合はパートナーに主導権が移る
IF LOCAL:0 == 0 && TFLAG:45 == 0
	TFLAG:45 = 1
;パートナーに主導権がある時にTARGET主導権時の実行ができない場合はプレイヤーに主導権が移る
ELSEIF LOCAL:1 == 0 && TFLAG:45
	TFLAG:45 = 0
ENDIF

IF TFLAG:45
	PRINTL 剃毛される
ELSE
	PRINTL 剃毛プレイ
ENDIF
STR:0 = 剃毛プレイ

CALL TRAIN_MESSAGE_B

;主導権がPLAYER
IF TFLAG:45 == 0
	;体力・気力減少
	LOSEBASE:体力 += 50
	LOSEBASE:気力 += 100

	;PALAM:潤滑を見てSOURCE:痛み
	IF PALAM:潤滑 < PALAMLV:1
		SOURCE:痛み = 600
	ELSEIF PALAM:潤滑 < PALAMLV:2
		SOURCE:痛み = 250
	ELSEIF PALAM:潤滑 < PALAMLV:3
		SOURCE:痛み = 100
	ELSEIF PALAM:潤滑 < PALAMLV:4
		SOURCE:痛み = 30
	ENDIF

	LOCAL:1 = 500

	;ABL:露出癖を見る
	IF ABL:露出癖 == 0
		TIMES LOCAL:1 , 0.80
	ELSEIF ABL:露出癖 == 1
		TIMES LOCAL:1 , 1.00
	ELSEIF ABL:露出癖 == 2
		TIMES LOCAL:1 , 1.20
	ELSEIF ABL:露出癖 == 3
		TIMES LOCAL:1 , 1.50
	ELSEIF ABL:露出癖 == 4
		TIMES LOCAL:1 , 2.00
	ELSE
		TIMES LOCAL:1 , 2.60
	ENDIF

	;ABL:マゾっ気を見る
	IF ABL:マゾっ気 == 0
		TIMES LOCAL:1 , 0.80
	ELSEIF ABL:マゾっ気 == 1
		TIMES LOCAL:1 , 1.00
	ELSEIF ABL:マゾっ気 == 2
		TIMES LOCAL:1 , 1.20
	ELSEIF ABL:マゾっ気 == 3
		TIMES LOCAL:1 , 1.40
	ELSEIF ABL:マゾっ気 == 4
		TIMES LOCAL:1 , 1.70
	ELSE
		TIMES LOCAL:1 , 2.00
	ENDIF

	;中毒充足
	SOURCE:中毒充足 += LOCAL:1
	;欲情追加
	SOURCE:欲情追加 += LOCAL:1
	;露出
	SOURCE:露出 += LOCAL:1
	;逸脱
	SOURCE:逸脱 += 1500

	;;小柄体型、小人体型以外
	;SIF TALENT:蓬莱人 == 0 && TALENT:具現 == 0
	;	TIMES SOURCE:露出 , 2.50

	;小柄体型、小人体型はcom_ableで弾いてる
	TIMES SOURCE:露出, 2.50

	;[パイパン]付加
	IF TALENT:TARGET:パイパン == 0
		PRINTL 
		PRINTFORML %CALLNAME:TARGET%は[%TALENTNAME:65%]になった
		TALENT:TARGET:パイパン = 1
	ENDIF

	;BASE:陰毛
	P:8 = BASE:TARGET:陰毛
	BASE:TARGET:陰毛 = 0

	;TALENT:オトコ オトコ
	IF TALENT:TARGET:オトコ == 0 && TALENT:PLAYER:オトコ == 0
		;EXP:レズ経験 レズ経験
		PRINTFORML %EXPNAME:40%+2
		EXP:TARGET:レズ経験 += 2
		EXP:PLAYER:レズ経験 += 2
	ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
		;EXP:ＢＬ経験 BL経験
		PRINTFORML %EXPNAME:41%+2
		EXP:TARGET:ＢＬ経験 += 2
		EXP:PLAYER:ＢＬ経験 += 2
	ENDIF

;主導権がTARGET
ELSE
	;体力・気力減少
	LOSEBASE:体力 += 25
	LOSEBASE:気力 += 50

	;ABL:サドっ気 サドっ気
	;SOURCE:征服 征服
	IF ABL:サドっ気 == 0
		SOURCE:征服 = 800
	ELSEIF ABL:サドっ気 == 1
		SOURCE:征服 = 1350
	ELSEIF ABL:サドっ気 == 2
		SOURCE:征服 = 2000
	ELSEIF ABL:サドっ気 == 3
		SOURCE:征服 = 3000
	ELSEIF ABL:サドっ気 == 4
		SOURCE:征服 = 4000
	ELSE
		SOURCE:征服 = 2500 + (ABL:サドっ気 * 500)
	ENDIF

	;[パイパン]付加
	IF TALENT:PLAYER:パイパン == 0
		PRINTL 
		PRINTFORML %CALLNAME:PLAYER%は[%TALENTNAME:65%]になった
		TALENT:PLAYER:パイパン = 1
	ENDIF

	P:8 = BASE:PLAYER:陰毛
	BASE:PLAYER:陰毛 = 0

	;TALENT:オトコ オトコ
	IF TALENT:TARGET:オトコ == 0 && TALENT:PLAYER:オトコ == 0
		;EXP:レズ経験 レズ経験
		PRINTFORML %EXPNAME:40%+2
		EXP:TARGET:レズ経験 += 2
		EXP:PLAYER:レズ経験 += 2
	ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
		;EXP:ＢＬ経験 BL経験
		PRINTFORML %EXPNAME:41%+2
		EXP:TARGET:ＢＬ経験 += 2
		EXP:PLAYER:ＢＬ経験 += 2
	ENDIF
ENDIF

RETURN 1

@TRAIN_MESSAGE_COM257
;TARGET主導権
IF TFLAG:45
	IF BASE:PLAYER:陰毛 < _COMF257_PUBES_GLOW()
		IF MAXBASE:PLAYER:陰毛 == 1000
			PRINTFORML %CALLNAME:TARGET%はようやく伸びてきた%CALLNAME:PLAYER%の陰毛を綺麗さっぱり剃り落した
		ELSE
			PRINTFORML %CALLNAME:TARGET%はまだ薄く産毛に近いような%CALLNAME:PLAYER%の陰毛を綺麗さっぱり剃り落した
		ENDIF
	ELSE
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の陰毛を綺麗さっぱり剃り落した
	ENDIF
	PRINTFORM %CALLNAME:TARGET%は
	;ABL:露出癖 露出癖
	SELECTCASE ABL:露出癖
		CASE 0, 1
			PRINTFORML 顔を真っ赤にしてツルツルになった%CALLNAME:PLAYER%の性器から目を背けている…
		CASE 2
			PRINTFORML 露わになった%CALLNAME:PLAYER%の性器を恥ずかしそうに見つめている…
		CASE 3
			PRINTL 少し顔を俯かせている…
		CASE 4
			PRINTFORML %CALLNAME:PLAYER%の性器が丸見えの状態をどことなく楽しんでいるようだ…
		CASEELSE
			PRINTFORML 陰毛をすべて剃り落して丸見えになった%CALLNAME:PLAYER%の性器を大いに楽しんでいるようだ…
	ENDSELECT
;PLAYER主導権
ELSE
	IF BASE:TARGET:陰毛 < _COMF257_PUBES_GLOW()
		IF MAXBASE:TARGET:陰毛 == 1000
			PRINTFORML %CALLNAME:PLAYER%はようやく伸びてきた%CALLNAME:TARGET%の陰毛を綺麗さっぱり剃り落した
		ELSE
			PRINTFORML %CALLNAME:PLAYER%はまだ薄く産毛に近いような%CALLNAME:TARGET%の陰毛を綺麗さっぱり剃り落した
		ENDIF
	ELSE
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の陰毛を綺麗さっぱり剃り落した
	ENDIF
	PRINTFORM %CALLNAME:TARGET%は
	;ABL:露出癖 露出癖
	SELECTCASE ABL:露出癖
		CASE 0, 1
			PRINT 顔を真っ赤にして
			;縄で緊縛
			IF TEQUIP:44
				PRINTFORML %CALLNAME:PLAYER%と視線を合わせないように背けている…
			ELSE
				PRINTL 手でツルツルになった性器を隠そうとしている…
			ENDIF
		CASE 2
			PRINTL 露わになった自分の性器を恥ずかしそうに見つめている…
		CASE 3
			PRINTL 少し顔を俯かせている…
		CASE 4
			PRINTL 性器が丸見えの状態に恥ずかしさを感じつつも、どことなく楽しんでいるようだ…
		CASEELSE
			PRINTL 空気が性器を撫でつける感触を大いに楽しんでいるようだ…
	ENDSELECT
ENDIF

;SYSTEM.ERBの@EVENTTURNENDに追加してもよかったけど、現状ここで済ませてしまう
;本体に取り込むならそのときそっちに持って行った方がいいかもです。
@EVENTTURNEND
REPEAT CHARANUM
	;昼
	IF TIME == 0
		LOCAL:5 = 20
		;日光浴
		SIF TALENT:COUNT:日光浴
			LOCAL:5 = 25
	ELSE
		LOCAL:5 = 35
		;月光浴
		SIF TALENT:COUNT:吸血鬼
			LOCAL:5 = 40
	ENDIF

	;回復早い
	IF TALENT:COUNT:回復早い
		TIMES LOCAL:5, 1.25
	;回復遅い
	ELSEIF TALENT:COUNT:回復遅い
		TIMES LOCAL:5, 0.75
	ENDIF

	;小柄体型
	SIF TALENT:COUNT:小柄体型
		TIMES LOCAL:5, 0.95

	SIF MAXBASE:COUNT:陰毛 == 300
		TIMES LOCAL:5, 0.95

	BASE:COUNT:陰毛 = MIN(BASE:COUNT:陰毛 + MAX(LOCAL:5, 1), MAXBASE:COUNT:陰毛)
	IF TALENT:COUNT:パイパン && BASE:COUNT:陰毛 > _COMF257_PUBES_GLOW()
		DRAWLINE
		PRINTFORML %CALLNAME:COUNT%の陰毛が生えそろいました
		PRINTFORML %CALLNAME:COUNT%は[%TALENTNAME:65%]ではなくなりました
		TALENT:COUNT:パイパン = 0
	;ELSEIF TALENT:COUNT:パイパン
		;PRINTFORML %CALLNAME:COUNT%は[%TALENTNAME:65%]です
	;ELSE
		;PRINTFORML %CALLNAME:COUNT%は[%TALENTNAME:65%]ではありません
	ENDIF
REND

