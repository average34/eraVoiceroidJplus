@COM_ABLE168
;角アナル挿入実行判定
;COM_ABLE共通判定
CALL COM_ABLE_COMMON, SELECTCOM
SIF RESULT == 0
	RETURN 0
;ウフフ中じゃないと無理
SIF TFLAG:44 == 0
	RETURN 0
;助手じゃないとダメ
SIF PLAYER != ASSI
	RETURN 0
;アナルバイブ挿入中はダメ
SIF TEQUIP:13
	RETURN 0
;浣腸＋アナルプラグ使用中はダメ
SIF TEQUIP:46
	RETURN 0
;空気浣腸使用中は不可
SIF TEQUIP:47
	RETURN 0
;バルーン使用中は不可
SIF TEQUIP:48
	RETURN 0
;電極使用中は不可
SIF TEQUIP:49
	RETURN 0
;巨峰挿入中は不可
SIF TEQUIP:70
	RETURN 0
;氷柱挿入中は不可
SIF TEQUIP:79
	RETURN 0
;クスコ着けているとダメ
SIF TEQUIP:61
	RETURN 0
;調教対象が小人体型の場合は主人が禁断の知識持ちじゃないとダメ
SIF TALENT:小人体型 && TALENT:MASTER:禁断の知識 == 0
	RETURN 0
;調教対象に角がないとダメ
SIF TALENT:角 == 0
	RETURN 0
;潤滑不足で、従順4以下・レズっ気4以下のASSIの場合角アナル挿入はできない（サドならＯＫ）
IF PALAM:3 < PALAMLV:2 && ASSIPLAY
	SIF (ABL:ASSI:親密 <= 4 || ABL:ASSI:レズっ気 <= 4) && TALENT:ASSI:サド == 0
		RETURN 0
ENDIF
;一定のＡ経験が必要
SIF EXP:Ａ経験 < EXPLV:3
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:90
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
IF TEQUIP:58
SIF ITEM:13 == 0 && NOITEM == 0
	RETURN 0
ENDIF
;caved!!!!が発動しているとダメ
SIF TFLAG:69 > 0
	RETURN 0
RETURN 1

@COM168
;角アナル挿入

;調教対象が緊縛していない反発刻印3のキモけーねだった場合、逆襲される可能性大
IF NAME_CSV(NO:TARGET, 0) == "慧音" && TEQUIP:1 && MARK:3 >= 3 && TEQUIP:44 == 0
	IF RAND:4 > 0 && TFLAG:69 == 0
		TFLAG:69 = 3
		JUMP CAVED
	ENDIF
ENDIF

PRINTL 角アナル挿入
STR:0 = 角アナル挿入
CALL TRAIN_MESSAGE_B

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOSEBASE:体力 += 700
LOSEBASE:気力 += 350

SOURCE:3 = 100
SOURCE:6 = 2500
SOURCE:12 = 1200
SOURCE:13 = 2000
SOURCE:14 = 2700

;ABL:Ａ感覚をみる
IF ABL:Ａ感覚 == 0
	SOURCE:2 = 11
	SOURCE:13 += 100
ELSEIF ABL:Ａ感覚 == 1
	SOURCE:2 = 110
	SOURCE:13 += 700
ELSEIF ABL:Ａ感覚 == 2
	SOURCE:2 = 550
	SOURCE:13 += 1500
ELSEIF ABL:Ａ感覚 == 3
	SOURCE:2 = 1100
	SOURCE:13 += 3000
ELSEIF ABL:Ａ感覚 == 4
	SOURCE:2 = 2200
	SOURCE:13 += 5000
ELSE
	SOURCE:2 = 3300
	SOURCE:13 += 8000
ENDIF

;EXP:Ａ経験をみる
IF EXP:Ａ経験 < EXPLV:1
	TIMES SOURCE:2 , 0.10
	SOURCE:6 = 26000
ELSEIF EXP:Ａ経験 < EXPLV:2
	TIMES SOURCE:2 , 0.30
	SOURCE:6 = 13000
ELSEIF EXP:Ａ経験 < EXPLV:3
	TIMES SOURCE:2 , 0.50
	SOURCE:6 = 6500
ELSEIF EXP:Ａ経験 < EXPLV:4
	TIMES SOURCE:2 , 1.00
	SOURCE:6 = 2000
ELSEIF EXP:Ａ経験 < EXPLV:5
	TIMES SOURCE:2 , 1.40
	SOURCE:6 = 1000
ELSE
	TIMES SOURCE:2 , 1.60
	SOURCE:6 = 500
ENDIF

;PALAM:潤滑をみる
IF PALAM:3 < PALAMLV:1
	TIMES SOURCE:2 , 0.40
	SOURCE:6 += 10000
ELSEIF PALAM:3 < PALAMLV:2
	TIMES SOURCE:2 , 0.80
	SOURCE:6 += 3600
ELSEIF PALAM:3 < PALAMLV:3
	TIMES SOURCE:2 , 1.00
	SOURCE:6 += 1200
ELSEIF PALAM:3 < PALAMLV:4
	TIMES SOURCE:2 , 1.40
	SOURCE:6 += 200
ELSEIF PALAM:3 >= PALAMLV:4
	TIMES SOURCE:2 , 1.80
	SOURCE:6 += 100
ENDIF

;PALAM:欲情をみる
IF PALAM:5 < PALAMLV:1
	TIMES SOURCE:2 , 0.60
	TIMES SOURCE:13 , 0.60
ELSEIF PALAM:5 < PALAMLV:2
	TIMES SOURCE:2 , 0.80
	TIMES SOURCE:13 , 0.80
ELSEIF PALAM:5 < PALAMLV:3
	TIMES SOURCE:2 , 1.00
	TIMES SOURCE:13 , 1.00
ELSEIF PALAM:5 < PALAMLV:4
	TIMES SOURCE:2 , 1.20
	TIMES SOURCE:13 , 1.20
ELSEIF PALAM:5 >= PALAMLV:4
	TIMES SOURCE:2 , 1.40
	TIMES SOURCE:13 , 1.40
ENDIF

;小柄体形
SIF TALENT:小柄体型
	TIMES SOURCE:6 , 2.00

;Ａ敏感、鈍感をみる
;快Ａ自体のチェックは後でまとめてやる
IF TALENT:Ａ鈍感
	TIMES SOURCE:6 , 1.50
	TIMES SOURCE:13 , 1.50
	TIMES SOURCE:14 , 1.50
ELSEIF TALENT:Ａ敏感
	TIMES SOURCE:6 , 0.60
	TIMES SOURCE:13 , 0.60
	TIMES SOURCE:14 , 0.60
ENDIF

;処女で貞操観念
IF EXP:Ｖ経験 == 0 && TALENT:貞操観念
	SOURCE:13 /= 3
ENDIF

EXP:Ａ経験 += 25
PRINTL Ａ経験＋２５
EXP:Ａ拡張経験 += 2
PRINTL Ａ拡張経験＋２

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	PRINTS EXPNAME:40
	PRINTL +3
	EXP:レズ経験 += 3
ELSEIF TALENT:オトコ == 1 && TALENT:PLAYER:オトコ == 1
	PRINTS EXPNAME:41
	PRINTL +10
	EXP:ＢＬ経験 += 10
ENDIF

IF ASSIPLAY == 0
	IF ABL:Ａ感覚 >= 3
		TFLAG:30 += 2
	ELSE
		TFLAG:30 += 1
	ENDIF
ENDIF

RETURN 1

@TRAIN_MESSAGE_COM168
PRINTFORM %CALLNAME:PLAYER%は自分の角を%CALLNAME:TARGET%の
SIF ABL:Ａ感覚 >= 3
	PRINT 充分に開発された
PRINTFORMW アナルに限界まで挿し込んだ…

