@COM15
;二プルキャップ
IF TEQUIP:90
	PRINTL 触手乳首責め
	STR:0 = 触手乳首責め
ELSE
	;実行者が[淫具知識]を持っているなら効果増
	SIF TALENT:PLAYER:58
		PRINT 高性能
	PRINTL 二プルキャップ
	STR:0 = 二プルキャップ
ENDIF

LOSEBASE:0 += 20
LOSEBASE:1 += 70

SOURCE:12 = 120
SOURCE:14 = 70

;ABL:Ｂ感覚をみる
IF ABL:Ｂ感覚 == 0
	A = 100
ELSEIF ABL:Ｂ感覚 == 1
	A = 300
ELSEIF ABL:Ｂ感覚== 2
	A = 800
ELSEIF ABL:Ｂ感覚 == 3
	A = 1500
ELSEIF ABL:Ｂ感覚 == 4
	A = 2300
ELSE
	A = 1400 + (ABL:Ｂ感覚 * 300)
ENDIF

;助手が幼児退行
SIF TALENT:PLAYER:131
	TIMES SOURCE:17 , 1.20

;助手が幼稚
SIF TALENT:PLAYER:132
	TIMES SOURCE:17 , 1.20

IF TALENT:110
	TIMES A , 1.50
ENDIF
IF TALENT:108
	TIMES A , 1.50
ELSEIF TALENT:107
	TIMES A , 0.60
ENDIF

;実行者が[淫具知識]を持っているなら効果増
SIF TALENT:PLAYER:58
	TIMES A , 1.20

SOURCE:17 += A

IF TALENT:122 == 0 && TALENT:PLAYER:122 == 0
	PRINTS EXPNAME:40
	PRINTL +1
	EXP:40 += 1
ELSEIF TALENT:122 == 1 && TALENT:PLAYER:122 == 1
	PRINTS EXPNAME:41
	PRINTL +1
	EXP:41 += 1
ENDIF

CALL TRAIN_MESSAGE_B

;ニプルキャップの着脱
IF TEQUIP:90
	IF TEQUIP:15
		TEQUIP:15 = 0
	ELSE
		TEQUIP:15 = 1
		STAIN:5 |= 2
		STAIN:5 |= 4
	ENDIF
	T = 0
ELSE
	IF TEQUIP:15
		TEQUIP:15 = 0
	ELSE
		TEQUIP:15 = 1
	ENDIF
ENDIF

RETURN 1

@TRAIN_MESSAGE_COM15
;ニプルキャップの着脱
IF TEQUIP:90
	IF TEQUIP:15
		PRINTFORML 触手は%CALLNAME:TARGET%の乳首から離れた
	ELSE
		PRINTFORML 細かく蠢く触手が%CALLNAME:TARGET%の乳首を弄り始めた
	ENDIF
ELSE
	IF TEQUIP:15
		PRINTFORML %CALLNAME:TARGET%からクリップローターを取り外した
	ELSE
		PRINTFORML %CALLNAME:TARGET%の乳首に クリップローターを貼り付けた
	ENDIF
ENDIF

@EQUIP_COM15
;二プルキャップ装備中
IF TEQUIP:90
	PRINTL ＜触手乳首責め中＞
ELSE
	PRINTL ＜二プルキャップ装備中＞
ENDIF

LOSEBASE:0 += 5
LOSEBASE:1 += 20

;ABL:Ｂ感覚をみる
IF ABL:Ｂ感覚 == 0
	A = 40
ELSEIF ABL:Ｂ感覚 == 1
	A = 120
ELSEIF ABL:Ｂ感覚 == 2
	A = 250
ELSEIF ABL:Ｂ感覚 == 3
	A = 450
ELSEIF ABL:Ｂ感覚 == 4
	A = 600
ELSE
	A = 375 + (ABL:Ｂ感覚 * 750)
ENDIF

;PALAM:欲情をみる
IF PALAM:5 < PALAMLV:1
	TIMES A , 0.80
ELSEIF PALAM:5 < PALAMLV:2
	TIMES A , 0.90
ELSEIF PALAM:5 < PALAMLV:3
	TIMES A , 1.00
ELSEIF PALAM:5 < PALAMLV:4
	TIMES A , 1.10
ELSEIF PALAM:5 >= PALAMLV:4
	TIMES A , 1.20
ENDIF

;ABL:従順をみる
IF ABL:親密 == 0
	TIMES A , 0.80
ELSEIF ABL:親密 == 1
	TIMES A , 0.90
ELSEIF ABL:親密 == 2
	TIMES A , 1.00
ELSEIF ABL:親密 == 3
	TIMES A , 1.10
ELSEIF ABL:親密 == 4
	TIMES A , 1.20
ELSE
	A = A * (ABL:親密 * 5 + 105) / 100
ENDIF

;実行者が[淫具知識]を持っているなら効果増
SIF TALENT:PLAYER:58
	TIMES A , 1.20

SOURCE:17 += A
SOURCE:11 += 50
SOURCE:12 += 50

IF TALENT:122 == 0 && TALENT:PLAYER:122 == 0
	PRINTS EXPNAME:40
	PRINTL +1
	EXP:40 += 1
	EXP:PLAYER:40 += 1
ELSEIF TALENT:122 == 1 && TALENT:PLAYER:122 == 1
	PRINTS EXPNAME:41
	PRINTL +1
	EXP:41 += 1
	EXP:PLAYER:41 += 1
ENDIF

SIF TEQUIP:90
	T += 1

RETURN 1
