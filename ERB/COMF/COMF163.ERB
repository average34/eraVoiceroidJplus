@COM_ABLE163
;わかめ酒実行判定
;COM_ABLE共通判定
CALL COM_ABLE_COMMON, SELECTCOM
SIF RESULT == 0
	RETURN 0
;ウフフ中じゃないと無理
SIF TFLAG:44 == 0
	RETURN 0
;調教者が酒豪、若しくは調教対象が酒豪でないとダメ
SIF TALENT:TARGET:217 == 0 && TALENT:PLAYER:217 == 0
	RETURN 0
;性器が汚れているとMASTERの場合クンニはできない（調教者が汚れ無視か愛液、母乳のよごれならＯＫ）
SIF STAIN:3 & 14 && ASSIPLAY == 0 && TALENT:MASTER:64 == 0
	RETURN 0
;性器が汚れており、汚臭敏感で従順3以下のASSIの場合クンニはできない（調教者が汚れ無視か愛液、母乳のよごれならＯＫ）
IF STAIN:3 & 14 && ASSIPLAY
	SIF ABL:ASSI:親密 <= 3 && TALENT:ASSI:62 && TALENT:ASSI:64 == 0
		RETURN 0
ENDIF
;小柄体型の場合、巨乳以外だとダメ
SIF TALENT:100 && TALENT:110 == 0
	RETURN 0
;オトコだとダメ
SIF TALENT:122
	RETURN 0
;バイブまたは焼芋挿入中のときは技巧が4以上ないとダメ
SIF (TEQUIP:11 || TEQUIP:71) && ABL:PLAYER:技巧 < 4
	RETURN 0
;縄で緊縛中または対象の従順が10以上ないとダメ
SIF TEQUIP:44 && ABL:親密 < 10
	RETURN 0
;クスコ装着中はダメ
SIF TEQUIP:61
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:90
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
IF TEQUIP:58
	SIF ITEM:13 == 0 && NOITEM == 0
		RETURN 0
ENDIF
RETURN 1

@COM163
;わかめ酒
IF TEQUIP:71
	PRINTL 芋焼酎
	STR:0 = 芋焼酎
ELSE
	PRINTL わかめ酒
	STR:0 = わかめ酒
ENDIF

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------

LOSEBASE:0 += 20
LOSEBASE:1 += 60

W = RAND:3

SOURCE:10 = 5000
SOURCE:11 = 1000
SOURCE:12 = 500
SOURCE:13 = 500
SOURCE:14 = 1000

IF W == 1
	SOURCE:6 = 500
	TIMES SOURCE:13 , 1.30
ENDIF

IF TALENT:110 && W == 0
	;ABL:Ｂ感覚をみる
	IF ABL:Ｂ感覚 == 0
		SOURCE:17 = 75
	ELSEIF ABL:Ｂ感覚 == 1
		SOURCE:17 = 150
	ELSEIF ABL:Ｂ感覚 == 2
		SOURCE:17 = 450
	ELSEIF ABL:Ｂ感覚 == 3
		SOURCE:17 = 900
	ELSEIF ABL:Ｂ感覚 == 4
		SOURCE:17 = 1700
	ELSE
		SOURCE:17 = 2600
	ENDIF
	
	;Ｂ敏感、鈍感をみる
	SIF TALENT:107
		TIMES SOURCE:0 , 0.60
	SIF TALENT:108
		TIMES SOURCE:0 , 1.50
ELSE
	;ABL:Ｃ感覚をみる
	IF ABL:Ｃ感覚 == 0
		SOURCE:0 = 100
	ELSEIF ABL:Ｃ感覚 == 1
		SOURCE:0 = 250
	ELSEIF ABL:Ｃ感覚 == 2
		SOURCE:0 = 500
	ELSEIF ABL:Ｃ感覚 == 3
		SOURCE:0 = 1000
	ELSEIF ABL:Ｃ感覚 == 4
		SOURCE:0 = 1800
	ELSE
		SOURCE:0 = 2900
	ENDIF
	
	;Ｃ敏感、鈍感をみる
	SIF TALENT:101
		TIMES SOURCE:0 , 0.60
	SIF TALENT:102
		TIMES SOURCE:0 , 1.50
ENDIF

;PALAM:欲情をみる
IF PALAM:5 < PALAMLV:1
	TIMES SOURCE:11, 0.30
	TIMES SOURCE:13, 0.30
ELSEIF PALAM:5 < PALAMLV:2
	TIMES SOURCE:11, 0.60
	TIMES SOURCE:13, 0.60
ELSEIF PALAM:5 < PALAMLV:3
	TIMES SOURCE:11, 1.00
	TIMES SOURCE:13, 1.00
ELSEIF PALAM:5 < PALAMLV:4
	TIMES SOURCE:11, 1.30
	TIMES SOURCE:13, 1.30
ELSEIF PALAM:5 >= PALAMLV:4
	TIMES SOURCE:11, 1.60
	TIMES SOURCE:13, 1.60
ENDIF

;自尊心
SIF TALENT:15
	SOURCE:13 /= 2
;保守的
SIF TALENT:24
	SOURCE:13 /= 3
;薬毒耐性
SIF TALENT:24
	SOURCE:11 /= 4

CALL TRAIN_MESSAGE_B

;汚れ移動
IF TALENT:110 && W == 0
	
ELSE
	;奴隷のＶ⇔調教者の口の汚れが移動
	STAIN:3 |= STAIN:PLAYER:0
	STAIN:PLAYER:0 |= STAIN:3
ENDIF

IF TALENT:122 == 0 && TALENT:PLAYER:122 == 0
	PRINTS EXPNAME:40
	PRINTL +1
	EXP:40 += 1
ELSEIF TALENT:122 == 1 && TALENT:PLAYER:122 == 1
	PRINTS EXPNAME:41
	PRINTL +1
	EXP:41 += 1
ENDIF

RETURN 1

@TRAIN_MESSAGE_COM163
IF TALENT:110 && W == 0
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%に豊かな胸を
	PRINTFORML 抱き寄せるように命令すると、胸の窪みに酒を注ぎこんだ
	PRINTFORMW 酒の刺激なのか%CALLNAME:TARGET%の顔は仄かに紅みを帯びている……
	PRINT 　
	PRINT 　
	PRINTFORM 十分酒が馴染んだ頃、%CALLNAME:PLAYER%は
	IF (TALENT:130 && EX:5 > 0) && PALAM:3 >= PALAMLV:5
		PRINTFORML %CALLNAME:TARGET%の唾液と母乳が混ざった酒を
	ELSEIF (TALENT:130 && EX:5 > 0)
		PRINTFORML 母乳が混ざり、マイルドな味になった酒を
	ELSEIF PALAM:3 >= PALAMLV:5
		PRINTFORML %CALLNAME:TARGET%の唾液がたっぷり混ざった酒を
	ELSE
		PRINTFORML %CALLNAME:TARGET%の両乳房で作られた窪みに注がれている酒を
	ENDIF
	IF TALENT:PLAYER:217
		PRINTFORML 待っていましたと言わんばかりに飲み干した。
		PRINTFORML そしてもったいないと言いながら%CALLNAME:TARGET%の乳房に付いている
		PRINTFORMW 僅かな酒を根こそぎ舐めとった……
	ELSE
		PRINTFORMW 一気に飲み干した。
	ENDIF
ELSE
	PRINTFORM %CALLNAME:PLAYER%は
	IF W == 1
		PRINTFORML %CALLNAME:TARGET%を正座させ、両足を閉じさせると
	ELSE
		PRINTFORML %CALLNAME:TARGET%の両足を閉じさせると
	ENDIF
	PRINTFORML 太ももと下腹部とで作られた窪みに酒を注ぎこんだ
	PRINTFORMW 酒の刺激なのか%CALLNAME:TARGET%の顔は仄かに紅みを帯びている……
	PRINT 　
	PRINT 　
	PRINTFORM 十分酒が馴染んだ頃、%CALLNAME:PLAYER%は
	IF TEQUIP:71 && PALAM:3 >= PALAMLV:5
		PRINTFORML %CALLNAME:TARGET%の愛液と芋の香りが混ざった酒を
	ELSEIF TEQUIP:71
		PRINTFORML 芋のよい香りが移った酒を
	ELSEIF PALAM:3 >= PALAMLV:5
		PRINTFORML %CALLNAME:TARGET%の愛液がたっぷり混ざった酒を
	ELSE
		PRINTFORML %CALLNAME:TARGET%の両足とデルタ地帯にできた窪みに注がれている酒を
	ENDIF
	IF TALENT:PLAYER:217
		PRINTFORML 待っていましたと言わんばかりに飲み干した。
		PRINTFORML そしてもったいないと言いながら%CALLNAME:TARGET%の太ももに残った
		PRINTFORMW 僅かな酒をも逃すまいと舐め始めた……
	ELSE
		PRINTFORMW 一気に飲み干した。
	ENDIF
ENDIF

