@COM_ABLE307
;櫛で梳かす実行判定
;COM_ABLE共通判定
CALL COM_ABLE_COMMON, SELECTCOM
SIF RESULT == 0
	RETURN 0
;ウフフ中は無理
SIF TFLAG:44 == 1
	RETURN 0
;アイテムを持っているか、アイテムなし設定でないとダメ
SIF ITEM:櫛 == 0 && NOITEM == 0
	RETURN 0
;親密3以上or好感度300以上or恋人が必要
SIF ABL:親密 < 3 && CFLAG:2 < 300 && TALENT:恋人 == 0
	RETURN 0
RETURN 1

@TRAIN_MESSAGE_COM307
;櫛で梳かす
;主導権で分岐
IF TFLAG:45
	PRINTFORMW %CALLNAME:TARGET%は自分の髪を%CALLNAME:PLAYER%に櫛で梳かさせた…
ELSE
	PRINTFORMW %CALLNAME:PLAYER%は%CALLNAME:TARGET%の髪を櫛で梳かした…
ENDIF

IF TFLAG:18 == 1
	PRINTFORMW %CALLNAME:TARGET%は気持ちよさそうにしている…
ELSEIF TFLAG:18 == -1
	PRINTFORML %CALLNAME:TARGET%の髪に櫛を入れていると手応えがあった…
	PRINTFORM %CALLNAME:TARGET%は目尻に涙を浮かべ
	;主導権基準値で分岐
	T = TARGET
	CALL GET_INITIATIVE_RATE
	;強気なキャラ
	IF R > 0
		PRINTFORMW %CALLNAME:PLAYER%を睨んでいる…
	ELSE
		PRINTFORMW %CALLNAME:PLAYER%の様子を窺っている…
	ENDIF
ENDIF

@COM307
;櫛で梳かす

PRINTL 櫛で梳かす
CALL GET_SUCCESS_RATE
B = 95 + A / 10
SIF B > 99
	B = 99
A = RAND:100
IF A <= 9
	;10％で大成功
	TFLAG:18 = 1
ELSEIF A >= B
	;10～1％で失敗
	TFLAG:18 = -1
ELSE
	;残りは成功
	TFLAG:18 = 0
ENDIF

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL COM_ORDER_COMMON
;判定値がバカみたいに高いとランダムで髪梳きに派生
IF 200 < A && RAND:3 == 0
	;主導権で分岐
	IF TFLAG:45
		PRINTFORMW %CALLNAME:TARGET%に持っていた櫛を取り上げられてしまった…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は何か言いたそうに%CALLNAME:PLAYER%を見つめている…
	ENDIF
	PRINTL  
	PRINTL 髪梳き
	CALL COM306B
	RETURN 1
;実行できない場合
ELSEIF RESULT == 0
	PRINTW 髪に触ろうとした手を払われてしまった…
	RETURN 0
;実行できる場合
ELSE
	CALL COM307B
	RETURN 1
ENDIF

@COM307B
SELECTCOM = 307
STR:0 = 櫛で梳かす
CALL TRAIN_MESSAGE_B

LOSEBASE:気力 += 60

;固定で獲得するソース
SOURCE:歓楽 = 0
SOURCE:接触 = 200

;ABL:従順をみる
IF ABL:親密 <= 1
	SOURCE:歓楽 += (ABL:親密 * 20)
ELSEIF ABL:親密 <= 3
	SOURCE:歓楽 += 100 + (ABL:親密 * 20)
ELSEIF ABL:親密 <= 5
	SOURCE:歓楽 += 200 + (ABL:親密 * 25)
ELSEIF ABL:親密 <= 8
	SOURCE:歓楽 += 300 + (ABL:親密 * 25)
ELSEIF ABL:親密 <= 11
	SOURCE:歓楽 += 500 + (ABL:親密 * 30)
ELSE
	SOURCE:歓楽 += 1000 + (ABL:親密 * 25)
ENDIF

;プレイヤーの技巧を見る
;櫛補正で+3する
A = ABL:PLAYER:技巧 + 3
IF A <= 1
	SOURCE:恭順追加 = (A * 65)
ELSEIF A <= 3
	SOURCE:恭順追加 = 200 + (A * 70)
ELSEIF A <= 5
	SOURCE:恭順追加 = 500 + (A * 70)
ELSEIF A <= 8
	SOURCE:恭順追加 = 700 + (A * 80)
ELSEIF A <= 11
	SOURCE:恭順追加 = 1000 + (A * 85)
ELSE
	SOURCE:恭順追加 = 3000 + (A * 60)
ENDIF

;好感度をみる
IF CFLAG:2 <= 500
	SOURCE:歓楽 += CFLAG:2
ELSEIF CFLAG:2 <= 5000
	SOURCE:歓楽 += 500 + (CFLAG:2 - 500) / 3
ELSE
	SOURCE:歓楽 += 2000 + (CFLAG:2 - 5000) / 5
ENDIF
SIF SOURCE:歓楽 < 0
	SOURCE:歓楽 = 0

;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:従順をみる
	IF ABL:親密 <= 1
		SOURCE:受動 += 100 + (ABL:親密 * 20)
	ELSEIF ABL:親密 <= 3
		SOURCE:受動 += 200 + (ABL:親密 * 30)
	ELSEIF ABL:親密 <= 5
		SOURCE:受動 += 300 + (ABL:親密 * 50)
	ELSEIF ABL:親密 <= 8
		SOURCE:受動 += 500 + (ABL:親密 * 80)
	ELSEIF ABL:親密 <= 11
		SOURCE:受動 += 800 + (ABL:親密 * 100)
	ELSE
		SOURCE:受動 += 1000 + (ABL:親密 * 120)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:サドっ気 == 0
		SOURCE:征服 = 800
	ELSEIF ABL:サドっ気 == 1
		SOURCE:征服 = 1000
	ELSEIF ABL:サドっ気 == 2
		SOURCE:征服 = 1500
	ELSEIF ABL:サドっ気 == 3
		SOURCE:征服 = 2000
	ELSEIF ABL:サドっ気 == 4
		SOURCE:征服 = 2500
	ELSE
		SOURCE:征服 = 1000 + (ABL:サドっ気 * 400)
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:6 - 1
TFLAG:30 += A

IF TFLAG:18 == -1
	TIMES SOURCE:歓楽 , 0.10
	TIMES SOURCE:恭順追加 , 0.10
	TIMES SOURCE:征服 , 0.50
	TIMES SOURCE:受動 , 0.50
	TFLAG:30 -= 10
	TFLAG:37 -= 5
ELSEIF TFLAG:18 == 0
	TIMES SOURCE:歓楽 , 1.00
	TIMES SOURCE:恭順追加 , 1.00
	TIMES SOURCE:征服 , 1.00
	TIMES SOURCE:受動 , 1.00
	TFLAG:37 += A
	;恋慕または恋人ありで愛情経験追加
	IF TALENT:恋慕 || TALENT:恋人
		EXP:愛情経験 += 1
		PRINTFORML %EXPNAME:23%+1
	ENDIF
ELSE
	TIMES SOURCE:歓楽 , 2.00
	TIMES SOURCE:恭順追加 , 2.00
	TIMES SOURCE:征服 , 2.00
	TIMES SOURCE:受動 , 2.00
	TFLAG:30 += 2
	TFLAG:37 += 5 + A
	;愛情経験追加
	IF TALENT:恋慕 || TALENT:恋人
		EXP:愛情経験 += 3
		PRINTFORML %EXPNAME:23%+3
	ELSE
		EXP:愛情経験 += 1
		PRINTFORML %EXPNAME:23%+1
	ENDIF
ENDIF


RETURN 1

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_307
A = 0
S = 0
V = 20

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;男嫌い
IF TALENT:男嫌い && TALENT:PLAYER:オトコ
	A -= 7
	S = 1
ENDIF

;恋慕
IF TALENT:恋慕 && ASSIPLAY == 0
	A += 5
	S = 5
ENDIF

;デート中
IF TEQUIP:2
	A += 3
	S = 1
ENDIF

;ムードボーナス
B = 0
IF TFLAG:37 < 10
	B -= 10
ELSEIF TFLAG:37 < 30
	B -= 1
ELSEIF TFLAG:37 < 50
	B += 1
ELSEIF TFLAG:37 < 100
	B += TFLAG:37 / 10
ELSEIF TFLAG:37 < 300
	B += TFLAG:37 / 5
ELSE
	B += TFLAG:37 / 4
ENDIF
A += B
IF B < 0
	B = B * -1
	S = 1
ELSE
	S = 1
ENDIF

;好感度
IF CFLAG:2 < 300
	B = 0
	IF CFLAG:2 < 0
		B = 20
	ELSEIF CFLAG:2 < 100
		B = 10
	ELSE
		B = 5
	ENDIF
	A -= B
	S = 1
ENDIF

;好感度
IF CFLAG:2 >= 400
	B = 0
	B = CFLAG:2 / 30
	A += B
	S = 1
ENDIF

IF ((FLAG:23 & 1p27) == 0) && FLAG:2 != 1
	;男嫌い
	IF TALENT:男嫌い && TALENT:PLAYER:オトコ
		PRINT  - 
		PRINTS TALENTNAME:82
		PRINTV '(,7,')
		S = 1
	ENDIF

	;恋慕
	IF TALENT:恋慕 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:85
		PRINTV '(,5,')
		S = 5
	ENDIF

	;デート中
	IF TEQUIP:2
		PRINT  + 
		PRINT デート中
		PRINTV '(,3,')
		S = 1
	ENDIF

	;ムードボーナス
	B = 0
	IF TFLAG:37 < 10
		B -= 10
	ELSEIF TFLAG:37 < 30
		B -= 1
	ELSEIF TFLAG:37 < 50
		B += 1
	ELSEIF TFLAG:37 < 100
		B += TFLAG:37 / 10
	ELSEIF TFLAG:37 < 300
		B += TFLAG:37 / 5
	ELSE
		B += TFLAG:37 / 4
	ENDIF
	IF B < 0
		B = B * -1
		PRINT  - 
		PRINT ムード悪い
		PRINTV '(,B,')
		S = 1
	ELSE
		PRINT  + 
		PRINT ムード良い
		PRINTV '(,B,')
		S = 1
	ENDIF

	;好感度
	IF CFLAG:2 < 300
		B = 0
		PRINT  - 
		IF CFLAG:2 < 0
			B = 20
		ELSEIF CFLAG:2 < 100
			B = 10
		ELSE
			B = 5
		ENDIF
		PRINT 好感度不足
		PRINTV '(,B,')
		S = 1
	ENDIF

	;好感度
	IF CFLAG:2 >= 400
		B = 0
		SIF S
			PRINT  + 
		B = CFLAG:2 / 30
		PRINT 好感度
		PRINTV '(,B,')
		S = 1
	ENDIF

	;合計を表示(20以上で実行)
	PRINT  = 
	PRINTV A

	;同性の方が抵抗少ないような気もするが、とりあえず保留
	;SIF TALENT:MASTER:オトコ && TALENT:オトコ == 0
	;	V += 5
	IF A < V
		PRINT  < 
		PRINT 実行値
		PRINTV V
	ELSEIF A == V
		PRINT  = 
		PRINT 実行値
		PRINTV V
	ELSEIF A > V
		PRINT  > 
		PRINT 実行値
		PRINTV V
	ENDIF
	WAIT
ENDIF

