@COM_ABLE372
;着衣尻愛撫実行判定
;COM_ABLE共通判定
CALL COM_ABLE_COMMON, SELECTCOM
SIF RESULT == 0
	RETURN 0
;ウフフ中は無理
SIF TFLAG:44 == 1
	RETURN 0
;デート中は淫乱じゃないと無理
SIF TEQUIP:2 && TALENT:76 == 0
	RETURN 0
;欲望5以上かつ、従順7以上or好感度700以上or恋人が必要
SIF ABL:1 < 5 || (ABL:0 < 7 && CFLAG:2 < 700 && TALENT:153 == 0)
	RETURN 0
;潤滑不足で、従順3以下・レズっ気3以下のASSIの場合尻愛撫はできない（サドならＯＫ）
IF PALAM:3 < PALAMLV:2 && ASSIPLAY
	SIF (ABL:ASSI:0 <= 3 || ABL:ASSI:9 <= 3) && TALENT:ASSI:83 == 0
		RETURN 0
ENDIF
RETURN 1

@COM372
;尻愛撫
PRINTL 尻愛撫
CALL GET_SUCCESS_RATE
B = 90 + A / 10
SIF B > 99
	B = 99
A = RAND:100
IF A <= 9
	;10％で大成功
	TFLAG:18 = 1
ELSEIF A >= B
	;10～1％で失敗
	TFLAG:18 = -1
ELSE
	;残りは成功
	TFLAG:18 = 0
ENDIF

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

STR:0 = 尻愛撫
CALL TRAIN_MESSAGE_B

LOSEBASE:1 += 100

;固定で獲得するソース
SOURCE:18 = 50
SOURCE:9 = 1000

;ABL:従順をみる
IF ABL:0 <= 1
	SOURCE:18 += (ABL:0 * 20)
	SOURCE:9 += (ABL:0 * 100)
ELSEIF ABL:0 <= 3
	SOURCE:18 += 100 + (ABL:0 * 20)
	SOURCE:9 += 200 + (ABL:0 * 105)
ELSEIF ABL:0 <= 5
	SOURCE:18 += 200 + (ABL:0 * 20)
	SOURCE:9 += 500 + (ABL:0 * 110)
ELSEIF ABL:0 <= 8
	SOURCE:18 += 300 + (ABL:0 * 30)
	SOURCE:9 += 700 + (ABL:0 * 120)
ELSEIF ABL:0 <= 11
	SOURCE:18 += 400 + (ABL:0 * 30)
	SOURCE:9 += 1200 + (ABL:0 * 135)
ELSE
	SOURCE:18 += 800 + (ABL:0 * 15)
	SOURCE:9 += 2000 + (ABL:0 * 90)
ENDIF

;好感度をみる
IF CFLAG:2 <= 500
	SOURCE:18 += CFLAG:2
ELSEIF CFLAG:2 <= 5000
	SOURCE:18 += 500 + (CFLAG:2 - 500) / 3
ELSE
	SOURCE:18 += 2000 + (CFLAG:2 - 5000) / 5
ENDIF
SIF SOURCE:18 < 0
	SOURCE:18 = 0

;ABL:Ａ感覚をみる
IF ABL:5 == 0
	SOURCE:2 = 20
	SOURCE:3 = 50
	SOURCE:13 = 100
ELSEIF ABL:5 == 1
	SOURCE:2 = 75
	SOURCE:3 = 100
	SOURCE:13 = 110
ELSEIF ABL:5 == 2
	SOURCE:2 = 300
	SOURCE:3 = 160
	SOURCE:13 = 130
ELSEIF ABL:5 == 3
	SOURCE:2 = 700
	SOURCE:3 = 200
	SOURCE:13 = 170
ELSEIF ABL:5 == 4
	SOURCE:2 = 1100
	SOURCE:3 = 230
	SOURCE:13 = 250
ELSE
	SOURCE:2 = 500 + (ABL:5 * 200)
	SOURCE:3 = 200 + (ABL:14 * 10)
	SOURCE:13 = 250 + (ABL:5 * 50)
ENDIF

;TALENT:恋慕をみる
SIF TALENT:85
	TIMES SOURCE:3 , 1.50
;TALENT:恋人をみる
SIF TALENT:153
	TIMES SOURCE:3 , 1.50
;TALENT:親愛をみる
SIF TALENT:88
	TIMES SOURCE:3 , 2.00


;着衣補正
TIMES SOURCE:2 , 0.60
TIMES SOURCE:3 , 0.80
TIMES SOURCE:13 , 0.60

;EXP:Ａ経験をみる
IF EXP:1 < EXPLV:1
	TIMES SOURCE:2 , 0.20
	TIMES SOURCE:13, 0.20
	;SOURCE:6 = 50
ELSEIF EXP:1 < EXPLV:2
	TIMES SOURCE:2 , 0.50
	TIMES SOURCE:13, 0.50
	;SOURCE:6 = 40
ELSEIF EXP:1 < EXPLV:3
	TIMES SOURCE:2 , 1.00
	TIMES SOURCE:13, 1.00
	;SOURCE:6 = 30
ELSEIF EXP:1 < EXPLV:4
	TIMES SOURCE:2 , 1.20
	TIMES SOURCE:13, 1.20
	;SOURCE:6 = 20
ELSEIF EXP:1 < EXPLV:5
	TIMES SOURCE:2 , 1.60
	TIMES SOURCE:13, 1.60
	;SOURCE:6 = 10
ELSE
	TIMES SOURCE:2 , 1.80
	TIMES SOURCE:13, 1.80
	;SOURCE:6 = 5
ENDIF

;PALAM:欲情をみる
IF PALAM:5 < PALAMLV:1
	TIMES SOURCE:2 , 0.30
	TIMES SOURCE:13, 0.30
ELSEIF PALAM:5 < PALAMLV:2
	TIMES SOURCE:2 , 0.60
	TIMES SOURCE:13, 0.60
ELSEIF PALAM:5 < PALAMLV:3
	TIMES SOURCE:2 , 1.00
	TIMES SOURCE:13, 1.00
ELSEIF PALAM:5 < PALAMLV:4
	TIMES SOURCE:2 , 1.10
	TIMES SOURCE:13, 1.10
ELSEIF PALAM:5 >= PALAMLV:4
	TIMES SOURCE:2 , 1.20
	TIMES SOURCE:13, 1.20
ENDIF


;Ａ敏感、鈍感をみる
;快Ａ自体のチェックは後でまとめてやる
IF TALENT:105
	;TIMES SOURCE:6 , 1.50
	TIMES SOURCE:13 , 1.50
ELSEIF TALENT:106
	;TIMES SOURCE:6 , 0.60
	TIMES SOURCE:13 , 0.60
ENDIF

;処女で貞操観念
IF EXP:0 == 0 && TALENT:30
	SOURCE:13 /= 3
ENDIF

;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:従順をみる
	IF ABL:0 <= 1
		SOURCE:21 += 300 + (ABL:0 * 50)
	ELSEIF ABL:0 <= 3
		SOURCE:21 += 600 + (ABL:0 * 100)
	ELSEIF ABL:0 <= 5
		SOURCE:21 += 800 + (ABL:0 * 120)
	ELSEIF ABL:0 <= 8
		SOURCE:21 += 1000 + (ABL:0 * 140)
	ELSEIF ABL:0 <= 11
		SOURCE:21 += 1200 + (ABL:0 * 160)
	ELSE
		SOURCE:21 += 1500 + (ABL:0 * 180)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:15 == 0
		SOURCE:20 = 600
	ELSEIF ABL:15 == 1
		SOURCE:20 = 900
	ELSEIF ABL:15 == 2
		SOURCE:20 = 1200
	ELSEIF ABL:15 == 3
		SOURCE:20 = 1600
	ELSEIF ABL:15 == 4
		SOURCE:20 = 2000
	ELSE
		SOURCE:20 = 1000 + (ABL:15 * 300)
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:10 + 5
;合意がないと好感度が下がる
IF TALENT:152 == 0 && TFLAG:48 == 0
	A -= 10
	SOURCE:15 += 500
ENDIF
TFLAG:30 += A
TFLAG:37 += A / 2

;レズ経験
IF TALENT:122 == 0 && TALENT:PLAYER:122 == 0
	PRINTS EXPNAME:40
	PRINTL +2
	EXP:40 += 2
	EXP:PLAYER:40 += 2
ELSEIF TALENT:122 == 1 && TALENT:PLAYER:122 == 1
	PRINTS EXPNAME:41
	PRINTL +5
	EXP:41 += 5
	EXP:PLAYER:41 += 5
ENDIF

IF TFLAG:18 == -1
	TIMES SOURCE:18 , 0.10
	TIMES SOURCE:9 , 0.50
	TIMES SOURCE:20 , 0.50
	TIMES SOURCE:21 , 0.50
	TFLAG:30 -= 10
	TFLAG:37 -= 5
ELSEIF TFLAG:18 == 0
	TIMES SOURCE:18 , 1.00
	TIMES SOURCE:9 , 1.00
	TIMES SOURCE:20 , 1.00
	TIMES SOURCE:21 , 1.00
	TFLAG:37 += A
ELSE
	TIMES SOURCE:18 , 2.00
	TIMES SOURCE:9 , 2.00
	TIMES SOURCE:20 , 2.00
	TIMES SOURCE:21 , 2.00
	TFLAG:30 += 2
	TFLAG:37 += 5 + A
	;大成功時はＡ経験増加
	EXP:1 += 1
	PRINTL Ａ経験＋１
ENDIF

;既成事実１に相当
TFLAG:200 = 1

RETURN 1

;-------------------------------------------------
;地の文
;-------------------------------------------------
@TRAIN_MESSAGE_COM372
PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%の
IF TFLAG:18 == 1
	PRINTFORML 菊穴の辺りを指でまさぐりながら
ENDIF
;100,小柄体型
IF TALENT:100
	PRINT 小振りな
;149,小人体型
ELSEIF TALENT:149
	PRINT 小さな
ENDIF
IF PALAM:5 < 10000
	PRINTW 尻を軽く撫で回した…
ELSEIF PALAM:5 < 30000
	PRINTW 尻を撫で回した…
ELSE
	PRINTW 尻を揉みしだいた…
ENDIF
IF TFLAG:18 == 1
	IF PALAM:5 >= 30000
		PRINTFORML %CALLNAME:TARGET%は何やら物足りなさそうにしている…
	ELSE
		PRINTFORML %CALLNAME:TARGET%は頬を赤く染めている…
	ENDIF
ELSEIF TFLAG:18 == -1
	PRINTFORML ……%CALLNAME:TARGET%は痛がっている。どうやら力を入れすぎたらしい…
ENDIF

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_372
A = 0
S = 0
V = 110

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;ABL:欲望
IF ABL:1
	A += ABL:1 * 3
ENDIF
;ABL:A感覚
IF ABL:5
	A += ABL:5 * 2
ENDIF
;ABL:奉仕精神
IF ABL:6
	A += ABL:6 * 4
ENDIF

;PALAM:欲情
IF PALAM:5 < PALAMLV:1
	L = 0
ELSEIF PALAM:5 < PALAMLV:2
	L = 1
ELSEIF PALAM:5 < PALAMLV:3
	L = 2
ELSEIF PALAM:5 < PALAMLV:4
	L = 3
ELSEIF PALAM:5 < PALAMLV:5
	L = 4
ELSE
	L = 5
ENDIF
IF L
	A += L * 3
ENDIF

;恥じらい
IF TALENT:35
	A -= 2
ENDIF
;快感の否定
IF TALENT:71
	A -= 5
ENDIF
;男嫌い
IF TALENT:82 && TALENT:PLAYER:122
	A -= 12
ENDIF
;恋慕
IF TALENT:85 && ASSIPLAY == 0
	A += 5
	S = 5
ENDIF

;媚薬
IF TEQUIP:21
	A += 6
ENDIF

;好感度
IF CFLAG:2 < 3000
	B = 0
	IF CFLAG:2 < 1000
		B = 50
	ELSEIF CFLAG:2 < 2000
		B = 20
	ELSEIF CFLAG:2 < 2500
		B = 10
	ELSE
		B = 5
	ENDIF
	A -= B
ENDIF
;好感度
IF CFLAG:2 >= 4000
	B = 0
	IF CFLAG:2 < 5000
		B = 5
	ELSE
		B = CFLAG:2 / 300
	ENDIF
	A += B
ENDIF

IF ((FLAG:23 & 1p27) == 0) && FLAG:2 != 1
	;ABL:欲望
	IF ABL:1
		SIF S
			PRINT  + 
		PRINTS ABLNAME:1
		PRINTV 'LV,ABL:1,'(,ABL:1 * 3,')
		S = 1
	ENDIF
	;ABL:A感覚
	IF ABL:5
		SIF S
			PRINT  + 
		PRINTS ABLNAME:5
		PRINTV 'LV,ABL:5,'(,ABL:5 * 2,')
		S = 1
	ENDIF
	;ABL:奉仕精神
	IF ABL:6
		SIF S
			PRINT  + 
		PRINTS ABLNAME:6
		PRINTV 'LV,ABL:6,'(,ABL:6 * 4,')
		S = 1
	ENDIF
	IF MARK:1
		SIF S
			PRINT  + 
		PRINTS MARKNAME:1
		PRINTV 'LV,MARK:1,'(,MARK:1 * 3,')
		S = 1
	ENDIF
	;PALAM:欲情
	IF PALAM:5 < PALAMLV:1
		L = 0
	ELSEIF PALAM:5 < PALAMLV:2
		L = 1
	ELSEIF PALAM:5 < PALAMLV:3
		L = 2
	ELSEIF PALAM:5 < PALAMLV:4
		L = 3
	ELSEIF PALAM:5 < PALAMLV:5
		L = 4
	ELSE
		L = 5
	ENDIF
	IF L
		SIF S
			PRINT  + 
		PRINTS PALAMNAME:5
		PRINTV 'LV,L,'(,L * 3,')
		S = 1
	ENDIF

	;恥じらい
	IF TALENT:35
		PRINT  - 
		PRINTS TALENTNAME:35
		PRINTV '(,2,')
		S = 1
	ENDIF
	;快感の否定
	IF TALENT:71
		PRINT  - 
		PRINTS TALENTNAME:71
		PRINTV '(,5,')
		S = 1
	ENDIF
	;男嫌い
	IF TALENT:82 && TALENT:PLAYER:122
		PRINT  - 
		PRINTS TALENTNAME:82
		PRINTV '(,12,')
		S = 1
	ENDIF
	;恋慕
	IF TALENT:85 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:85
		PRINTV '(,5,')
		S = 5
	ENDIF

	;媚薬
	IF TEQUIP:21
		SIF S
			PRINT  + 
		PRINTS ITEMNAME:26
		PRINTV '(,6,')
		S = 1
	ENDIF

	;好感度
	IF CFLAG:2 < 3000
		B = 0
		PRINT  - 
		IF CFLAG:2 < 1000
			B = 50
		ELSEIF CFLAG:2 < 2000
			B = 20
		ELSEIF CFLAG:2 < 2500
			B = 10
		ELSE
			B = 5
		ENDIF
		PRINT 好感度不足
		PRINTV '(,B,')
		S = 1
	ENDIF
	;好感度
	IF CFLAG:2 >= 4000
		B = 0
		SIF S
			PRINT  + 
		IF CFLAG:2 < 5000
			B = 5
		ELSE
			B = CFLAG:2 / 300
		ENDIF
		PRINT 好感度
		PRINTV '(,B,')
		S = 1
	ENDIF

	;合計を表示(110以上で実行)
	PRINT  = 
	PRINTV A

	SIF A < V
		PRINT  < 
	SIF A == V
		PRINT  = 
	SIF A > V
		PRINT  > 
	PRINT 実行値
	PRINTV V
	WAIT
ENDIF

