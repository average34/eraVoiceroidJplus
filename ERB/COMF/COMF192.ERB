@COM_ABLE192
;パイズリする実行判定
;COM_ABLE共通判定
CALL COM_ABLE_COMMON, SELECTCOM
SIF RESULT == 0
	RETURN 0
;ウフフ中じゃないと無理
SIF TFLAG:44 == 0
	RETURN 0
;性器が汚れているとMASTERの場合パイズリするはできない（調教者が汚れ無視か愛液、Ｐ、母乳のよごれならＯＫ）
SIF (STAIN:ペニス >= 4) && ((STAIN:ペニス != 18) && (STAIN:ペニス != 19)) && (ASSIPLAY == 0) && (TALENT:MASTER:汚れ無視 == 0)
	RETURN 0
;性器が汚れており、汚臭敏感で従順3以下のASSIの場合パイズリはできない（調教者が汚れ無視か愛液、Ｐ、母乳のよごれならＯＫ）
IF (STAIN:ペニス >= 4) && ((STAIN:ペニス != 18) && (STAIN:ペニス != 19)) && ASSIPLAY
	SIF ABL:ASSI:親密 <= 3 && TALENT:ASSI:汚臭敏感 && TALENT:ASSI:汚れ無視 == 0
		RETURN 0
ENDIF
;[絶壁]か[オトコ]だと不可
SIF TALENT:PLAYER:140 || TALENT:PLAYER:オトコ
	RETURN 0
;[爆乳]なら技巧LV1以上、[巨乳]でなければ技巧3以上、[貧乳]だと技巧4以上必要
SIF ((TALENT:PLAYER:141 == 1) && (ABL:技巧 == 0)) || ((TALENT:PLAYER:141 == 0) && (TALENT:PLAYER:巨乳 == 1) && (ABL:技巧 < 2)) || ((TALENT:PLAYER:141 == 0) && (TALENT:PLAYER:巨乳 == 0) && (ABL:技巧 < 3)) || (TALENT:PLAYER:貧乳 && (ABL:技巧 < 4))
	RETURN 0
;オトコかふたなりじゃないとダメ
SIF (TALENT:ふたなり || TALENT:オトコ) == 0
	RETURN 0
;オナホール使用中はダメ
SIF TEQUIP:17
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:90
	RETURN 0
RETURN 1

@COM192
;パイズリする


PRINTL パイズリする
STR:0 = パイズリする
CALL TRAIN_MESSAGE_B

LOSEBASE:体力 += 50
LOSEBASE:気力 += 180

SOURCE:情愛 = 1000
SOURCE:達成感 = 1000
SOURCE:露出 = 220
SOURCE:逸脱 = 50

;ABL:Ｃ感覚をみる
IF ABL:Ｃ感覚 == 0
	SOURCE:快Ｃ = 1000
	SOURCE:中毒充足 = 800
ELSEIF ABL:Ｃ感覚 == 1
	SOURCE:快Ｃ = 1600
	SOURCE:中毒充足 = 1000
ELSEIF ABL:Ｃ感覚 == 2
	SOURCE:快Ｃ = 2200
	SOURCE:中毒充足 = 1300
ELSEIF ABL:Ｃ感覚 == 3
	SOURCE:快Ｃ = 2700
	SOURCE:中毒充足 = 1700
ELSEIF ABL:Ｃ感覚 == 4
	SOURCE:快Ｃ = 3300
	SOURCE:中毒充足 = 2200
ELSE
	SOURCE:快Ｃ = 1500 + ABL:Ｃ感覚 * 500
	SOURCE:中毒充足 = 2800
ENDIF

;奴隷のＰ⇔調教者の口の汚れが移動
STAIN:ペニス |= STAIN:PLAYER:口
STAIN:PLAYER:口 |= STAIN:ペニス


IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	PRINTS EXPNAME:40
	PRINTL +1
	EXP:レズ経験 += 1
	EXP:PLAYER:レズ経験 += 1
ELSEIF TALENT:オトコ == 1 && TALENT:PLAYER:オトコ == 1
	PRINTS EXPNAME:41
	PRINTL +1
	EXP:ＢＬ経験 += 1
	EXP:PLAYER:ＢＬ経験 += 1
ENDIF

;プレイヤーのABL:技巧をみる
IF ABL:PLAYER:技巧 == 0
	TIMES SOURCE:快Ｃ , 0.50
	TIMES SOURCE:中毒充足 , 0.50
ELSEIF ABL:PLAYER:技巧 == 1
	TIMES SOURCE:快Ｃ , 0.80
	TIMES SOURCE:中毒充足 , 0.80
ELSEIF ABL:PLAYER:技巧 == 2
	TIMES SOURCE:快Ｃ , 1.00
	TIMES SOURCE:中毒充足 , 1.00
ELSEIF ABL:PLAYER:技巧 == 3
	TIMES SOURCE:快Ｃ , 1.20
	TIMES SOURCE:中毒充足 , 1.50
ELSEIF ABL:PLAYER:技巧 == 4
	TIMES SOURCE:快Ｃ , 1.50
	TIMES SOURCE:中毒充足 , 2.50
ELSE
	SOURCE:快Ｃ = SOURCE:快Ｃ * (ABL:PLAYER:技巧 * 75 + 25) / 200
	SOURCE:中毒充足 = SOURCE:中毒充足 * (ABL:PLAYER:技巧 * 75 + 25) / 100
ENDIF

;ABL:奉仕精神をみる
IF ABL:PLAYER:奉仕精神 == 0
	TIMES SOURCE:情愛 , 0.60
;	TIMES SOURCE:中毒充足 , 0.60
ELSEIF ABL:PLAYER:奉仕精神 == 1
	TIMES SOURCE:情愛 , 0.80
;	TIMES SOURCE:中毒充足 , 0.80
ELSEIF ABL:PLAYER:奉仕精神 == 2
	TIMES SOURCE:情愛 , 1.00
;	TIMES SOURCE:中毒充足 , 1.00
ELSEIF ABL:PLAYER:奉仕精神 == 3
	TIMES SOURCE:情愛 , 1.50
;	TIMES SOURCE:中毒充足 , 1.50
ELSEIF ABL:PLAYER:奉仕精神 == 4
	TIMES SOURCE:情愛 , 2.50
;	TIMES SOURCE:中毒充足 , 2.50
ELSE
	SOURCE:情愛 = SOURCE:情愛 * (ABL:PLAYER:技巧 * 75 + 25) / 100
;	SOURCE:中毒充足 = SOURCE:中毒充足 * (ABL:PLAYER:技巧 * 75 + 25) / 100
ENDIF

;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:従順をみる
	IF ABL:親密 <= 1
		SOURCE:受動 += 300 + (ABL:親密 * 50)
	ELSEIF ABL:親密 <= 3
		SOURCE:受動 += 600 + (ABL:親密 * 100)
	ELSEIF ABL:親密 <= 5
		SOURCE:受動 += 800 + (ABL:親密 * 120)
	ELSEIF ABL:親密 <= 8
		SOURCE:受動 += 1000 + (ABL:親密 * 140)
	ELSEIF ABL:親密 <= 11
		SOURCE:受動 += 1200 + (ABL:親密 * 160)
	ELSE
		SOURCE:受動 += 1500 + (ABL:親密 * 180)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:サドっ気 == 0
		SOURCE:征服 = 600
	ELSEIF ABL:サドっ気 == 1
		SOURCE:征服 = 900
	ELSEIF ABL:サドっ気 == 2
		SOURCE:征服 = 1200
	ELSEIF ABL:サドっ気 == 3
		SOURCE:征服 = 1600
	ELSEIF ABL:サドっ気 == 4
		SOURCE:征服 = 2000
	ELSE
		SOURCE:征服 = 1000 + (ABL:サドっ気 * 300)
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:10 + 5
;合意がないと好感度が下がる
IF TALENT:合意 == 0 && TFLAG:48 == 0
	A -= 9
	SOURCE:反感追加 += 400
ENDIF
TFLAG:30 += A
TFLAG:37 += A / 2

RETURN 1



;パイズリする
@TRAIN_MESSAGE_COM192
PRINTFORM %CALLNAME:PLAYER%は
	IF TALENT:PLAYER:貧乳
		PRINTFORM 薄い胸で懸命に%CALLNAME:TARGET%のペニスを
	ELSEIF TALENT:PLAYER:巨乳
		PRINTFORM 豊満な胸で%CALLNAME:TARGET%のペニスを包み込んで
	ELSE
		PRINTFORM 胸で%CALLNAME:TARGET%のペニスをはさんで
	ENDIF
PRINTW 擦りあげている…

