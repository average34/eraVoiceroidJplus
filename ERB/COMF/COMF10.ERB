@COM10
;ローター

;実行者が[淫具知識]を持っているなら効果増
SIF TALENT:PLAYER:淫具知識
	PRINT 高性能
PRINTL ローター
STR:0 = ローター
CALL TRAIN_MESSAGE_B

LOSEBASE:体力 += 10
LOSEBASE:気力 += 80

SOURCE:12 = 120
SOURCE:14 = 70

;ABL:Ｃ感覚をみる
IF ABL:Ｃ感覚 == 0
	SOURCE:0 = 200
ELSEIF ABL:Ｃ感覚 == 1
	SOURCE:0 = 400
ELSEIF ABL:Ｃ感覚 == 2
	SOURCE:0 = 900
ELSEIF ABL:Ｃ感覚 == 3
	SOURCE:0 = 1600
ELSEIF ABL:Ｃ感覚 == 4
	SOURCE:0 = 2400
ELSE
	SOURCE:0 = 1500 + (ABL:Ｃ感覚 * 300)
ENDIF

;実行者が[淫具知識]を持っているなら効果増
SIF TALENT:PLAYER:淫具知識
	TIMES SOURCE:0 , 1.20

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	PRINTS EXPNAME:40
	PRINTL +1
	EXP:レズ経験 += 1
	EXP:PLAYER:レズ経験 += 1
ELSEIF TALENT:オトコ == 1 && TALENT:PLAYER:オトコ == 1
	PRINTS EXPNAME:41
	PRINTL +1
	EXP:ＢＬ経験 += 1
	EXP:PLAYER:ＢＬ経験 += 1
ENDIF


;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:従順をみる
	IF ABL:親密 <= 1
		SOURCE:21 += 300 + (ABL:親密 * 50)
	ELSEIF ABL:親密 <= 3
		SOURCE:21 += 600 + (ABL:親密 * 100)
	ELSEIF ABL:親密 <= 5
		SOURCE:21 += 800 + (ABL:親密 * 120)
	ELSEIF ABL:親密 <= 8
		SOURCE:21 += 1000 + (ABL:親密 * 140)
	ELSEIF ABL:親密 <= 11
		SOURCE:21 += 1200 + (ABL:親密 * 160)
	ELSE
		SOURCE:21 += 1500 + (ABL:親密 * 180)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:サドっ気 == 0
		SOURCE:20 = 600
	ELSEIF ABL:サドっ気 == 1
		SOURCE:20 = 900
	ELSEIF ABL:サドっ気 == 2
		SOURCE:20 = 1200
	ELSEIF ABL:サドっ気 == 3
		SOURCE:20 = 1600
	ELSEIF ABL:サドっ気 == 4
		SOURCE:20 = 2000
	ELSE
		SOURCE:20 = 1000 + (ABL:サドっ気 * 300)
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:10 + 5
;合意がないと好感度が下がる
IF TALENT:合意 == 0 && TFLAG:48 == 0
	A -= 7
	SOURCE:15 += 200
ENDIF
TFLAG:30 += A
TFLAG:37 += A / 2

RETURN 1

