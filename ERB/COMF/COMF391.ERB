;衣装替え実行判定
@COM_ABLE391
;COM_ABLE共通判定
CALL COM_ABLE_COMMON, SELECTCOM
SIF RESULT == 0
	RETURN 0
;ウフフ中でないとダメ
SIF TFLAG:44 == 0
	RETURN 0
;恋慕か合意＋恋人じゃないとダメ
SIF TALENT:85 == 0 && (TALENT:152 == 0 || TALENT:153 == 0)
	RETURN 0
;解除はいつでも可能
SIF TEQUIP:65
	RETURN 1
;助手じゃダメ
SIF ASSIPLAY
	RETURN 0
;野外プレイ中はダメ
SIF TEQUIP:54
	RETURN 0
;羞恥プレイ中はダメ
SIF TEQUIP:57
	RETURN 0
;お風呂場プレイ中はダメ
SIF TEQUIP:58
	RETURN 0
;お医者プレイ中はダメ
SIF TEQUIP:62
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:90
	RETURN 0
RETURN 1

;-------------------------------------------------
;衣装名
;-------------------------------------------------
@CLOTH_NAME
VARSET TSTR, ""

TSTR:1 = 学ラン
TSTR:2 = セーラー服
TSTR:3 = ブルマ
TSTR:4 = 水着
TSTR:5 = スクール水着（紺）
TSTR:6 = スクール水着（白）
TSTR:7 = 白衣
TSTR:8 = ナース服
TSTR:9 = 女性警察官制服
TSTR:10 = ボンテージスーツ
TSTR:11 = 浴衣
TSTR:12 = 巫女服
TSTR:13 = 割烹着
TSTR:14 = 弦巻マキのライブ服
TSTR:15 = メイド服
TSTR:16 = アキバ系メイド服
TSTR:17 = リボン
TSTR:18 = 小悪魔風衣装
TSTR:19 = 魔法少女コスチューム
TSTR:20 = ゴシックロリータ衣装
TSTR:21 = バニーガール
TSTR:22 = Yシャツ
TSTR:23 = 普段着
TSTR:24 = %CALLNAME:MASTER%の服

;-------------------------------------------------
;衣装名（省略版）
;-------------------------------------------------
@CLOTH_EQUIP_NAME
VARSET TSTR, ""

TSTR:1 = 学ラン
TSTR:2 = セーラー服
TSTR:3 = ブルマ
TSTR:4 = 水着
TSTR:5 = スク水（紺）
TSTR:6 = スク水（白）
TSTR:7 = 白衣
TSTR:8 = ナース服
TSTR:9 = 女性警察官制服
TSTR:10 = ボンテージスーツ
TSTR:11 = 浴衣
TSTR:12 = 巫女服
TSTR:13 = 割烹着
TSTR:14 = マキのライブ服
TSTR:15 = メイド服
TSTR:16 = メイド服（秋）
TSTR:17 = リボン
TSTR:18 = 小悪魔風衣装
TSTR:19 = 魔法少女コス
TSTR:20 = ゴスロリ
TSTR:21 = バニーガール
TSTR:22 = 裸Yシャツ
TSTR:23 = 普段着
TSTR:24 = %CALLNAME:MASTER%の服
TSTR:255 = ボロボロの布切れ

;-------------------------------------------------
;衣装名（省略版） 服の上から～の表示に使用
;-------------------------------------------------
@GET_CLOTH_SHORT_NAME(ARG:0)
#FUNCTIONS
	SELECTCASE ARG:0
		CASE 0, 1 TO 3, 8 TO 10, 12 TO 16, 18 TO 21, 23, 24
			RETURNF "服"
		CASE 4 TO 6
			RETURNF "水着"
		CASE 7
			RETURNF "白衣"
		CASE 11
			RETURNF "浴衣"
		CASE 17
			RETURNF "リボン"
		CASE 22
			RETURNF "シャツ"
		CASE 255
			RETURNF "布切れ"
	ENDSELECT

;-------------------------------------------------
;衣装名（下着） 下着の上から～の表示に使用
;-------------------------------------------------
@GET_UNDERWARE_NAME(ARG:0)
#FUNCTIONS
	IF TEQUIP:20
		RETURNF "オムツ"
	ENDIF
	SELECTCASE ARG:0
		CASE 0, 1, 2, 7 TO 16, 18 TO 21, 23, 24
			RETURNF "下着"
		CASE 3
			RETURNF "ブルマ"
		CASE 4 TO 6
			RETURNF "水着"
		CASE 17
			RETURNF "リボン"
		CASE 22
			RETURNF "シャツ"
		CASE 255
			RETURNF "布切れ"
	ENDSELECT

@UNDERWARED
#FUNCTION
	IF TEQUIP:20
		RETURNF 1
	ELSEIF TEQUIP:65 == 0 || TEQUIP:65 == 17 || TEQUIP:65 == 22 || TEQUIP:65 == 255
		RETURNF 0
	ENDIF
	RETURNF 1
	
@NO_UNDERWARED
#FUNCTION
	IF UNDERWARED()
		RETURNF 0
	ENDIF
	RETURNF 1
	
	
@T_COUNT_391
;コンフィグ等で使ってるのと違いT:1 = 1～～なので注意
T:0 = 0
T:1 = 1
T:2 = 2
T:3 = 4
T:4 = 8
T:5 = 16
T:6 = 32
T:7 = 64
T:8 = 128
T:9 = 256
T:10 = 512
T:11 = 1024
T:12 = 2048
T:13 = 4096
T:14 = 8192
T:15 = 16384
T:16 = 32768
T:17 = 65536
T:18 = 131072
T:19 = 262144
T:20 = 524288
T:21 = 1048576
T:22 = 2097152
T:23 = 4194304
T:24 = 8388608
T:25 = 16777216
T:26 = 33554432
T:27 = 67108864
T:28 = 134217728
T:29 = 268435456
T:30 = 536870912

;衣装替え
@COM391
PRINTL 衣装替え
STR:0 = 衣装替え

LOSEBASE:0 += 0
LOSEBASE:1 += 20

;衣装名取得
CALL CLOTH_NAME

;リボンは相愛必要
SIF TALENT:89 == 0
	TSTR:17 = ""

;普段着、%CALLNAME:MASTER%の服は常に所持
FLAG:70 |= 4194304
FLAG:70 |= 8388608

;REPEATに使う変数取得
CALL T_COUNT_391
;改行用
C = 0
REPEAT 30
	;TSTR:COUNTが設定されていなかったら表示しない
	SIF TSTR:COUNT == ""
		CONTINUE

	IF (FLAG:70 & (T:COUNT)) && TEQUIP:65 != COUNT
		PRINTFORMLC [{COUNT,3}] %TSTR:COUNT%
		LOCAL:COUNT = 1
		C += 1
		;改行するのか？ 続けて出力するのか？
		IF C > 2
			PRINTL 
			C = 0
		ELSE
			PRINT 
		ENDIF
	ENDIF
REND
;最後のループで改行してなかったら、ここで改行しておく
SIF C != 0
	PRINTL 
SIF TEQUIP:65
	PRINTL [100] 脱がせる
PRINTL [999] 戻る
$INPUT_LOOP
INPUT
IF RESULT == 999
	RETURN 0
ELSEIF RESULT > 1000 || RESULT == 0 || (RESULT == 100 && TEQUIP:65 == 0)
	PRINTL 正しい値を入力して下さい
	GOTO INPUT_LOOP
ELSE
	IF TEQUIP:65
		IF TFLAG:263
			PRINTFORML 歪に膨らんだ%CALLNAME:TARGET%の%GET_UNDERWARE_NAME(TEQUIP:65)%を下ろしていくと、床に汚物がこぼれ落ちた
		ENDIF
		IF TFLAG:262 >= 4
			SIF TFLAG:264
				PRINT ローションと
			PRINTFORM 大量の尿で濡れた
		ELSEIF TFLAG:264 || TEQUIP:58 ||(TEQUIP:54 && (TIME:5 == 3 || TIME:5 == 4 || TIME:5 == 5 || TIME:5 == 7 || TIME:5 == 8))
			SIF TFLAG:264
				PRINT ローションで
			PRINTFORM 濡れてしまった
		ENDIF
		SIF TFLAG:265
			PRINT 精液まみれの
		PRINTFORM %CALLNAME:TARGET%の%GET_CLOTH_SHORT_NAME(TEQUIP:65)%を脱が
		IF TFLAG:263
			PRINTFORM し、お尻を綺麗に
		ENDIF
		TFLAG:262 = 0
		TFLAG:263 = 0
		TFLAG:264 = 0
		TFLAG:265 = 0
		IF RESULT == 100
			PRINTFORMW した
			TEQUIP:65 = 0
		ELSE
			PRINTFORML した…
		ENDIF
	ENDIF
	IF RESULT != 100
		IF RESULT == 17 && (FLAG:70 & 65536) && TALENT:89 && TEQUIP:65 != 17
			TEQUIP:65 = 17
			PRINTFORMW %CALLNAME:TARGET%は自らの裸体にリボンを巻きつけている……
		ELSE
			TEQUIP:65 = RESULT
			PRINTFORMW %CALLNAME:TARGET%に%TSTR:RESULT%を着せた
		ENDIF
	ENDIF
ENDIF

CALL KOJO_MESSAGE_COM
RETURN 0

;装備中の処理
@EQUIP_COM391
;一時ソース計算
;これに補正をかける

;露出に使用
A = 500
;逸脱に使用
B = 500
;欲情、歓楽に使用
C = 500

;PALAM:欲情をみる
IF PALAM:5 < PALAMLV:1
	TIMES C , 0.80
ELSEIF PALAM:5 < PALAMLV:2
	TIMES C , 0.90
ELSEIF PALAM:5 < PALAMLV:3
	TIMES C , 1.00
ELSEIF PALAM:5 < PALAMLV:4
	TIMES C , 1.10
ELSEIF PALAM:5 >= PALAMLV:4
	TIMES C , 1.20
ENDIF

;ABL:露出癖をみる
IF ABL:7 == 0
	TIMES A, 1.50
ELSEIF ABL:7 == 1
	TIMES A, 1.20
ELSEIF ABL:7 == 2
	TIMES A, 0.80
ELSEIF ABL:7 == 3
	TIMES A, 0.60
ELSEIF ABL:7 == 4
	TIMES A, 0.40
ELSE
	TIMES A, 0.20
ENDIF

;ABL:奉仕精神をみる
IF ABL:6 == 0
	TIMES B, 1.40
ELSEIF ABL:6 == 1
	TIMES B, 1.20
ELSEIF ABL:6 == 2
ELSEIF ABL:7 == 3
	TIMES B, 0.80
ELSEIF ABL:6 == 4
	TIMES B, 0.60
ELSE
	TIMES B, 0.40
ENDIF

;素直
SIF TALENT:13
	TIMES C , 1.20

;好奇心
SIF TALENT:23
	TIMES C , 1.20

;目立ちたがり
SIF TALENT:28
	TIMES A , 0.80
;解放
SIF TALENT:33
	TIMES A , 0.80

;恥じらい
SIF TALENT:35
	TIMES A , 2.00
;恥薄い
SIF TALENT:36
	TIMES A , 0.50

;どんな服でも濡れると恥かしい
IF TEQUIP:58 ||(TEQUIP:54 && (TIME:5 == 3 || TIME:5 == 4 || TIME:5 == 5 || TIME:5 == 7 || TIME:5 == 8))
	;ずぶ濡れ
	TIMES A , 1.30
ELSEIF (DAY:2 == 1 && (TIME:5 == 3 || TIME:5 == 4|| TIME:5 == 7))
	;やや濡れ
	TIMES A , 1.10
ENDIF
;汚れていると恥かしい
SIF TFLAG:263
	TIMES A , 2.0
SIF TFLAG:262 >= 4
	TIMES A , 1.5
SIF TFLAG:264
	TIMES A , 1.2
SIF TFLAG:265
	TIMES A , 1.2
;ここまで

;名前表示
CALL CLOTH_EQUIP_NAME
PRINTFORM ＜コスプレ中[%TSTR:(TEQUIP:65)%]
IF TFLAG:262
	PRINTFORM [尿]
ENDIF
IF TFLAG:263
	PRINTFORM [汚物]
ENDIF
IF TFLAG:264
	PRINTFORM [ローション]
ENDIF
IF TFLAG:265
	PRINTFORM [精液]
ENDIF
PRINTFORML ＞

IF TEQUIP:65 == 1
	;PRINTL ＜コスプレ中[学ラン]＞
	;露出度低
	TIMES A , 0.50
ELSEIF TEQUIP:65 == 2
	;PRINTL ＜コスプレ中[セーラー服]＞
	IF TEQUIP:58 ||(TEQUIP:54 && (TIME:5 == 3 || TIME:5 == 4 || TIME:5 == 5 || TIME:5 == 7 || TIME:5 == 8))
		PRINTFORML セーラー服のシャツが透けて下着がくっきりと見える…
		;濡れると透ける
		TIMES A , 2.00
		TIMES C , 0.80
	ELSEIF (DAY:2 == 1 && (TIME:5 == 3 || TIME:5 == 4|| TIME:5 == 7))
		PRINTFORML セーラー服のシャツが透けて下着が見えている…
		;湿気が強い湿って透ける
		TIMES A , 1.50
	ELSE
		;露出度低
		TIMES A , 0.50
		TIMES C , 1.50
	ENDIF
ELSEIF TEQUIP:65 == 3
	;PRINTL ＜コスプレ中[ブルマ]＞
	;露出度高
	TIMES A , 1.50
	TIMES C , 1.20
ELSEIF TEQUIP:65 == 4
	;PRINTL ＜コスプレ中[水着]＞
	;露出度高
	TIMES A , 1.50
	TIMES C , 1.50
	;貧乳だと恥かしい
	SIF TALENT:109
		TIMES A , 1.50
ELSEIF TEQUIP:65 == 5
	;PRINTL ＜コスプレ中[スク水（紺）]＞
	;露出度高
	TIMES A , 2.00
	TIMES C , 0.80
	;貧乳だと恥かしい
	SIF TALENT:109
		UP:8 += A/8
	;巨乳だと痛い
	SIF TALENT:110
		SOURCE:6 = 100
ELSEIF TEQUIP:65 == 6
	;PRINTL ＜コスプレ中[スク水（白）]＞
	IF TEQUIP:58 ||(TEQUIP:54 && (TIME:5 == 3 || TIME:5 == 4 || TIME:5 == 5 || TIME:5 == 7 || TIME:5 == 8))
		PRINTFORML %CALLNAME:TARGET%の水着はスケスケになっている…
		;濡れると透ける
		TIMES A , 3.00
		TIMES C , 0.40
	ELSEIF PALAM:3 > PALAMLV:3 || (DAY:2 == 1 && (TIME:5 == 3 || TIME:5 == 4|| TIME:5 == 7))
		PRINTFORML %CALLNAME:TARGET%の水着は少し透けている…
		;湿気が強いor濡れると湿って透ける
		TIMES A , 2.50
		TIMES C , 0.60
	ELSE
		;露出度高
		TIMES A , 2.00
		TIMES C , 0.80
	ENDIF
	;貧乳だと恥かしい
	SIF TALENT:109
		UP:8 += A/8
	;巨乳だと痛い
	SIF TALENT:110
		SOURCE:6 = 100
ELSEIF TEQUIP:65 == 7
	;PRINTL ＜コスプレ中[白衣]＞
	;露出度低
	TIMES A , 0.50
	TIMES C , 1.50
ELSEIF TEQUIP:65 == 8
	;PRINTL ＜コスプレ中[ナース服]＞
	;露出度低
	TIMES A , 0.50
	TIMES C , 1.50
ELSEIF TEQUIP:65 == 9
	;PRINTL ＜コスプレ中[女性警官制服]＞
	;露出度高
	TIMES A , 1.50
	TIMES C , 1.70
ELSEIF TEQUIP:65 == 10
	;PRINTL ＜コスプレ中[ボンテージスーツ]＞
	;露出度高
	TIMES A , 1.50
	IF TALENT:83
		TIMES C , 2.50
	ELSE
		TIMES C , 1.50
	ENDIF
	;征服
	SOURCE:20 += A/2
	;貧乳だと恥かしい
	SIF TALENT:109
		UP:8 += A/8
ELSEIF TEQUIP:65 == 11
	;PRINTL ＜コスプレ中[浴衣]＞
	;露出度低
	TIMES A , 0.50
	IF DAY:2 == 1
		TIMES C , 2.50
	ELSE
		TIMES C , 2.00
	ENDIF
ELSEIF TEQUIP:65 == 12
	;PRINTL ＜コスプレ中[巫女服]＞
	;露出度低
	TIMES A , 0.50
	TIMES C , 1.20
ELSEIF TEQUIP:65 == 13
	;PRINTL ＜コスプレ中[割烹着]＞
	;露出度中
	TIMES A , 1.20
	TIMES C , 1.50
ELSEIF TEQUIP:65 == 14
	;PRINTL ＜コスプレ中[マキのライブ服]＞
	;露出度中
	TIMES A , 1.20
	TIMES C , 1.50
ELSEIF TEQUIP:65 == 15
	;PRINTL ＜コスプレ中[メイド服]＞
	;露出度低
	TIMES A , 0.50
	TIMES C , 1.30
	;家務
	SOURCE:16 += A/8
ELSEIF TEQUIP:65 == 16
	;PRINTL ＜コスプレ中[メイド服（秋）]＞
	;露出度中
	TIMES A , 1.20
	TIMES C , 1.60
	;家務
	SOURCE:16 += A/6
	;達成感
	SOURCE:5 += A/12
ELSEIF TEQUIP:65 == 17
	;PRINTL ＜コスプレ中[裸リボン]＞
	;露出度超
	;愛のためならこのくらい…
	TIMES A , 2.50
	TIMES C , 2.00
	IF TIME:4 & 2
		;結婚記念日
		IF TIME:1 == 17 || TIME:1 == 44 || TIME:1 == 47|| TIME:1 == 48
			;イベント時愛情経験+5
			PRINTS EXPNAME:23
			PRINTL +5
			EXP:23 += 5
		ELSE
			;それ以外+3
			PRINTS EXPNAME:23
			PRINTL +3
			EXP:23 += 3
		ENDIF
	ELSEIF TIME:4 & 1
		;誕生日愛情経験+3
		PRINTS EXPNAME:23
		PRINTL +3
		EXP:23 += 3
	ELSEIF TIME:1 == 17 || TIME:1 == 44 || TIME:1 == 47|| TIME:1 == 48
		;イベント時愛情経験+2
		PRINTS EXPNAME:23
		PRINTL +2
		EXP:23 += 2
	ELSE
		;愛情経験+1
		PRINTS EXPNAME:23
		PRINTL +1
		EXP:23 += 1
	ENDIF
ELSEIF TEQUIP:65 == 18
	;PRINTL ＜コスプレ中[小悪魔風衣装]＞
	;露出度低
	TIMES A , 0.50
	IF TALENT:87
		TIMES C , 2.50
	ELSE
		TIMES C , 1.50
	ENDIF
ELSEIF TEQUIP:65 == 19
	;PRINTL ＜コスプレ中[魔法少女コス]＞
	;露出度中
	TIMES A , 1.20
	TIMES C , 1.50
ELSEIF TEQUIP:65 == 20
	;PRINTL ＜コスプレ中[ゴスロリ]＞
	;露出度低
	TIMES A , 0.50
	TIMES C , 1.50
ELSEIF TEQUIP:65 == 21
	;PRINTL ＜コスプレ中[バニーガール]＞
	;露出度高
	TIMES A , 1.50
	TIMES C , 0.80
	;巨乳だと露出度うｐ
	SIF TALENT:110
		TIMES A , 1.20
	;貧乳だと恥かしい
	SIF TALENT:109
		UP:8 += A/8
ELSEIF TEQUIP:65 == 22
	;PRINTL ＜コスプレ中[裸Yシャツ]＞
	IF TEQUIP:58 ||(TEQUIP:54 && (TIME:5 == 3 || TIME:5 == 4 || TIME:5 == 5 || TIME:5 == 7 || TIME:5 == 8))
		PRINTFORML %CALLNAME:TARGET%のシャツはスケスケになって肌にへばりついている…
		;濡れると透ける
		TIMES A , 2.50
		TIMES C , 0.60
	ELSEIF (DAY:2 == 1 && (TIME:5 == 3 || TIME:5 == 4 || TIME:5 == 7))
		PRINTFORML %CALLNAME:TARGET%のシャツは少し透けている…
		;湿気が強い湿って透ける
		TIMES A , 2.00
		TIMES C , 0.80
	ELSE
		;露出度高
		TIMES A , 1.50
		TIMES C , 1.20
	ENDIF
ELSEIF TEQUIP:65 == 23
	;PRINTFORML ＜コスプレ中[%CALLNAME:TARGET%の服]＞
	IF TFLAG:263
		PRINTFORML %CALLNAME:TARGET%の服のお尻の部分が汚物色に染まり、歪に膨らんでいる…
	ELSEIF TFLAG:262 >= 4
		PRINTFORML %CALLNAME:TARGET%の服は尿で濡れて脚にへばりついている…
	ENDIF
	;ELSEIF TEQUIP:58 ||(TEQUIP:54 && (TIME:5 == 3 || TIME:5 == 4 || TIME:5 == 5 || TIME:5 == 7 || TIME:5 == 8))
	;	PRINTFORML %CALLNAME:TARGET%の服はすっかり濡れて肌にへばりついている…
	;ELSEIF (DAY:2 == 1 && (TIME:5 == 3 || TIME:5 == 4 || TIME:5 == 7))
	;	PRINTFORML %CALLNAME:TARGET%の服は少し濡れてしまった…
	;ENDIF
	;露出度極低
	TIMES A , 0.20
	TIMES C , 1.20
ELSEIF TEQUIP:65 == 24
	;PRINTFORML ＜コスプレ中[%CALLNAME:MASTER%の服]＞
	;露出度極低
	TIMES A , 0.20
	TIMES C , 1.80
ELSEIF TEQUIP:65 == 255
	;PRINTL ＜コスプレ中[ボロボロの布切れ]＞
	;露出度高
	TIMES A , 2.50
	TIMES B , 1.50
	TIMES C , 0.20
	;そして屈辱的である
	SOURCE:13 += A/20
ENDIF

SOURCE:12 += A/20
SOURCE:13 += A/40
SOURCE:14 += B+(A/60)
SOURCE:18 += C/20
SOURCE:10 += (A/30)+(C/60)
RETURN 1

;-------------------------------------------------
;衣装替えのメッセージ　現在はなし
;-------------------------------------------------
@TRAIN_MESSAGE_COM391

;-------------------------------------------------
;メイン関数
;FLAG:70：持ってる服（ビット演算）

;SHOP.ERBの方の処理
;ELSEIF RESULT == 161
;	CALL CLOTHES_SHOP
;-------------------------------------------------
@CLOTHES_SHOP
VARSET LOCAL, 0

;購入条件を満たしていない場合LOCAL:COUNTを-1にして表示しない
T = 1
CALL GET_EXISTANCE
SIF E == 0
	LOCAL:13 = -1
T = 42
CALL GET_EXISTANCE
SIF E == 0
	LOCAL:14 = -1
T = 9
CALL GET_EXISTANCE
SIF E == 0
	LOCAL:16 = -1

;衣装名取得
CALL CLOTH_NAME
;REPEATに使う変数取得
CALL T_COUNT_391

;普段着、%CALLNAME:MASTER%の服は常に所持
FLAG:70 |= 4194304
FLAG:70 |= 8388608

PRINTL 所持衣装:
;改行用
C = 0
REPEAT 30
	;TSTR:COUNTが設定されていなかったら表示しない
	IF TSTR:COUNT == ""
		CONTINUE
	;LOCAL:COUNTが-1なら表示しない
	ELSEIF LOCAL:COUNT < 0
		CONTINUE
	ENDIF

	IF FLAG:70 & (T:COUNT)
		PRINTFORMLC [{COUNT,2}] %TSTR:COUNT%
	ELSE
		CONTINUE
	ENDIF
	
	C += 1
	;改行するのか？ 続けて出力するのか？
	IF C > 2
		PRINTL 
		C = 0
	ELSE
		PRINT 
	ENDIF
REND
;最後のループで改行してなかったら、ここで改行しておく
SIF C != 0
	PRINTL 
PRINTL 

;以下販売表示
REPEAT 30
	;TSTR:COUNTが設定されていなかったら表示しない
	IF TSTR:COUNT == ""
		CONTINUE
	;LOCAL:COUNTが-1なら表示しない
	ELSEIF LOCAL:COUNT < 0
		CONTINUE
	ENDIF
	IF FLAG:70 & (T:COUNT)
		CONTINUE
	ELSE
		C = 1
	ENDIF
REND
SIF C
	PRINTL どの衣装を購入しますか？($10000)
;改行用
C = 0
REPEAT 30
	;TSTR:COUNTが設定されていなかったら表示しない
	IF TSTR:COUNT == ""
		CONTINUE
	;LOCAL:COUNTが-1なら表示しない
	ELSEIF LOCAL:COUNT < 0
		CONTINUE
	ENDIF
	IF FLAG:70 & (T:COUNT)
		CONTINUE
	ELSE
		PRINTFORMLC [{COUNT,2}] %TSTR:COUNT%
		LOCAL:COUNT = 1
	ENDIF
	C += 1
	;改行するのか？ 続けて出力するのか？
	IF C > 2
		PRINTL 
		C = 0
	ELSE
		PRINT 
	ENDIF
REND
;最後のループで改行してなかったら、ここで改行しておく
SIF C != 0
	PRINTL 
PRINTL [100] 戻る
$INPUT_LOOP
INPUT
IF RESULT == 100
	RETURN 0
ELSEIF RESULT > 100
	PRINTW 正しい値を入力して下さい
	GOTO INPUT_LOOP
ELSEIF FLAG:70 & (T:RESULT)
	;ここで各衣装の説明文表示できると良いんですが
	PRINTW 既に所持しています
	GOTO INPUT_LOOP
ELSEIF LOCAL:RESULT == 0
	PRINTW 正しい値を入力して下さい
	GOTO INPUT_LOOP
ELSEIF MONEY <= 10000
	PRINTW お金が足りません
ELSE
	PRINTFORMW ＜%TSTR:RESULT%を購入しました＞
	MONEY -= 10000
	FLAG:70 |= T:RESULT
ENDIF
RESTART
RETURN 0


