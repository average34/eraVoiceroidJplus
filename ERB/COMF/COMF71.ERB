;-------------------------------------------------------------------------------
;	くぱぁ拡張ダメだこりゃ版
;	一時変数にG,H,I,J,Kを使用
;
;	秘貝開帳のコマンドが大幅に分岐するようになります
;	CおなにゅだけじゃなくてVやAでもするようになるよ！
;	あと趣味！ 軽やかに趣味！
;
;	- 使い方 -
;	JもしくはCPMで使用する場合は、24行目のFはそのまま(0)にしておいてください
;	YMで使用する場合は24行目のFを1にしてください
;
;-------------------------------------------------------------------------------

@COM71
;秘貝開帳
PRINTL 秘貝開帳

;-------------------------------------------------
;変数初期化
;-------------------------------------------------
F = 0
G = 0
H = 0
I = 0
J = 0
K = 0
;  デバッグ用変数表示
;D = 0
;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------

STR:0 = 秘貝開帳

;-------------------------------------------------------------------------------
;  羞恥分岐用一時関数Hの計算
;-------------------------------------------------------------------------------
;  露出癖代入
H = ABL:露出癖
;  恥じらいで-1
SIF TALENT:恥じらい
	H -= 1
;  目立ちたがりで+1
SIF TALENT:目立ちたがり
	H += 1
;  淫乱で+1
SIF TALENT:淫乱
	H += 1
;  主導権が奴隷で-1、主人で+1
IF F == 0
	IF TFLAG:45
		H -= 1
	ELSE
		H += 1
	ENDIF
ENDIF
;  ランダムで+-1
G = RAND:3
IF G == 0
	H -= 1
ELSEIF G == 1
	H += 1
ELSE
ENDIF
;  ランダム用Gの初期化、Hが0以下の場合は0に
;  処女膜が有る場合Hの最大値を3に
G = 0
SIF H < 0
	H = 0
SIF TALENT:処女 && H > 3
	H = 3
;-------------------------------------------------------------------------------
;  自慰分岐用一時関数Iの計算
;  I = 3以上で自慰箇所分岐、5以上で自慰効果上昇
;-------------------------------------------------------------------------------
;  自慰中毒LV代入
I = ABL:自慰中毒
;  先のH(羞恥)で判断、Hが低いとマイナス
I -= (5 - H)
;  自制心で-1
SIF TALENT:自制心
	I -= 1
;  自慰しやすいで+1
SIF TALENT:自慰しやすい
	I += 1
;  淫乱で+1
SIF TALENT:淫乱
	I += 1
;  主導権が奴隷で+1、主人で-1
IF F == 0
	IF TFLAG:45
		I += 1
	ELSE
		I -= 1
	ENDIF
ENDIF
;  ランダムで+-1
G = RAND:3
IF G == 0
	I -= 1
ELSEIF G == 1
	I += 1
ELSE
ENDIF
;  ランダム用Gの初期化、Iが0以下の場合は0に
G = 0
SIF I < 0
	I = 0
;-------------------------------------------------------------------------------
;  自慰分岐用一時関数Jの計算
;  J = 1でC自慰、J = 2でV自慰、J = 3でA自慰、J = 0で自慰しない
;-------------------------------------------------------------------------------
IF I >= 3
	G = RAND:3
	;  C自慰分岐
	IF G == 1
		J = 1
	;  V自慰分岐
	ELSEIF G == 2
		J = 2
		;  潤滑LV3以下、処女もしくはV経験10以下の場合V自慰せずC自慰
		SIF ( PALAM:潤滑 < PALAMLV:3 || TALENT:処女 || EXP:Ｖ経験 <= 10 )
			J = 1
	;  A自慰分岐
	ELSEIF G == 3
		J = 3
		;  潤滑LV2以下、A感覚0もしくはA経験10以下の場合A自慰せずC自慰
		SIF ( PALAM:潤滑 < PALAMLV:2 || ABL:Ａ感覚 == 0 || EXP:Ａ経験 <= 10 )
			J = 1
	ELSE
		I = 0
		J = 0
	ENDIF
ENDIF
G = 0
;-------------------------------------------------------------------------------
;  膣内射精フラグ時の分岐用関数Kの計算
;  K = 0で膣内に精液なし、K = 1で表示だけ、
;  K = 2でいきんで出せ、K = 3で指でかきだせ
;-------------------------------------------------------------------------------
IF F == 0
	IF TFLAG:703 == 1
		;追加自慰が無しのとき K = いきめ
		IF J == 0
			K = 2
		;追加自慰がＶのとき K = かきだせ
		ELSEIF J == 2
			K = 3
		;その他 K = 表示だけ
		ELSE
			K = 1
		ENDIF
	ENDIF
ELSEIF F == 1
	IF TFLAG:63
		;追加自慰が無しのとき K = いきめ
		IF J == 0
			K = 2
		;追加自慰がＶのとき K = かきだせ
		ELSEIF J == 2
			K = 3
		;その他 K = 表示だけ
		ELSE
			K = 1
		ENDIF
	ENDIF
ENDIF
;-------------------------------------------------------------------------------
;  K = 2以上のとき
;  50%で膣内射精フラグの消去
;-------------------------------------------------------------------------------
IF F == 0
	IF K >= 2
		G = RAND:99
		IF G >= 50
			TFLAG:703 = 0
			SIF CFLAG:101 > 0
				CFLAG:101 -= 1
			SIF CFLAG:102 > 0
				CFLAG:102 -= 1
		ELSE
		ENDIF
	ENDIF
ELSEIF F == 1
	IF K >= 2
		G = RAND:99
		IF G >= 50
			TFLAG:63 = 0
		ELSE
		ENDIF
	ENDIF
ENDIF
G = 0
;-------------------------------------------------
;  デバッグ用変数表示
;-------------------------------------------------
IF D > 0 
	PRINT { H(羞恥ＬＶ) = 
	PRINTV H
	PRINT  } 
	PRINT { I(自慰有無) = 
	PRINTV I
	PRINT  } 
	PRINT { J(自慰箇所) = 
	PRINTV J
	PRINT  } 
	PRINT { K(膣内精旗) = 
	PRINTV K
	PRINTL  } 
ENDIF
;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOSEBASE:体力 += 50
LOSEBASE:気力 += 90

SOURCE:液体追加 = 10
SOURCE:露出 = 250
SOURCE:屈従 = 250
SOURCE:逸脱 = 500

;Ｃオナニー
IF J == 1
	;ABL:Ｃ感覚をみる
	IF ABL:Ｃ感覚 == 0
		SOURCE:快Ｃ = 50
		SOURCE:屈従 += 200
	ELSEIF ABL:Ｃ感覚 == 1
		SOURCE:快Ｃ = 100
		SOURCE:屈従 += 300
	ELSEIF ABL:Ｃ感覚 == 2
		SOURCE:快Ｃ = 175
		SOURCE:屈従 += 600
	ELSEIF ABL:Ｃ感覚 == 3
		SOURCE:快Ｃ = 350
		SOURCE:屈従 += 900
	ELSEIF ABL:Ｃ感覚 == 4
		SOURCE:快Ｃ = 700
		SOURCE:屈従 += 1300
	ELSE
		SOURCE:快Ｃ = 400 + (ABL:Ｃ感覚 * 100)
		SOURCE:屈従 += 700 + (ABL:Ｃ感覚 * 200)
	ENDIF
	IF I >= 5
		TIMES SOURCE:快Ｃ , 2.00
		TIMES SOURCE:露出 , 2.00
	ENDIF
;Ｖオナニー
ELSEIF J == 2
	;ABL:Ｖ感覚をみる
	IF ABL:Ｖ感覚 == 0
		SOURCE:快Ｖ = 25
		SOURCE:屈従 += 300
	ELSEIF ABL:Ｖ感覚 == 1
		SOURCE:快Ｖ = 50
		SOURCE:屈従 += 450
	ELSEIF ABL:Ｖ感覚 == 2
		SOURCE:快Ｖ = 88
		SOURCE:屈従 += 900
	ELSEIF ABL:Ｖ感覚 == 3
		SOURCE:快Ｖ = 175
		SOURCE:屈従 += 1350
	ELSEIF ABL:Ｖ感覚 == 4
		SOURCE:快Ｖ = 350
		SOURCE:屈従 += 1950
	ELSE
		SOURCE:快Ｖ = 200 + (ABL:Ｃ感覚 * 50)
		SOURCE:屈従 += 1050 + (ABL:Ｃ感覚 * 300)
	ENDIF
	IF I >= 5
		TIMES SOURCE:快Ｖ , 2.00
		TIMES SOURCE:露出 , 2.00
	ENDIF
;Ａオナニー
ELSEIF J == 3
	;ABL:Ａ感覚をみる
	IF ABL:Ａ感覚 == 0
		SOURCE:快Ａ = 25
		SOURCE:屈従 += 400
	ELSEIF ABL:Ａ感覚 == 1
		SOURCE:快Ａ = 50
		SOURCE:屈従 += 600
	ELSEIF ABL:Ａ感覚 == 2
		SOURCE:快Ａ = 88
		SOURCE:屈従 += 1200
	ELSEIF ABL:Ａ感覚 == 3
		SOURCE:快Ａ = 175
		SOURCE:屈従 += 1800
	ELSEIF ABL:Ａ感覚 == 4
		SOURCE:快Ａ = 350
		SOURCE:屈従 += 2600
	ELSE
		SOURCE:快Ａ = 200 + (ABL:Ｃ感覚 * 50)
		SOURCE:屈従 += 1400 + (ABL:Ｃ感覚 * 400)
	ENDIF
	IF I >= 5
		TIMES SOURCE:快Ａ , 2.00
		TIMES SOURCE:露出 , 2.00
	ENDIF
ENDIF

;ABL:露出癖をみる
IF ABL:露出癖 == 0
	SOURCE:不潔 = 2000
	SOURCE:露出 += 1500
ELSEIF ABL:露出癖 == 1
	SOURCE:不潔 = 1000
	SOURCE:露出 += 1250
	SOURCE:液体追加 *= 5
ELSEIF ABL:露出癖 == 2
	SOURCE:不潔 = 500
	SOURCE:露出 += 750
	SOURCE:液体追加 *= 10
ELSEIF ABL:露出癖 == 3
	SOURCE:不潔 = 200
	SOURCE:露出 += 500
	SOURCE:液体追加 *= 50
ELSEIF ABL:露出癖 == 4
	SOURCE:不潔 = 100
	SOURCE:露出 += 250
	SOURCE:液体追加 *= 100
ELSE
	SOURCE:不潔 = 50
	SOURCE:液体追加 *= 200
ENDIF

;EXP:自慰経験をみる
IF EXP:自慰経験 < EXPLV:1
	TIMES SOURCE:不潔, 1.50
	TIMES SOURCE:屈従, 0.20
ELSEIF EXP:自慰経験 < EXPLV:2
	TIMES SOURCE:不潔, 1.20
	TIMES SOURCE:屈従, 0.50
ELSEIF EXP:自慰経験 < EXPLV:3
	TIMES SOURCE:不潔, 1.00
	TIMES SOURCE:屈従, 0.80
ELSEIF EXP:自慰経験 < EXPLV:4
	TIMES SOURCE:不潔, 0.80
	TIMES SOURCE:屈従, 1.00
ELSEIF EXP:自慰経験 < EXPLV:5
	TIMES SOURCE:不潔, 0.50
	TIMES SOURCE:屈従, 1.20
ELSE
	TIMES SOURCE:不潔, 0.20
	TIMES SOURCE:屈従, 1.50
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES SOURCE:不潔 , 1.80
	TIMES SOURCE:液体追加 , 0.50
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES SOURCE:不潔 , 1.50
	TIMES SOURCE:液体追加 , 0.80
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES SOURCE:不潔 , 1.20
	TIMES SOURCE:液体追加 , 1.20
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES SOURCE:不潔 , 0.80
	TIMES SOURCE:液体追加 , 1.50
ELSEIF PALAM:欲情 >= PALAMLV:4
	TIMES SOURCE:不潔 , 0.50
	TIMES SOURCE:液体追加 , 1.80
ENDIF

;恥じらい・恥薄い
SIF TALENT:恥じらい
	TIMES SOURCE:不潔 , 1.20
SIF TALENT:恥薄い
	TIMES SOURCE:不潔 , 0.50

;それぞれの箇所の敏感、鈍感をみる
;快Ｃ自体のチェックは後でまとめてやる
IF J == 1
	IF TALENT:Ｃ敏感
		TIMES SOURCE:快Ｃ , 1.50
		TIMES SOURCE:屈従 , 1.50
		TIMES SOURCE:逸脱 , 1.50
	ELSEIF TALENT:Ｃ鈍感
		TIMES SOURCE:快Ｃ , 0.60
		TIMES SOURCE:屈従 , 0.60
		TIMES SOURCE:逸脱 , 1.50
	ENDIF
ELSEIF J == 2
	IF TALENT:Ｖ敏感
		TIMES SOURCE:快Ｖ , 1.50
		TIMES SOURCE:屈従 , 1.50
		TIMES SOURCE:逸脱 , 1.50
	ELSEIF TALENT:Ｖ鈍感
		TIMES SOURCE:快Ｖ , 0.60
		TIMES SOURCE:屈従 , 0.60
		TIMES SOURCE:逸脱 , 1.50
	ENDIF
ELSEIF J == 3
	IF TALENT:Ａ敏感
		TIMES SOURCE:快Ａ , 1.50
		TIMES SOURCE:屈従 , 1.50
		TIMES SOURCE:逸脱 , 1.50
	ELSEIF TALENT:Ａ鈍感
		TIMES SOURCE:快Ａ , 0.60
		TIMES SOURCE:屈従 , 0.60
		TIMES SOURCE:逸脱 , 1.50
	ENDIF
ELSE
ENDIF

;処女、貞操観念
IF TALENT:処女
	TIMES SOURCE:不潔 , 1.20
	IF TALENT:処女 == 1
		TIMES SOURCE:不潔 , 2.00
		TIMES SOURCE:屈従 , 2.00
		SOURCE:露出 += 2000
		SIF TALENT:貞操観念
			TIMES SOURCE:露出 , 2.00
	ENDIF
ENDIF

CALL TRAIN_MESSAGE_B

;素質発覚イベント
CALL FINE_VIRGIN

IF ABL:露出癖 >= 3 && J > 0
	EXP:自慰経験 += 1
	PRINTL 自慰経験＋１
ENDIF

;奴隷のＶ⇔指の汚れが移動
STAIN:ヴァギナ |= STAIN:手
STAIN:手 |= STAIN:ヴァギナ
;奴隷のＡ⇔指の汚れが移動
IF J == 3
	STAIN:アナル |= STAIN:手
	STAIN:手 |= STAIN:アナル
ENDIF

;同性チェック
IF F == 0
	T = TARGET
	U = PLAYER
	V:0 = 1
	V:1 = 1
	IF TALENT:T:オトコ == 0 && TALENT:U:オトコ == 0 && V:0
		PRINTFORML %EXPNAME:40%+{V:0}
		EXP:T:レズ経験 += V:0
		EXP:U:レズ経験 += V:0
	ELSEIF TALENT:T:オトコ == 1 && TALENT:U:オトコ == 1 && V:1
		PRINTFORML %EXPNAME:41%+{V:1}
		EXP:T:ＢＬ経験 += V:1
		EXP:U:ＢＬ経験 += V:1
	ENDIF
ENDIF

;既成事実１に相当
TFLAG:200 = 1



;主導権をチェック
IF F == 0
	IF TFLAG:45 == 0
		;主導権が主人公側
		;ABL:従順をみる
		IF ABL:親密 <= 1
			SOURCE:受動 += 300 + (ABL:親密 * 50)
		ELSEIF ABL:親密 <= 3
			SOURCE:受動 += 600 + (ABL:親密 * 100)
		ELSEIF ABL:親密 <= 5
			SOURCE:受動 += 800 + (ABL:親密 * 120)
		ELSEIF ABL:親密 <= 8
			SOURCE:受動 += 1000 + (ABL:親密 * 140)
		ELSEIF ABL:親密 <= 11
			SOURCE:受動 += 1200 + (ABL:親密 * 160)
		ELSE
			SOURCE:受動 += 1500 + (ABL:親密 * 180)
		ENDIF
	ELSE
		;主導権が相手側
		;ABL:サドっ気をみる
		IF ABL:サドっ気 == 0
			SOURCE:征服 = 600
		ELSEIF ABL:サドっ気 == 1
			SOURCE:征服 = 900
		ELSEIF ABL:サドっ気 == 2
			SOURCE:征服 = 1200
		ELSEIF ABL:サドっ気 == 3
			SOURCE:征服 = 1600
		ELSEIF ABL:サドっ気 == 4
			SOURCE:征服 = 2000
		ELSE
			SOURCE:征服 = 1000 + (ABL:サドっ気 * 300)
		ENDIF
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:10 + 5

IF F == 0
	;合意がないと好感度が下がる
	IF TALENT:合意 == 0 && TFLAG:48 == 0
		A -= 8
		SOURCE:反感追加 += 300
	ENDIF
	TFLAG:30 += A
	TFLAG:37 += A / 2
ELSEIF F == 1
	CFLAG:2 += A
ENDIF

RETURN 1

@TRAIN_MESSAGE_COM71
;  合意か主人のサド(素質)を見る
IF TALENT:合意 || TALENT:MASTER:サド
	PRINTFORM %CALLNAME:PLAYER%に頼まれた%CALLNAME:TARGET%は、
ELSE
	PRINTFORM %CALLNAME:PLAYER%に命令された%CALLNAME:TARGET%は、
ENDIF

;  Hで羞恥関連分岐
IF H == 0
	PRINTFORML 涙を浮かべてこちらを睨んだが、
	;  合意か主人のサド(素質)を見る
	IF TALENT:合意 || TALENT:MASTER:サド
		PRINTFORM 再度懇願されるとしぶしぶといった様子で自分の
	ELSE
		PRINTFORM 再度命令されると絶望した様子で自分の
	ENDIF
	;小柄体形
	IF TALENT:小柄体型
		PRINT 幼いワレメに
	ELSE
		PRINT 陰唇に
	ENDIF
	PRINTL 指を当て、左右に広げて見せた
ELSEIF H == 1
	PRINTFORML 顔を真っ赤にしながらもおずおずと自分の陰唇を拡げて見せた
ELSEIF H == 2
	PRINTFORML 下腹部に手を置き、
	PRINTFORML 人差し指と中指でチョキを作るようにして%CALLNAME:PLAYER%に自分の秘所を曝した
	PRINTFORML 広がりきった陰門からこちらを覗く膣口が
	PRINTFORML まるで何かを待っているかのように怪しげに蠢いている……
ELSEIF H == 3
	PRINTFORML 慣れた様子で片手で陰唇を開いて
	PRINTFORML こちらの反応を窺うように見つめてきた
ELSEIF H == 4
	PRINTFORML 両手の指を使って陰唇だけでなく、膣口も同時に拡げた
	PRINTFORML 膣の中まで見られていることに感じているのか、%CALLNAME:TARGET%の頬は仄かに紅く染まっている
ELSEIF H >= 5 && TALENT:淫乱
	PRINTFORML 両手の指を陰唇にひっかけて膣口ごと
	PRINTFORML %CALLNAME:PLAYER%に見せつけるように大きく拡げた
	PRINTFORML だらしなく開いた膣口から覗く膣内はたっぷりと粘液にまみれた襞がざわめき、
	PRINTFORML まるでこちらを誘うかのようだ……
ELSEIF H >= 5
	PRINTFORML 両手の指を陰唇にひっかけ、
	PRINTFORML 膣口ごと%CALLNAME:PLAYER%に見せつけるように大きく拡げた
	PRINTFORML 広がりきった下の口からは愛液がとめどなく溢れ出ている……
ENDIF
WAIT

;  膣内射精フラグとか処女とか
IF K == 1
	PRINTL  
	PRINTFORML 先ほど膣内にたっぷりと注ぎ込んだ精液が、%CALLNAME:TARGET%の呼吸のリズムに合わせて、
	PRINTFORML 膣口からこぽり、こぽりと溢れ出ている……
ELSEIF K == 2
	PRINTL  
	PRINTFORM 先ほどたっぷりと注ぎ込んだものを膣圧で押し出すように
	;  合意か主人のサド(素質)を見る
	IF TALENT:合意 || TALENT:MASTER:サド
		PRINTFORML 言うと、
	ELSE
		PRINTFORML 命じると、
	ENDIF
	PRINTFORML 精液と愛液が混じりあって薄く濁った液体がとろとろと流れ出てきた……
;  処女＆淫乱なし
ELSEIF TALENT:処女 && TALENT:淫乱 == 0 
	PRINTL  
	PRINTFORML 陰門の入り口に純潔の証が頼りなさげにひくついているのが見て取れる
;  処女＆淫乱あり
ELSEIF TALENT:処女 && TALENT:淫乱
	PRINTL  
	PRINTFORML 陰門の入り口には純潔の証が貫かれるのを待っているかのように艶めいている
ENDIF

;  Iで自慰関連分岐
IF J > 0
	IF J == 1
		PRINTL  
		PRINTFORML じっと見られているだけでは我慢が出来なくなったのか、
		PRINTFORML %CALLNAME:TARGET%は空いた指で陰核を擦り始めた……
		SIF I >= 5
			PRINTFORML %CALLNAME:TARGET%の指が陰核をかすめるたびに膣口から愛液を滴らせている……
	ELSEIF J == 2
		PRINTL  
		PRINTFORML じっと見られているだけでは我慢が出来なくなったのか、
		PRINTFORML %CALLNAME:TARGET%は膣口を広げた指を、ぬかるんだそこに挿し込んだ……
		IF K == 3
			PRINTFORM それを見た%CALLNAME:PLAYER%が、
			PRINTFORM 自分で膣内の精液を掻き出すように
			IF TALENT:合意 || TALENT:MASTER:サド
				PRINTFORML 言うと、
			ELSE
				PRINTFORML 命じると、
			ENDIF
			PRINTFORML うなずいた%CALLNAME:TARGET%の指が自らの膣壁を引っ掻くたびに
			PRINTFORML 白濁が泡立ちながら流れ落ちてくる……
		ELSEIF I >= 5
			PRINTFORML ぐちゅぐちゅと下品な音を立てながら
			PRINTFORML 自らの感じる箇所を探すのに必死になっている……
		ENDIF
	ELSEIF J == 3
		PRINTL  
		PRINTFORML %CALLNAME:TARGET%は迸る愛液をたっぷりと指にまぶすと、不浄の門すら拡げてみせた……
		IF I >= 5
			PRINTFORML じっと見られているだけでは我慢が出来なくなったのか、
			PRINTFORML 拡げている菊門に指を抜き差し始めた
		ENDIF
	ENDIF
ENDIF

IF (TALENT:MASTER:恥じらい || TALENT:MASTER:オトコ) && CFLAG:2 < 20
	PRINTL  
	PRINTFORML 自分で命令を出しておきながらいざ%CALLNAME:TARGET%の痴態を目の当たりにした%CALLNAME:PLAYER%は
	PRINTL 微かな罪悪感と羞恥心を煽られ、朱に染まった顔を僅かにそむけてしまった
ENDIF

PRINTL  

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_71
A = 0
S = 0
V = 70

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;ABL:欲望
IF ABL:欲望
	A += ABL:欲望 * 3
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	A += ABL:奉仕精神 * 4
ENDIF
;ABL:露出癖
IF ABL:露出癖
	A += ABL:露出癖 * 3
ENDIF

IF MARK:快楽依存
	A += MARK:快楽依存 * 3
ENDIF

;PALAM:欲情
IF PALAM:欲情 < PALAMLV:1
	L = 0
ELSEIF PALAM:欲情 < PALAMLV:2
	L = 1
ELSEIF PALAM:欲情 < PALAMLV:3
	L = 2
ELSEIF PALAM:欲情 < PALAMLV:4
	L = 3
ELSEIF PALAM:欲情 < PALAMLV:5
	L = 4
ELSE
	L = 5
ENDIF
IF L
	A += L * 3
ENDIF

;自制心
IF TALENT:自制心
	A -= 5
ENDIF
;恥じらい
IF TALENT:恥じらい
	A -= 5
ENDIF
;恥薄い
IF TALENT:恥薄い
	A += 2
ENDIF
;男嫌い
IF TALENT:男嫌い && TALENT:PLAYER:オトコ
	A -= 5
ENDIF
;恋慕
IF TALENT:恋慕 && ASSIPLAY == 0
	A += 5
	S = 5
ENDIF

;媚薬
IF TEQUIP:21
	A += 6
ENDIF


;好感度
IF CFLAG:2 < 1000
	B = 0
	IF CFLAG:2 < 300
		B = 50
	ELSEIF CFLAG:2 < 500
		B = 20
	ELSEIF CFLAG:2 < 700
		B = 10
	ELSE
		B = 5
	ENDIF
	A -= B
ENDIF
;好感度
IF CFLAG:2 >= 1500
	B = 0
	IF CFLAG:2 < 2000
		B = 5
	ELSE
		B = CFLAG:2 / 150
	ENDIF
	A += B
ENDIF

;合意
IF F == 0
	IF TALENT:合意 == 0
		A -= 20
	ENDIF
ENDIF

IF ((FLAG:23 & 1p27) == 0) && FLAG:2 != 1
	;ABL:欲望
	IF ABL:欲望
		SIF S
			PRINT  + 
		PRINTS ABLNAME:1
		PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 3,')
		S = 1
	ENDIF
	;ABL:奉仕精神
	IF ABL:奉仕精神
		SIF S
			PRINT  + 
		PRINTS ABLNAME:6
		PRINTV 'LV,ABL:奉仕精神,'(,ABL:奉仕精神 * 4,')
		S = 1
	ENDIF
	;ABL:露出癖
	IF ABL:露出癖
		SIF S
			PRINT  + 
		PRINTS ABLNAME:7
		PRINTV 'LV,ABL:露出癖,'(,ABL:露出癖 * 3,')
		S = 1
	ENDIF

	IF MARK:快楽依存
		SIF S
			PRINT  + 
		PRINTS MARKNAME:1
		PRINTV 'LV,MARK:快楽依存,'(,MARK:快楽依存 * 3,')
		S = 1
	ENDIF

	;PALAM:欲情
	IF PALAM:欲情 < PALAMLV:1
		L = 0
	ELSEIF PALAM:欲情 < PALAMLV:2
		L = 1
	ELSEIF PALAM:欲情 < PALAMLV:3
		L = 2
	ELSEIF PALAM:欲情 < PALAMLV:4
		L = 3
	ELSEIF PALAM:欲情 < PALAMLV:5
		L = 4
	ELSE
		L = 5
	ENDIF
	IF L
		SIF S
			PRINT  + 
		PRINTS PALAMNAME:5
		PRINTV 'LV,L,'(,L * 3,')
		S = 1
	ENDIF

	;自制心
	IF TALENT:自制心
		PRINT  - 
		PRINTS TALENTNAME:20
		PRINTV '(,5,')
		S = 1
	ENDIF
	;恥じらい
	IF TALENT:恥じらい
		PRINT  - 
		PRINTS TALENTNAME:35
		PRINTV '(,5,')
		S = 1
	ENDIF
	;恥薄い
	IF TALENT:恥薄い
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:36
		PRINTV '(,2,')
		S = 1
	ENDIF
	;男嫌い
	IF TALENT:男嫌い && TALENT:PLAYER:オトコ
		PRINT  - 
		PRINTS TALENTNAME:82
		PRINTV '(,5,')
		S = 1
	ENDIF
	;恋慕
	IF TALENT:恋慕 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:85
		PRINTV '(,5,')
		S = 5
	ENDIF

	;媚薬
	IF TEQUIP:21
		SIF S
			PRINT  + 
		PRINTS ITEMNAME:26
		PRINTV '(,6,')
		S = 1
	ENDIF


	;好感度
	IF CFLAG:2 < 1000
		B = 0
		PRINT  - 
		IF CFLAG:2 < 300
			B = 50
		ELSEIF CFLAG:2 < 500
			B = 20
		ELSEIF CFLAG:2 < 700
			B = 10
		ELSE
			B = 5
		ENDIF
		PRINT 好感度不足
		PRINTV '(,B,')
		S = 1
	ENDIF
	;好感度
	IF CFLAG:2 >= 1500
		B = 0
		SIF S
			PRINT  + 
		IF CFLAG:2 < 2000
			B = 5
		ELSE
			B = CFLAG:2 / 150
		ENDIF
		PRINT 好感度
		PRINTV '(,B,')
		S = 1
	ENDIF

	;合意
	IF F == 0
		IF TALENT:合意 == 0
			PRINT  - 
			PRINTFORM %TALENTNAME:152%無し
			PRINTV '(,20,')
			S = 1
		ENDIF
	ENDIF

	;'合計を表示(70以上で実行)
	PRINT  = 
	PRINTV A

	SIF A < V
		PRINT  < 
	SIF A == V
		PRINT  = 
	SIF A > V
		PRINT  > 
	PRINT 実行値
	PRINTV V
	WAIT
ENDIF

