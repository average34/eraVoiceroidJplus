@COM_ABLE123
;キス背面座位実行判定
;COM_ABLE共通判定
CALL COM_ABLE_COMMON, SELECTCOM
SIF RESULT == 0
	RETURN 0
;ウフフ中じゃないと無理
SIF TFLAG:44 == 0
	RETURN 0
;バイブ挿入中はダメ
SIF TEQUIP:11
	RETURN 0
;クスコ挿入中はダメ
SIF TEQUIP:61
	RETURN 0
;焼芋挿入中は不可
SIF TEQUIP:71
	RETURN 0
;オトコだとダメ
SIF TALENT:オトコ
	RETURN 0
;異常経験フィルタ
SIF ((FLAG:61 & 1) == 0) && (EXP:Ｖ経験 < EXPLV:1) && (ASSIPLAY && (TALENT:PLAYER:オトコ == 0))
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
IF TEQUIP:58
SIF ITEM:プレイマット == 0 && NOITEM == 0
	RETURN 0
ENDIF
;触手調教中はダメ
SIF TEQUIP:90
	RETURN 0
;小人体型持ちは、双方が調教者と調教対象ならOK
;それ以外の場合は主人が禁断の知識持ちじゃないとダメ
SIF TALENT:小人体型 && (TALENT:MASTER:禁断の知識 == 0 && TALENT:PLAYER:小人体型 == 0)
	RETURN 0
;調教者がオトコ、半人半妖、ふたなり、もしくはペニスバンドがないとダメ
SIF TALENT:PLAYER:半人半妖 == 0 && TALENT:PLAYER:ふたなり == 0 && TALENT:PLAYER:オトコ == 0 && ITEM:PBAND == 0
	RETURN 0
;処女で、従順4以下・レズっ気4以下のASSIの場合キス背面座位はできない（サドならＯＫ）
IF EXP:Ｖ経験 == 0 && ASSIPLAY
	SIF (ABL:ASSI:親密 <= 4 || ABL:ASSI:レズっ気 <= 4) && TALENT:ASSI:サド == 0
		RETURN 0
ENDIF
;潤滑不足で、従順3以下・レズっ気3以下のASSIの場合キス背面座位はできない（サドならＯＫ）
IF PALAM:3 < PALAMLV:2 && ASSIPLAY
	SIF (ABL:ASSI:親密 <= 3 || ABL:ASSI:レズっ気 <= 3) && TALENT:ASSI:サド == 0
		RETURN 0
ENDIF
RETURN 1

@COM123
;キス背面座位

PRINTL キス背面座位

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

STR:0 = キス背面座位
CALL TRAIN_MESSAGE_B

;V経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:19 = 1
SIF TALENT:恋慕 && ASSIPLAY == 0 && EXP:Ｖ経験 == 0
	TFLAG:20 = 1

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
SIF TALENT:PLAYER:ふたなり || TALENT:PLAYER:オトコ
	BASE:PLAYER:射精 += CALC_EJAC(SELECTCOM, PLAYER)

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOSEBASE:体力 += 30
LOSEBASE:気力 += 150

;キス経験で気力消費軽減（何度もキスしたがる、の再現だとでも脳内補完）
IF EXP:キス経験 < 100
	LOSEBASE:気力 += 50
ELSEIF EXP:キス経験 < 500
	LOSEBASE:気力 += 40
ELSE
	LOSEBASE:気力 += 25
ENDIF

SOURCE:露出 = 200

;Ｍ感覚を設定している場合快Ｍソース等取得
SIF FLAG:62 & 4194304
	CALL SOURCE_M_COM123

;ABL:Ｖ感覚をみる
IF ABL:Ｖ感覚 == 0
	SOURCE:快Ｖ = 25
	SOURCE:情愛 = 50
ELSEIF ABL:Ｖ感覚 == 1
	SOURCE:快Ｖ = 100
	SOURCE:情愛 = 100
ELSEIF ABL:Ｖ感覚 == 2
	SOURCE:快Ｖ = 150
	SOURCE:情愛 = 200
ELSEIF ABL:Ｖ感覚 == 3
	SOURCE:快Ｖ = 300
	SOURCE:情愛 = 300
ELSEIF ABL:Ｖ感覚 == 4
	SOURCE:快Ｖ = 500
	SOURCE:情愛 = 500
ELSE
	SOURCE:快Ｖ = 125 + (ABL:Ｖ感覚 * 175)
	SOURCE:情愛 = 200 + (ABL:Ｖ感覚 * 100)
ENDIF

;EXP:Ｖ経験をみる
IF EXP:Ｖ経験 < EXPLV:1
	TIMES SOURCE:快Ｖ , 0.20
	SOURCE:痛み = 3000

	IF ASSIPLAY && TALENT:PLAYER:オトコ == 0
		EXP:異常経験 += 3
		PRINTFORML %EXPNAME:50%＋３
	ENDIF
ELSEIF EXP:Ｖ経験 < EXPLV:2
	TIMES SOURCE:快Ｖ , 0.60
	SOURCE:痛み = 240
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES SOURCE:快Ｖ , 1.00
	SOURCE:痛み = 30
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES SOURCE:快Ｖ , 1.20
	SOURCE:痛み = 5
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES SOURCE:快Ｖ , 1.40
	SOURCE:痛み = 0
ELSE
	TIMES SOURCE:快Ｖ , 1.60
	SOURCE:痛み = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:3 < PALAMLV:1
	TIMES SOURCE:快Ｖ , 0.40
	SOURCE:痛み += 600
	TIMES SOURCE:痛み , 2.60
ELSEIF PALAM:3 < PALAMLV:2
	TIMES SOURCE:快Ｖ , 0.70
	SOURCE:痛み += 180
	TIMES SOURCE:痛み , 1.00
ELSEIF PALAM:3 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:痛み , 0.50
ELSEIF PALAM:3 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.20
	TIMES SOURCE:痛み , 0.20
ELSEIF PALAM:3 >= PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.60
	TIMES SOURCE:痛み , 0.10
ENDIF

;調教者がふたなり
SIF TALENT:PLAYER:ふたなり
	TIMES SOURCE:快Ｖ , 2.50


;小柄体形
SIF TALENT:小柄体型
	TIMES SOURCE:痛み , 2.00

;貞操観念
IF TALENT:貞操観念 && TALENT:恋慕 == 0
	IF EXP:Ｖ経験 == 0
		TIMES SOURCE:情愛 , 0.60
		SOURCE:反感追加 = 10000
	ELSE
		TIMES SOURCE:情愛 , 0.60
		SOURCE:反感追加 = 1000
	ENDIF
;貞操無頓着
ELSEIF TALENT:貞操無頓着 || TALENT:親愛 && ASSIPLAY == 0
	IF EXP:Ｖ経験 == 0
		TIMES SOURCE:情愛 , 0.60
		SOURCE:反感追加 = 300
	ENDIF
ELSE
	IF EXP:Ｖ経験 == 0
		SOURCE:反感追加 = 3000
	ENDIF
ENDIF

;PALAM:欲情をみる
IF PALAM:5 < PALAMLV:1
	TIMES SOURCE:快Ｖ , 0.80
	TIMES SOURCE:快Ｃ , 0.80
	TIMES SOURCE:情愛 , 0.60
ELSEIF PALAM:5 < PALAMLV:2
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:快Ｃ , 1.00
	TIMES SOURCE:情愛 , 1.00
ELSEIF PALAM:5 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 1.20
	TIMES SOURCE:快Ｃ , 1.10
	TIMES SOURCE:情愛 , 1.50
ELSEIF PALAM:5 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.40
	TIMES SOURCE:快Ｃ , 1.20
	TIMES SOURCE:情愛 , 2.00
ELSEIF PALAM:5 >= PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.60
	TIMES SOURCE:快Ｃ , 1.30
	TIMES SOURCE:情愛 , 2.60
ENDIF

;ABL:親密をみる
IF ABL:親密 == 0
	TIMES SOURCE:快Ｖ , 1.50
	TIMES SOURCE:情愛 , 1.00
	TIMES SOURCE:反感追加 , 2.00
ELSEIF ABL:親密 == 1
	TIMES SOURCE:快Ｖ , 1.50
	TIMES SOURCE:情愛 , 1.30
	TIMES SOURCE:反感追加 , 1.80
ELSEIF ABL:親密 == 2
	TIMES SOURCE:快Ｖ , 1.50
	TIMES SOURCE:情愛 , 1.50
	TIMES SOURCE:反感追加 , 1.60
ELSEIF ABL:親密 == 3
	TIMES SOURCE:快Ｖ , 1.80
	TIMES SOURCE:情愛 , 1.90
	TIMES SOURCE:反感追加 , 1.40
ELSEIF ABL:親密 == 4
	TIMES SOURCE:快Ｖ , 2.10
	TIMES SOURCE:情愛 , 2.20
	TIMES SOURCE:反感追加 , 1.20
ELSE
	SOURCE:快Ｖ = SOURCE:快Ｖ * (ABL:親密 * 20 + 150) / 100
	SOURCE:情愛 = SOURCE:情愛 * (ABL:親密 * 20 + 160) / 100
	TIMES SOURCE:反感追加 , 1.00
ENDIF

;プレイヤーのABL:技巧をみる
IF ABL:PLAYER:技巧 == 0
	SOURCE:情愛 += 100
	SOURCE:液体追加 = 0
	A = 0
ELSEIF ABL:PLAYER:技巧 == 1
	SOURCE:情愛 += 150
	SOURCE:液体追加 += 50
	A = 0
ELSEIF ABL:PLAYER:技巧 == 2
	SOURCE:情愛 += 200
	SOURCE:液体追加 += 100
	A = 0
ELSEIF ABL:PLAYER:技巧 == 3
	SOURCE:情愛 += 300
	SOURCE:液体追加 += 150
	A = 25
ELSEIF ABL:PLAYER:技巧 == 4
	SOURCE:情愛 += 500
	SOURCE:液体追加 += 250
	A = 50
ELSE
	SOURCE:情愛 += 50 + (ABL:PLAYER:技巧 * 150)
	SOURCE:液体追加 += 25 + (ABL:PLAYER:技巧 * 75)
	A = (ABL:PLAYER:技巧 * 50) - 100
ENDIF

SOURCE:快Ｃ += A + (ABL:Ｃ感覚 * 20)
SOURCE:快Ｂ += A * 2 + (ABL:Ｂ感覚 * 30)

SIF EXP:Ｖ経験 >= EXPLV:3
	SOURCE:快Ｖ += A

;TALENT:恋慕をみる
SIF TALENT:恋慕
	TIMES SOURCE:情愛 , 2.00


;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:従順をみる
	IF ABL:親密 <= 1
		SOURCE:受動 += 300 + (ABL:親密 * 50)
	ELSEIF ABL:親密 <= 3
		SOURCE:受動 += 600 + (ABL:親密 * 100)
	ELSEIF ABL:親密 <= 5
		SOURCE:受動 += 800 + (ABL:親密 * 120)
	ELSEIF ABL:親密 <= 8
		SOURCE:受動 += 1000 + (ABL:親密 * 140)
	ELSEIF ABL:親密 <= 11
		SOURCE:受動 += 1200 + (ABL:親密 * 160)
	ELSE
		SOURCE:受動 += 1500 + (ABL:親密 * 180)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:サドっ気 == 0
		SOURCE:征服 = 600
	ELSEIF ABL:サドっ気 == 1
		SOURCE:征服 = 900
	ELSEIF ABL:サドっ気 == 2
		SOURCE:征服 = 1200
	ELSEIF ABL:サドっ気 == 3
		SOURCE:征服 = 1600
	ELSEIF ABL:サドっ気 == 4
		SOURCE:征服 = 2000
	ELSE
		SOURCE:征服 = 1000 + (ABL:サドっ気 * 300)
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:10 + 5
SIF TALENT:恋慕 || TALENT:恋人
	A += 5
SIF TALENT:親愛
	A += 5
;合意がないと好感度が下がる
IF TALENT:合意 == 0 && TFLAG:48 == 0
	A -= 9
	SOURCE:反感追加 += 400
ENDIF
TFLAG:30 += A
TFLAG:37 += A / 2

EXP:Ｖ経験 += 1
PRINTL Ｖ経験＋１

;既成事実3に相当
TFLAG:200 = 3

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK

IF E >= 1
	SIF ABL:親密 >= 3 && ABL:欲望 >= 3 && FLAG:6
		PRINTFORML %CALLNAME:TARGET%は背を仰け反らせて%CALLNAME:PLAYER%にしなだれかかった…
;PRINTFORML 　
	;射精先タイプの設定
	TFLAG:703 = 1
	;射精先選択の可・不可
	;TFLAG:704 = 0
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;奴隷のＶ⇔調教者のＰの汚れが移動
STAIN:ヴァギナ |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:ヴァギナ

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	PRINTS EXPNAME:40
	PRINTL +4
	EXP:レズ経験 += 4
	EXP:PLAYER:レズ経験 += 4
ENDIF

RETURN 1

;キス背面座位
@TRAIN_MESSAGE_COM123
VARSET LOCAL, 0
;前回コマンドが性交系かチェック
SIF PREVCOM == 20 || PREVCOM == 21 || PREVCOM == 24 || PREVCOM == 25 || PREVCOM == 34 || PREVCOM == 120 || PREVCOM == 121 || PREVCOM == 122 || PREVCOM == 123
	LOCAL = 1
;前回と今回のプレイヤーが同じで、前回のコマンドが性交系であり、抜かないフラグがある場合
IF TFLAG:60 && LOCAL && ((ASSIPLAY && TFLAG:50) || (ASSIPLAY == 0 && TFLAG:50 == 0))
	;前回のコマンドがキス後背位かキス背面座位
	IF PREVCOM == 121 || PREVCOM == 123
		PRINTFORMW %CALLNAME:TARGET%を背後から抱きしめ、舌を絡めあいながら再び腰を動かし始めた…
	;前回のコマンドが背面座位
	ELSEIF PREVCOM == 25
		PRINTFORMW %CALLNAME:TARGET%を背後から抱きしめ、口づけしながら再び%CALLNAME:TARGET%の身体を揺らし始めた…
	;他のコマンド
	ELSE
		PRINTFORMW 挿入したまま反対を向かせた%CALLNAME:TARGET%の身体を抱き寄せ、キスしながら腰を揺さぶり始めた…
	ENDIF
	SIF TFLAG:31
		TFLAG:31 = 2
ELSE
	PRINTFORM %CALLNAME:TARGET%の身体を後ろから抱き上げると、口づけしながら
	SIF PALAM:3 >= PALAMLV:4
		PRINT 濡れそぼった
	IF TALENT:小柄体型
		PRINT 幼いワレメ
	ELSE
		PRINT 秘裂
	ENDIF
	PRINTFORMW を待ち構えていたモノでゆっくり貫いた…
ENDIF
IF TEQUIP:57
	PRINTFORMW %CALLNAME:TARGET%は顔を羞恥に染めながら時折艶っぽい声を上げている…
ENDIF

;-------------------------------------------------
;Ｍ感覚を設定している場合快Ｍソース等取得
;-------------------------------------------------
@SOURCE_M_COM123
VARSET LOCAL, 0

;屈従・反感ソース
LOCAL:13 += 100
LOCAL:14 += 10

;ABL:Ｍ感覚をみる
IF ABL:Ｍ感覚 == 0
	LOCAL:24 += 5
ELSEIF ABL:Ｍ感覚 == 1
	LOCAL:24 += 20
ELSEIF ABL:Ｍ感覚 == 2
	LOCAL:24 += 100
ELSEIF ABL:Ｍ感覚 == 3
	LOCAL:24 += 250
ELSEIF ABL:Ｍ感覚 == 4
	LOCAL:24 += 400
ELSE
	LOCAL:24 += 150 + (ABL:Ｍ感覚 * 100)
ENDIF

;PLAYERの技巧を見る
SIF ABL:PLAYER:技巧 >= 2
	LOCAL:24 = LOCAL:24 * (100 + (ABL:PLAYER:技巧 / 2)) / 100

;技巧を見る
SIF ABL:TARGET:技巧 >= 2
	LOCAL:24 = LOCAL:24 * (100 + (ABL:TARGET:技巧 / 2)) / 100

;EXP:キス経験をみる
IF EXP:キス経験 < 1
	TIMES LOCAL:24 , 1.00
ELSEIF EXP:キス経験 < 4
	TIMES LOCAL:14 , 0.95
	TIMES LOCAL:24 , 1.05
ELSEIF EXP:キス経験 < 20
	TIMES LOCAL:14 , 0.90
	TIMES LOCAL:24 , 1.10
ELSEIF EXP:キス経験 < 50
	TIMES LOCAL:14 , 0.85
	TIMES LOCAL:24 , 1.15
ELSEIF EXP:キス経験 < 200
	TIMES LOCAL:14 , 0.80
	TIMES LOCAL:24 , 1.20
ELSEIF EXP:キス経験 < 400
	TIMES LOCAL:14 , 0.75
	TIMES LOCAL:24 , 1.25
ELSEIF EXP:キス経験 < 700
	TIMES LOCAL:14 , 0.70
	TIMES LOCAL:24 , 1.30
ELSEIF EXP:キス経験 < 1000
	TIMES LOCAL:14 , 0.65
	TIMES LOCAL:24 , 1.35
ELSEIF EXP:キス経験 < 1500
	TIMES LOCAL:14 , 0.60
	TIMES LOCAL:24 , 1.40
ELSEIF EXP:キス経験 < 2000
	TIMES LOCAL:14 , 0.55
	TIMES LOCAL:24 , 1.45
ELSEIF EXP:キス経験 < 3000
	TIMES LOCAL:14 , 0.50
	TIMES LOCAL:24 , 1.50
ELSEIF EXP:キス経験 < 5000
	TIMES LOCAL:14 , 0.45
	TIMES LOCAL:24 , 1.55
ELSEIF EXP:キス経験 < 10000
	TIMES LOCAL:14 , 0.40
	TIMES LOCAL:24 , 1.60
ELSE
	TIMES LOCAL:14 , 0.45
	TIMES LOCAL:24 , 1.65
ENDIF

;ABL:親密をみる
LOCAL:24 = LOCAL:24 * (80 + ABL:親密 + (ABL:親密 / 2)) / 100

;TALENT:恋慕をみる
SIF TALENT:恋慕
	TIMES LOCAL:24 , 1.50
;TALENT:恋人をみる
SIF TALENT:恋人
	TIMES LOCAL:24 , 1.50
;TALENT:親愛をみる
SIF TALENT:親愛
	TIMES LOCAL:24 , 2.00
;TALENT:相愛をみる
SIF TALENT:相愛
	TIMES LOCAL:24 , 2.00

;反発感情をみる
IF MARK:反発感情 == 1
	TIMES LOCAL:24 , 0.50
	TIMES LOCAL:13 , 1.15
ELSEIF MARK:反発感情 == 2
	TIMES LOCAL:24 , 0.25
	TIMES LOCAL:13 , 1.45
ELSEIF MARK:反発感情 == 3
	TIMES LOCAL:24 , 0.10
	TIMES LOCAL:13 , 1.90
ENDIF

SOURCE:屈従 += LOCAL:13
SOURCE:逸脱 += LOCAL:14
SOURCE:快Ｍ += LOCAL:24

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_123
A = 0
S = 0
V = 180

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;ABL:欲望
IF ABL:欲望
	A += ABL:欲望 * 3
ENDIF
;ABL:Ｖ感覚
IF ABL:Ｖ感覚
	A += ABL:Ｖ感覚 * 2
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	A += ABL:奉仕精神 * 4
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	A += ABL:精液中毒 * 1
ENDIF

IF MARK:快楽依存
	A += MARK:快楽依存 * 3
ENDIF

;PALAM:潤滑（の不足）
IF PALAM:3 < PALAMLV:3
	A -= 5
ENDIF
;PALAM:欲情
IF PALAM:5 < PALAMLV:1
	L = 0
ELSEIF PALAM:5 < PALAMLV:2
	L = 1
ELSEIF PALAM:5 < PALAMLV:3
	L = 2
ELSEIF PALAM:5 < PALAMLV:4
	L = 3
ELSEIF PALAM:5 < PALAMLV:5
	L = 4
ELSE
	L = 5
ENDIF
IF L
	A += L * 3
ENDIF

;恥じらい
IF TALENT:恥じらい
	A -= 2
ENDIF
;快感の否定
IF TALENT:快感の否定
	A -= 5
ENDIF
;男嫌い
IF TALENT:男嫌い && TALENT:PLAYER:オトコ
	A -= 12
ENDIF
;恋慕
IF TALENT:恋慕 && ASSIPLAY == 0
	A += 5
	S = 5
ENDIF

;処女
IF TALENT:処女
	A -= 20
ELSEIF EXP:Ｖ経験 < EXPLV:2
	A -= 5
ENDIF

;一線越えない（処女のみ）
IF TALENT:一線越えない && TALENT:処女
	A -= 20
ENDIF

;調教者がふたなり
IF TALENT:PLAYER:ふたなり
	A += 4
ENDIF

;媚薬
IF TEQUIP:21
	A += 6
ENDIF

;童貞
IF TALENT:PLAYER:童貞
	A += 2
ENDIF


;ムードボーナス
B = 0
IF TFLAG:37 < 10
	B -= 10
ELSEIF TFLAG:37 < 30
	B -= 1
ELSEIF TFLAG:37 < 50
	B += 1
ELSEIF TFLAG:37 < 100
	B += TFLAG:37 / 10
ELSEIF TFLAG:37 < 300
	B += TFLAG:37 / 8
ELSE
	B += TFLAG:37 / 7
ENDIF
A += B
IF B < 0
	B = B * -1
ELSE
ENDIF


;好感度
IF CFLAG:2 < 2000
	B = 0
	IF CFLAG:2 < 1000
		B = 50
	ELSEIF CFLAG:2 < 1300
		B = 20
	ELSEIF CFLAG:2 < 1700
		B = 10
	ELSE
		B = 5
	ENDIF
	A -= B
ENDIF
;好感度
IF CFLAG:2 >= 2500
	B = 0
	IF CFLAG:2 < 3000
		B = 5
	ELSE
		B = CFLAG:2 / 250
	ENDIF
	A += B
ENDIF

Y = 0
;愛液の汚れ
SIF STAIN:PLAYER:ペニス & 1
	Y += 1
;精液の汚れ
SIF STAIN:PLAYER:ペニス & 4
	Y += 3
;アナルの汚れ
SIF STAIN:PLAYER:ペニス & 8
	Y += 7
SIF STAIN:PLAYER:ペニス & 16
	Y += 15

SIF TALENT:汚臭鈍感
	Y /= 3
SIF TALENT:汚臭敏感
	Y *= 2

;汚れあり
IF Y
	A -= Y
	;汚臭鈍感
	IF TALENT:汚臭鈍感
	ELSEIF TALENT:汚臭敏感
	ELSE
	ENDIF
ENDIF

;合意
IF TALENT:合意 == 0
	A -= 30
ENDIF

IF ((FLAG:23 & 1p27) == 0) && FLAG:2 != 1
	;ABL:欲望
	IF ABL:欲望
		SIF S
			PRINT  + 
		PRINTS ABLNAME:1
		PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 3,')
		S = 1
	ENDIF
	;ABL:Ｖ感覚
	IF ABL:Ｖ感覚
		SIF S
			PRINT  + 
		PRINTS ABLNAME:4
		PRINTV 'LV,ABL:Ｖ感覚,'(,ABL:Ｖ感覚 * 2,')
		S = 1
	ENDIF
	;ABL:奉仕精神
	IF ABL:奉仕精神
		SIF S
			PRINT  + 
		PRINTS ABLNAME:6
		PRINTV 'LV,ABL:奉仕精神,'(,ABL:奉仕精神 * 4,')
		S = 1
	ENDIF
	;ABL:精液中毒
	IF ABL:精液中毒
		SIF S
			PRINT  + 
		PRINTS ABLNAME:11
		PRINTV 'LV,ABL:精液中毒,'(,ABL:精液中毒 * 1,')
		S = 1
	ENDIF

	IF MARK:快楽依存
		SIF S
			PRINT  + 
		PRINTS MARKNAME:1
		PRINTV 'LV,MARK:快楽依存,'(,MARK:快楽依存 * 3,')
		S = 1
	ENDIF

	;PALAM:潤滑（の不足）
	IF PALAM:3 < PALAMLV:3
		PRINT  - 
		PRINT 潤滑不足
		PRINTV '(,5,')
		S = 1
	ENDIF
	;PALAM:欲情
	IF PALAM:5 < PALAMLV:1
		L = 0
	ELSEIF PALAM:5 < PALAMLV:2
		L = 1
	ELSEIF PALAM:5 < PALAMLV:3
		L = 2
	ELSEIF PALAM:5 < PALAMLV:4
		L = 3
	ELSEIF PALAM:5 < PALAMLV:5
		L = 4
	ELSE
		L = 5
	ENDIF
	IF L
		SIF S
			PRINT  + 
		PRINTS PALAMNAME:5
		PRINTV 'LV,L,'(,L * 3,')
		S = 1
	ENDIF

	;恥じらい
	IF TALENT:恥じらい
		PRINT  - 
		PRINTS TALENTNAME:35
		PRINTV '(,2,')
		S = 1
	ENDIF
	;快感の否定
	IF TALENT:快感の否定
		PRINT  - 
		PRINTS TALENTNAME:71
		PRINTV '(,5,')
		S = 1
	ENDIF
	;男嫌い
	IF TALENT:男嫌い && TALENT:PLAYER:オトコ
		PRINT  - 
		PRINTS TALENTNAME:82
		PRINTV '(,12,')
		S = 1
	ENDIF
	;恋慕
	IF TALENT:恋慕 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:85
		PRINTV '(,5,')
		S = 5
	ENDIF

	;処女
	IF TALENT:処女
		PRINT  - 
		PRINTS TALENTNAME:0
		PRINTV '(,20,')
		S = 1
	ELSEIF EXP:Ｖ経験 < EXPLV:2
		PRINT  - 
		PRINTS EXPNAME:0
		PRINT 不足
		PRINTV '(,5,')
		S = 1
	ENDIF

	;一線越えない（処女のみ）
	IF TALENT:一線越えない && TALENT:処女
		PRINT  - 
		PRINTS TALENTNAME:27
		PRINTV '(,20,')
		S = 1
	ENDIF

	;調教者がふたなり
	IF TALENT:PLAYER:ふたなり
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:121
		PRINTV '(,4,')
		S = 1
	ENDIF

	;媚薬
	IF TEQUIP:21
		SIF S
			PRINT  + 
		PRINTS ITEMNAME:26
		PRINTV '(,6,')
		S = 1
	ENDIF

	;童貞
	IF TALENT:PLAYER:童貞
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:154
		PRINTV '(,2,')
		S = 1
	ENDIF


	;ムードボーナス
	B = 0
	IF TFLAG:37 < 10
		B -= 10
	ELSEIF TFLAG:37 < 30
		B -= 1
	ELSEIF TFLAG:37 < 50
		B += 1
	ELSEIF TFLAG:37 < 100
		B += TFLAG:37 / 10
	ELSEIF TFLAG:37 < 300
		B += TFLAG:37 / 8
	ELSE
		B += TFLAG:37 / 7
	ENDIF
	IF B < 0
		B = B * -1
		PRINT  - 
		PRINT ムード悪い
		PRINTV '(,B,')
		S = 1
	ELSE
		PRINT  + 
		PRINT ムード良い
		PRINTV '(,B,')
		S = 1
	ENDIF


	;好感度
	IF CFLAG:2 < 2000
		B = 0
		PRINT  - 
		IF CFLAG:2 < 1000
			B = 50
		ELSEIF CFLAG:2 < 1300
			B = 20
		ELSEIF CFLAG:2 < 1700
			B = 10
		ELSE
			B = 5
		ENDIF
		PRINT 好感度不足
		PRINTV '(,B,')
		S = 1
	ENDIF
	;好感度
	IF CFLAG:2 >= 2500
		B = 0
		SIF S
			PRINT  + 
		IF CFLAG:2 < 3000
			B = 5
		ELSE
			B = CFLAG:2 / 250
		ENDIF
		PRINT 好感度
		PRINTV '(,B,')
		S = 1
	ENDIF

	Y = 0
	;愛液の汚れ
	SIF STAIN:PLAYER:ペニス & 1
		Y += 1
	;精液の汚れ
	SIF STAIN:PLAYER:ペニス & 4
		Y += 3
	;アナルの汚れ
	SIF STAIN:PLAYER:ペニス & 8
		Y += 7
	SIF STAIN:PLAYER:ペニス & 16
		Y += 15

	SIF TALENT:汚臭鈍感
		Y /= 3
	SIF TALENT:汚臭敏感
		Y *= 2

	;汚れあり
	IF Y
		PRINT  - 
		;汚臭鈍感
		IF TALENT:汚臭鈍感
			PRINT 汚れあり、
			PRINTS TALENTNAME:61
		ELSEIF TALENT:汚臭敏感
			PRINT 汚れあり、
			PRINTS TALENTNAME:62
		ELSE
			PRINT 汚れあり
		ENDIF
		PRINTV '(,Y,')
		S = 1
	ENDIF

	;合意
	IF TALENT:合意 == 0
		PRINT  - 
		PRINTFORM %TALENTNAME:152%無し
		PRINTV '(,30,')
		S = 1
	ENDIF

	;合計を表示(120以上で実行)
	PRINT  = 
	PRINTV A

	V = 120
	SIF A < V
		PRINT  < 
	SIF A == V
		PRINT  = 
	SIF A > V
		PRINT  > 
	PRINT 実行値
	PRINTV V
	WAIT
ENDIF
