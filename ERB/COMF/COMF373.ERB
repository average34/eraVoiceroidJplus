@COM_ABLE373
;着衣フェラチオ実行判定
;COM_ABLE共通判定
CALL COM_ABLE_COMMON, SELECTCOM
SIF RESULT == 0
	RETURN 0
;ウフフ中は無理
SIF TFLAG:44 == 1
	RETURN 0
;助手では無理
SIF ASSIPLAY
	RETURN 0
;マスターがふたなりかオトコじゃないと無理
SIF TALENT:MASTER:ふたなり == 0 && TALENT:MASTER:オトコ == 0
	RETURN 0
;[猫舌]だと技巧8以上じゃないと無理
SIF ABL:技巧 < 8 && TALENT:猫舌
	RETURN 0
;デート中は淫乱じゃないと無理
SIF TEQUIP:2 && TALENT:淫乱 == 0
	RETURN 0
;欲望6以上かつ、奉仕精神6以上かつ、親密8以上or好感度800以上or恋人が必要
SIF ABL:欲望 < 6 || ABL:奉仕精神 < 6 || (ABL:親密 < 8 && CFLAG:2 < 800 && TALENT:恋人 == 0)
	RETURN 0
RETURN 1

@COM373
;フェラチオ
;対象が恋慕さとりか、猫舌だと変化
IF NAME_CSV(NO:TARGET, 0) == "さとり" && TALENT:恋慕
	PRINTL 読心フェラ
ELSEIF TALENT:猫舌
	PRINTL 猫舌フェラ
ELSE
	PRINTL フェラチオ
ENDIF


;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

STR:0 = フェラチオ
CALL TRAIN_MESSAGE_B

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
SIF TALENT:MASTER:オトコ  || TALENT:MASTER:ふたなり
	BASE:PLAYER:射精 += CALC_EJAC(SELECTCOM, MASTER)

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOSEBASE:体力 += 10
LOSEBASE:気力 += 120

SOURCE:屈従 = 800
SOURCE:逸脱 = 700

;上のほうで計算した汚れデータ
SOURCE:不潔 = Y*40 + 100

;Ｍ感覚を設定している場合快Ｍソース等取得
SIF FLAG:62 & 4194304
	CALL SOURCE_M_COM31

;ABL:奉仕精神をみる
IF ABL:奉仕精神 == 0
	SOURCE:性行動 = 420
	SOURCE:達成感 = 150
	TIMES SOURCE:不潔 , 4.00
ELSEIF ABL:奉仕精神 == 1
	SOURCE:性行動 = 500
	SOURCE:達成感 = 300
	TIMES SOURCE:不潔 , 2.50
ELSEIF ABL:奉仕精神 == 2
	SOURCE:性行動 = 580
	SOURCE:達成感 = 600
	TIMES SOURCE:不潔 , 1.50
ELSEIF ABL:奉仕精神 == 3
	SOURCE:性行動 = 660
	SOURCE:達成感 = 900
	TIMES SOURCE:不潔 , 1.00
ELSEIF ABL:奉仕精神 == 4
	SOURCE:性行動 = 740
	SOURCE:達成感 = 1500
	TIMES SOURCE:不潔 , 0.50
ELSE
	SOURCE:性行動 = 620 + (ABL:奉仕精神 * 40)
	SOURCE:達成感 = 450 + (ABL:奉仕精神 * 350)
	TIMES SOURCE:不潔 , 0.10
ENDIF


;ABL:技巧をみる
IF ABL:技巧 == 0
	TIMES SOURCE:性行動 , 0.50
	TIMES SOURCE:達成感 , 0.50
ELSEIF ABL:技巧 == 1
	TIMES SOURCE:性行動 , 0.80
	TIMES SOURCE:達成感 , 0.80
ELSEIF ABL:技巧 == 2
	TIMES SOURCE:性行動 , 1.00
	TIMES SOURCE:達成感 , 1.00
ELSEIF ABL:技巧 == 3
	TIMES SOURCE:性行動 , 1.20
	TIMES SOURCE:達成感 , 1.20
ELSEIF ABL:技巧 == 4
	TIMES SOURCE:性行動 , 1.50
	TIMES SOURCE:達成感 , 1.50
ELSE
	SOURCE:性行動 = SOURCE:性行動 * (ABL:技巧 * 25 + 75) / 100
	SOURCE:達成感 = SOURCE:達成感 * (ABL:技巧 * 25 + 75) / 100
ENDIF

;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:従順をみる
	IF ABL:親密 <= 1
		SOURCE:受動 += 300 + (ABL:親密 * 50)
	ELSEIF ABL:親密 <= 3
		SOURCE:受動 += 600 + (ABL:親密 * 100)
	ELSEIF ABL:親密 <= 5
		SOURCE:受動 += 800 + (ABL:親密 * 120)
	ELSEIF ABL:親密 <= 8
		SOURCE:受動 += 1000 + (ABL:親密 * 140)
	ELSEIF ABL:親密 <= 11
		SOURCE:受動 += 1200 + (ABL:親密 * 160)
	ELSE
		SOURCE:受動 += 1500 + (ABL:親密 * 180)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:サドっ気 == 0
		SOURCE:征服 = 800
	ELSEIF ABL:サドっ気 == 1
		SOURCE:征服 = 1100
	ELSEIF ABL:サドっ気 == 2
		SOURCE:征服 = 1400
	ELSEIF ABL:サドっ気 == 3
		SOURCE:征服 = 1800
	ELSEIF ABL:サドっ気 == 4
		SOURCE:征服 = 2300
	ELSE
		SOURCE:征服 = 1000 + (ABL:サドっ気 * 350)
	ENDIF
ENDIF


;デート中は
IF TEQUIP:2 > 0
	;ポルノ映画以外は
	IF TEQUIP:2 != 22
		;逸脱追加
		SOURCE:逸脱 += 3000
		;さらに露出追加
		SOURCE:露出 += 4000
		;さらに異常経験追加
		EXP:異常経験 += 2
		PRINTL 異常経験+2
	ELSE
	;ポルノ映画は
		;逸脱追加
		SOURCE:逸脱 += 1500
		;さらに露出追加
		SOURCE:露出 += 2000
		;さらに異常経験追加
		EXP:異常経験 += 1
		PRINTL 異常経験+1
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:10 + 5
SIF TALENT:恋慕 || TALENT:恋人
	A += 2
SIF TALENT:親愛
	A += 3
;合意がないと好感度が下がる
IF TALENT:合意 == 0 && TFLAG:48 == 0
	A -= 8
	SOURCE:懐疑 += 300
ENDIF
TFLAG:30 += A
TFLAG:37 += A / 2

;既成事実2に相当
TFLAG:200 = 2

;-------------------------------------------------
;読心フェラの場合効果アップ（2倍）
;-------------------------------------------------

;対象が恋慕さとりだと変化
IF NAME_CSV(NO:TARGET, 0) == "さとり" && TALENT:恋慕
	TIMES SOURCE:性行動 , 2.00
	TIMES SOURCE:達成感 , 2.00
ENDIF


;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK

IF E
;射精している
	TIMES SOURCE:性行動 , 3.00

	;ABL:精液中毒をみる
	IF ABL:精液中毒 == 0
		TIMES SOURCE:達成感 , 2.00
		TIMES SOURCE:屈従, 4.00
	ELSEIF ABL:精液中毒 == 1
		TIMES SOURCE:達成感 , 3.00
		SOURCE:中毒充足 = 500
		TIMES SOURCE:屈従, 3.00
	ELSEIF ABL:精液中毒 == 2
		TIMES SOURCE:達成感 , 4.00
		SOURCE:中毒充足 = 1200
		TIMES SOURCE:屈従, 2.50
	ELSEIF ABL:精液中毒 == 3
		TIMES SOURCE:達成感 , 6.00
		SOURCE:中毒充足 = 3000
		SOURCE:液体追加 += 500 + (ABL:精液中毒 * 100)
		TIMES SOURCE:露出, 1.50
		TIMES SOURCE:屈従, 2.00
	ELSEIF ABL:精液中毒 == 4
		TIMES SOURCE:達成感 , 9.00
		SOURCE:中毒充足 = 6000
		SOURCE:液体追加 += 700 + (ABL:精液中毒 * 200)
		TIMES SOURCE:露出, 4.00
		TIMES SOURCE:屈従, 1.50
	ELSE
		SOURCE:中毒充足 = (ABL:精液中毒 * 3000) - 3000
		SOURCE:達成感 = SOURCE:達成感 * (ABL:精液中毒 * 300) / 100
		SOURCE:液体追加 += 500 + (ABL:精液中毒 * 300)
		SOURCE:露出 = SOURCE:露出 * (ABL:精液中毒 * 200) / 130
		IF ABL:精液中毒 <= 10
			SOURCE:屈従 = SOURCE:屈従 * (150 - ABL:精液中毒 * 10) / 100
		ELSE
			TIMES SOURCE:屈従, 0.50
		ENDIF
	ENDIF

	;射精先タイプの設定
	TFLAG:703 = 4
	;射精先選択の可・不可
	TFLAG:704 = 0
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF


PRINTFORML %EXPNAME:22%+1
EXP:フェラ経験 += 1

;奴隷の口⇔調教者のＰの汚れが移動
STAIN:口 |= STAIN:MASTER:ペニス
STAIN:MASTER:ペニス |= STAIN:口

;精液中毒Lv2以上、技巧Lv10以上、ペニス以外の汚れがある時なら汚れをなめ取ってくれる
IF ABL:精液中毒 >= 2 && ABL:技巧 >= 10 && (STAIN:MASTER:ペニス & 29)
	STAIN:MASTER:ペニス = 2
;	TFLAG:8 = 1
ENDIF

;レズ経験
IF TALENT:オトコ == 0 && TALENT:MASTER:オトコ == 0
	PRINTFORML %EXPNAME:40%+7
	EXP:レズ経験 += 7
	EXP:MASTER:レズ経験 += 7
;BL経験
ELSEIF TALENT:オトコ && TALENT:MASTER:オトコ
	PRINTFORML %EXPNAME:41%+7
	EXP:ＢＬ経験 += 7
	EXP:MASTER:ＢＬ経験 += 7
ENDIF

PRINTL 

;フェラ経験が20以上だと好感度ボーナス
SIF EXP:フェラ経験 >= EXPLV:3
	TFLAG:30 += 1

;調教者がふたなり
IF TALENT:MASTER:ふたなり
	SOURCE:屈従 /= 2
ENDIF

TFLAG:100 = 1

RETURN 1

@TRAIN_MESSAGE_COM373
;主導権で分岐
IF TFLAG:45
	PRINTFORM %CALLNAME:TARGET%は
	;デート中(デート中は淫乱必須、カラオケ以外(カラオケは基本個室))
	IF TEQUIP:2 > 0 && TEQUIP:2 != 6
		;精液中毒7以上
		IF ABL:精液中毒 >= 7
			;保守的又は貞操観念又は恥じらい
			IF TALENT:保守的 || TALENT:貞操観念 || TALENT:恥じらい
				PRINT 辺りを入念に確認してから
			;目立ちたがり又は解放又は恥薄い又は中毒しやすい又は倒錯的
			ELSEIF TALENT:目立ちたがり || TALENT:解放 || TALENT:恥薄い || TALENT:中毒しやすい || TALENT:倒錯的
				PRINT 周囲を気にする事なく
			ENDIF
		ELSE
			PRINT 辺りを入念に確認してから
		ENDIF
	ENDIF
	;淫乱かつ精液中毒9以上かつ欲情3000万以上or欲情30000000以上)
	IF TALENT:淫乱 && (ABL:精液中毒 >= 9) && (PALAM:欲情 >= 30000000)
		PRINTFORML %CALLNAME:MASTER%のペニスを乱暴に引っぱり出すと
	ELSE
		;技巧10以上
		SIF ABL:技巧 >= 10
			PRINT 口を使って器用に
		PRINTFORML %CALLNAME:MASTER%のペニスを露出させると
	ENDIF
	;キス魔かつ欲情500万以下
	SIF TALENT:キス魔 && PALAM:欲情 < 5000000
		PRINT 先端に一度キスをしてから、
	;淫乱又は精液中毒7以上かつ欲情3000万以上
	IF (TALENT:淫乱 || ABL:精液中毒 >= 7) && PALAM:欲情 >= 30000000
		PRINT 呼吸も荒く、余裕の無い表情で
	;淫乱又は精液中毒5以上かつ欲情100万以上
	ELSEIF (TALENT:淫乱 || ABL:精液中毒 >= 5) && PALAM:欲情 >= 1000000
		PRINT 上気した表情で、
	;サド又は小悪魔又はサドっ気5以上
	ELSEIF TALENT:サド || TALENT:小悪魔 || ABL:サドっ気 >= 5
		PRINT 意地悪な笑みを浮かべながら、
	;マゾ又は、臆病かつマゾっ気3以上、又はマゾっ気5以上
	ELSEIF TALENT:マゾ || (TALENT:臆病 && ABL:マゾっ気 >= 3) || ABL:マゾっ気 >= 5
		PRINTFORML 媚びる様な上目遣いで%CALLNAME:MASTER%を見つめながら、
	;恥じらい
	ELSEIF TALENT:恥じらい
		PRINT 頬を紅潮させながらも、
	ENDIF
	;猫舌
	IF TALENT:猫舌
		PRINT 舌の裏側を使って丁寧に舐め回した…
	;精液中毒5以上かつ欲情500万以上
	ELSEIF ABL:精液中毒 >= 5 && PALAM:欲情 >= 5000000
		PRINTL 貪るようにしゃぶりついた…
	;技巧12以上
	ELSEIF ABL:技巧 >= 12
		PRINTL 時折鈴口に舌先をねじ込むような仕草をしつつ
		PRINTL 丹念に刺激した…
	;幼児退行or幼稚
	ELSEIF TALENT:幼児 || TALENT:幼稚
		PRINT 飴玉を転がすように舐め回した…
	ELSE
		PRINT じっくりと舌を這わせた…
	ENDIF
ELSE
	;淫乱かつ精液中毒9以上かつ欲情3000万以上
	IF TALENT:淫乱 && ABL:精液中毒 >= 9 && PALAM:欲情 >= 30000000
		PRINT 甘えておねだりする
	;淫乱又は精液中毒5以上
	ELSEIF TALENT:淫乱 || ABL:精液中毒 >= 5
		PRINT 待ち切れない様子の
	ELSE
		PRINTFORM %CALLNAME:MASTER%が
	ENDIF
	PRINTFORML %CALLNAME:TARGET%の前にペニスを露出させると
	;淫乱又は精液中毒7以上かつ欲情3000万以上…現状オトコ無しだから大丈夫…だよね←あなたが居ます
	IF (TALENT:淫乱 || ABL:精液中毒 >= 7) && PALAM:欲情 > 30000000
		IF TALENT:オトコ
			PRINT 熱に浮かされる様に、
		ELSE
			PRINT 発情した雌の顔で、
		ENDIF
	;淫乱又は精液中毒5以上かつ欲情100万以上
	ELSEIF (TALENT:淫乱 || ABL:精液中毒 >= 5) && (PALAM:欲情 >= 1000000)
		PRINT うっとりとした表情で、
	ELSEIF TALENT:マゾ || (TALENT:臆病 && ABL:マゾっ気 >= 3) || ABL:マゾっ気 >= 5
		PRINTFORML 媚びるような上目遣いで%CALLNAME:MASTER%を見つめながら
	;サド又は小悪魔又はサドっ気5以上
	ELSEIF TALENT:サド || TALENT:小悪魔 || ABL:サドっ気 >= 5
		PRINTFORML 妖しげな瞳で%CALLNAME:MASTER%を見つめながら
	;恥じらい
	ELSEIF TALENT:恥じらい
		PRINT 一瞬躊躇うような仕草を見せがらも、
	ENDIF
	;キス魔かつ欲情500万以下
	SIF TALENT:キス魔 && (PALAM:欲情 < 5000000)
		PRINT 先端に一度キスをしてから、
	;猫舌
	IF TALENT:猫舌
		PRINT 舌の裏側を使って丁寧に舐め回した…
	;精液中毒5以上かつ欲情500万以上
	ELSEIF ABL:精液中毒 >= 5 && (PALAM:欲情 >= 5000000)
		PRINTL 夢中でしゃぶりついた…
	;技巧12以上
	ELSEIF ABL:技巧 >= 12
		PRINT カリの裏側をなぞるように舌を這わせた…
	;幼児退行、幼稚
	ELSEIF TALENT:幼児 || TALENT:幼稚
		PRINT 氷菓のように舐め回した…
	ELSE
		PRINT ゆっくりと舌を這わせた…
	ENDIF
ENDIF
PRINTW 

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_373
A = 0
S = 0
V = 105
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;ABL:欲望
IF ABL:欲望
	A += ABL:欲望
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	A += ABL:奉仕精神 * 3
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	A += ABL:精液中毒 * 4
ENDIF
;快楽依存
IF MARK:快楽依存
	A += MARK:快楽依存
ENDIF


;PALAM:欲情
IF PALAM:欲情 < PALAMLV:1
	L = 0
ELSEIF PALAM:欲情 < PALAMLV:2
	L = 1
ELSEIF PALAM:欲情 < PALAMLV:3
	L = 2
ELSEIF PALAM:欲情 < PALAMLV:4
	L = 3
ELSEIF PALAM:欲情 < PALAMLV:5
	L = 4
ELSE
	L = 5
ENDIF
IF L
	A += L
ENDIF

;恥じらい
SIF TALENT:恥じらい
	A -= 3
;汚臭鈍感
SIF TALENT:汚臭鈍感
	A += 1
;汚臭敏感
SIF TALENT:汚臭敏感
	A -= 3
;献身的
SIF TALENT:献身的
	A += 6
;快感の否定
SIF TALENT:快感の否定
	A -= 1
;男嫌い
SIF TALENT:男嫌い && TALENT:MASTER:オトコ
	A -= 12
;恋慕
SIF TALENT:恋慕
	A += 5
;調教者がふたなり
SIF TALENT:MASTER:ふたなり
	A += 8

Y = 0
;愛液の汚れ
SIF STAIN:MASTER:ペニス & 1
	Y += 1
;精液の汚れ
SIF STAIN:MASTER:ペニス & 4
	Y += 3
;アナルの汚れ
SIF STAIN:MASTER:ペニス & 8
	Y += 7
SIF STAIN:MASTER:ペニス & 16
	Y += 15

SIF TALENT:汚臭鈍感
	Y /= 3
SIF TALENT:汚臭敏感
	Y *= 2

;汚れあり
SIF Y
	A -= Y

;好感度
IF CFLAG:2 < 1000
	B = 0
	IF CFLAG:2 < 500
		B = 50
	ELSEIF CFLAG:2 < 750
		B = 20
	ELSEIF CFLAG:2 < 900
		B = 10
	ELSE
		B = 5
	ENDIF
	A -= B
;好感度
ELSEIF CFLAG:2 >= 1500
	B = 0
	IF CFLAG:2 < 2000
		B = 5
	ELSE
		B = CFLAG:2 / 200
	ENDIF
	A += B
ENDIF

;合意
IF TALENT:合意 == 0
	A -= 10
ENDIF

IF ((FLAG:23 & 1p27) == 0) && FLAG:2 != 1
	;ABL:欲望
	IF ABL:欲望
		SIF S
			PRINT  + 
		PRINTS ABLNAME:1
		PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 1,')
		S = 1
	ENDIF
	;ABL:奉仕精神
	IF ABL:奉仕精神
		SIF S
			PRINT  + 
		PRINTS ABLNAME:6
		PRINTV 'LV,ABL:奉仕精神,'(,ABL:奉仕精神 * 3,')
		S = 1
	ENDIF
	;ABL:精液中毒
	IF ABL:精液中毒
		SIF S
			PRINT  + 
		PRINTS ABLNAME:11
		PRINTV 'LV,ABL:精液中毒,'(,ABL:精液中毒 * 4,')
		S = 1
	ENDIF

	IF MARK:快楽依存
		SIF S
			PRINT  + 
		PRINTS MARKNAME:1
		PRINTV 'LV,MARK:快楽依存,'(,MARK:快楽依存 * 1,')
		S = 1
	ENDIF


	;PALAM:欲情
	IF PALAM:欲情 < PALAMLV:1
		L = 0
	ELSEIF PALAM:欲情 < PALAMLV:2
		L = 1
	ELSEIF PALAM:欲情 < PALAMLV:3
		L = 2
	ELSEIF PALAM:欲情 < PALAMLV:4
		L = 3
	ELSEIF PALAM:欲情 < PALAMLV:5
		L = 4
	ELSE
		L = 5
	ENDIF
	IF L
		SIF S
			PRINT  + 
		PRINTS PALAMNAME:5
		PRINTV 'LV,L,'(,L * 1,')
		S = 1
	ENDIF

	;恥じらい'
	IF TALENT:恥じらい
		PRINT  - 
		PRINTS TALENTNAME:35
		PRINTV '(,3,')
		S = 1
	ENDIF
	;汚臭鈍感
	IF TALENT:汚臭鈍感
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:61
		PRINTV '(,1,')
		S = 1
	ENDIF
	;汚臭敏感
	IF TALENT:汚臭敏感
		PRINT  - 
		PRINTS TALENTNAME:62
		PRINTV '(,3,')
		S = 1
	ENDIF
	;献身的
	IF TALENT:献身的
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:63
		PRINTV '(,6,')
		S = 1
	ENDIF
	;快感の否定
	IF TALENT:快感の否定
		PRINT  - 
		PRINTS TALENTNAME:71
		PRINTV '(,1,')
		S = 1
	ENDIF
	;男嫌い
	IF TALENT:男嫌い && TALENT:MASTER:オトコ
		PRINT  - 
		PRINTS TALENTNAME:82
		PRINTV '(,12,')
		S = 1
	ENDIF
	;恋慕
	IF TALENT:恋慕
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:85
		PRINTV '(,5,')
		S = 1
	ENDIF

	;調教者がふたなり
	IF TALENT:MASTER:ふたなり
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:121
		PRINTV '(,8,')
		S = 1
	ENDIF

	Y = 0
	;愛液の汚れ
	SIF STAIN:MASTER:ペニス & 1
		Y += 1
	;精液の汚れ
	SIF STAIN:MASTER:ペニス & 4
		Y += 3
	;アナルの汚れ
	SIF STAIN:MASTER:ペニス & 8
		Y += 7
	SIF STAIN:MASTER:ペニス & 16
		Y += 15

	SIF TALENT:汚臭鈍感
		Y /= 3
	SIF TALENT:汚臭敏感
		Y *= 2

	;汚れあり
	IF Y
		PRINT  - 
		;汚臭鈍感
		IF TALENT:汚臭鈍感
			PRINT 汚れあり、
			PRINTS TALENTNAME:61
		ELSEIF TALENT:汚臭敏感
			PRINT 汚れあり、
			PRINTS TALENTNAME:62
		ELSE
			PRINT 汚れあり
		ENDIF
		PRINTV '(,Y,')
		S = 1
	ENDIF
	;好感度
	IF CFLAG:2 < 1000
		B = 0
		PRINT  - 
		IF CFLAG:2 < 500
			B = 50
		ELSEIF CFLAG:2 < 750
			B = 20
		ELSEIF CFLAG:2 < 900
			B = 10
		ELSE
			B = 5
		ENDIF
		PRINT 好感度不足
		PRINTV '(,B,')
		S = 1
	;好感度
	ELSEIF CFLAG:2 >= 1500
		B = 0
		SIF S
			PRINT  + 
		IF CFLAG:2 < 2000
			B = 5
		ELSE
			B = CFLAG:2 / 200
		ENDIF
		PRINT 好感度
		PRINTV '(,B,')
		S = 1
	ENDIF

	;合意
	IF TALENT:合意 == 0
		PRINT  - 
		PRINTFORM %TALENTNAME:152%無し
		PRINTV '(,10,')
		S = 1
	ENDIF

	;合計を表示(105以上で実行)
	PRINT  = 
	PRINTV A

	SIF A < V
		PRINT  < 
	SIF A == V
		PRINT  = 
	SIF A > V
		PRINT  > 
	PRINT 実行値
	PRINTV V
	WAIT
ENDIF

