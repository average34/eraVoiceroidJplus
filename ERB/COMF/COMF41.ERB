@COM41
;鞭
;実際には苦痛があるためもっと体力気力をもっていかれる

PRINTL 鞭
STR:0 = 鞭
CALL TRAIN_MESSAGE_B


;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	LOSEBASE:0 += 100
	LOSEBASE:1 += 80
	
	SOURCE:14 = 1000
	
	;PALAM:苦痛をみる
	IF PALAM:9 < PALAMLV:1
		SOURCE:6 = 1000
	ELSEIF PALAM:9 < PALAMLV:2
		SOURCE:6 = 1500
	ELSEIF PALAM:9 < PALAMLV:3
		SOURCE:6 = 2200
	ELSEIF PALAM:9 < PALAMLV:4
		SOURCE:6 = 3000
	ELSEIF PALAM:9 >= PALAMLV:4
		SOURCE:6 = 4000
	ENDIF
	
	;ABL:サドっ気をみる
	IF ABL:PLAYER:15 == 0
		TIMES SOURCE:6 , 1.00
	ELSEIF ABL:PLAYER:15 == 1
		TIMES SOURCE:6 , 1.05
	ELSEIF ABL:PLAYER:15 == 2
		TIMES SOURCE:6 , 1.10
	ELSEIF ABL:PLAYER:15 == 3
		TIMES SOURCE:6 , 1.20
	ELSEIF ABL:PLAYER:15 == 4
		TIMES SOURCE:6 , 1.30
	ELSE
		SOURCE:6 = SOURCE:6 * (ABL:PLAYER:15 * 5 + 125) / 100
	ENDIF
	
	IF TALENT:122 == 0 && TALENT:PLAYER:122 == 0
		PRINTS EXPNAME:40
		PRINTL +2
		EXP:40 += 2
		EXP:PLAYER:40 += 2
	ELSEIF TALENT:122 == 1 && TALENT:PLAYER:122 == 1
		PRINTS EXPNAME:41
		PRINTL +2
		EXP:41 += 2
		EXP:PLAYER:41 += 2
	ENDIF
	
	SIF ASSIPLAY == 0 && ABL:8 >= 2
		TFLAG:30 += 1

ELSE
	;主導権が相手側
	LOSEBASE:0 += 20
	LOSEBASE:1 += 120
	
	SOURCE:14 = 1000
	
	;ABL:サドっ気をみる
	IF ABL:15 == 0
		SOURCE:20 = 700
		SOURCE:22 = 3500
	ELSEIF ABL:15 == 1
		SOURCE:20 = 1200
		SOURCE:22 = 2700
	ELSEIF ABL:15 == 2
		SOURCE:20 = 1800
		SOURCE:22 = 2000
	ELSEIF ABL:15 == 3
		SOURCE:20 = 2500
		SOURCE:22 = 1200
	ELSEIF ABL:15 == 4
		SOURCE:20 = 3200
		SOURCE:22 = 800
	ELSE
		SOURCE:20 = 2000 + (ABL:15 * 400)
		SOURCE:22 = 650 - (ABL:15 * 30)
		SIF SOURCE:22 < 0
			SOURCE:22 = 0
	ENDIF

	;反発感情が高いほど熱が入る
	IF MARK:3 == 1
		SOURCE:20 += 500
	ELSEIF MARK:3 == 2
		SOURCE:20 += 1200
	ELSEIF MARK:3 >= 3
		SOURCE:20 += 2500
	ENDIF
	
	;サド持ちは懐疑1/4
	SIF TALENT:83
		TIMES SOURCE:22 , 0.25

	;小悪魔持ちは懐疑1/2
	SIF TALENT:87
		TIMES SOURCE:22 , 0.50
	
	IF TALENT:122 == 0 && TALENT:PLAYER:122 == 0
		PRINTS EXPNAME:40
		PRINTL +2
		EXP:40 += 2
		EXP:PLAYER:40 += 2
	ELSEIF TALENT:122 == 1 && TALENT:PLAYER:122 == 1
		PRINTS EXPNAME:41
		PRINTL +2
		EXP:41 += 2
		EXP:PLAYER:41 += 2
	ENDIF
	
	SIF ASSIPLAY == 0 && ABL:15 >= 1
		TFLAG:30 += 1
	
	;[サド]取得用カウント
	SIF ASSIPLAY == 0
		TFLAG:61 += 1
ENDIF


EXP:50 += 1
PRINTFORML %EXPNAME:50%＋１

RETURN 1

