@COM_ABLE38
;尻素股実行判定
;COM_ABLE共通判定
CALL COM_ABLE_COMMON, SELECTCOM
SIF RESULT == 0
	RETURN 0
;ウフフ中じゃないと無理
SIF TFLAG:44 == 0
	RETURN 0
;調教者がオトコか半人半妖かふたなりじゃないとダメ
SIF TALENT:PLAYER:半人半妖 == 0 && TALENT:PLAYER:ふたなり == 0 && TALENT:PLAYER:オトコ == 0
	RETURN 0
;潤滑が高くないとダメ
SIF PALAM:3 < 2000
	RETURN 0
;アナルバイブ挿入中はダメ
SIF TEQUIP:13
	RETURN 0
;アナルビーズ使用中は不可
SIF TEQUIP:19
	RETURN 0
;浣腸＋アナルプラグ使用中はダメ
SIF TEQUIP:46
	RETURN 0
;空気浣腸使用中は不可
SIF TEQUIP:47
	RETURN 0
;バルーン使用中は不可
SIF TEQUIP:48
	RETURN 0
;電極使用中は不可
SIF TEQUIP:49
	RETURN 0
;クスコ挿入中はダメ
SIF TEQUIP:61
	RETURN 0
;巨峰挿入中は不可
SIF TEQUIP:70
	RETURN 0
;縄使用中は不可
SIF TEQUIP:44
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:90
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
IF TEQUIP:58
SIF ITEM:プレイマット == 0 && NOITEM == 0
	RETURN 0
ENDIF
RETURN 1

@COM38
;尻素股

PRINTL 尻素股

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

STR:0 = 尻素股
CALL TRAIN_MESSAGE_B

;-------------------------------------------------
;実行決定
;-------------------------------------------------
;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
SIF TALENT:PLAYER:半人半妖 || TALENT:PLAYER:オトコ  || TALENT:PLAYER:ふたなり
	BASE:PLAYER:射精 += CALC_EJAC(SELECTCOM, PLAYER)

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
;尻素股
LOSEBASE:体力 += 40
LOSEBASE:気力 += 130

SOURCE:屈従 = 1200
SOURCE:逸脱 = 400

;上のほうで計算した汚れデータ
SOURCE:不潔 = Y*10 + 60

;ABL:奉仕精神をみる
IF ABL:奉仕精神 == 0
	SOURCE:性行動 = 200
	SOURCE:達成感 = 100
	TIMES SOURCE:不潔 , 4.00
ELSEIF ABL:奉仕精神 == 1
	SOURCE:性行動 = 250
	SOURCE:達成感 = 180
	TIMES SOURCE:不潔 , 2.50
ELSEIF ABL:奉仕精神 == 2
	SOURCE:性行動 = 300
	SOURCE:達成感 = 250
	TIMES SOURCE:不潔 , 1.50
ELSEIF ABL:奉仕精神 == 3
	SOURCE:性行動 = 350
	SOURCE:達成感 = 350
	TIMES SOURCE:不潔 , 1.00
ELSEIF ABL:奉仕精神 == 4
	SOURCE:性行動 = 400
	SOURCE:達成感 = 500
	TIMES SOURCE:不潔 , 0.50
ELSE
	SOURCE:性行動 = 325 + (ABL:奉仕精神 * 25)
	SOURCE:達成感 = 50 + (ABL:奉仕精神 * 150)
	TIMES SOURCE:不潔 , 0.10
ENDIF
	
;ABL:技巧をみる
IF ABL:技巧 == 0
	TIMES SOURCE:性行動 , 0.70
	TIMES SOURCE:達成感 , 0.70
ELSEIF ABL:技巧 == 1
	TIMES SOURCE:性行動 , 0.90
	TIMES SOURCE:達成感 , 0.90
ELSEIF ABL:技巧 == 2
	TIMES SOURCE:性行動 , 1.00
	TIMES SOURCE:達成感 , 1.00
ELSEIF ABL:技巧 == 3
	TIMES SOURCE:性行動 , 1.20
	TIMES SOURCE:達成感 , 1.20
ELSEIF ABL:技巧 == 4
	TIMES SOURCE:性行動 , 1.40
	TIMES SOURCE:達成感 , 1.40
ELSE
	SOURCE:性行動 = SOURCE:性行動 * (ABL:技巧 * 10 + 110) / 100
	SOURCE:達成感 = SOURCE:達成感 * (ABL:技巧 * 10 + 110) / 100
ENDIF

;ABL:A感覚をみる
IF ABL:Ａ感覚 == 0
	SOURCE:快Ａ = 0
ELSEIF ABL:Ａ感覚 == 1
	SOURCE:快Ａ = 10
ELSEIF ABL:Ａ感覚 == 2
	SOURCE:快Ａ = 50
ELSEIF ABL:Ａ感覚 == 3
	SOURCE:快Ａ = 200
ELSEIF ABL:Ａ感覚 == 4
	SOURCE:快Ａ = 400
ELSE
	SOURCE:快Ａ = (ABL:Ａ感覚 * 600) - 1500
ENDIF

;PALAM:潤滑
IF PALAM:3 < PALAMLV:1
	TIMES SOURCE:快Ｃ , 0.30
	TIMES SOURCE:性行動 , 0.60
ELSEIF PALAM:3 < PALAMLV:2
	TIMES SOURCE:快Ｃ , 0.60
	TIMES SOURCE:性行動 , 0.80
ELSEIF PALAM:3 < PALAMLV:3
	TIMES SOURCE:快Ｃ , 1.00
	TIMES SOURCE:性行動 , 1.00
ELSEIF PALAM:3 < PALAMLV:4
	TIMES SOURCE:快Ｃ , 1.50
	TIMES SOURCE:性行動 , 1.20
ELSEIF PALAM:3 < PALAMLV:5
	TIMES SOURCE:快Ｃ , 2.00
	TIMES SOURCE:性行動 , 1.40
ELSE
	TIMES SOURCE:快Ｃ , 2.50
	TIMES SOURCE:性行動 , 1.60
ENDIF

;主導権判定
;パートナーに主導権
IF TFLAG:45 == 1
	;ABL:サドっ気をみる
	IF ABL:サドっ気 == 0
		SOURCE:征服 = 800
	ELSEIF ABL:サドっ気 == 1
		SOURCE:征服 = 1100
	ELSEIF ABL:サドっ気 == 2
		SOURCE:征服 = 1400
	ELSEIF ABL:サドっ気 == 3
		SOURCE:征服 = 1800
	ELSEIF ABL:サドっ気 == 4
		SOURCE:征服 = 2300
	ELSE
		SOURCE:征服 = 1000 + (ABL:サドっ気 * 350)
	ENDIF
;それ以外
ELSE
	;ABL:従順をみる
	IF ABL:親密 <= 1
		SOURCE:受動 += 300 + (ABL:親密 * 50)
	ELSEIF ABL:親密 <= 3
		SOURCE:受動 += 600 + (ABL:親密 * 100)
	ELSEIF ABL:親密 <= 5
		SOURCE:受動 += 800 + (ABL:親密 * 120)
	ELSEIF ABL:親密 <= 8
		SOURCE:受動 += 1000 + (ABL:親密 * 140)
	ELSEIF ABL:親密 <= 11
		SOURCE:受動 += 1200 + (ABL:親密 * 160)
	ELSE
		SOURCE:受動 += 1500 + (ABL:親密 * 180)
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:10 + 5
SIF TALENT:恋慕 || TALENT:恋人
	A += 2
SIF TALENT:親愛
	A += 3
;合意がないと好感度が下がる
IF TALENT:合意 == 0 && TFLAG:48 == 0
	A -= 8
	SOURCE:反感追加 += 300
ENDIF
TFLAG:30 += A
TFLAG:37 += A / 2

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK
;;オーバーフロー対策に、値が大きすぎる場合と、負の場合は調整を加える
;IF BASE:PLAYER:射精 > 100000000 || BASE:PLAYER:射精 < 0
;	BASE:PLAYER:射精 = 100000000
;ENDIF
;S = BASE:PLAYER:射精
;EJAC = MAXBASE:PLAYER:射精
;
;IF  S > EJAC * 2
;	E = 2
;ELSEIF S > EJAC
;	E = 1
;ELSE
;	E = 0
;ENDIF

IF E
;射精している
	TIMES SOURCE:性行動 , 2.00

	;ABL:精液中毒をみる
	IF ABL:精液中毒 == 0
		SOURCE:中毒充足 = 0
		TIMES SOURCE:達成感 , 1.50
		TIMES SOURCE:屈従, 1.40
	ELSEIF ABL:精液中毒 == 1
		SOURCE:中毒充足 = 200
		TIMES SOURCE:達成感 , 2.00
		TIMES SOURCE:屈従, 1.00
	ELSEIF ABL:精液中毒 == 2
		SOURCE:中毒充足 = 400
		TIMES SOURCE:達成感 , 2.50
		TIMES SOURCE:屈従, 0.80
	ELSEIF ABL:精液中毒 == 3
		SOURCE:中毒充足 = 700
		TIMES SOURCE:達成感 , 3.00
		TIMES SOURCE:屈従, 0.50
	ELSEIF ABL:精液中毒 == 4
		SOURCE:中毒充足 = 1000
		TIMES SOURCE:達成感 , 4.00
		TIMES SOURCE:屈従, 0.20
	ELSE
		SOURCE:中毒充足 = 250 + (ABL:精液中毒 * 250)
		SOURCE:達成感 = SOURCE:達成感 * (ABL:精液中毒 * 50 + 250) / 100
		TIMES SOURCE:屈従, 0.00
	ENDIF

	;射精先タイプの設定
	TFLAG:703 = 5
	;射精先選択の可・不可
	TFLAG:704 = 0
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;;大量射精
;IF E == 2
;	TIMES SOURCE:中毒充足 , 1.50
;	TIMES SOURCE:達成感 , 1.20
;
;	EXP:PLAYER:射精経験 += 2
;	EXP:精液経験 += 2
;	PRINTL 大量射精
;	PRINTL 精液経験＋２
;	;精液汚れ
;	STAIN:PLAYER:ペニス |= 4
;
;	BASE:PLAYER:射精 -= EJAC*2
;	SIF BASE:PLAYER:射精 >= EJAC
;		BASE:PLAYER:射精 = EJAC-1
;
;	;素股で射精させたフラグ
;	TFLAG:9 = 2
;;通常の射精
;ELSEIF E == 1
;	EXP:PLAYER:射精経験 += 1
;	EXP:精液経験 += 1
;	PRINTL 射精
;	PRINTL 精液経験＋１
;	;精液汚れ
;	STAIN:PLAYER:ペニス |= 4
;
;	BASE:PLAYER:射精 -= EJAC
;	SIF BASE:PLAYER:射精 >= EJAC
;		BASE:PLAYER:射精 = EJAC-1
;
;	;素股で射精させたフラグ
;	TFLAG:9 = 1
;ENDIF

;奴隷のA⇔調教者のＰの汚れが移動
STAIN:アナル |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:アナル

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	PRINTS EXPNAME:40
	PRINTL +7
	EXP:レズ経験 += 7
	EXP:PLAYER:レズ経験 += 7
ELSEIF TALENT:オトコ == 1 && TALENT:PLAYER:オトコ == 1
	PRINTS EXPNAME:41
	PRINTL +7
	EXP:ＢＬ経験 += 7
	EXP:PLAYER:ＢＬ経験 += 7
ENDIF

SIF ASSIPLAY == 0
	TFLAG:30 += 1

TFLAG:100 = 1

;既成事実2に相当
TFLAG:200 = 2

;調教者がふたなり
IF TALENT:PLAYER:ふたなり
	SOURCE:屈従 /= 2
ENDIF

RETURN 1

;--------------------------------------------------
;調教時メッセージ
;--------------------------------------------------
;尻素股
@TRAIN_MESSAGE_COM38
;パートナーに主導権
IF TFLAG:45
	PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%に跨り、ペニスを尻の割れ目ではさみ、
	;恋慕＋淫乱＋欲情Lv5以上
	IF TALENT:恋慕 && TALENT:淫乱 && PALAM:5 > PALAMLV:6
		PRINTFORM 快楽に溺れきった表情で%CALLNAME:PLAYER%を見つめながら
	ELSEIF TALENT:恋慕 && PALAM:5 > PALAMLV:6
		PRINT 今にも蕩けそうな表情を浮かべながら
	ELSEIF TALENT:淫乱 && PALAM:5 > PALAMLV:6
		PRINTFORM 懇願するような眼差しで
	ELSEIF PALAM:5 > PALAMLV:5
		PRINT 口の端から涎を溢しながら
	ELSEIF TALENT:恋慕 && TALENT:淫乱
		PRINTFORM 陶酔しきった瞳で%CALLNAME:PLAYER%を見つめながら
	ELSEIF TALENT:恋慕
		PRINT 幸せそうな表情を浮かべながら
	ELSEIF TALENT:淫乱
		PRINT 淫猥な目つきで挑発しながら
	ELSEIF TALENT:恥薄い
		PRINT 大胆に腰を動かしながら
	ELSEIF TALENT:感情乏しい
		PRINT うっすらと上気した顔で
	ELSE
		PRINT 恥ずかしそうに
	ENDIF
	PRINTFORM ペニスの裏側に手を添えて、
	PRINT 自らのお尻の
	;小柄体型
	IF TALENT:小柄体型
		PRINT 小さなワレメ
	ELSE
		PRINT ワレメ
	ENDIF
	PRINTL に擦りつけている…
;プレイヤーに主導権
ELSE
	PRINTFORM %CALLNAME:PLAYER%は自分のペニスを
	;小柄体型&ひんぬー
	SIF TALENT:小柄体型 && TALENT:貧乳
		PRINT 肉付きの薄い
	PRINTFORML %CALLNAME:TARGET%のお尻に挟んで激しく擦った
	IF TALENT:恋慕 == 0 && TALENT:親愛 == 0 && TALENT:相愛 == 0 && MARK:反発感情 > 1
		PRINTFORML %CALLNAME:TARGET%はあまりの屈辱に、目に涙を浮かべていた…
	ELSEIF ASSIPLAY == 0 && TALENT:MASTER:虜 && TALENT:主人 && PALAM:5 > PALAMLV:5
		PRINTFORML %CALLNAME:TARGET%は巧みに腰を操り、%CALLNAME:PLAYER%の射精感を引き出した…
	ELSEIF ASSIPLAY == 0 && TALENT:恋慕 && PALAM:5 > PALAMLV:5
		PRINTFORML %CALLNAME:TARGET%は、愛する人にお尻を使われて気持ちが高ぶっているようだ…
	ELSEIF PALAM:8 > PALAMLV:5
		PRINTFORML %CALLNAME:TARGET%は、さすがにお尻を使われることは恥ずかしいと思っているようだ…
	ELSE
		PRINTFORML %CALLNAME:TARGET%は、%CALLNAME:PLAYER%のなすがままにされていた…
	ENDIF
ENDIF

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_38
A = 0
S = 0
V = 75

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;ABL:欲望
IF ABL:欲望
	A += ABL:欲望 * 2
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	A += ABL:奉仕精神 * 4
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	A += ABL:精液中毒 * 1
ENDIF

IF MARK:快楽依存
	A += MARK:快楽依存 * 2
ENDIF


;PALAM:欲情
IF PALAM:5 < PALAMLV:1
	L = 0
ELSEIF PALAM:5 < PALAMLV:2
	L = 1
ELSEIF PALAM:5 < PALAMLV:3
	L = 2
ELSEIF PALAM:5 < PALAMLV:4
	L = 3
ELSEIF PALAM:5 < PALAMLV:5
	L = 4
ELSE
	L = 5
ENDIF
IF L
	A += L * 3
ENDIF

;恥じらい
IF TALENT:恥じらい
	A -= 1
ENDIF
;快感の否定
IF TALENT:快感の否定
	A -= 3
ENDIF
;男嫌い
IF TALENT:男嫌い && TALENT:PLAYER:オトコ
	A -= 7
ENDIF
;恋慕
IF TALENT:恋慕 && ASSIPLAY == 0
	A += 3
	S = 3
ENDIF

;調教者がふたなり
IF TALENT:PLAYER:ふたなり
	A += 8
ENDIF

;媚薬
IF TEQUIP:21
	A += 6
ENDIF

Y = 0
;愛液の汚れ
SIF STAIN:PLAYER:ペニス & 1
	Y += 1
;精液の汚れ
SIF STAIN:PLAYER:ペニス & 4
	Y += 3
;アナルの汚れ
SIF STAIN:PLAYER:ペニス & 8
	Y += 7
SIF STAIN:PLAYER:ペニス & 16
	Y += 15

SIF TALENT:汚臭鈍感
	Y /= 3
SIF TALENT:汚臭敏感
	Y *= 2

;素股では汚れの影響が少なめ
Y /= 3

;汚れあり
IF Y
	A -= Y
	;汚臭鈍感
	IF TALENT:汚臭鈍感
	ELSEIF TALENT:汚臭敏感
	ELSE
	ENDIF
ENDIF
;好感度
IF CFLAG:2 < 1000
	B = 0
	IF CFLAG:2 < 500
		B = 50
	ELSEIF CFLAG:2 < 750
		B = 20
	ELSEIF CFLAG:2 < 900
		B = 10
	ELSE
		B = 5
	ENDIF
	A -= B
ENDIF
;好感度
IF CFLAG:2 >= 1500
	B = 0
	IF CFLAG:2 < 2000
		B = 5
	ELSE
		B = CFLAG:2 / 200
	ENDIF
	A += B
ENDIF

;合意
IF TALENT:合意 == 0
	A -= 5
ENDIF

IF ((FLAG:23 & 1p27) == 0) && FLAG:2 != 1
	;ABL:欲望
	IF ABL:欲望
		SIF S
			PRINT  + 
		PRINTS ABLNAME:1
		PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 2,')
		S = 1
	ENDIF
	;ABL:奉仕精神
	IF ABL:奉仕精神
		SIF S
			PRINT  + 
		PRINTS ABLNAME:6
		PRINTV 'LV,ABL:奉仕精神,'(,ABL:奉仕精神 * 4,')
		S = 1
	ENDIF
	;ABL:精液中毒
	IF ABL:精液中毒
		SIF S
			PRINT  + 
		PRINTS ABLNAME:11
		PRINTV 'LV,ABL:精液中毒,'(,ABL:精液中毒 * 1,')
		S = 1
	ENDIF

	IF MARK:快楽依存
		SIF S
			PRINT  + 
		PRINTS MARKNAME:1
		PRINTV 'LV,MARK:快楽依存,'(,MARK:快楽依存 * 2,')
		S = 1
	ENDIF


	;PALAM:欲情
	IF PALAM:5 < PALAMLV:1
		L = 0
	ELSEIF PALAM:5 < PALAMLV:2
		L = 1
	ELSEIF PALAM:5 < PALAMLV:3
		L = 2
	ELSEIF PALAM:5 < PALAMLV:4
		L = 3
	ELSEIF PALAM:5 < PALAMLV:5
		L = 4
	ELSE
		L = 5
	ENDIF
	IF L
		SIF S
			PRINT  + 
		PRINTS PALAMNAME:5
		PRINTV 'LV,L,'(,L * 3,')
		S = 1
	ENDIF

	;恥じらい'
	IF TALENT:恥じらい
		PRINT  - 
		PRINTS TALENTNAME:35
		PRINTV '(,1,')
		S = 1
	ENDIF
	;快感の否定
	IF TALENT:快感の否定
		PRINT  - 
		PRINTS TALENTNAME:71
		PRINTV '(,3,')
		S = 1
	ENDIF
	;男嫌い
	IF TALENT:男嫌い && TALENT:PLAYER:オトコ
		PRINT  - 
		PRINTS TALENTNAME:82
		PRINTV '(,7,')
		S = 1
	ENDIF
	;恋慕
	IF TALENT:恋慕 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:85
		PRINTV '(,3,')
		S = 3
	ENDIF

	;調教者がふたなり
	IF TALENT:PLAYER:ふたなり
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:121
		PRINTV '(,8,')
		S = 1
	ENDIF

	;媚薬
	IF TEQUIP:21
		SIF S
			PRINT  + 
		PRINTS ITEMNAME:21
		PRINTV '(,6,')
		S = 1
	ENDIF

	Y = 0
	;愛液の汚れ
	SIF STAIN:PLAYER:ペニス & 1
		Y += 1
	;精液の汚れ
	SIF STAIN:PLAYER:ペニス & 4
		Y += 3
	;アナルの汚れ
	SIF STAIN:PLAYER:ペニス & 8
		Y += 7
	SIF STAIN:PLAYER:ペニス & 16
		Y += 15

	SIF TALENT:汚臭鈍感
		Y /= 3
	SIF TALENT:汚臭敏感
		Y *= 2

	;素股では汚れの影響が少なめ
	Y /= 3

	;汚れあり
	IF Y
		PRINT  - 
		;汚臭鈍感
		IF TALENT:汚臭鈍感
			PRINT 汚れあり、
			PRINTS TALENTNAME:61
		ELSEIF TALENT:汚臭敏感
			PRINT 汚れあり、
			PRINTS TALENTNAME:62
		ELSE
			PRINT 汚れあり
		ENDIF
		PRINTV '(,Y,')
		S = 1
	ENDIF
	;好感度
	IF CFLAG:2 < 1000
		B = 0
		PRINT  - 
		IF CFLAG:2 < 500
			B = 50
		ELSEIF CFLAG:2 < 750
			B = 20
		ELSEIF CFLAG:2 < 900
			B = 10
		ELSE
			B = 5
		ENDIF
		PRINT 好感度不足
		PRINTV '(,B,')
		S = 1
	ENDIF
	;好感度
	IF CFLAG:2 >= 1500
		B = 0
		SIF S
			PRINT  + 
		IF CFLAG:2 < 2000
			B = 5
		ELSE
			B = CFLAG:2 / 200
		ENDIF
		PRINT 好感度
		PRINTV '(,B,')
		S = 1
	ENDIF

	;合意
	IF TALENT:合意 == 0
		PRINT  - 
		PRINTFORM %TALENTNAME:152%無し
		PRINTV '(,5,')
		S = 1
	ENDIF

	;合計を表示(75以上で実行)
	PRINT  = 
	PRINTV A

	SIF A < V
		PRINT  < 
	SIF A == V
		PRINT  = 
	SIF A > V
		PRINT  > 
	PRINT 実行値
	PRINTV V
	WAIT
ENDIF
