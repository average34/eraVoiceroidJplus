@COM301
;会話

PRINTL 会話
STR:0 = 会話
CALL GET_SUCCESS_RATE
B = 95 + A / 10
SIF B > 99
	B = 99
A = RAND:100
IF A <= 9
	;10％で大成功
	TFLAG:18 = 1
ELSEIF A >= B
	;5～1％で失敗
	TFLAG:18 = -1
ELSE
	;残りは成功
	TFLAG:18 = 0
ENDIF

CALL TRAIN_MESSAGE_B

LOSEBASE:1 += 50

;固定で獲得するソース
SOURCE:18 = 200

;ABL:従順をみる
IF ABL:0 <= 1
	SOURCE:18 += (ABL:0 * 60)
ELSEIF ABL:0 <= 3
	SOURCE:18 += 200 + (ABL:0 * 60)
ELSEIF ABL:0 <= 5
	SOURCE:18 += 500 + (ABL:0 * 60)
ELSEIF ABL:0 <= 8
	SOURCE:18 += 750 + (ABL:0 * 75)
ELSEIF ABL:0 <= 11
	SOURCE:18 += 1000 + (ABL:0 * 75)
ELSE
	SOURCE:18 += 2000 + (ABL:0 * 30)
ENDIF

;好感度をみる
IF CFLAG:2 <= 500
	SOURCE:18 += CFLAG:2
ELSEIF CFLAG:2 <= 5000
	SOURCE:18 += 500 + (CFLAG:2 - 500) / 3
ELSE
	SOURCE:18 += 2000 + (CFLAG:2 - 5000) / 5
ENDIF
SIF SOURCE:18 < 0
	SOURCE:18 = 0

;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:従順をみる
	IF ABL:0 == 0
		SOURCE:21 = 200
	ELSEIF ABL:0 == 1
		SOURCE:21 = 300
	ELSEIF ABL:0 == 2
		SOURCE:21 = 400
	ELSEIF ABL:0 == 3
		SOURCE:21 = 500
	ELSEIF ABL:0 == 4
		SOURCE:21 = 600
	ELSE
		SOURCE:21 = 500 + (ABL:0 * 50)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:15 == 0
		SOURCE:20 = 300
	ELSEIF ABL:15 == 1
		SOURCE:20 = 400
	ELSEIF ABL:15 == 2
		SOURCE:20 = 500
	ELSEIF ABL:15 == 3
		SOURCE:20 = 600
	ELSEIF ABL:15 == 4
		SOURCE:20 = 700
	ELSE
		SOURCE:20 = 300 + (ABL:15 * 100)
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:5 - 1
TFLAG:30 += A

IF TFLAG:18 == -1
	TIMES SOURCE:18 , 0.10
	TIMES SOURCE:20 , 0.50
	TIMES SOURCE:21 , 0.50
	TFLAG:30 -= 3
	TFLAG:37 -= 5
ELSEIF TFLAG:18 == 0
	TIMES SOURCE:18 , 1.00
	TIMES SOURCE:20 , 1.00
	TIMES SOURCE:21 , 1.00
	TFLAG:37 += A
ELSE
	TIMES SOURCE:18 , 2.00
	TIMES SOURCE:20 , 2.00
	TIMES SOURCE:21 , 2.00
	TFLAG:30 += 2
	TFLAG:37 += 10 + A
ENDIF

RETURN 1

