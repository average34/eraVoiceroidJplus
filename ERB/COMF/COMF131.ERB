@COM_ABLE131
;拘束される実行判定
;COM_ABLE共通判定
CALL COM_ABLE_COMMON, SELECTCOM
SIF RESULT == 0
	RETURN 0
;ウフフ中じゃないと無理
SIF TFLAG:44 == 0
	RETURN 0
;触手調教中はダメ
SIF TEQUIP:90
	RETURN 0
;異常経験フィルタ
SIF (FLAG:61 & 1) == 0
	RETURN 0
;鰻風呂入浴中、蟲風呂入浴中、荊で緊縛中、蜘蛛の糸で緊縛中はダメ
SIF TEQUIP:72 || TEQUIP:73 || TEQUIP:78 || TEQUIP:92
	RETURN 0
;解除はいつでも可能
SIF TEQUIP:MASTER:44
	RETURN 1
;アイテムを持っているか、アイテムなし設定で無いとダメ
SIF ITEM:14 == 0 && NOITEM == 0
	RETURN 0
;パートナーの技巧が3無いとできない
SIF ABL:TARGET:技巧 <= 2
	RETURN 0
;お医者プレイ中はダメ
SIF TEQUIP:62
	RETURN 0
;主導権がパートナーにないとダメ
SIF TFLAG:45 == 0
	RETURN 0
RETURN 1

@COM131
;拘束される

PRINTL 拘束される
STR:0 = 拘束される

;緊縛経験が高いと消費減少
IF EXP:PLAYER:緊縛経験 < EXPLV:3
	LOSEBASE:体力 += 50
	LOSEBASE:気力 += 200
ELSEIF EXP:PLAYER:緊縛経験 < EXPLV:4
	LOSEBASE:体力 += 30
	LOSEBASE:気力 += 150
ELSE
	LOSEBASE:体力 += 10
	LOSEBASE:気力 += 100
ENDIF

A = 250
SOURCE:征服 = 800
SOURCE:達成感 = 800
SOURCE:懐疑 = 500
SOURCE:逸脱 = 500

;PALAM:欲情をみる
IF PALAM:5 < PALAMLV:1
	TIMES A , 0.60
ELSEIF PALAM:5 < PALAMLV:2
	TIMES A , 0.80
ELSEIF PALAM:5 < PALAMLV:3
	TIMES A , 1.00
ELSEIF PALAM:5 < PALAMLV:4
	TIMES A , 1.20
ELSEIF PALAM:5 >= PALAMLV:4
	TIMES A , 1.40
ENDIF

;ABL:従順をみる
IF ABL:親密 == 0
	TIMES A , 0.20
ELSEIF ABL:親密 == 1
	TIMES A , 0.50
ELSEIF ABL:親密 == 2
	TIMES A , 0.80
ELSEIF ABL:親密 == 3
	TIMES A , 1.10
ELSEIF ABL:親密 == 4
	TIMES A , 1.40
ELSE
	A = A * (ABL:親密 * 7 + 115) / 100
ENDIF

;ABL:サドっ気をみる
IF ABL:サドっ気 == 0
	TIMES A , 0.80
ELSEIF ABL:サドっ気 == 1
	TIMES A , 1.00
ELSEIF ABL:サドっ気 == 2
	TIMES A , 1.30
ELSEIF ABL:サドっ気 == 3
	TIMES A , 1.60
ELSEIF ABL:サドっ気 == 4
	TIMES A , 2.00
ELSE
	A = A * (ABL:サドっ気 * 50 + 50) / 100
ENDIF

;ABL:マゾっ気をみる
IF ABL:PLAYER:マゾっ気 == 0
	TIMES SOURCE:征服 , 1.00
ELSEIF ABL:PLAYER:マゾっ気 == 1
	TIMES SOURCE:征服 , 1.05
ELSEIF ABL:PLAYER:マゾっ気 == 2
	TIMES SOURCE:征服 , 1.10
ELSEIF ABL:PLAYER:マゾっ気 == 3
	TIMES SOURCE:征服 , 1.20
ELSEIF ABL:PLAYER:マゾっ気 == 4
	TIMES SOURCE:征服 , 1.30
ELSE
	SOURCE:征服 = SOURCE:征服 * (ABL:PLAYER:マゾっ気 * 5 + 125) / 100
ENDIF

;倒錯的
SIF TALENT:倒錯的
	TIMES A , 2.00

SOURCE:征服 += A
SOURCE:達成感 += A
SOURCE:懐疑 += A
SOURCE:逸脱 += A

EXP:PLAYER:緊縛経験 += 5
PRINTFORML 緊縛経験＋５(%CALLNAME:PLAYER%)

CALL TRAIN_MESSAGE_B

;縄の着脱
IF TEQUIP:PLAYER:44
	TEQUIP:PLAYER:44 = 0
ELSE
	TEQUIP:PLAYER:44 = 1
ENDIF

RETURN 1

@TRAIN_MESSAGE_COM131
;縄の着脱
IF TEQUIP:PLAYER:44
	PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%から縄をほどいた
ELSE
	PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%を縄で縛った
ENDIF

@EQUIP_COM131
;緊縛中

PRINTFORML ＜緊縛中（%CALLNAME:PLAYER%）＞

;緊縛経験が高いと消費減少
IF EXP:PLAYER:緊縛経験 < EXPLV:3
	LOSEBASE:体力 += 20
	LOSEBASE:気力 += 125
ELSEIF EXP:PLAYER:緊縛経験 < EXPLV:4
	LOSEBASE:体力 += 15
	LOSEBASE:気力 += 100
ELSE
	LOSEBASE:体力 += 10
	LOSEBASE:気力 += 75
ENDIF

;ABL:サドっ気をみる
IF ABL:サドっ気 == 0
	A = 60
ELSEIF ABL:マゾっ気 == 1
	A = 180
ELSEIF ABL:マゾっ気 == 2
	A = 300
ELSEIF ABL:マゾっ気 == 3
	A = 480
ELSEIF ABL:マゾっ気 == 4
	A = 700
ELSE
	A = 475 + (ABL:マゾっ気 * 75)
ENDIF

;倒錯的
SIF TALENT:倒錯的
	TIMES A , 2.00

;PALAM:欲情をみる
IF PALAM:5 < PALAMLV:1
	TIMES A , 0.60
ELSEIF PALAM:5 < PALAMLV:2
	TIMES A , 0.80
ELSEIF PALAM:5 < PALAMLV:3
	TIMES A , 1.00
ELSEIF PALAM:5 < PALAMLV:4
	TIMES A , 1.20
ELSEIF PALAM:5 >= PALAMLV:4
	TIMES A , 1.40
ENDIF

SOURCE:征服 += A
SOURCE:達成感 += A
SOURCE:懐疑 += A
SOURCE:逸脱 += A


IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	PRINTS EXPNAME:40
	PRINTL +1
	EXP:レズ経験 += 1
	EXP:PLAYER:レズ経験 += 1
ELSEIF TALENT:オトコ == 1 && TALENT:PLAYER:オトコ == 1
	PRINTS EXPNAME:41
	PRINTL +1
	EXP:ＢＬ経験 += 1
	EXP:PLAYER:ＢＬ経験 += 1
ENDIF

SIF ASSIPLAY == 0 && ABL:サドっ気 >= 2
	TFLAG:30 += 1

EXP:PLAYER:緊縛経験 += 2
PRINTFORML 緊縛経験＋２(%CALLNAME:PLAYER%)
EXP:PLAYER:異常経験 += 1
PRINTFORML %EXPNAME:50%＋１(%CALLNAME:PLAYER%)

RETURN 1
