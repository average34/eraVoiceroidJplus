@COM_ABLE256
;挿入子宮口責め実行判定
;COM_ABLE共通判定
CALL COM_ABLE_COMMON, SELECTCOM
SIF RESULT == 0
	RETURN 0
;正常位実行判定流用
;ウフフ中じゃないと無理
SIF TFLAG:44 == 0
	RETURN 0
;バイブ挿入中はダメ
SIF TEQUIP:11
	RETURN 0
;クスコ挿入中はダメ
SIF TEQUIP:61
	RETURN 0
;焼芋入中は不可
SIF TEQUIP:71
	RETURN 0
;オトコだとダメ
SIF TALENT:オトコ
	RETURN 0
;小人体型持ちは、双方が調教者と調教対象ならOK
;それ以外の場合は主人が禁断の知識持ちじゃないとダメ
SIF TALENT:小人体型 && (TALENT:MASTER:禁断の知識 == 0 && TALENT:PLAYER:小人体型 == 0)
	RETURN 0
;調教者がオトコ、半人半妖、ふたなり、もしくはペニスバンドがないとダメ
SIF TALENT:PLAYER:半人半妖 == 0 && TALENT:PLAYER:ふたなり == 0 && TALENT:PLAYER:オトコ == 0 && ITEM:PBAND == 0
	RETURN 0
;処女で、従順4以下・レズっ気4以下のASSIの場合正常位はできない（サドならＯＫ）
IF EXP:Ｖ経験 == 0 && ASSIPLAY
	SIF (ABL:ASSI:親密 <= 4 || ABL:ASSI:レズっ気 <= 4) && TALENT:ASSI:サド == 0
		RETURN 0
ENDIF
;潤滑不足で、従順3以下・レズっ気3以下のASSIの場合正常位はできない（サドならＯＫ）
IF PALAM:潤滑 < PALAMLV:2 && ASSIPLAY
	SIF (ABL:ASSI:親密 <= 3 || ABL:ASSI:レズっ気 <= 3) && TALENT:ASSI:サド == 0
		RETURN 0
ENDIF
;触手調教中はダメ
SIF TEQUIP:90
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
IF TEQUIP:58
	SIF ITEM:プレイマット == 0 && NOITEM == 0
		RETURN 0
ENDIF
;前回コマンドが正常位・後背位・対面座位・背面座位・騎乗位・乱れ牡丹・挿入Ｇスポ責め・挿入子宮口責めでないとダメ
SIF PREVCOM != 20 && PREVCOM != 21 && PREVCOM != 24 && PREVCOM != 25 && PREVCOM != 34 && PREVCOM != 88 && PREVCOM != 255 && PREVCOM != 256
	RETURN 0
;調教者の技巧3以上かつ前回コマンド実行者が今回も実行していないとダメ
IF ABL:PLAYER:技巧 > 2 && ((ASSIPLAY && TFLAG:50) || (ASSIPLAY == 0 && TFLAG:50 == 0))
	RETURN 1
ELSE
	RETURN 0
ENDIF
RETURN 1

@COM256
;挿入子宮口責め

TFLAG:42 = 0
IF SELECTCOM == 34
	PRINTL 騎乗子宮口責め
	STR:0 = 騎乗子宮口責め
ELSE
	PRINTL 挿入子宮口責め
	STR:0 = 挿入子宮口責め
ENDIF

;派生元コマンドをPREVCOMに登録、SELECTCOMをこのコマンド番号に変更してからCALL TRAIN_MESSAGE_B
SIF SELECTCOM != 256
	PREVCOM = SELECTCOM
SELECTCOM = 256

CALL TRAIN_MESSAGE_B

;V経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:19 = 1
SIF TALENT:恋慕 && ASSIPLAY == 0 && EXP:Ｖ経験 == 0
	TFLAG:20 = 1

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
SIF TALENT:PLAYER:ふたなり || TALENT:PLAYER:オトコ
	BASE:PLAYER:射精 += CALC_EJAC(SELECTCOM, PLAYER)

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
IF PREVCOM == 34
	LOSEBASE:体力 += 60
	LOSEBASE:気力 += 150
	SOURCE:露出 = 900
ELSE
	LOSEBASE:体力 += 50
	LOSEBASE:気力 += 100
	SOURCE:露出 = 400
ENDIF

SOURCE:快Ｃ = 0

;前方向挿入の場合（正常位・対面座位・騎乗位）
IF PREVCOM == 20 || PREVCOM == 24 || PREVCOM == 34
	;ABL:Ｃ感覚をみる
	IF ABL:Ｃ感覚 == 0
		SOURCE:快Ｃ = 20
	ELSEIF ABL:Ｃ感覚 == 1
		SOURCE:快Ｃ = 100
	ELSEIF ABL:Ｃ感覚 == 2
		SOURCE:快Ｃ = 400
	ELSEIF ABL:Ｃ感覚 == 3
		SOURCE:快Ｃ = 900
	ELSEIF ABL:Ｃ感覚 == 4
		SOURCE:快Ｃ = 1400
	ELSE
		SOURCE:快Ｃ = 100 + (ABL:Ｃ感覚 * 400)
	ENDIF
ENDIF

;ABL:Ｖ感覚をみる
IF ABL:Ｖ感覚 == 0
	SOURCE:快Ｖ = 10
	SOURCE:情愛 = 100
ELSEIF ABL:Ｖ感覚 == 1
	SOURCE:快Ｖ = 30
	SOURCE:情愛 = 180
ELSEIF ABL:Ｖ感覚 == 2
	SOURCE:快Ｖ = 400
	SOURCE:情愛 = 250
ELSEIF ABL:Ｖ感覚 == 3
	SOURCE:快Ｖ = 800
	SOURCE:情愛 = 380
ELSEIF ABL:Ｖ感覚 == 4
	SOURCE:快Ｖ = 2700
	SOURCE:情愛 = 500
ELSE
	SOURCE:快Ｖ = 300 + (ABL:Ｖ感覚 * 700)
	SOURCE:情愛 = 200 + (ABL:Ｖ感覚 * 120)
ENDIF

;EXP:Ｖ経験をみる
IF EXP:Ｖ経験 < EXPLV:1
	TIMES SOURCE:快Ｖ , 0.20
	SOURCE:痛み = 300
ELSEIF EXP:Ｖ経験 < EXPLV:2
	TIMES SOURCE:快Ｖ , 0.60
	SOURCE:痛み = 100
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES SOURCE:快Ｖ , 1.00
	SOURCE:痛み = 10
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES SOURCE:快Ｖ , 1.20
	SOURCE:痛み = 0
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES SOURCE:快Ｖ , 1.30
	SOURCE:痛み = 0
ELSE
	TIMES SOURCE:快Ｖ , 1.80
	SOURCE:痛み = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:潤滑 < PALAMLV:1
	TIMES SOURCE:快Ｃ , 0.50
	TIMES SOURCE:快Ｖ , 0.10
	SOURCE:痛み += 1000
	TIMES SOURCE:痛み , 3.00
ELSEIF PALAM:潤滑 < PALAMLV:2
	TIMES SOURCE:快Ｃ , 0.60
	TIMES SOURCE:快Ｖ , 0.40
	SOURCE:痛み += 300
	TIMES SOURCE:痛み , 1.00
ELSEIF PALAM:潤滑 < PALAMLV:3
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:痛み , 0.50
ELSEIF PALAM:潤滑 < PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.40
	TIMES SOURCE:痛み , 0.20
ELSEIF PALAM:潤滑 >= PALAMLV:4
	TIMES SOURCE:快Ｖ , 1.80
	TIMES SOURCE:痛み , 0.10
ENDIF

;対面座位だと効果が高い
IF PREVCOM == 24
	TIMES SOURCE:快Ｖ , 1.30
	TIMES SOURCE:情愛 , 1.10
ENDIF

;背面座位だと効果が低い
IF PREVCOM == 25 || PREVCOM == 88
	TIMES SOURCE:快Ｖ , 0.70
	TIMES SOURCE:情愛 , 0.90
ENDIF

;調教者がふたなり
IF ASSIPLAY
	SIF TALENT:ASSI:ふたなり
		TIMES SOURCE:快Ｖ , 2.50
ENDIF

;小柄体形
SIF TALENT:小柄体型
	TIMES SOURCE:痛み , 2.00

;貞操観念
IF TALENT:貞操観念
	IF EXP:Ｖ経験 == 0
		TIMES SOURCE:情愛 , 0.60
		TIMES SOURCE:逸脱, 5.00
		SOURCE:反感追加 = 2000
	ELSE
		TIMES SOURCE:情愛 , 0.60
		SOURCE:反感追加 = 1000
	ENDIF
;貞操無頓着
ELSEIF TALENT:貞操無頓着
	IF EXP:Ｖ経験 == 0
		TIMES SOURCE:情愛 , 0.60
		SOURCE:反感追加 = 300
	ENDIF
ELSE
	IF EXP:Ｖ経験 == 0
		SOURCE:反感追加 = 3000
	ENDIF
ENDIF

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	TIMES SOURCE:快Ｃ , 0.60
	TIMES SOURCE:快Ｖ , 0.60
	TIMES SOURCE:情愛 , 0.30
ELSEIF PALAM:欲情 < PALAMLV:2
	TIMES SOURCE:快Ｃ , 0.80
	TIMES SOURCE:快Ｖ , 0.80
	TIMES SOURCE:情愛 , 0.60
ELSEIF PALAM:欲情 < PALAMLV:3
	TIMES SOURCE:快Ｃ , 1.00
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:情愛 , 1.00
ELSEIF PALAM:欲情 < PALAMLV:4
	TIMES SOURCE:快Ｃ , 1.20
	TIMES SOURCE:快Ｖ , 1.20
	TIMES SOURCE:情愛 , 1.50
ELSEIF PALAM:欲情 >= PALAMLV:4
	TIMES SOURCE:快Ｃ , 1.50
	TIMES SOURCE:快Ｖ , 1.50
	TIMES SOURCE:情愛 , 1.80
ENDIF

;ABL:従順をみる
IF ABL:親密 == 0
	TIMES SOURCE:快Ｃ , 0.50
	TIMES SOURCE:快Ｖ , 0.50
	TIMES SOURCE:情愛 , 0.60
	TIMES SOURCE:反感追加 , 2.00
ELSEIF ABL:親密 == 1
	TIMES SOURCE:快Ｃ , 0.80
	TIMES SOURCE:快Ｖ , 0.80
	TIMES SOURCE:情愛 , 0.80
	TIMES SOURCE:反感追加 , 1.50
ELSEIF ABL:親密 == 2
	TIMES SOURCE:快Ｃ , 1.00
	TIMES SOURCE:快Ｖ , 1.00
	TIMES SOURCE:情愛 , 1.00
	TIMES SOURCE:反感追加 , 1.00
ELSEIF ABL:親密 == 3
	TIMES SOURCE:快Ｃ , 1.20
	TIMES SOURCE:快Ｖ , 1.30
	TIMES SOURCE:情愛 , 1.20
	TIMES SOURCE:反感追加 , 0.80
ELSEIF ABL:親密 == 4
	TIMES SOURCE:快Ｃ , 1.40
	TIMES SOURCE:快Ｖ , 1.60
	TIMES SOURCE:情愛 , 1.40
	TIMES SOURCE:反感追加 , 0.60
ELSE
	SOURCE:快Ｃ = SOURCE:快Ｃ * (ABL:親密 * 10 + 110) / 100
	SOURCE:快Ｖ = SOURCE:快Ｖ * (ABL:親密 * 20 + 100) / 100
	SOURCE:情愛 = SOURCE:情愛 * (ABL:親密 * 10 + 110) / 100
	TIMES SOURCE:反感追加 , 0.30
ENDIF

IF PREVCOM == 34
	;ABL:奉仕精神をみる
	IF ABL:奉仕精神 == 0
		SOURCE:性行動 = 200
		SOURCE:達成感 = 50
		SOURCE:逸脱 = 300
	ELSEIF ABL:奉仕精神 == 1
		SOURCE:性行動 = 250
		SOURCE:達成感 = 200
		SOURCE:逸脱 = 100
	ELSEIF ABL:奉仕精神 == 2
		SOURCE:性行動 = 350
		SOURCE:達成感 = 550
		SOURCE:逸脱 = 30
	ELSEIF ABL:奉仕精神 == 3
		SOURCE:性行動 = 450
		SOURCE:達成感 = 900
		SOURCE:逸脱 = 0
	ELSEIF ABL:奉仕精神 == 4
		SOURCE:性行動 = 600
		SOURCE:達成感 = 1500
		SOURCE:逸脱 = 0
	ELSE
		SOURCE:性行動 = 375 + (ABL:奉仕精神 * 75)
		SOURCE:達成感 = 450 + (ABL:奉仕精神 * 350)
		SOURCE:逸脱 = 0
	ENDIF
	;ABL:技巧をみる
	IF ABL:技巧 == 0
		TIMES SOURCE:快Ｖ , 0.30
		TIMES SOURCE:情愛 , 0.30
	ELSEIF ABL:技巧 == 1
		TIMES SOURCE:快Ｖ , 0.60
		TIMES SOURCE:情愛 , 0.60
	ELSEIF ABL:技巧 == 2
		TIMES SOURCE:快Ｖ , 1.00
		TIMES SOURCE:情愛 , 1.00
	ELSEIF ABL:技巧 == 3
		TIMES SOURCE:快Ｖ , 1.10
		TIMES SOURCE:情愛 , 1.10
	ELSEIF ABL:技巧 == 4
		TIMES SOURCE:快Ｖ , 1.30
		TIMES SOURCE:情愛 , 1.20
	ELSE
	SOURCE:快Ｖ = SOURCE:快Ｖ * (ABL:技巧 * 100 + 100) / 100
	SOURCE:情愛 = SOURCE:情愛 * (ABL:技巧 * 10 + 90) / 100
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:12 + 8
SIF TALENT:恋慕 || TALENT:恋人
	A += 5
SIF TALENT:親愛
	A += 5
;合意がないと好感度が下がる
IF TALENT:合意 == 0 && TFLAG:48 == 0
	A -= 15
	SOURCE:反感追加 += 1200
ENDIF
TFLAG:30 += A
TFLAG:37 += A / 2

EXP:Ｖ経験 += 1
PRINTL Ｖ経験＋1

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK

;射精時の処理
IF E >= 1
	;コマンドごとに固有の処理はこの下に書く
	IF ABL:親密 >= 3 && ABL:欲望 >= 3 && FLAG:6 && TEQUIP:PLAYER:85 != 1
	PRINTFORML %CALLNAME:TARGET%の子宮に熱い精液が注ぎ込まれていった…
	PRINTFORML 
	ENDIF

	;射精先タイプの設定
	TFLAG:703 = 1
	;射精先選択の可・不可
	TFLAG:704 = 0
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;射精に関係なく行われる処理はここから
;奴隷のＶ⇔調教者のＰの汚れが移動
STAIN:ヴァギナ |= STAIN:PLAYER:ペニス
STAIN:PLAYER:ペニス |= STAIN:ヴァギナ

IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	PRINTFORML %EXPNAME:40%＋4
	EXP:レズ経験 += 4
ENDIF

;IF ASSIPLAY == 0
;	IF ABL:Ｖ感覚 >= 3
;		TFLAG:30 += 2
;	ELSE
;		TFLAG:30 += 1
;	ENDIF
;ENDIF

RETURN 1

@TRAIN_MESSAGE_COM256
;騎乗位
IF PREVCOM == 34
	PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の胸に手を衝き、ペニスを根元まで飲み込んだ…
	IF EXP:Ｖ経験 <= 30
		PRINTFORMW %CALLNAME:TARGET%はそのまま腰を前後に揺すっている…
	ELSEIF EXP:Ｖ経験 >= 31 && EXP:Ｖ経験 <= 50 && TALENT:ふたなり
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の腹にペニスを押し付けている…
	ELSEIF EXP:Ｖ経験 >= 31 && EXP:Ｖ経験 <= 50
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の陰毛でクリトリスを刺激しているようだ…
	ELSEIF EXP:Ｖ経験 >= 51 && EXP:Ｖ経験 <= 80
		PRINTFORMW %CALLNAME:TARGET%は腰をくねらせ、挿入感を愉しんでは熱い息を吐いている…
	ELSEIF EXP:Ｖ経験 >= 81 && EXP:Ｖ経験 <= 120
		PRINTFORML %CALLNAME:TARGET%はクン、クンと腰を沈ませ、深奥を突く感触に酔っている…
		PRINTFORMW コリコリした子宮口は綻び、法悦とともに仔を孕む用意を始めている…
	ELSEIF EXP:Ｖ経験 >= 121
		PRINTFORML %CALLNAME:TARGET%はリズミカルに腰を小さく縦に揺すり、牝そのものの顔で涎を垂らしている…
		PRINTFORMW 子作りの快楽に理性は蕩け、牝の肉体は子種を求めて貪欲にひくついている…
	ENDIF
ELSE
	;正常位
	SIF PREVCOM == 20
		PRINTFORMW 腰を掴んで深く突き、%CALLNAME:TARGET%の子宮をコツンと叩いた…
	;後背位
	SIF PREVCOM == 21
		PRINTFORMW 尻を掴んで深く突き、%CALLNAME:TARGET%の子宮をコツンと叩いた…
	;対面座位
	SIF PREVCOM == 24
		PRINTFORMW 腰の括れを深く抱いて、%CALLNAME:TARGET%の子宮をグリグリと刺激した…
	;背面座位・乱れ牡丹
	SIF PREVCOM == 25 || PREVCOM == 88
		PRINTFORMW 腰を前に押すようにして、%CALLNAME:TARGET%の深奥まで突き上げた…
	IF EXP:Ｖ経験 <= 30 || (EXP:Ｖ経験 >= 31 && EXP:Ｖ経験 <= 50 && (PREVCOM == 21 || PREVCOM == 25 || PREVCOM == 88))
		PRINTFORMW %CALLNAME:TARGET%は異物感が強いのか、苦しそうにしている…
	ELSEIF EXP:Ｖ経験 >= 31 && EXP:Ｖ経験 <= 50 && TALENT:ふたなり
		PRINTFORMW %CALLNAME:TARGET%は陰毛と臍の窪みにペニスを嬲られ、鼻声を漏らしている…
	ELSEIF EXP:Ｖ経験 >= 31 && EXP:Ｖ経験 <= 50
		PRINTFORMW %CALLNAME:TARGET%は陰毛にクリトリスを擦られ、鼻声を漏らしている…
	ELSEIF EXP:Ｖ経験 >= 51 && EXP:Ｖ経験 <= 80
		PRINTFORMW %CALLNAME:TARGET%は長く甘い息を吐きながら、挿入感を噛み締めている…
	ELSEIF EXP:Ｖ経験 >= 81 && EXP:Ｖ経験 <= 120
		PRINTFORML 子宮が子種を受ける準備を済ませたらしく、先端のくびれが膣奥の窪みで引っかかる…
		PRINTFORMW %CALLNAME:TARGET%の子宮をノックする度、口をパクパクさせて激しく震えている…
	ELSEIF EXP:Ｖ経験 >= 121
		PRINTFORML 低い位置に下りてきた%CALLNAME:TARGET%の子宮を突いて揺すってやると、
		PRINTFORMW %CALLNAME:TARGET%は狂ったように悶え、牝として最高の快楽に溺れた…
	ENDIF
ENDIF

