@COM_ABLE199
;お嫁さんで楽しむ実行判定
;COM_ABLE共通判定
CALL COM_ABLE_COMMON, SELECTCOM
SIF RESULT == 0
	RETURN 0
;ウフフ中じゃないと無理
SIF TFLAG:44 == 0
	RETURN 0
;新妻プレイ中じゃないとダメ
SIF TEQUIP:59 == 0
	RETURN 0
RETURN 1

@COM199
;お嫁さんで楽しむ

PRINTL お嫁さんで楽しむ
STR:0 = お嫁さんで楽しむ

;地の文カットのために先にTFLAG:17を取得
IF RAND:5 == 0
	TFLAG:17 = 0
ELSEIF RAND:4 == 0
	TFLAG:17 = 1
ELSEIF RAND:3 == 0
	TFLAG:17 = 2
ELSEIF RAND:2 == 0
	TFLAG:17 = 3
ELSE
	TFLAG:17 = 4
ENDIF

CALL TRAIN_MESSAGE_B

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOSEBASE:体力 += 0
LOSEBASE:気力 += 30

SOURCE:中毒充足 = 50
SOURCE:恭順追加 = 120

;奉仕精神をみる
IF ABL:奉仕精神 == 0
	SOURCE:情愛 = 10
ELSEIF ABL:奉仕精神 == 1
	SOURCE:情愛 = 40
ELSEIF ABL:奉仕精神 == 2
	SOURCE:情愛 = 80
ELSEIF ABL:奉仕精神 == 3
	SOURCE:情愛 = 140
ELSEIF ABL:奉仕精神 == 4
	SOURCE:情愛 = 200
ELSE
	SOURCE:情愛 = 200 + (ABL:奉仕精神 * 20)
ENDIF

;ABL:マゾっ気をみる
IF ABL:マゾっ気 == 0
	TIMES SOURCE:情愛 , 0.80
	TIMES SOURCE:恭順追加 , 0.80
	SOURCE:液体追加 = 10
	SOURCE:欲情追加 = 10
ELSEIF ABL:マゾっ気 == 1
	TIMES SOURCE:情愛 , 1.00
	TIMES SOURCE:恭順追加 , 1.00
	SOURCE:液体追加 = 40
	SOURCE:欲情追加 = 60
ELSEIF ABL:マゾっ気 == 2
	TIMES SOURCE:情愛 , 1.30
	TIMES SOURCE:恭順追加 , 1.20
	SOURCE:液体追加 = 80
	SOURCE:欲情追加 = 120
ELSEIF ABL:マゾっ気 == 3
	TIMES SOURCE:情愛 , 1.40
	TIMES SOURCE:恭順追加 , 1.40
	SOURCE:液体追加 = 140
	SOURCE:欲情追加 = 240
ELSEIF ABL:マゾっ気 == 4
	TIMES SOURCE:情愛 , 1.70
	TIMES SOURCE:恭順追加 , 1.50
	SOURCE:液体追加 = 220
	SOURCE:欲情追加 = 360
ELSE
	SOURCE:情愛 = SOURCE:情愛 * (ABL:マゾっ気 * 15 + 125) / 100
	SOURCE:露出 = SOURCE:恭順追加 * (ABL:マゾっ気 * 10 + 120) / 100
	SOURCE:液体追加 = 100 + (ABL:マゾっ気 * 20)
	SOURCE:欲情追加 = 150 + (ABL:マゾっ気 * 35)
ENDIF

SIF PALAM:欲情 >= PALAMLV:3
	TFLAG:200 = 1

;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:従順をみる
	IF ABL:親密 <= 1
		SOURCE:受動 += 300 + (ABL:親密 * 50)
	ELSEIF ABL:親密 <= 3
		SOURCE:受動 += 600 + (ABL:親密 * 100)
	ELSEIF ABL:親密 <= 5
		SOURCE:受動 += 800 + (ABL:親密 * 120)
	ELSEIF ABL:親密 <= 8
		SOURCE:受動 += 1000 + (ABL:親密 * 140)
	ELSEIF ABL:親密 <= 11
		SOURCE:受動 += 1200 + (ABL:親密 * 160)
	ELSE
		SOURCE:受動 += 1500 + (ABL:親密 * 180)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:サドっ気 == 0
		SOURCE:征服 = 600
	ELSEIF ABL:サドっ気 == 1
		SOURCE:征服 = 900
	ELSEIF ABL:サドっ気 == 2
		SOURCE:征服 = 1200
	ELSEIF ABL:サドっ気 == 3
		SOURCE:征服 = 1600
	ELSEIF ABL:サドっ気 == 4
		SOURCE:征服 = 2000
	ELSE
		SOURCE:征服 = 1000 + (ABL:サドっ気 * 300)
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:10 + 5
SIF TALENT:恋慕 || TALENT:恋人
	A += 2
SIF TALENT:親愛
	A += 3
;合意がないと好感度が下がる
IF TALENT:合意 == 0 && TFLAG:48 == 0
	A -= 8
	SOURCE:反感追加 += 300
ENDIF
TFLAG:30 += A
TFLAG:37 += A

RETURN 1

;お嫁さんで楽しむ
@TRAIN_MESSAGE_COM199
PRINTFORM %CALLNAME:PLAYER%は
IF TFLAG:17 == 0
	PRINTFORMW %CALLNAME:TARGET%をお姫様だっこしてあげた…
	IF TALENT:恋慕 && RAND:3 >= 1
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:MASTER%の首に手をまわし、じっと見上げてくる…
	ELSEIF TALENT:恋人
		PRINTFORMW %CALLNAME:TARGET%は恥ずかしそうにしながらも、黙って%CALLNAME:PLAYER%に抱かれていた…
	ELSEIF TALENT:合意
		PRINTFORMW %CALLNAME:TARGET%は暴れこそしないが、体を硬直させている…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%が激しく暴れたため、危うく落としてしまうところだった…
	ENDIF
ELSEIF TFLAG:17 == 1
	PRINTFORMW エプロンの紐を引っ張った…
	IF TALENT:恋慕 && RAND:3 >= 1
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:MASTER%の悪戯に、いやんいやんと身をくねらせている…
	ELSEIF TALENT:恋人
		PRINTFORMW %CALLNAME:TARGET%はそっと離れた。どうやらエプロンを脱ぐのが嫌なようだ…
	ELSEIF TALENT:合意
		PRINTFORMW %CALLNAME:TARGET%がエプロンを脱ごうとしたので、%CALLNAME:MASTER%は悪戯を断念した…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%がそのままエプロンを脱いでしまったので、つけ直す羽目になった…
	ENDIF
ELSEIF TFLAG:17 == 2
	PRINTFORMW %CALLNAME:TARGET%をぎゅっと抱きしめた…
	IF TALENT:恋慕 && RAND:3 >= 1
		PRINTFORMW %CALLNAME:TARGET%もしっかりと%CALLNAME:MASTER%の体を抱きしめた…
	ELSEIF TALENT:恋人
		PRINTFORMW %CALLNAME:TARGET%は恥ずかしそうにしながらも、そっと%CALLNAME:PLAYER%の頭を撫でた…
	ELSEIF TALENT:合意
		PRINTFORMW %CALLNAME:TARGET%は弱々しいながらも抵抗している…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は腕を突っ張っている…
	ENDIF
ELSEIF TFLAG:17 == 3
	PRINTFORMW %CALLNAME:TARGET%の名前を呼んだ…
	IF TALENT:恋慕 && RAND:3 >= 1
		PRINTFORMW %CALLNAME:TARGET%は満面の笑みで、返事の代わりに%CALLNAME:MASTER%の名前を呼んだ…
	ELSEIF TALENT:恋人
		PRINTFORMW %CALLNAME:TARGET%はわずかに顔を紅潮させ、しっかりと返事をしてきた…
	ELSEIF TALENT:合意
		PRINTFORMW %CALLNAME:TARGET%は消え入りそうなほど小声で返事をした…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:MASTER%を無視した…
	ENDIF
ELSEIF TFLAG:17 == 4
	PRINTFORMW %CALLNAME:TARGET%の新妻姿をじっと見つめた…
	IF TALENT:恋慕 && RAND:3 >= 1
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:MASTER%を挑発するようにポーズをとっている…
	ELSEIF TALENT:恋人
		PRINTFORMW %CALLNAME:TARGET%は恥ずかしそうに%CALLNAME:PLAYER%を見つめている…
	ELSEIF TALENT:合意
		PRINTFORMW %CALLNAME:TARGET%は部屋の隅っこに逃げてしまった…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%がエプロンを脱ごうとしたので、%CALLNAME:MASTER%はあわてて押さえつけた…
	ENDIF
ENDIF

