@COM53
;ビデオカメラ
STR:0 = ビデオカメラ

;カウントは10回とします
K = 10

IF TEQUIP:53
;ビデオ撮影がされていれば終了させる
;テープは終了時に数を減らす
	TEQUIP:53 = 0
	;ITEM:28 -= 1
	PRINTW ★★★ビデオ撮影を終了します★★★
ELSE
;ビデオ撮影してなければ初期化して開始
;TODO 起動条件を作成する
	TEQUIP:53 = 1
	TFLAG:70 = 0
	REPEAT K
		S = COUNT + 11
		FLAG:S = 0
	REND

	IF ASSI > 0
		PRINTL 撮影者を選択してください。途中変更はできません
		PRINTL （撮影者を変更する場合、一度撮影を終了してください）
		PRINTL 
		PRINTFORML [0] %CALLNAME:MASTER%
		PRINTFORML [1] %CALLNAME:ASSI%
		PRINTFORML 
	
		$INPUT_LOOP
		INPUT
	
		IF RESULT == 0
			FLAG:22 = 1
			PRINTFORML 撮影者は %CALLNAME:MASTER% です
		ELSEIF RESULT == 1
			FLAG:22 = 2
			PRINTFORML 撮影者は %CALLNAME:ASSI% です
		ELSE
			GOTO INPUT_LOOP
		ENDIF
	ELSE
		FLAG:22 = 0
	ENDIF

	PRINTW ★★★ビデオ撮影を開始します★★★
ENDIF

RETURN 1


@EQUIP_COM53
;ビデオ撮影中･･･
;カウントは10回とします
K = 10

IF TFLAG:70 == 0
;初回はカウントに入れない
	TFLAG:70 += 1

	EXP:異常経験 += 1
	PRINTL 異常経験＋１
ELSEIF TFLAG:70 > 0 && TFLAG:70 <= K
	S = TFLAG:70 + 10
	IF TFLAG:90 >= 1
		FLAG:S = TFLAG:90
		TFLAG:90 = 0
	ELSE
		FLAG:S = SELECTCOM
	ENDIF
	;TODO 装備状態や助手による調教の記録をどうするか？
	PRINTFORML ＜ビデオ撮影中{TFLAG:70}/{K}＞

	TFLAG:70 += 1

	;パラメータ変化
	;TODO バランス調整(ビデオによるパラメータ変化は考慮しないか？)
	LOSEBASE:体力 += 0
	LOSEBASE:気力 += 50

	A = 370
	B = 1750
	C = 700

	;PALAM:欲情をみる
	IF PALAM:欲情 < PALAMLV:1
		TIMES A , 0.80
	ELSEIF PALAM:欲情 < PALAMLV:2
		TIMES A , 0.90
	ELSEIF PALAM:欲情 < PALAMLV:3
		TIMES A , 1.00
	ELSEIF PALAM:欲情 < PALAMLV:4
		TIMES A , 1.10
	ELSEIF PALAM:欲情 >= PALAMLV:4
		TIMES A , 1.20
	ENDIF

	;ABL:従順をみる
	IF ABL:親密 == 0
		TIMES A , 0.40
	ELSEIF ABL:親密 == 1
		TIMES A , 0.60
	ELSEIF ABL:親密 == 2
		TIMES A , 0.80
	ELSEIF ABL:親密 == 3
		TIMES A , 1.00
	ELSEIF ABL:親密 == 4
		TIMES A , 1.10
	ELSE
		A = A * (ABL:親密 * 5 + 95) / 100
	ENDIF

	;ABL:露出癖をみる
	IF ABL:露出癖 == 0
		TIMES A , 0.80
		TIMES C , 2.00
	ELSEIF ABL:露出癖 == 1
		TIMES A , 1.00
		TIMES C , 1.70
	ELSEIF ABL:露出癖 == 2
		TIMES A , 1.30
		TIMES C , 1.40
	ELSEIF ABL:露出癖 == 3
		TIMES A , 1.60
		TIMES C , 1.00
	ELSEIF ABL:露出癖 == 4
		TIMES A , 2.00
		TIMES C , 0.80
	ELSE
		A = A * (ABL:露出癖 * 50 + 50) / 100
		IF ABL:露出癖 <= 10
			C = C * (110 - ABL:露出癖 * 10) / 100
		ELSE
			TIMES C , 0.10
		ENDIF
	ENDIF

	;倒錯的
	SIF TALENT:倒錯的
		TIMES A , 2.00
	;目立ちたがり
	SIF TALENT:目立ちたがり
		TIMES A , 1.50
	;臆病
	SIF TALENT:臆病
		TIMES C , 1.70


	SOURCE:液体追加 += A
	SOURCE:露出 += B
	SOURCE:逸脱 += C

	UP:欲情 += A
	UP:恐怖 += SOURCE:逸脱

	EXP:異常経験 += 1
	PRINTL 異常経験＋１

ENDIF

;テープ残量がなくなると強制的にEQUIPフラグを外す
IF TFLAG:70 <= 0 || TFLAG:70 > K
	TEQUIP:53 = 0
	;ITEM:28 -= 1
	PRINTL ＜テープの残量がなくなりました。ビデオ撮影を終了します＞
ENDIF

SIF ASSIPLAY == 0 && ABL:露出癖 >= 3
	TFLAG:30 += 1

RETURN 1
