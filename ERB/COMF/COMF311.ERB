;6/1版
;TFLAG:81を連続実行ペナルティーとして利用
@COM_ABLE311
;甘い言葉実行判定
;COM_ABLE共通判定
CALL COM_ABLE_COMMON, SELECTCOM
SIF RESULT == 0
	RETURN 0
;助手は無理
SIF ASSIPLAY
	RETURN 0
;悩ましいけどとりあえずウフフ中は無理
SIF TFLAG:44 == 1
	RETURN 0
RETURN 1

@COM311
;甘い言葉
CALL GET_SUCCESS_RATE
B = 90 + A / 10
SIF B > 99
	B = 99
A = RAND:100
IF A <= 9
	;10％で大成功
	TFLAG:18 = 1
ELSEIF A >= B
	;10～1％で失敗
	TFLAG:18 = -1
ELSE
	;残りは成功
	TFLAG:18 = 0
ENDIF
A = 0
B = -100
;ランダムボーナス
IF TFLAG:18 == -1
	B -= 50
ELSEIF TFLAG:18 == 1
	A += 50
ELSE
	A += RAND:16
	B -= RAND:16
ENDIF

;好感度ボーナス
IF CFLAG:2 > 3000
	A += 24 + (CFLAG:2 / 500)
ELSEIF CFLAG:2 > 1000
	A += 30
ELSEIF CFLAG:2 > 500
	A += 10
ELSE
	B -= 30
ENDIF

;好意
IF PALAM:好意 > PALAMLV:9
	A += 50
ELSEIF PALAM:好意 > PALAMLV:7
	A += 30
ELSEIF PALAM:好意 > PALAMLV:5
	A += 20
ELSEIF PALAM:好意 > PALAMLV:4
	A += 10
ELSEIF PALAM:好意 > PALAMLV:3
	A += 5
ELSEIF PALAM:好意 > PALAMLV:2
	B -= 5
ELSEIF PALAM:好意 > PALAMLV:1
	B -= 15
ELSE
	B -= 30
ENDIF
;相愛
SIF TALENT:相愛
	A += 25
;親愛
SIF TALENT:親愛
	A += 25
;恋慕
SIF TALENT:恋慕
	A += 25
;恋人
SIF TALENT:恋人
	A += 25
;パートナー
SIF TALENT:パートナー
	A += 10
;既成事実
SIF MARK:既成事実
	A += MARK:既成事実 * 10
;デート中
SIF TEQUIP:2
	A += 25

;連続実行ペナルティー
TFLAG:81 += 1
A -= (A / 5) * (TFLAG:81 - 1)
SIF A < 0
	A = 0

;狂気
IF TALENT:狂気
	A += RAND:50
	B -= RAND:50
ENDIF

;謎の魅力
SIF TALENT:PLAYER:謎の魅力
	TIMES A , 1.20
;素直
SIF TALENT:素直
	TIMES A , 1.10
;解放
SIF TALENT:解放
	TIMES A , 1.10
;抑圧
SIF TALENT:抑圧
	TIMES A , 0.90
;抵抗
SIF TALENT:抵抗
	TIMES B , 1.10
;感情乏しい
IF TALENT:感情乏しい
	TIMES A , 0.80
	TIMES B , 0.80
ENDIF
;無関心
IF TALENT:無関心
	TIMES A , 0.90
	TIMES B , 0.90
ENDIF
;貞操観念
IF TALENT:貞操観念
	TIMES A , 1.10
	TIMES B , 1.10
ENDIF
;貞操無頓着
IF TALENT:貞操無頓着
	TIMES A , 0.90
	TIMES B , 0.90
ENDIF
;ツンデレ
IF TALENT:ツンデレ
	TIMES A , 1.20
	TIMES B , 1.20
ENDIF
;恥じらい
IF TALENT:恥じらい
	TIMES A , 1.10
	TIMES B , 1.10
ENDIF
;恥薄い
IF TALENT:恥薄い
	TIMES A , 0.90
	TIMES B , 0.90
ENDIF
;処女
IF EXP:Ｖ経験 == 0
	TIMES A , 1.50
	TIMES B , 1.50
ENDIF
;一線超えない
SIF TALENT:一線越えない
	TIMES B , 1.20

;ムードボーナス
IF TFLAG:37 < 10
	B -= 100
ELSEIF TFLAG:37 < 30
	B -= 50
ELSEIF TFLAG:37 < 50
	B += 25
ELSEIF TFLAG:37 < 100
	B += 100
ELSEIF TFLAG:37 < 150
	B += 150
ELSEIF TFLAG:37 < 300
	B += 200
ELSE
	B += (220 + TFLAG:37 / 10)
ENDIF



;効果判定
IF (A+B) > 300
	TFLAG:18 = 4
ELSEIF (A+B) > 200
	TFLAG:18 = 3
ELSEIF (A+B) > 100
	TFLAG:18 = 2
ELSEIF (A+B) > 50
	TFLAG:18 = 1
ELSEIF (A+B) > 0
	TFLAG:18 = 0
ELSEIF (A+B) > -50
	TFLAG:18 = -1
ELSE
	TFLAG:18 = -2
ENDIF
PRINTFORML 甘い言葉
STR:0 = 甘い言葉
CALL TRAIN_MESSAGE_B

LOSEBASE:気力 += 70

;固定で獲得するソース
SOURCE:歓楽 = 50

;ABL:従順をみる
IF ABL:親密 <= 1
	SOURCE:歓楽 += (ABL:親密 * 20)
ELSEIF ABL:親密 <= 3
	SOURCE:歓楽 += 100 + (ABL:親密 * 20)
ELSEIF ABL:親密 <= 5
	SOURCE:歓楽 += 200 + (ABL:親密 * 20)
ELSEIF ABL:親密 <= 8
	SOURCE:歓楽 += 300 + (ABL:親密 * 30)
ELSEIF ABL:親密 <= 11
	SOURCE:歓楽 += 400 + (ABL:親密 * 30)
ELSE
	SOURCE:歓楽 += 800 + (ABL:親密 * 15)
ENDIF

;好感度をみる
IF CFLAG:2 <= 500
	SOURCE:歓楽 += CFLAG:2
ELSEIF CFLAG:2 <= 5000
	SOURCE:歓楽 += 500 + (CFLAG:2 - 500) / 3
ELSE
	SOURCE:歓楽 += 2000 + (CFLAG:2 - 5000) / 5
ENDIF
SIF SOURCE:歓楽 < 0
	SOURCE:歓楽 = 0

;Aをみる
IF A > 200
	SOURCE:情愛 = 800 + (CFLAG:2 / 10)
	SIF SOURCE:情愛 > 10000
		SOURCE:情愛 = 10000
	SIF SOURCE:情愛 < 0
		SOURCE:情愛 = 0
ELSEIF A > 150
	SOURCE:情愛 = 500
ELSEIF A > 100
	SOURCE:情愛 = 300
ELSEIF A >50
	SOURCE:情愛 = 200
ELSEIF A >30
	SOURCE:情愛 = 150
ELSE
	SOURCE:情愛 = 100
ENDIF

;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:従順をみる
	IF ABL:親密 <= 1
		SOURCE:受動 += 300 + (ABL:親密 * 50)
	ELSEIF ABL:親密 <= 3
		SOURCE:受動 += 600 + (ABL:親密 * 100)
	ELSEIF ABL:親密 <= 5
		SOURCE:受動 += 800 + (ABL:親密 * 120)
	ELSEIF ABL:親密 <= 8
		SOURCE:受動 += 1000 + (ABL:親密 * 140)
	ELSEIF ABL:親密 <= 11
		SOURCE:受動 += 1200 + (ABL:親密 * 160)
	ELSE
		SOURCE:受動 += 1500 + (ABL:親密 * 180)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:サドっ気 == 0
		SOURCE:征服 = 600
	ELSEIF ABL:サドっ気 == 1
		SOURCE:征服 = 900
	ELSEIF ABL:サドっ気 == 2
		SOURCE:征服 = 1200
	ELSEIF ABL:サドっ気 == 3
		SOURCE:征服 = 1600
	ELSEIF ABL:サドっ気 == 4
		SOURCE:征服 = 2000
	ELSE
		SOURCE:征服 = 1000 + (ABL:サドっ気 * 300)
	ENDIF
ENDIF
;好感度ランダム変化
TFLAG:30 = (RAND:6 -1)
;ムード加算はかなり幅が出る感じに
C = RAND:4 +2

;成功度によって変化
IF TFLAG:18 == 4
	TIMES SOURCE:情愛 , 1.20
	TIMES SOURCE:歓楽 , 1.20
	TIMES SOURCE:征服 , 1.50
	TIMES SOURCE:受動 , 1.50
	TFLAG:30 += 3
	TFLAG:37 += C * 3
ELSEIF TFLAG:18 == 3
	TIMES SOURCE:情愛 , 0.90
	TIMES SOURCE:歓楽 , 0.90
	TIMES SOURCE:征服 , 1.30
	TIMES SOURCE:受動 , 1.30
	TFLAG:30 += 2
	TFLAG:37 += C * 2
ELSEIF TFLAG:18 == 2
	TIMES SOURCE:情愛 , 0.75
	TIMES SOURCE:歓楽 , 0.75
	TIMES SOURCE:征服 , 1.20
	TIMES SOURCE:受動 , 1.20
	TFLAG:30 += 1
	TFLAG:37 += C * 2
ELSEIF TFLAG:18 == 1
	TIMES SOURCE:情愛 , 0.60
	TIMES SOURCE:歓楽 , 0.60
	TIMES SOURCE:征服 , 1.00
	TIMES SOURCE:受動 , 1.00
	TFLAG:37 += C
ELSEIF TFLAG:18 == 0
	TIMES SOURCE:情愛 , 0.50
	TIMES SOURCE:歓楽 , 0.50
	TIMES SOURCE:征服 , 0.80
	TIMES SOURCE:受動 , 0.80
	TFLAG:37 += C
ELSEIF TFLAG:18 == -1
	TIMES SOURCE:情愛 , 0.30
	TIMES SOURCE:歓楽 , 0.30
	TIMES SOURCE:征服 , 0.50
	TIMES SOURCE:受動 , 0.50
	TFLAG:30 -= 5
	TFLAG:37 -= 5
ELSE
	TIMES SOURCE:情愛 , 0.10
	TIMES SOURCE:歓楽 , 0.10
	TIMES SOURCE:征服 , 0.30
	TIMES SOURCE:受動 , 0.30
	TFLAG:30 -= 10
	TFLAG:37 -= 10
ENDIF
;Aが低い場合懐疑が発生し、さらにムードと好感度にもマイナスが
IF A < 100
	SOURCE:懐疑 += 100
	TFLAG:30 -= 5
	TFLAG:37 -= C
ELSEIF A < 50
	SOURCE:懐疑 += 200
	TFLAG:30 -= 10
	TFLAG:37 -= C * 2
ENDIF
;失敗してても懐疑が発生
IF TFLAG:18 == -1
	SOURCE:懐疑 += 800
ELSEIF TFLAG:18 == -2
	SOURCE:懐疑 += 2000
ENDIF
;デート中は更にムードアップ
SIF TEQUIP:2
	TFLAG:37 += 11
RETURN 1

@TRAIN_MESSAGE_COM311
LOCAL = RAND:7
;相愛
IF LOCAL == 0 && TALENT:相愛
	PRINTFORMW %CALLNAME:PLAYER%は%CALLNAME:TARGET%に高らかに愛を謳いあげた
;恋人
ELSEIF LOCAL == 1 && TALENT:恋人
	PRINTFORMW %CALLNAME:PLAYER%は%CALLNAME:TARGET%に愛を囁いた
;パートナーに主導権
ELSEIF LOCAL == 2 && TFLAG:45
	PRINTFORMW %CALLNAME:TARGET%は頬を赤らめ、%CALLNAME:PLAYER%に甘い言葉をねだってきた。
;その他
ELSEIF LOCAL == 3
	PRINTFORMW %CALLNAME:PLAYER%は%CALLNAME:TARGET%の耳元でそっと愛を囁いた。
ELSEIF LOCAL == 4
	PRINTFORMW %CALLNAME:PLAYER%は%CALLNAME:TARGET%の美しさを雄弁に褒め称えた。
ELSEIF LOCAL == 5
	PRINTFORMW %CALLNAME:PLAYER%は自分の想いを詩にして、%CALLNAME:TARGET%に贈った。
ELSE
	PRINTFORMW %CALLNAME:PLAYER%は%CALLNAME:TARGET%に甘い言葉をかけた
ENDIF
