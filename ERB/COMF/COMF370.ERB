@COM_ABLE370
;抱き合う実行判定
;COM_ABLE共通判定
CALL COM_ABLE_COMMON, SELECTCOM
SIF RESULT == 0
	RETURN 0
;ウフフ中は無理
SIF TFLAG:44 == 1
	RETURN 0
;欲望4以上かつ、親密8以上or好感度1000以上or恋人が必要
SIF ABL:欲望 < 4 || (ABL:親密 < 8 && CFLAG:2 < 1000 && TALENT:恋人 == 0)
	RETURN 0
;デート中は恋人か親愛か解放か恥薄いが必要
SIF TALENT:恋人 == 0 && TALENT:親愛 == 0 && TALENT:解放 == 0 && TALENT:恥薄い == 0 && TEQUIP:2 > 0 && TEQUIP:2 != 7
	RETURN 0
;温泉デート中は親愛か相愛が必要
SIF TALENT:親愛 == 0 && TALENT:相愛 == 0 && TEQUIP:2 == 7
	RETURN 0
RETURN 1

@COM370
;抱き合う
PRINTL 抱き合う
CALL GET_SUCCESS_RATE
B = 90 + A / 10
SIF B > 99
	B = 99
A = RAND:100
IF A <= 9
	;10％で大成功
	TFLAG:18 = 1
ELSEIF A >= B
	;10～1％で失敗
	TFLAG:18 = -1
ELSE
	;残りは成功
	TFLAG:18 = 0
ENDIF

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL COM_ORDER_COMMON
IF RESULT == 0
	;目標の半分にすら届かない場合白い目で見られる
	IF A < V / 2
		PRINTFORML %CALLNAME:TARGET%は信じられないものを見るような目でこちらを見ている……
		PRINTFORMW %CALLNAME:TARGET%の好感度が50下がりました。
		CFLAG:2 -= 50
	ELSE
		PRINTFORMW %CALLNAME:TARGET%に突き飛ばされました
	ENDIF
	RETURN 0
ENDIF
STR:0 = 抱き合う

TFLAG:17 = 0
;(ムード25以上or酔い17以上)且つデート中ではない
IF (TFLAG:37 >= 25 || TFLAG:47 >= 17) && TEQUIP:2 == 0
	TFLAG:17 = 1
;(ムード35以上or酔い27以上)且つ温泉以外のデートスポットかつ(恥薄い、快感に素直、淫乱、倒錯的)
ELSEIF (TFLAG:37 >= 35 || TFLAG:47 >= 27) && (TEQUIP:2 > 0 && (TEQUIP:2 != 6)) && (TALENT:恥薄い || TALENT:快感に素直 || TALENT:淫乱 || TALENT:倒錯的)
	TFLAG:17 = 1
ENDIF
;好感度3500より大きい、欲望Lv8、恋慕、上二つのどっちか
SIF CFLAG:2 > 3500 && ABL:欲望 >= 8 && TALENT:恋慕 && TFLAG:17
	TFLAG:17 = 2
;TFLAG:17でおおまかな状況を見られるように

CALL TRAIN_MESSAGE_B

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
IF TFLAG:17 == 2
	LOSEBASE:体力 += 20
	LOSEBASE:気力 += 180
ELSE
	LOSEBASE:気力 += 80
ENDIF

;固定で獲得するソース
SOURCE:歓楽 = 50
SOURCE:接触 = 1100
SOURCE:露出 = 500

SIF TEQUIP:2 != 0
	SOURCE:露出 += 1500
SIF TFLAG:17 == 2
	SOURCE:露出 += 1000

;ABL:親密をみる
IF ABL:親密 <= 1
	SOURCE:歓楽 += ((ABL:親密 * 20) / 3)
	SOURCE:接触 += (ABL:親密 * 90)
ELSEIF ABL:親密 <= 3
	SOURCE:歓楽 += 100 + ((ABL:親密 * 20) / 3)
	SOURCE:接触 += 200 + (ABL:親密 * 95)
ELSEIF ABL:親密 <= 5
	SOURCE:歓楽 += 200 + ((ABL:親密 * 20) / 3)
	SOURCE:接触 += 500 + (ABL:親密 * 100)
ELSEIF ABL:親密 <= 8
	SOURCE:歓楽 += 300 + ((ABL:親密 * 30) / 3)
	SOURCE:接触 += 700 + (ABL:親密 * 110)
ELSEIF ABL:親密 <= 11
	SOURCE:歓楽 += 400 + ((ABL:親密 * 30) / 3)
	SOURCE:接触 += 1200 + (ABL:親密 * 125)
ELSE
	SOURCE:歓楽 += 800 + ((ABL:親密 * 15) / 3)
	SOURCE:接触 += 2000 + (ABL:親密 * 80)
ENDIF

;好感度をみる
IF CFLAG:2 <= 500
	SOURCE:歓楽 += CFLAG:2
ELSEIF CFLAG:2 <= 5000
	SOURCE:歓楽 += 500 + (CFLAG:2 - 500) / 3
ELSE
	SOURCE:歓楽 += 2000 + (CFLAG:2 - 5000) / 5
ENDIF
SIF SOURCE:歓楽 < 0
	SOURCE:歓楽 = 0

;ABL:Ｃ感覚をみる
IF TFLAG:17 == 2
	IF ABL:Ｃ感覚 == 0
		SOURCE:快Ｃ = 5
		SOURCE:情愛 = 20
		SIF TEQUIP:2 != 0
			TIMES SOURCE:露出 , 0.50
	ELSEIF ABL:Ｃ感覚 == 1
		SOURCE:快Ｃ = 25
		SOURCE:情愛 = 40
		SIF TEQUIP:2 != 0
			TIMES SOURCE:露出 , 0.80
	ELSEIF ABL:Ｃ感覚 == 2
		SOURCE:快Ｃ = 125
		SOURCE:情愛 = 60
		SIF TEQUIP:2 != 0
			TIMES SOURCE:露出 , 1.20
		EXP:奉仕快楽経験 += 1
	ELSEIF ABL:Ｃ感覚 == 3
		SOURCE:快Ｃ = 300
		SOURCE:情愛 = 80
		SIF TEQUIP:2 != 0
			TIMES SOURCE:露出 , 1.50
		EXP:奉仕快楽経験 += 2
	ELSEIF ABL:Ｃ感覚 == 4
		SOURCE:快Ｃ = 500
		SOURCE:情愛 = 90
		SIF TEQUIP:2 != 0
			TIMES SOURCE:露出 , 1.80
		EXP:奉仕快楽経験 += 3
	ELSE
		SOURCE:快Ｃ = 160 + (ABL:Ｃ感覚 * 100)
		SOURCE:情愛 = 80 + (ABL:Ｃ感覚 * 4)
		SIF TEQUIP:2 != 0
			SOURCE:露出 *= (ABL:Ｃ感覚 * 10 + 90) / 100
		EXP:奉仕快楽経験 += ABL:Ｃ感覚 - 1
	ENDIF
ELSE
	IF ABL:Ｃ感覚 == 0
		SOURCE:情愛 = 20
	ELSEIF ABL:Ｃ感覚 == 1
		SOURCE:情愛 = 40
	ELSEIF ABL:Ｃ感覚 == 2
		SOURCE:情愛 = 60
	ELSEIF ABL:Ｃ感覚 == 3
		SOURCE:情愛 = 80
	ELSEIF ABL:Ｃ感覚 == 4
		SOURCE:情愛 = 90
	ELSE
		SOURCE:情愛 = 80 + (ABL:Ｃ感覚 * 4)
	ENDIF
ENDIF


;ABL:Ｂ感覚をみる
IF TFLAG:17 == 2
	IF ABL:Ｂ感覚 == 0
		SOURCE:快Ｂ = 5
		SOURCE:情愛 = 20
		SIF TEQUIP:2 != 0
			TIMES SOURCE:露出 , 0.50
	ELSEIF ABL:Ｂ感覚 == 1
		SOURCE:快Ｂ = 25
		SOURCE:情愛 = 40
		SIF TEQUIP:2 != 0
			TIMES SOURCE:露出 , 0.80
	ELSEIF ABL:Ｂ感覚 == 2
		SOURCE:快Ｂ = 125
		SOURCE:情愛 = 60
		SIF TEQUIP:2 != 0
			TIMES SOURCE:露出 , 1.20
		EXP:奉仕快楽経験 += 1
	ELSEIF ABL:Ｂ感覚 == 3
		SOURCE:快Ｂ = 300
		SOURCE:情愛 = 80
		SIF TEQUIP:2 != 0
			TIMES SOURCE:露出 , 1.50
		EXP:奉仕快楽経験 += 2
	ELSEIF ABL:Ｂ感覚 == 4
		SOURCE:快Ｂ = 500
		SOURCE:情愛 = 90
		SIF TEQUIP:2 != 0
			TIMES SOURCE:露出 , 1.80
		EXP:奉仕快楽経験 += 3
	ELSE
		SOURCE:快Ｂ = 160 + (ABL:Ｂ感覚 * 100)
		SOURCE:情愛 = 80 + (ABL:Ｂ感覚 * 4)
		SIF TEQUIP:2 != 0
			SOURCE:露出 = SOURCE:露出 * (ABL:Ｂ感覚 * 10 + 90) / 100
		EXP:奉仕快楽経験 += ABL:Ｂ感覚 - 1
	ENDIF
ELSE
	IF ABL:Ｂ感覚 == 0
		SOURCE:情愛 = 20
	ELSEIF ABL:Ｂ感覚 == 1
		SOURCE:情愛 = 40
	ELSEIF ABL:Ｂ感覚 == 2
		SOURCE:情愛 = 60
	ELSEIF ABL:Ｂ感覚 == 3
		SOURCE:情愛 = 80
	ELSEIF ABL:Ｂ感覚 == 4
		SOURCE:情愛 = 90
	ELSE
		SOURCE:情愛 = 80 + (ABL:Ｂ感覚 * 4)
	ENDIF
ENDIF


;TALENT:恋慕をみる
SIF TALENT:恋慕
	TIMES SOURCE:情愛 , 1.50
;TALENT:恋人をみる
SIF TALENT:恋人
	TIMES SOURCE:情愛 , 1.50
;TALENT:親愛をみる
SIF TALENT:親愛
	TIMES SOURCE:情愛 , 2.00


;着衣補正		面倒なんで、元のソースをコピーして倍率をかける形で実装
IF TEQUIP:2 != 7
	TIMES SOURCE:情愛 , 0.80
	TIMES SOURCE:快Ｃ , 0.60
	TIMES SOURCE:快Ｂ , 0.60
	TIMES SOURCE:露出 , 0.50
ENDIF

;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:親密をみる
	IF ABL:親密 <= 1
		SOURCE:受動 += 300 + (ABL:親密 * 50)
	ELSEIF ABL:親密 <= 3
		SOURCE:受動 += 600 + (ABL:親密 * 100)
	ELSEIF ABL:親密 <= 5
		SOURCE:受動 += 800 + (ABL:親密 * 120)
	ELSEIF ABL:親密 <= 8
		SOURCE:受動 += 1000 + (ABL:親密 * 140)
	ELSEIF ABL:親密 <= 11
		SOURCE:受動 += 1200 + (ABL:親密 * 160)
	ELSE
		SOURCE:受動 += 1500 + (ABL:親密 * 180)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:サドっ気 == 0
		SOURCE:征服 = 600
	ELSEIF ABL:サドっ気 == 1
		SOURCE:征服 = 900
	ELSEIF ABL:サドっ気 == 2
		SOURCE:征服 = 1200
	ELSEIF ABL:サドっ気 == 3
		SOURCE:征服 = 1600
	ELSEIF ABL:サドっ気 == 4
		SOURCE:征服 = 2000
	ELSE
		SOURCE:征服 = 1000 + (ABL:サドっ気 * 300)
	ENDIF
ENDIF

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
SIF TALENT:PLAYER:ふたなり || TALENT:PLAYER:オトコ
	BASE:PLAYER:射精 += CALC_EJAC(SELECTCOM, PLAYER)

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK

IF E
;射精している
	SOURCE:性行動 = 500 + (ABL:技巧 * 300)

	;ABL:精液中毒をみる
	IF ABL:精液中毒 == 0
		SOURCE:中毒充足 = 0
		TIMES SOURCE:達成感 , 1.00
	ELSEIF ABL:精液中毒 == 1
		SOURCE:中毒充足 = 100
		TIMES SOURCE:達成感 , 1.25
	ELSEIF ABL:精液中毒 == 2
		SOURCE:中毒充足 = 300
		TIMES SOURCE:達成感 , 1.50
	ELSEIF ABL:精液中毒 == 3
		SOURCE:中毒充足 = 750
		TIMES SOURCE:達成感 , 2.00
	ELSEIF ABL:精液中毒 == 4
		SOURCE:中毒充足 = 1500
		TIMES SOURCE:達成感 , 2.50
	ELSE
		SOURCE:中毒充足 = (ABL:精液中毒 * 750) - 750
		SOURCE:達成感 = SOURCE:達成感 * (ABL:精液中毒 * 50 + 350) / 200
	ENDIF

	;大量射精ボーナス
	IF E == 2
		TIMES SOURCE:中毒充足 , 2.00
		TIMES SOURCE:達成感 , 1.50
		PRINT 大量
	ENDIF

	;着衣なので温泉以外では精液経験増えず、フラグも無し
	;汚れは相手に移動しない。温泉なら次のコマンド前に汚れ消えるし
	EXP:PLAYER:射精経験 += E
	PRINTFORML 射精(%CALLNAME:PLAYER%)
	IF TEQUIP:2 == 7
		;射精(=1)、大量射精(=3)
		J = 1 + ((E == 2)*2)
		EXP:精液経験 += J
		PRINTFORML 精液経験+{J}
		TFLAG:9 = E
	ELSE
		;精液汚れ←温泉時は汚れリセットされるので温泉以外のみ実行
		STAIN:PLAYER:ペニス |= 4
	ENDIF
	PRINTL 

	BASE:PLAYER:射精 -= EJAC * E
	SIF BASE:PLAYER:射精 >= EJAC
		BASE:PLAYER:射精 = EJAC - 1
ENDIF

J = 0
J:1 = 0
;キス経験者同士ならキスする
IF TALENT:キス未経験 == 0 && TALENT:PLAYER:キス未経験 == 0
	IF TFLAG:17 == 2
		EXP:キス経験 += 2
		EXP:PLAYER:キス経験 += 2
		PRINTFORML %EXPNAME:96%+2
	ELSE
		EXP:キス経験 += 1
		EXP:PLAYER:キス経験 += 1
		PRINTFORML %EXPNAME:96%+1
	ENDIF
	J = 1
ENDIF

;百合の園
IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	IF TFLAG:17 == 2
		J += 4
	ELSE
		J += 1
	ENDIF
;ハッテン
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	IF TFLAG:17 == 2
		J:1 += 4 + J
	ELSE
		J:1 += 1 + J
	ENDIF
	J = 0
ELSE
	J = 0
ENDIF

;レズ、BL経験
IF J > 0
	EXP:レズ経験 += J
	EXP:PLAYER:レズ経験 += J
	PRINTFORML %EXPNAME:40%+{J}
ELSEIF J:1 > 0
	EXP:ＢＬ経験 += J:1
	EXP:PLAYER:ＢＬ経験 += J:1
	PRINTFORML %EXPNAME:41%+{J:1}
ENDIF

;好感度ランダム変化
A = RAND:6 - 1
TFLAG:30 += A

;成功度合いボーナス
IF TFLAG:18 == -1
	TIMES SOURCE:歓楽 , 0.10
	TIMES SOURCE:接触 , 0.50
	TIMES SOURCE:征服 , 0.50
	TIMES SOURCE:受動 , 0.50
	TFLAG:30 -= 10
	TFLAG:37 -= 5
ELSEIF TFLAG:18 == 0
	TIMES SOURCE:歓楽 , 1.00
	TIMES SOURCE:接触 , 1.00
	TIMES SOURCE:征服 , 1.00
	TIMES SOURCE:受動 , 1.00
	TFLAG:37 += A
	;恋慕または恋人ありで愛情経験追加
	IF TALENT:恋慕 || TALENT:恋人
		EXP:愛情経験 += 2
		PRINTFORML %EXPNAME:23%+2
	ENDIF
ELSE
	TIMES SOURCE:歓楽 , 2.00
	TIMES SOURCE:接触 , 2.00
	TIMES SOURCE:征服 , 2.00
	TIMES SOURCE:受動 , 2.00
	TFLAG:30 += 2
	TFLAG:37 += 5 + A
	;愛情経験追加
	IF TALENT:恋慕 || TALENT:恋人
		EXP:愛情経験 += 6
		PRINTFORML %EXPNAME:23%+6
	ELSE
		EXP:愛情経験 += 2
		PRINTFORML %EXPNAME:23%+2
	ENDIF
ENDIF

J = 0
IF TFLAG:17 == 2
	IF (ABL:Ｃ感覚 + ABL:Ｂ感覚) > 2
		J = (ABL:Ｃ感覚 + ABL:Ｂ感覚) - 2
		;淫乳
		SIF TALENT:淫核
			J += 1
		;淫核
		SIF TALENT:淫乳
			J += 1
		PRINTFORML %EXPNAME:21%+{J}
	ENDIF
ENDIF

;素質発覚イベント
CALL FIND_BUSTSIZE
;既成事実１に相当
SIF TFLAG:17 == 2
	TFLAG:200 = 1

RETURN 1

;------------------------------
; 地の文
;------------------------------
@TRAIN_MESSAGE_COM370
;主導権がパートナー
IF TFLAG:45
	PRINTFORM %CALLNAME:TARGET%は
	IF TEQUIP:2
		IF (TALENT:目立ちたがり && TALENT:解放 && TALENT:恥薄い && TALENT:淫核 && TALENT:淫乱 && TALENT:淫乳) == 0
			PRINT 周囲を気にしつつも
		ELSEIF (TALENT:臆病 == 0 && TALENT:恥じらい == 0 && (TALENT:目立ちたがり || TALENT:解放) && TALENT:恋慕) && CFLAG:2 > 3500 && ABL:欲望 >= 8
			PRINT 人目もはばからず
		ENDIF
	ENDIF
	SIF TALENT:恋慕 == 0
		PRINT 遠慮がちな様子で
	PRINTFORM %CALLNAME:PLAYER%
	IF TALENT:恋慕
		;温泉以外のデートスポット且つ幼児退行or幼稚
		IF TEQUIP:2 != 7 && (TALENT:幼児 || TALENT:幼稚)
			PRINT の袖を引っ張り、
		ELSE
			PRINT の背中に手を廻すと、
		ENDIF
		;好感度3500より大きい
		IF CFLAG:2 > 3500
			;欲望Lv12以上+いつもの+欲情100000以上
			IF ABL:欲望 >= 12 && TFLAG:17 == 2 && PALAM:欲情 > PALAMLV:7
				PRINT 蕩けきった表情で
			;欲望Lv10以上、淫乱、(サドor小悪魔)+いつもの
			ELSEIF ABL:欲望 >= 10 && TALENT:淫乱 && (TALENT:サド || TALENT:小悪魔) && TFLAG:17 == 2
				PRINT 妖しい笑みを浮かべながら
			;(臆病or恥じらい)、欲望Lv8以上、デート中
			ELSEIF (TALENT:臆病 || TALENT:恥じらい) && ABL:欲望 >= 8 && TEQUIP:2
				PRINT 羞恥に頬を染めながらも
			ENDIF
		ELSEIF TFLAG:17 == 1
			PRINT 優しく微笑みながら
		ENDIF
		PRINTL 
		;欲望Lv12以上+いつもの
		IF ABL:欲望 >= 12 && TFLAG:17 == 2
			;淫核or淫乳
			IF TALENT:淫核 && TALENT:淫乳
				PRINT 身をよじりながら絡み付くように
			;淫乳
			ELSEIF TALENT:淫乳
				;貧乳
				IF TALENT:貧乳
					PRINT 控えめな
				;巨乳
				ELSEIF TALENT:巨乳
					PRINT 豊かな
				ELSE
					PRINT 形の良い
				ENDIF
				PRINT 胸をぐいぐいと押し付けるようにして
			;淫核
			ELSEIF TALENT:淫核
				PRINT 下半身を擦り付けるように
			ELSE
				PRINT 身体を密着させるようにして
			ENDIF
		ELSE
			IF CFLAG:2 >= 3500
				;楽観的
				IF TALENT:楽観的
					PRINT 嬉しそうに
				;恥じらい
				ELSEIF TALENT:恥じらい
					PRINT 顔を埋めるようにして
				;臆病
				ELSEIF TALENT:臆病
					PRINT おずおずと
				;幼児退行or幼稚
				ELSEIF TALENT:幼児 || TALENT:幼稚
					PRINT しがみつくように
				ELSE
					PRINT ぎゅっと
				ENDIF
			ELSE
				PRINT 軽く
			ENDIF
		ENDIF
	ELSE
		PRINT に
	ENDIF
	PRINTW 抱きついてきた…
ELSE
	PRINTFORM %CALLNAME:PLAYER%は
	SIF TALENT:恋慕 == 0
		PRINT 戸惑い気味な
	PRINTFORM %CALLNAME:TARGET%の腰に腕を廻して
	SIF ABL:Ｃ感覚 < 3 && ABL:Ｂ感覚 < 3
		PRINT 優しく
	IF TALENT:恋慕 == 0
		PRINTW 抱き寄せた…
	ELSE
		PRINTL 抱き寄せると
		SIF (TALENT:目立ちたがり && TALENT:解放 && TALENT:恥薄い && TALENT:淫核 && TALENT:淫乱 && TALENT:淫乳) == 0 && TEQUIP:2 != 0
			PRINT 周囲を気にしつつ、
		PRINTFORM %CALLNAME:TARGET%も
		IF TFLAG:17 == 2
			IF TALENT:淫核 && TALENT:淫乳
				PRINT 身をよじりながら、絡み付くように
			ELSEIF TALENT:淫核
				PRINT 下半身を擦り付けるように
			ELSEIF TALENT:淫乳
				IF TALENT:貧乳
					PRINT 控えめな
				ELSEIF TALENT:巨乳
					PRINT 豊かな
				ELSE
					PRINT 形の良い
				ENDIF
				PRINT 胸をぐいぐいと押し付けるようにして
			ELSE
				PRINT 身体を密着させるようにして
			ENDIF
		ELSE
			IF TALENT:楽観的 && CFLAG:2 > 3500
				PRINT 嬉しそうに
			ELSEIF TALENT:恥じらい && CFLAG:2 > 3500
				PRINT 顔を埋めるようにして
			ELSEIF TALENT:臆病
				PRINT おずおずと
			ELSEIF TALENT:幼児 && CFLAG:2 > 3500
				PRINT しがみつくように
			ELSEIF CFLAG:2 < 3500
				PRINT ぎゅっと
			ELSE
				PRINT 軽く
			ENDIF
		ENDIF
		PRINTW 抱き返してきた…
	ENDIF
ENDIF

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_370
;実行判定

A = 0
S = 0
V = 65
;すべての命令に共通の要素を考慮
;(親密が高いと命令に従いやすいなど)
CALL COM_ORDER

;男嫌い
IF TALENT:男嫌い && TALENT:PLAYER:オトコ
	A -= 7
ENDIF

;ABL:欲望
IF ABL:欲望
	A += ABL:欲望 * 1
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	A += ABL:奉仕精神 * 2
ENDIF
;PALAM:欲情
IF PALAM:欲情 < PALAMLV:1
	L = 0
ELSEIF PALAM:欲情 < PALAMLV:2
	L = 1
ELSEIF PALAM:欲情 < PALAMLV:3
	L = 2
ELSEIF PALAM:欲情 < PALAMLV:4
	L = 3
ELSEIF PALAM:欲情 < PALAMLV:5
	L = 4
ELSE
	L = 5
ENDIF
IF L
	A += L * 1
ENDIF

;恥じらい
IF TALENT:恥じらい
	A -= 5
ENDIF
;献身的
IF TALENT:献身的
	A += 6
ENDIF
;快感の否定
IF TALENT:快感の否定
	A -= 1
ENDIF
;恋慕
IF TALENT:恋慕 && ASSIPLAY == 0
	A += 5
	S = 5
ENDIF

;キス未経験
IF TALENT:キス未経験
	A -= 30
ENDIF

;合意
IF TALENT:合意
	A += 20
	S = 5
ENDIF

;一線越えない（合意が無い時のみ）
IF TALENT:合意 == 0 && TALENT:一線越えない
	A -= 15
ENDIF

;ムードボーナス
B = 0
IF TFLAG:37 < 10
	B -= 10
ELSEIF TFLAG:37 < 30
	B -= 1
ELSEIF TFLAG:37 < 50
	B += 1
ELSEIF TFLAG:37 < 100
	B += TFLAG:37 / 10
ELSEIF TFLAG:37 < 300
	B += TFLAG:37 / 5
ELSE
	B += TFLAG:37 / 4
ENDIF
A += B
IF B < 0
	B = B * -1
ELSE
ENDIF


;好感度
IF CFLAG:2 < 500
	B = 0
	IF CFLAG:2 < 100
		B = 20
	ELSEIF CFLAG:2 < 300
		B = 10
	ELSE
		B = 5
	ENDIF
	A -= B
ENDIF

;好感度
IF CFLAG:2 >= 700
	B = 0
	B = CFLAG:2 / 80
	A += B
ENDIF

IF ((FLAG:23 & 1p27) == 0) && FLAG:2 != 1
	;男嫌い
	IF TALENT:男嫌い && TALENT:PLAYER:オトコ
		PRINT  - 
		PRINTS TALENTNAME:82
		PRINTV '(,7,')
		S = 1
	ENDIF

	;ABL:欲望
	IF ABL:欲望
		SIF S
			PRINT  + 
		PRINTS ABLNAME:1
		PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 1,')
		S = 1
	ENDIF
	;ABL:奉仕精神
	IF ABL:奉仕精神
		SIF S
			PRINT  + 
		PRINTS ABLNAME:6
		PRINTV 'LV,ABL:奉仕精神,'(,ABL:奉仕精神 * 2,')
		S = 1
	ENDIF
	;PALAM:欲情
	IF PALAM:欲情 < PALAMLV:1
		L = 0
	ELSEIF PALAM:欲情 < PALAMLV:2
		L = 1
	ELSEIF PALAM:欲情 < PALAMLV:3
		L = 2
	ELSEIF PALAM:欲情 < PALAMLV:4
		L = 3
	ELSEIF PALAM:欲情 < PALAMLV:5
		L = 4
	ELSE
		L = 5
	ENDIF
	IF L
		SIF S
			PRINT  + 
		PRINTS PALAMNAME:5
		PRINTV 'LV,L,'(,L * 1,')
		S = 1
	ENDIF

	;恥じらい'
	IF TALENT:恥じらい
		PRINT  - 
		PRINTS TALENTNAME:35
		PRINTV '(,5,')
		S = 1
	ENDIF
	;献身的
	IF TALENT:献身的
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:63
		PRINTV '(,6,')
		S = 1
	ENDIF
	;快感の否定
	IF TALENT:快感の否定
		PRINT  - 
		PRINTS TALENTNAME:71
		PRINTV '(,1,')
		S = 1
	ENDIF
	;恋慕
	IF TALENT:恋慕 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:85
		PRINTV '(,5,')
		S = 5
	ENDIF

	;キス未経験
	IF TALENT:キス未経験
		PRINT  - 
		PRINTS TALENTNAME:151
		PRINTV '(,20,')
		S = 1
	ENDIF

	;合意
	IF TALENT:合意
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:152
		PRINTV '(,20,')
		S = 5
	ENDIF

	;一線越えない（合意が無い時のみ）
	IF TALENT:合意 == 0 && TALENT:一線越えない
		PRINT  - 
		PRINTS TALENTNAME:27
		PRINTV '(,15,')
		S = 1
	ENDIF

	;ムードボーナス
	B = 0
	IF TFLAG:37 < 10
		B -= 10
	ELSEIF TFLAG:37 < 30
		B -= 1
	ELSEIF TFLAG:37 < 50
		B += 1
	ELSEIF TFLAG:37 < 100
		B += TFLAG:37 / 10
	ELSEIF TFLAG:37 < 300
		B += TFLAG:37 / 5
	ELSE
		B += TFLAG:37 / 4
	ENDIF
	IF B < 0
		B = B * -1
		PRINT  - 
		PRINT ムード悪い
		PRINTV '(,B,')
		S = 1
	ELSE
		PRINT  + 
		PRINT ムード良い
		PRINTV '(,B,')
		S = 1
	ENDIF


	;好感度
	IF CFLAG:2 < 500
		B = 0
		PRINT  - 
		IF CFLAG:2 < 100
			B = 20
		ELSEIF CFLAG:2 < 300
			B = 10
		ELSE
			B = 5
		ENDIF
		PRINT 好感度不足
		PRINTV '(,B,')
		S = 1
	ENDIF

	;好感度
	IF CFLAG:2 >= 700
		B = 0
		SIF S
			PRINT  + 
		B = CFLAG:2 / 80
		PRINT 好感度
		PRINTV '(,B,')
		S = 1
	ENDIF

	;合計を表示(65以上で実行)
	PRINT  = 
	PRINTV A


	IF A < V
		PRINT  < 
		PRINT 実行値
		PRINTV V
	ELSEIF A == V
		PRINT  = 
		PRINT 実行値
		PRINTV V
	ELSEIF A > V
		PRINT  > 
		PRINT 実行値
		PRINTV V
	ENDIF
	WAIT
ENDIF

