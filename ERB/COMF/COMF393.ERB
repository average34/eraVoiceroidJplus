@COM_ABLE393
;見つめあう実行判定
;COM_ABLE共通判定
CALL COM_ABLE_COMMON, SELECTCOM
SIF RESULT == 0
	RETURN 0
;ウフフ中は無理
SIF TFLAG:44 == 1
	RETURN 0
RETURN 1

@COM393
;見つめあう
;笑うなよｗｗｗ
;通常は何もしないと同じ効果。ただし、対象が[敏感]持ちのときには敏感な部分が大変なくらいｷｭﾝｷｭﾝになっちゃうの的なあれ→関数化し口上側で関数を呼んだ時のみに変更

PRINTL 見つめあう
STR:0 = 見つめあう
CALL TRAIN_MESSAGE_B

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOSEBASE:体力 += 0
LOSEBASE:気力 += 30

SOURCE:逸脱 = 50

;PALAM:欲情をみる
IF PALAM:欲情 < PALAMLV:1
	SOURCE:露出 = 10
ELSEIF PALAM:欲情 < PALAMLV:2
	SOURCE:露出 = 30
ELSEIF PALAM:欲情 < PALAMLV:3
	SOURCE:露出 = 60
ELSEIF PALAM:欲情 < PALAMLV:4
	SOURCE:露出 = 100
ELSEIF PALAM:欲情 >= PALAMLV:4
	SOURCE:露出 = 150
ENDIF

;奉仕精神をみる
IF ABL:奉仕精神 == 0
	SOURCE:情愛 = 0
ELSEIF ABL:奉仕精神 == 1
	SOURCE:情愛 = 20
ELSEIF ABL:奉仕精神 == 2
	SOURCE:情愛 = 40
ELSEIF ABL:奉仕精神 == 3
	SOURCE:情愛 = 70
ELSEIF ABL:奉仕精神 == 4
	SOURCE:情愛 = 110
ELSE
	SOURCE:情愛 = 50 + (ABL:奉仕精神 * 20)
ENDIF

;ABL:マゾっ気をみる
IF ABL:マゾっ気 == 0
	TIMES SOURCE:情愛 , 0.80
	TIMES SOURCE:露出 , 0.80
	SOURCE:液体追加 = 0
	SOURCE:欲情追加 = 0
ELSEIF ABL:マゾっ気 == 1
	TIMES SOURCE:情愛 , 1.00
	TIMES SOURCE:露出 , 1.00
	SOURCE:液体追加 = 20
	SOURCE:欲情追加 = 30
ELSEIF ABL:マゾっ気 == 2
	TIMES SOURCE:情愛 , 1.30
	TIMES SOURCE:露出 , 1.20
	SOURCE:液体追加 = 40
	SOURCE:欲情追加 = 70
ELSEIF ABL:マゾっ気 == 3
	TIMES SOURCE:情愛 , 1.40
	TIMES SOURCE:露出 , 1.40
	SOURCE:液体追加 = 70
	SOURCE:欲情追加 = 120
ELSEIF ABL:マゾっ気 == 4
	TIMES SOURCE:情愛 , 1.70
	TIMES SOURCE:露出 , 1.50
	SOURCE:液体追加 = 110
	SOURCE:欲情追加 = 180
ELSE
	SOURCE:情愛 = SOURCE:情愛 * (ABL:マゾっ気 * 15 + 125) / 100
	SOURCE:露出 = SOURCE:露出 * (ABL:マゾっ気 * 10 + 120) / 100
	SOURCE:液体追加 = 50 + (ABL:マゾっ気 * 20)
	SOURCE:欲情追加 = 75 + (ABL:マゾっ気 * 35)
ENDIF

RETURN 1

;-------------------------------------------------
;対象が[敏感]持ちのときだけの特別処理
;通常は呼ばれず口上側で呼ばれることを想定された処理
;-------------------------------------------------
@SOURCE_COM393
;所持している[敏感]の数を記録
VARSET LOCAL, 0
;102,Ｃ敏感
SIF TALENT:Ｃ敏感
	LOCAL:1 += 1
;104,Ｖ敏感
SIF TALENT:Ｖ敏感
	LOCAL:1 += 1
;106,Ａ敏感
SIF TALENT:Ａ敏感
	LOCAL:1 += 1
;108,Ｂ敏感
SIF TALENT:Ｂ敏感
	LOCAL:1 += 1
;597,Ｍ敏感
SIF TALENT:Ｍ敏感
	LOCAL:1 += 1

FOR LOCAL, 0, 5
	IF LOCAL == 0
		;3,Ｃ感覚
		;0,快Ｃ
		;102,Ｃ敏感
		A = 3
		S = 0
		T = 102
	ELSEIF LOCAL == 1
		;4,Ｖ感覚
		;1,快Ｖ
		;104,Ｖ敏感
		A = 4
		S = 1
		T = 104
	ELSEIF LOCAL == 2
		;5,Ａ感覚
		;2,快Ａ
		;106,Ａ敏感
		A = 5
		S = 2
		T = 106
	ELSEIF LOCAL == 3
		;14,Ｂ感覚
		;17,快Ｂ
		;108,Ｂ敏感
		A = 14
		S = 17
		T = 108
	ELSEIF LOCAL == 4
		;17,Ｍ感覚
		;24,快Ｍ
		;597,Ｍ敏感
		A = 17
		S = 24
		T = 597
	ENDIF


	;各敏感もちなら各快感SOURCE取得
	IF TALENT:T
		LOSEBASE:気力 += 10
		
		;ABL:親密をみる
		IF ABL:親密 == 0
			SOURCE:S = 2
		ELSEIF ABL:親密 == 1
			SOURCE:S = 4
		ELSEIF ABL:親密 == 2
			SOURCE:S = 6
		ELSEIF ABL:親密 == 3
			SOURCE:S = 8
		ELSEIF ABL:親密 == 4
			SOURCE:S = 10
		ELSE
			SOURCE:S = ABL:親密 * 2
		ENDIF
		
		;TALENT:恋慕をみる
		SIF TALENT:恋慕
			SOURCE:S *= 10
		;TALENT:恋人をみる
		SIF TALENT:恋人
			TIMES SOURCE:S , 1.10
		;TALENT:親愛をみる
		SIF TALENT:親愛
			TIMES SOURCE:S , 1.20
		
		;ABL:各感覚をみる
		IF ABL:A == 0
			TIMES SOURCE:S , 0.10
		ELSEIF ABL:A == 1
			TIMES SOURCE:S , 0.20
		ELSEIF ABL:A == 2
			TIMES SOURCE:S , 0.40
		ELSEIF ABL:A == 3
			TIMES SOURCE:S , 0.60
		ELSEIF ABL:A == 4
			TIMES SOURCE:S , 0.80
		ELSE
			SOURCE:S = SOURCE:S * (ABL:A * 10 + 50) / 100
		ENDIF
		
		;ABL:欲望をみる
		IF ABL:欲望 == 0
			TIMES SOURCE:S , 0.50
		ELSEIF ABL:欲望 == 1
			TIMES SOURCE:S , 0.80
		ELSEIF ABL:欲望 == 2
			TIMES SOURCE:S , 1.00
		ELSEIF ABL:欲望 == 3
			TIMES SOURCE:S , 1.10
		ELSEIF ABL:欲望 == 4
			TIMES SOURCE:S , 1.20
		ELSE
			SOURCE:S = SOURCE:S * (ABL:欲望 * 10 + 100) / 100
		ENDIF

		;PALAM:欲情をみる
		IF PALAM:欲情 < PALAMLV:1
			TIMES SOURCE:S , 1.00
		ELSEIF PALAM:欲情 < PALAMLV:2
			TIMES SOURCE:S , 1.25
		ELSEIF PALAM:欲情 < PALAMLV:3
			TIMES SOURCE:S , 1.50
		ELSEIF PALAM:欲情 < PALAMLV:4
			TIMES SOURCE:S , 1.75
		ELSEIF PALAM:欲情 >= PALAMLV:4
			TIMES SOURCE:S , 2.00
		ENDIF
		
		;[敏感]の数に応じてSOURCE減少
		SOURCE:S /= LOCAL:1
		
		;潤滑
		SOURCE:液体追加 += SOURCE:S
	ENDIF
NEXT

;-------------------------------------------------
;地の文
;-------------------------------------------------
@TRAIN_MESSAGE_COM393
;何もしない同様地の文なし

