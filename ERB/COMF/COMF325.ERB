@COM_ABLE325
;縁側でくつろぐ実行判定
;COM_ABLE共通判定
CALL COM_ABLE_COMMON, SELECTCOM
;もうめんどくさいから実行不可
;そのへんの神社でくつろぐのは普通じゃないしね
RETURN 0

SIF RESULT == 0
	RETURN 0
;ウフフ中は無理
SIF TFLAG:44 == 1
	RETURN 0
;神社でデート中じゃないと無理
SIF TEQUIP:2 != 2
	RETURN 0
;助手デート時は不可
SIF ASSIPLAY
	RETURN 0
;回数の抑制が出来たので、そのうち調合コマンドのようなボーナスを付けて一回当たりの恩恵を大きくしようかなと
SIF TFLAG:52
	RETURN 0
;助手が居る
IF ASSI > 0
	;霊夢か霊夢様がパートナーor助手に居ないと無理
	SIF NO:TARGET != 1 && NO:ASSI != 1
		RETURN 0
ELSE
	;霊夢か霊夢様がパートナーに居ないと無理
	SIF NO:TARGET != 1
		RETURN 0
ENDIF

RETURN 1

@COM325
PRINTL 縁側でくつろぐ

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

STR:0 = 縁側でくつろぐ

;気力消費
IF NO:TARGET == 1
	LOSEBASE:気力 += 30
ELSE
	LOSEBASE:気力 += 50
ENDIF


PRINTL 
;初期化
TFLAG:17 = 0
TFLAG:18 = 0
A:1 = 0
A:2 = 0

;縁側でくつろぐ実行済みフラグを立てておく
TFLAG:52 = 1


;回復処理
CALL HEALING_COM325


;地の文
CALL TRAIN_MESSAGE_B

;回復する場合
IF A:1 > 0 || A:2 > 0

	PRINTFORM %CALLNAME:TARGET%の
	;体力、耐力回復する場合
	SIF A:1 > 0
		PRINTFORM 体力と耐力が{A:1}
	;全て回復する場合
	SIF A:1 > 0 && A:2 > 0
		PRINT 、
	;気力回復する場合
	SIF A:2 > 0
		PRINTFORM 気力が{A:2}
	PRINTFORMW 回復した
ENDIF

;ABL:親密をみる
IF ABL:親密 <= 1
	SOURCE:18 += (ABL:親密 * 65)
ELSEIF ABL:親密 <= 3
	SOURCE:18 += 200 + (ABL:親密 * 70)
ELSEIF ABL:親密 <= 5
	SOURCE:18 += 500 + (ABL:親密 * 70)
ELSEIF ABL:親密 <= 8
	SOURCE:18 += 700 + (ABL:親密 * 80)
ELSEIF ABL:親密 <= 11
	SOURCE:18 += 1200 + (ABL:親密 * 85)
ELSE
	SOURCE:18 += 2000 + (ABL:親密 * 40)
ENDIF

;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:親密をみる
	IF ABL:親密 <= 1
		SOURCE:21 += 100 + (ABL:親密 * 20)
	ELSEIF ABL:親密 <= 3
		SOURCE:21 += 200 + (ABL:親密 * 30)
	ELSEIF ABL:親密 <= 5
		SOURCE:21 += 300 + (ABL:親密 * 40)
	ELSEIF ABL:親密 <= 8
		SOURCE:21 += 400 + (ABL:親密 * 50)
	ELSEIF ABL:親密 <= 11
		SOURCE:21 += 500 + (ABL:親密 * 60)
	ELSE
		SOURCE:21 += 600 + (ABL:親密 * 70)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:サドっ気 == 0
		SOURCE:20 = 200
	ELSEIF ABL:サドっ気 == 1
		SOURCE:20 = 300
	ELSEIF ABL:サドっ気 == 2
		SOURCE:20 = 400
	ELSEIF ABL:サドっ気 == 3
		SOURCE:20 = 550
	ELSEIF ABL:サドっ気 == 4
		SOURCE:20 = 700
	ELSE
		SOURCE:20 = 300 + (ABL:サドっ気 * 100)
	ENDIF
ENDIF


;好感度によるボーナス
IF 0
ELSE
	IF CFLAG:2 <= 500
		SOURCE:18 += CFLAG:2
	ELSEIF CFLAG:2 <= 5000
		SOURCE:18 += 500 + (CFLAG:2 - 500) / 3
	ELSE
		SOURCE:18 += 2000 + (CFLAG:2 - 5000) / 5
	ENDIF
	SIF SOURCE:18 < 0
		SOURCE:18 = 0
ENDIF

;好感度ランダム変化
A = RAND:6 - 1
TFLAG:30 += A

;成功度によるボーナス
IF TFLAG:18 == -1
	TIMES SOURCE:18 , 0.10
	TIMES SOURCE:19 , 0.50
	TIMES SOURCE:20 , 0.50
	TIMES SOURCE:21 , 0.50
	TIMES SOURCE:22 , 2.00
	TIMES SOURCE:23 , 2.00
	TFLAG:30 -= 10
	TFLAG:37 -= 5
ELSEIF TFLAG:18 == 0
	TIMES SOURCE:18 , 1.00
	TIMES SOURCE:19 , 1.00
	TIMES SOURCE:20 , 1.00
	TIMES SOURCE:21 , 1.00
	TIMES SOURCE:22 , 1.00
	TIMES SOURCE:23 , 1.00
	TFLAG:37 += A
ELSE
	TIMES SOURCE:18 , 2.00
	TIMES SOURCE:19 , 2.00
	TIMES SOURCE:20 , 2.00
	TIMES SOURCE:21 , 2.00
	TIMES SOURCE:22 , 0.50
	TIMES SOURCE:23 , 0.50
	TFLAG:30 += 2
	TFLAG:37 += 5 + A
ENDIF

PRINTL 

RETURN 1

;------------------------------
;体力回復を一元化
;A:1は体力、耐力
;A:2は気力
;------------------------------
@HEALING_COM325
BASE:TARGET:体力 += A:1
BASE:TARGET:気力 += A:2
BASE:TARGET:耐力 += A:1


;お茶請けなので限界突破はしない
CALL LIFE_LIMIT_MAXBASE
RETURN 0

;------------------------------
;地の文A
;------------------------------
@TRAIN_MESSAGE_COM325
PRINTFORM %CALLNAME:MASTER%
;霊夢以外がパートナーの場合は一緒に寛いでいる
SIF NO:TARGET != 1
	PRINTFORM と%CALLNAME:TARGET%
PRINT が神社の縁側でくつろいでいると、
;パートナーが霊夢かで霊夢の登場の仕方が変わる
IF NO:TARGET == 1
	PRINTFORM %CALLNAME:TARGET%が
ELSE
	PRINTFORMW 何らかの用事で戻っていたらしく、神社の奥から%CALLNAME:ASSI%が現れた
	PRINTFORM %CALLNAME:ASSI%は、
ENDIF
;霊夢がパートナーかつ恋慕で、反発なし
IF TFLAG:17 == 0
	;大成功
	IF TFLAG:18 == 1
		;高級な和菓子だったらこれが良いかな？練り切りは霊夢のイメージと遠いし、体力回復には向かなそうだし
		PRINTW 玉露と金鍔を持ってきてくれた
		PRINTFORML 普段より優しい%CALLNAME:TARGET%と一緒にお茶を満喫した…
	;失敗
	ELSEIF TFLAG:18 == -1
		PRINTW 煎茶と煎餅を持ってきてくれた
		PRINTFORML %CALLNAME:TARGET%と雑談を交えつつお茶を満喫した…
	;成功
	ELSEIF TFLAG:18 == 0
		PRINTW 煎茶と串団子を持ってきてくれた
		PRINTFORML %CALLNAME:TARGET%と雑談を交えつつお茶を満喫した…
	ENDIF
;霊夢様がパートナーかつ恋慕で、反発なし
ELSEIF TFLAG:17 == 1
	;大成功
	IF TFLAG:18 == 1
		PRINTW 煎茶と最中を持ってきてくれた
		PRINTFORML 普段より優しい%CALLNAME:TARGET%とお茶を満喫した…
	;失敗
	ELSEIF TFLAG:18 == -1
		PRINTW 白湯を持ってきてくれた
		PRINTFORML 仕方なく%CALLNAME:TARGET%と一緒に白湯を飲み干した…
	;成功
	ELSEIF TFLAG:18 == 0
		PRINTW 出涸らしのお茶を持ってきてくれた
		PRINTFORML %CALLNAME:TARGET%と雑談を交えつつお茶を満喫した…
	ENDIF
;霊夢が助手かつ恋慕
ELSEIF TFLAG:17 == 2
	;大成功
	IF TFLAG:18 == 1
		PRINTW しぶしぶと煎茶と串団子を持ってきてくれた
		PRINTFORML 微妙な空気の中、%CALLNAME:TARGET%とお茶を満喫した…
	;失敗
	ELSEIF TFLAG:18 == -1
		PRINTW 煮えたぎる白湯を持ってきてくれた
		PRINTFORML %CALLNAME:ASSI%に強く促され、仕方なく%CALLNAME:TARGET%と一緒に熱湯を飲み干した…
	;成功
	ELSEIF TFLAG:18 == 0
		PRINTW しぶしぶと煎茶と煎餅を持ってきてくれた
		PRINTFORML 気不味い空気の中、%CALLNAME:TARGET%とお茶を満喫した
		PRINTFORML …が味は分からなかった…
	ENDIF
;霊夢様が助手かつ恋慕…調合知識を条件に絡めるべきか悩み所
ELSEIF TFLAG:17 == 3
	;大成功
	IF TFLAG:18 == 1
		PRINTW 怪しく濁ったお茶を持ってきた…
		PRINTFORML 躊躇う%CALLNAME:MASTER%と%CALLNAME:TARGET%に、満面の笑顔を見せる%CALLNAME:MASTER%が無言の圧力を掛ける
		PRINTL 
		PRINTFORML 暫し%CALLNAME:TARGET%はお茶を睨んでいたが、呼吸を整えると意を決して飲み込んだ
		PRINTL 
		PRINTW 見た目に反して何とも無かったようだ……？
		PRINTL 
		PRINTL 暫くすると身体の内側から溢れる程に力が湧き出てきた…
	;失敗
	ELSEIF TFLAG:18 == -1
		PRINTW 怪しく濁ったお茶を持ってきた…
		PRINTFORML 躊躇う%CALLNAME:MASTER%と%CALLNAME:TARGET%に、満面の笑顔を見せる%CALLNAME:ASSI%が無言の圧力を掛ける
		PRINTL 
		PRINTW ……どうやらこれは覚悟を決めねばならないようだ
		PRINTFORML 暫し二人でお茶らしきモノを睨んでいたが、呼吸を整えると意を決して飲み込んだ
		PRINTL 
		;改行多いわ…クリックも多いわ…三点リーダーも多いわ…
		;両者蓬莱人
		IF TALENT:128 && TALENT:MASTER:128
			PRINTW 見た目に反して何とも無かったようだ……？
			PRINTL 
			PRINTW 何かがおかしい……急に身体が重くなり、悪寒が止まらない…
			PRINTFORMW 見れば%CALLNAME:TARGET%も小刻みに震えている
			PRINTL 
			PRINTFORMW 酷い倦怠感に%CALLNAME:MASTER%と%CALLNAME:TARGET%は徐々に動けなくなり、遂に意識を失った…
			PRINTL 
		;パートナーが蓬莱人
		ELSEIF TALENT:MASTER:128 == 0 && TALENT:128
			PRINTL 飲んで暫くすると全身を稲妻が駆け抜けたような衝撃に襲われ
			PRINTFORMW %CALLNAME:MASTER%は意識を手放した
			PRINTL 
			PRINTFORMW %CALLNAME:TARGET%が小刻みに震えていた事が気になる……
			PRINTL 
		;主人公が蓬莱人
		ELSEIF TALENT:128 == 0 && TALENT:MASTER:128
			PRINTW 見た目に反して何とも無かったようだ……？
			PRINTL 
			PRINTFORML 暫くすると%CALLNAME:TARGET%が短い悲鳴と共にぱたりと倒れ込んだ
			PRINTFORMW %CALLNAME:ASSI%に詰め寄ろうとするが……何かがおかしい
			PRINTW 急に身体が重くなり、悪寒が止まらない…
			PRINTL 
			PRINTFORMW 酷い倦怠感に%CALLNAME:MASTER%は徐々に動けなくなり、遂に意識を失った…
			PRINTL 
		;それ以外
		ELSE
			PRINTL 飲んで暫くすると全身を稲妻が駆け抜けたような衝撃に襲われ、
			PRINTFORMW %CALLNAME:MASTER%と%CALLNAME:TARGET%は意識を手放した…
		ENDIF
		CALL TRAIN_MESSAGE_COM325B
	;成功
	ELSEIF TFLAG:18 == 0
		PRINTW しぶしぶと煎茶と煎餅を持ってきてくれた
		PRINTFORML 気不味い空気の中、%CALLNAME:TARGET%とお茶を満喫した
		PRINTFORML …が味は分からなかった…
	ENDIF
;霊夢が非恋慕または反発または助手
ELSEIF TFLAG:17 == 4
	;大成功
	IF TFLAG:18 == 1
		;コレ書いてると腹減って来るわ
		PRINTW 煎茶と串団子を持ってきてくれた
		PRINTFORML %CALLNAME:TARGET%と雑談を交えつつお茶を満喫した…
	;失敗
	ELSEIF TFLAG:18 == -1
		PRINTW 出涸らしのお茶を持ってきてくれた
		;パートナーの場合は
		IF NO:TARGET == 1
			PRINTFORML %CALLNAME:TARGET%の機嫌を取りつつ、一緒にお茶を満喫した…
		ELSE
			PRINTFORML %CALLNAME:ASSI%の懐具合を気に掛けつつ、%CALLNAME:TARGET%とお茶を満喫した…
		ENDIF
	;成功
	ELSEIF TFLAG:18 == 0
		PRINTW 煎茶と煎餅を持ってきてくれた
		PRINTFORML %CALLNAME:TARGET%と雑談を交えつつお茶を満喫した…
	ENDIF
;霊夢様がパートナーかつ非恋慕
ELSEIF TFLAG:17 == 5
	PRINTW 自分の分だけ煎茶と煎餅を持ってきた
	PRINTFORML 仕方なくお茶が終わるのを傍で待つことにした…
;霊夢様が助手かつ非恋慕
ELSEIF TFLAG:17 == 6
	PRINTW 面倒臭そうに白湯を持ってきてくれた
	PRINTFORML 仕方なく%CALLNAME:TARGET%と一緒に白湯を飲み干した…
ENDIF
PRINTL 
;霊夢様がパートナー且つ恋慕、大成功時は追加
SIF TFLAG:17 == 1 && TFLAG:18 == 1
	PRINTW …？何だか妙な気分になってきた

RETURN 0

;------------------------------
;地の文B
;コマンド地の文で主観視点はおかしいので修正(口上ではないので不自然)
;------------------------------
@TRAIN_MESSAGE_COM325B
PRINTL 
PRINTW …
PRINTL 
PRINTW ……
PRINTL 
PRINTW ………
PRINTL 
;霊夢様が挿入可能部位を持っている場合、ｱｯｰｰｰｰｰ!!(肉棒、角、ペニスバンド)…角持つ可能性ありませんが一応判定
IF TALENT:ASSI:121 || TALENT:ASSI:122 || TALENT:ASSI:119 || ITEM:4 == 1
	IF ITEM:14
		PRINTFORML %EXPNAME:51%＋5 (%CALLNAME:MASTER%)
		PRINTW 
	ENDIF
	PRINTFORML %EXPNAME:1%＋10 (%CALLNAME:MASTER%)
	PRINTFORML %EXPNAME:2%＋10 (%CALLNAME:MASTER%)
	PRINTFORML %EXPNAME:30%＋50 (%CALLNAME:MASTER%)
	PRINTFORML %EXPNAME:31%＋10 (%CALLNAME:MASTER%)

	;霊夢様がふたなり、オトコだと射精
	IF TALENT:ASSI:121 || TALENT:ASSI:122
		PRINTFORML %EXPNAME:3%＋20 (%CALLNAME:ASSI%)
		PRINTFORML %EXPNAME:20%＋20 (%CALLNAME:MASTER%)
	ENDIF
	;主人公がふたなり、オトコだと射精
	IF TALENT:MASTER:121 || TALENT:MASTER:122
		PRINTFORML %EXPNAME:3%＋20 (%CALLNAME:MASTER%)
		PRINTFORML %EXPNAME:20%＋20 (%CALLNAME:ASSI%)
	ENDIF
	;ビデオテープ、カメラがある場合、撮影開始
	IF K > 0
		PRINTW 
		PRINTFORML %EXPNAME:91%＋{K} (%CALLNAME:ASSI%)
		PRINTFORML %EXPNAME:92%＋{K} (%CALLNAME:MASTER%)
	ENDIF
	PRINTL 
	;霊夢様のサドっ気変動
	IF ABL:ASSI:サドっ気 <= 4
		PRINTL 
		PRINTFORML %CALLNAME:ASSI%の%ABLNAME:15%が5になった
	ELSEIF ABL:ASSI:サドっ気 < 15
		PRINTL 
		PRINTFORML %CALLNAME:ASSI%の%ABLNAME:15%が{ABL:ASSI:サドっ気}になった
	ENDIF
ENDIF
PRINTL 
;異常経験取得あり
IF A > 0
	PRINTFORML %EXPNAME:50%＋{A} (%CALLNAME:MASTER%)
ENDIF

PRINTL 
PRINTL 
PRINTW ………
PRINTL 
PRINTW ……
PRINTL 
PRINTW …
PRINTL 
PRINTFORM %CALLNAME:MASTER%は神社の居間で眠っていたらし
IF G
	PRINT く、全身にだるさを感じていた
ELSE
	PRINT い…
ENDIF
PRINTL 
PRINTFORMW %CALLNAME:MASTER%が起きると、%CALLNAME:ASSI%は拗ねた様な表情で先に帰っていった
PRINTL 
;ｱｯ----が無い場合は2以上に増える事はない
SIF A > 1
	PRINTFORM %CALLNAME:MASTER%は尻を無性に気にしながら
PRINT 障子を開けると、既に
IF TIME == 0
	PRINTL 午後を回っているようだった
ELSE
	PRINTL 東の空が明るくなりかけていた
ENDIF
PRINTFORMW 
SIF TIME == 1
	PRINT 薄明りの中、
PRINTFORML 縁側で横になっている%CALLNAME:TARGET%を見ると、どうやらうなされている様子だ
PRINTFORMW %CALLNAME:ASSI%は先に帰宅したようなので、%CALLNAME:TARGET%を背負い帰宅した…
RETURN 0

;------------------------------
;TFLAG:18に大成功(=1)、成功(=0)、失敗(=-1)を格納
;分岐がややこしいので処理の共有化
;------------------------------
@GET_SUCCESS_RANK
;霊夢様は恋慕なしだと成功のみ
SIF TFLAG:17 >= 5
	RETURN 0

;標準成功レート
CALL GET_SUCCESS_RATE
B = 85 + A / 10
SIF B > 99
	B = 99
A = RAND:100
;霊夢、霊夢様がパートナーかつ恋慕で、反発なし
IF TFLAG:17 < 2
	SIF TALENT:153
		A -= 2
	SIF TALENT:88
		A -= 2
	SIF TALENT:89
		A -= 3
;霊夢、霊夢様が実行者かつ恋慕
ELSEIF TFLAG:17 < 4
	;誰かのメリットは別の誰かにはデメリットって感じで
	SIF TALENT:85
		A += 1
	SIF TALENT:153
		A += 2
	SIF TALENT:88
		A += 1
	SIF TALENT:89
		A += 3
	SIF TFLAG:37 >= 30
		A += 1
	SIF TFLAG:37 >= 60
		A += 1
ENDIF
IF A <= 9
	;20％で大成功
	TFLAG:18 = 1
ELSEIF A >= B
	;15～1％で失敗
	TFLAG:18 = -1
ELSE
	;残りは成功
	TFLAG:18 = 0
ENDIF

RETURN 0

;------------------------------
;此処だけややこしい事しているので分割
;蓬莱人が消えるって不死になってないな……と言う事でコメントアウト(使いたい方は個別で御利用くださいまし)
;助手は霊夢様のみ。被害者は主人、パートナーのみに限定
;使用変数:A、G、K
;------------------------------
@REIMUSAMA_EVENT_COM325
;蓬莱人消去有無を記録
G = 0
;主人、パートナーが蓬莱人の場合は素質変動
;IF TALENT:128 || TALENT:MASTER:128
;	;直後に戻ったりもするけど、そこのフラグ弄るのはやりすぎなので自重
;	IF TALENT:MASTER:128
;		PRINTFORM %CALLNAME:MASTER%
;		SIF TALENT:128
;			PRINTFORM と
;	ENDIF
;	SIF TALENT:128
;		PRINTFORM %CALLNAME:TARGET%
;	PRINTFORM は【%TALENTNAME:128%】ではなくなった…
;ENDIF

;主人公が蓬莱人の場合、謎の魅力と、回復早いも消去
IF TALENT:MASTER:128
;	TALENT:MASTER:128 = 0
;	IF TALENT:MASTER:92
;		TALENT:MASTER:92 = 0
;		PRINTFORMW %CALLNAME:MASTER%は【%TALENTNAME:92%】を失った…
;	ENDIF
;	IF TALENT:MASTER:111
;		TALENT:MASTER:111 = 0
;		PRINTFORMW %CALLNAME:MASTER%は【%TALENTNAME:111%】を失った…
;	ENDIF
	G = 1
	A += 1
ENDIF

;パートナーが蓬莱人の場合は素質変動(主人はソース取得に関係なし)
;固定で獲得するソース
IF TALENT:128
;	TALENT:128 = 0
	SOURCE:14 = 3000
	SOURCE:23 += 50000
ELSE
	SOURCE:6 = 20000
	SOURCE:14 = 500
ENDIF

;ビデオカメラがなければ、テープがあっても不可
SIF ITEM:6
	K = ITEM:28

;しかしそれは巧妙な罠だった
;霊夢様が挿入可能部位を持っている場合、ｱｯｰｰｰｰｰ!!(肉棒、角、ペニスバンド)…角持つ可能性ありませんが一応判定
IF TALENT:ASSI:121 || TALENT:ASSI:122 || TALENT:ASSI:119 || ITEM:4 == 1
	;縄があれば緊縛経験、異常経験を＋
	IF ITEM:14
		A += 1
		EXP:MASTER:緊縛経験 += 5
	ENDIF
	;A、絶頂、苦痛快楽、放尿経験、異常経験を＋(A挿入、放尿で1ずつ)
	A += 2
	EXP:MASTER:Ａ経験 += 10
	EXP:MASTER:絶頂経験 += 10
	EXP:MASTER:苦痛快楽経験 += 50
	EXP:MASTER:放尿経験 += 10

	;霊夢様がふたなり、オトコだと射精
	IF TALENT:ASSI:121 || TALENT:ASSI:122
		EXP:ASSI:射精経験 += 20
		EXP:MASTER:精液経験 += 20
	ELSE
		EXP:ASSI:絶頂経験 += 10
	ENDIF
	;主人公がふたなり、オトコだと射精
	IF TALENT:MASTER:121 || TALENT:MASTER:122
		EXP:MASTER:射精経験 += 20
		EXP:ASSI:精液経験 += 20
	ELSE
		EXP:MASTER:絶頂経験 += 10
	ENDIF

	;ビデオテープ、カメラがある場合、撮影開始
	IF K > 0
		;消費量は最大で10
		SIF K >= 10
			K = 10
		A += 1
		;テープの消費と経験の増加
		;ITEM:28 -= K
		EXP:ASSI:撮影経験 += K
		EXP:MASTER:被写経験 += K

		;弱味ゲット(相手は身に覚えがない事なので弱味にはならない)
;		IF TALENT:37 == 0
;			TALENT:37 = 1
;		PRINTFORML %CALLNAME:ASSI%は%CALLNAME:TARGET%の【%TALENTNAME:37%】を得た
;		ENDIF
	ENDIF

	;霊夢様のサドっ気変動
	IF ABL:ASSI:サドっ気 <= 4
		ABL:ASSI:サドっ気 = 5
	ELSEIF ABL:ASSI:サドっ気 < 15
		ABL:ASSI:サドっ気 += 1
	ENDIF
ENDIF
;異常経験取得あり
IF A > 0
	EXP:MASTER:異常経験 += A
ENDIF
;;パートナーの蓬莱人を消去
;IF TALENT:128
;	TALENT:128 = 0
;	PRINTL 
;	EXP:MASTER:異常経験 += 1
;	PRINTFORML %EXPNAME:50%+1 (%CALLNAME:TARGET%)
;ENDIF
;コレが無いとオチがつかないので、所持品関係なし。←気絶させた上での暴挙なので必要ないと判断
;EXP:TARGET:記憶消去経験 += 1
;PRINTFORML %EXPNAME:98%＋1 (%CALLNAME:TARGET%)

RETURN 0

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_325

A = 0
B = 0
S = 0
V = 50

;すべての命令に共通の要素を考慮
;(親密が高いと命令に従いやすいなど)
CALL COM_ORDER

;男嫌い
IF TALENT:82 && TALENT:MASTER:122
	A -= 7
ENDIF
;恥じらい
IF TALENT:35
	A -= 5
ENDIF
;献身的
IF TALENT:63
	A += 6
ENDIF
;恋慕
IF TALENT:85
	A += 5
ENDIF

;好感度不足
IF CFLAG:2 < 400
	IF CFLAG:2 < 50
		B = 40
	ELSEIF CFLAG:2 < 150
		B = 20
	ELSEIF CFLAG:2 < 300
		B = 10
	ELSE
		B = 5
	ENDIF
	A -= B
;好感度ボーナス
ELSEIF CFLAG:2 >= 500
	B = CFLAG:2 / 75
	A += B
ENDIF

IF ((FLAG:23 & 1p27) == 0) && FLAG:2 != 1
	;男嫌い
	IF TALENT:82 && TALENT:MASTER:122
		PRINT  - 
		PRINTS TALENTNAME:82
		PRINTV '(,7,')
		S = 1
	ENDIF
	;恥じらい
	IF TALENT:35
		PRINT  - 
		PRINTS TALENTNAME:35
		PRINTV '(,5,')
		S = 1
	ENDIF
	;献身的
	IF TALENT:63
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:63
		PRINTV '(,6,')
		S = 1
	ENDIF
	;恋慕
	IF TALENT:85
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:85
		PRINTV '(,5,')
		S = 1
	ENDIF

	;好感度
	IF CFLAG:2 < 400
		B = 0
		PRINT  - 
		IF CFLAG:2 < 50
			B = 40
		ELSEIF CFLAG:2 < 150
			B = 20
		ELSEIF CFLAG:2 < 300
			B = 10
		ELSE
			B = 5
		ENDIF
		PRINT 好感度不足
		PRINTV '(,B,')
		S = 1
	ENDIF

	;好感度
	IF CFLAG:2 >= 500
		B = 0
		SIF S
			PRINT  + 
		B = CFLAG:2 / 75
		PRINT 好感度
		PRINTV '(,B,')
		S = 1
	ENDIF

	;合計を表示(50以上で実行)
	PRINT  = 
	PRINTV A

	IF A < V
		PRINT  < 
		PRINT 実行値
		PRINTV V
	ELSEIF A == V
		PRINT  = 
		PRINT 実行値
		PRINTV V
	ELSEIF A > V
		PRINT  > 
		PRINT 実行値
		PRINTV V
	ENDIF
	WAIT
ENDIF

