@COM_ABLE366
;着衣騎乗位実行判定
;COM_ABLE共通判定
CALL COM_ABLE_COMMON, SELECTCOM
SIF RESULT == 0
	RETURN 0
;ウフフ中は無理
SIF TFLAG:44 == 1
	RETURN 0
;デート中は無理
SIF TEQUIP:2
	RETURN 0
;オムツ着用中は無理
SIF TEQUIP:20
	RETURN 0
;オトコだとダメ
SIF TALENT:122
	RETURN 0
;調教者がオトコ、半人半妖、ふたなり、もしくはペニスバンドがないとダメ
SIF TALENT:PLAYER:119 == 0 && TALENT:PLAYER:121 == 0 && TALENT:PLAYER:122 == 0 && ITEM:PBAND == 0
	RETURN 0
;小人体型持ちは、双方が調教者と調教対象ならOK
;それ以外の場合は主人が禁断の知識持ちじゃないとダメ
SIF TALENT:149 && (TALENT:MASTER:125 == 0 && TALENT:PLAYER:149 == 0)
	RETURN 0
;潤滑10000以上かつ、欲情10000以上が必要
SIF PALAM:3 < 10000 || PALAM:5 < 10000
	RETURN 0
;欲望9以上が必要
SIF ABL:1 < 9
	RETURN 0
;親密13以上or好感度6000以上が必要
SIF ABL:0 < 13 && CFLAG:2 < 6000
	RETURN 0
;合意が必要
SIF TALENT:152 == 0
	RETURN 0
;淫乱が必要
SIF TALENT:76 == 0
	RETURN 0
;ムードか酒の勢いが必要
SIF TFLAG:37 < 50 && TFLAG:47 < 30
	RETURN 0
;着衣挿入コマンド拡張コンフィグがONじゃないと無理
SIF FLAG:61 & 1p6 == 0
	RETURN 0
RETURN 1

@TRAIN_MESSAGE_COM366
;着衣騎乗位
;前回と今回の調教者が同じで、前回の調教が着衣騎乗位であり、抜かないフラグがある場合
IF TFLAG:60 && PREVCOM == 366 && ((ASSIPLAY && TFLAG:50) || (ASSIPLAY == 0 && TFLAG:50 == 0))
	PRINTFORMW %CALLNAME:TARGET%は挿入されたままのペニスを締め付け、再び腰を動かし始めた…
;同様に、前回の調教が着衣正常位か着衣後背位である場合
ELSEIF TFLAG:60 && (PREVCOM == 363 || PREVCOM == 365) && ((ASSIPLAY && TFLAG:50) || (ASSIPLAY == 0 && TFLAG:50 == 0))
	PRINTFORMW %CALLNAME:MASTER%は挿入したまま横になり、%CALLNAME:TARGET%を腰に乗せて下から突き上げた…
ELSE
	PRINTFORM %CALLNAME:TARGET%は%CALLNAME:PLAYER%の上にまたがり自分で
	IF PALAM:3 >5000
		PRINTFORM ぐしょぐしょに濡れた
	ELSE
		PRINTFORM 湿り気を帯びた
	ENDIF
	IF PREVCOM == 362 || PREVCOM == 363 || PREVCOM == 364 || PREVCOM == 365 || PREVCOM == 366
		PRINTFORM 下着をずらしたまま
	ELSE
		PRINTFORM 下着をずらして
	ENDIF
	PRINTFORM 、ゆっくりと腰を下ろし
	IF TALENT:100 && EXP:0 == 0
		PRINT 、男を知らない幼いワレメでペニスを飲み込んでいった…
	ELSEIF EXP:0 == 0
		PRINT 、男を知らない秘裂でペニスを飲み込んでいった…
	ELSE
		PRINT てきた…
	ENDIF
	IF EXP:0 >= EXPLV:3
		PRINTL  
		PRINTFORM %CALLNAME:TARGET%は
		SIF TALENT:100
			PRINT 小さな身体で懸命に
		SIF TALENT:110
			PRINT 大きく胸を揺らしながら
		PRINT 自ら腰を動かしている…
	ENDIF
	WAIT
ENDIF


@COM366
;着衣騎乗位

PRINTL 着衣騎乗位

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL COM_ORDER_COMMON
IF RESULT == 0
	;目標に届かない場合は白い目で見られる
	PRINTFORML %CALLNAME:TARGET%は信じられないものを見るような目でこちらを見ている……
	PRINTFORMW %CALLNAME:TARGET%の好感度が50下がりました。
	CFLAG:2 -= 50
	RETURN 0
ENDIF

;-------------------------------------------------
;実行決定
;-------------------------------------------------
STR:0 = 着衣騎乗位
CALL TRAIN_MESSAGE_B

;前回と今回の調教者が同じで、前回の調教が着衣騎乗位であり、抜かないフラグがある場合
IF TFLAG:60 && PREVCOM == 366 && ((ASSIPLAY && TFLAG:50) || (ASSIPLAY == 0 && TFLAG:50 == 0))
	SIF TFLAG:31
		TFLAG:31 = 2
;同様に、前回の調教が着衣正常位か着衣後背位である場合
ELSEIF TFLAG:60 && (PREVCOM == 363 || PREVCOM == 365) && ((ASSIPLAY && TFLAG:50) || (ASSIPLAY == 0 && TFLAG:50 == 0))
	SIF TFLAG:31
		TFLAG:31 = 2
ENDIF

;V経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:19 = 1
SIF TALENT:85 && ASSIPLAY == 0 && EXP:0 == 0
	TFLAG:20 = 1

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
SIF TALENT:PLAYER:121 || TALENT:PLAYER:122
	BASE:PLAYER:2 += CALC_EJAC(SELECTCOM, PLAYER)

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOSEBASE:0 += 70
LOSEBASE:1 += 160

SOURCE:12 = 1000
SOURCE:14 = 500
SOURCE:21 = 200
SOURCE:22 = 500

;上のほうで計算した汚れデータ
SOURCE:8 = Y*10 + 60

;ABL:奉仕精神をみる
IF ABL:6 == 0
	SOURCE:4 = 200
	SOURCE:5 = 50
	SOURCE:14 = 300
	TIMES SOURCE:8 , 4.00
ELSEIF ABL:6 == 1
	SOURCE:4 = 250
	SOURCE:5 = 200
	SOURCE:14 = 100
	TIMES SOURCE:8 , 2.50
ELSEIF ABL:6 == 2
	SOURCE:4 = 350
	SOURCE:5 = 550
	SOURCE:14 = 30
	TIMES SOURCE:8 , 1.50
ELSEIF ABL:6 == 3
	SOURCE:4 = 450
	SOURCE:5 = 900
	SOURCE:14 = 0
	TIMES SOURCE:8 , 1.00
ELSEIF ABL:6 == 4
	SOURCE:4 = 600
	SOURCE:5 = 1500
	SOURCE:14 = 0
	TIMES SOURCE:8 , 0.50
ELSE
	SOURCE:4 = 375 + (ABL:6 * 75)
	SOURCE:5 = 450 + (ABL:6 * 350)
	SOURCE:14 = 0
	TIMES SOURCE:8 , 0.10
ENDIF

;ABL:Ｖ感覚をみる
IF ABL:4 == 0
	SOURCE:1 = 20
	SOURCE:3 = 25
	TIMES SOURCE:4 , 0.50
ELSEIF ABL:4 == 1
	SOURCE:1 = 75
	SOURCE:3 = 75
	TIMES SOURCE:4 , 0.80
ELSEIF ABL:4 == 2
	SOURCE:1 = 200
	SOURCE:3 = 125
	TIMES SOURCE:4 , 1.00
ELSEIF ABL:4 == 3
	SOURCE:1 = 500
	SOURCE:3 = 175
	TIMES SOURCE:4 , 1.20
ELSEIF ABL:4 == 4
	SOURCE:1 = 850
	SOURCE:3 = 300
	TIMES SOURCE:4 , 1.50
ELSE
	SOURCE:1 = 475 + (ABL:4 * 125)
	SOURCE:3 = 125 + (ABL:4 * 60)
	SOURCE:4 = SOURCE:4 * (ABL:4 * 25 + 75) / 100
ENDIF

;EXP:Ｖ経験をみる
IF EXP:0 < EXPLV:1
	TIMES SOURCE:1 , 0.20
	SOURCE:6 = 5000
	SOURCE:12 = 12000
	SOURCE:14 = 120000
ELSEIF EXP:0 < EXPLV:2
	TIMES SOURCE:1 , 0.60
	SOURCE:6 = 220
ELSEIF EXP:0 < EXPLV:3
	TIMES SOURCE:1 , 1.00
	SOURCE:6 = 30
ELSEIF EXP:0 < EXPLV:4
	TIMES SOURCE:1 , 1.20
	SOURCE:6 = 5
ELSEIF EXP:0 < EXPLV:5
	TIMES SOURCE:1 , 1.40
ELSE
	TIMES SOURCE:1 , 1.50
ENDIF


;PALAM:潤滑
IF PALAM:3 < PALAMLV:1
	TIMES SOURCE:1 , 0.20
	SOURCE:6 += 900
	TIMES SOURCE:6 , 3.00
	SOURCE:12 += 2000
ELSEIF PALAM:3 < PALAMLV:2
	TIMES SOURCE:1 , 0.60
	SOURCE:6 += 250
	TIMES SOURCE:6 , 1.00
	SOURCE:12 += 400
ELSEIF PALAM:3 < PALAMLV:3
	TIMES SOURCE:1 , 1.00
	TIMES SOURCE:6 , 0.50
ELSEIF PALAM:3 < PALAMLV:4
	TIMES SOURCE:1 , 1.30
	TIMES SOURCE:6 , 0.30
ELSEIF PALAM:3 < PALAMLV:5
	TIMES SOURCE:1 , 1.60
	TIMES SOURCE:6 , 0.20
ELSE
	TIMES SOURCE:1 , 2.00
	TIMES SOURCE:6 , 0.10
ENDIF

;小柄体形
SIF TALENT:100
	TIMES SOURCE:6 , 2.00

;貞操観念
IF TALENT:30 && TALENT:85 == 0
	IF EXP:0 == 0
		TIMES SOURCE:3 , 0.60
		TIMES SOURCE:14, 15.00
		SOURCE:15 = 13000
	ELSE
		TIMES SOURCE:3 , 0.60
		TIMES SOURCE:14, 15.00
		SOURCE:15 = 1300
	ENDIF
;貞操無頓着
ELSEIF TALENT:31 || TALENT:88 && ASSIPLAY == 0
	IF EXP:0 == 0
		TIMES SOURCE:14, 0.50
		SOURCE:15 = 350
	ELSE
		TIMES SOURCE:14, 0.50
	ENDIF
ELSE
	IF EXP:0 == 0
		SOURCE:15 = 3500
	ENDIF
ENDIF

;PALAM:欲情
IF PALAM:5 < PALAMLV:1
	TIMES SOURCE:1 , 0.60
	TIMES SOURCE:3 , 0.30
ELSEIF PALAM:5 < PALAMLV:2
	TIMES SOURCE:1 , 0.80
	TIMES SOURCE:3 , 0.60
ELSEIF PALAM:5 < PALAMLV:3
	TIMES SOURCE:1 , 1.00
	TIMES SOURCE:3 , 1.00
ELSEIF PALAM:5 < PALAMLV:4
	TIMES SOURCE:1 , 1.20
	TIMES SOURCE:3 , 1.50
ELSEIF PALAM:5 < PALAMLV:5
	TIMES SOURCE:1 , 1.40
	TIMES SOURCE:3 , 1.80
ELSE
	TIMES SOURCE:1 , 1.60
	TIMES SOURCE:3 , 2.20
ENDIF

;ABL:従順をみる
IF ABL:0 == 0
	TIMES SOURCE:1 , 0.50
	TIMES SOURCE:3 , 0.60
	TIMES SOURCE:15 , 2.00
	SOURCE:13 += 700
ELSEIF ABL:0 == 1
	TIMES SOURCE:1 , 0.80
	TIMES SOURCE:3 , 0.80
	TIMES SOURCE:15 , 1.50
	SOURCE:13 += 500
ELSEIF ABL:0 == 2
	TIMES SOURCE:1 , 1.00
	TIMES SOURCE:3 , 1.00
	TIMES SOURCE:15 , 1.00
	SOURCE:13 += 300
ELSEIF ABL:0 == 3
	TIMES SOURCE:1 , 1.30
	TIMES SOURCE:3 , 1.20
	TIMES SOURCE:15 , 0.80
	SOURCE:13 += 100
ELSEIF ABL:0 == 4
	TIMES SOURCE:1 , 1.60
	TIMES SOURCE:3 , 1.40
	TIMES SOURCE:15 , 0.60
	SOURCE:13 += 30
ELSE
	SOURCE:1 = SOURCE:1 * (ABL:0 * 20 + 100) / 100
	SOURCE:3 = SOURCE:3 * (ABL:0 * 10 + 110) / 100
	TIMES SOURCE:15 , 0.30
	SOURCE:13 += 0
ENDIF

;ABL:技巧をみる
IF ABL:2 == 0
	TIMES SOURCE:1 , 0.50
	TIMES SOURCE:3 , 0.60
ELSEIF ABL:2 == 1
	TIMES SOURCE:1 , 0.80
	TIMES SOURCE:3 , 0.80
ELSEIF ABL:2 == 2
	TIMES SOURCE:1 , 1.00
	TIMES SOURCE:3 , 1.00
ELSEIF ABL:2 == 3
	TIMES SOURCE:1 , 1.30
	TIMES SOURCE:3 , 1.20
ELSEIF ABL:2 == 4
	TIMES SOURCE:1 , 1.60
	TIMES SOURCE:3 , 1.40
ELSE
	SOURCE:1 = SOURCE:1 * (ABL:2 * 20 + 100) / 100
	SOURCE:3 = SOURCE:3 * (ABL:2 * 10 + 110) / 100
ENDIF


;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:従順をみる
	IF ABL:0 <= 1
		SOURCE:21 += 300 + (ABL:0 * 50)
	ELSEIF ABL:0 <= 3
		SOURCE:21 += 600 + (ABL:0 * 100)
	ELSEIF ABL:0 <= 5
		SOURCE:21 += 800 + (ABL:0 * 120)
	ELSEIF ABL:0 <= 8
		SOURCE:21 += 1000 + (ABL:0 * 140)
	ELSEIF ABL:0 <= 11
		SOURCE:21 += 1200 + (ABL:0 * 160)
	ELSE
		SOURCE:21 += 1500 + (ABL:0 * 180)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:15 == 0
		SOURCE:20 = 800
	ELSEIF ABL:15 == 1
		SOURCE:20 = 1100
	ELSEIF ABL:15 == 2
		SOURCE:20 = 1400
	ELSEIF ABL:15 == 3
		SOURCE:20 = 1800
	ELSEIF ABL:15 == 4
		SOURCE:20 = 2300
	ELSE
		SOURCE:20 = 1000 + (ABL:15 * 350)
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:13 + 2
SIF TALENT:85 || TALENT:153
	A += 5
SIF TALENT:88
	A += 5
;合意がないと好感度が下がる
IF TALENT:152 == 0 && TFLAG:48 == 0
	A -= 14
	SOURCE:15 += 400
;恋慕でも反感
	IF TALENT:85
		SOURCE:15 += 200
	ELSE
;恋慕なしだと反感増加
		SOURCE:15 += 600
	ENDIF
ENDIF
TFLAG:30 += A
TFLAG:37 += A / 2

EXP:0 += 1
PRINTL Ｖ経験＋１

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK
;;オーバーフロー対策に、値が大きすぎる場合と、負の場合は調整を加える
;IF BASE:PLAYER:2 > 100000000 || BASE:PLAYER:2 < 0
;	BASE:PLAYER:2 = 100000000
;ENDIF
;S = BASE:PLAYER:2
;EJAC = MAXBASE:PLAYER:2
;
;IF  S > EJAC * 2
;	E = 2
;ELSEIF S > EJAC
;	E = 1
;ELSE
;	E = 0
;ENDIF

IF E
;射精している
	TIMES SOURCE:4 , 3.00
	TIMES SOURCE:5 , 5.00
	TIMES SOURCE:13 , 1.50

	;ABL:精液中毒をみる
	IF ABL:11 == 0
		SOURCE:7 = 0
	ELSEIF ABL:11 == 1
		SOURCE:7 = 300
	ELSEIF ABL:11 == 2
		SOURCE:7 = 1000
	ELSEIF ABL:11 == 3
		SOURCE:7 = 1500
	ELSEIF ABL:11 == 4
		SOURCE:7 = 2200
	ELSE
		SOURCE:7 = 1000 + (ABL:11 * 400)
	ENDIF
	
	;射精先タイプの設定
	TFLAG:703 = 1
	;射精先選択の可・不可
	;TFLAG:704 = 0
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;;大量射精
;IF E == 2
;	TIMES SOURCE:7 , 1.50
;	TIMES SOURCE:5 , 1.20
;
;	EXP:PLAYER:3 += 2
;	EXP:20 += 25
;	PRINTL 大量射精
;	PRINTL 精液経験＋25
;	;精液汚れ
;	STAIN:PLAYER:2 |= 4
;
;	BASE:PLAYER:2 -= EJAC*2
;	SIF BASE:PLAYER:2 >= EJAC
;		BASE:PLAYER:2 = EJAC-1
;	;セックスで射精フラグ
;	TFLAG:2 = 2
;;通常の射精
;ELSEIF E == 1
;	EXP:PLAYER:3 += 1
;	EXP:20 += 15
;	PRINTL 射精
;	PRINTL 精液経験＋15
;	;精液汚れ
;	STAIN:PLAYER:2 |= 4
;
;	BASE:PLAYER:2 -= EJAC
;	SIF BASE:PLAYER:2 >= EJAC
;		BASE:PLAYER:2 = EJAC-1
;	;セックスで射精フラグ
;	TFLAG:2 = 1
;ENDIF

STAIN:3 |= STAIN:PLAYER:2
STAIN:PLAYER:2 |= STAIN:3

IF TALENT:122 == 0 && TALENT:PLAYER:122 == 0
	PRINTS EXPNAME:40
	PRINTL +7
	EXP:40 += 7
	EXP:PLAYER:40 += 7
ENDIF

;IF ASSIPLAY == 0
;	IF ABL:4 >= 3
;		TFLAG:30 += 3
;	ELSE
;		TFLAG:30 += 2
;	ENDIF
;ENDIF

TFLAG:100 = 1

;既成事実３に相当
TFLAG:200 = 3

;調教者がふたなり
IF TALENT:PLAYER:121
	SOURCE:13 /= 2
ENDIF

RETURN 1

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_366
A = 0
S = 0
V = 160
;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;ABL:欲望
IF ABL:1
	A += ABL:1 * 3
ENDIF
;ABL:Ｖ感覚
IF ABL:4
	A += ABL:4 * 2
ENDIF
;ABL:奉仕精神
IF ABL:6
	A += ABL:6 * 4
ENDIF
;ABL:精液中毒
IF ABL:11
	A += ABL:11 * 1
ENDIF

IF MARK:1
	A += MARK:1 * 3
ENDIF

;PALAM:潤滑（の不足）
IF PALAM:3 < PALAMLV:3
	A -= 5
ENDIF
;PALAM:欲情
IF PALAM:5 < PALAMLV:1
	L = 0
ELSEIF PALAM:5 < PALAMLV:2
	L = 1
ELSEIF PALAM:5 < PALAMLV:3
	L = 2
ELSEIF PALAM:5 < PALAMLV:4
	L = 3
ELSEIF PALAM:5 < PALAMLV:5
	L = 4
ELSE
	L = 5
ENDIF
IF L
	A += L * 3
ENDIF

;恥じらい
IF TALENT:35
	A -= 5
ENDIF
;快感の否定
IF TALENT:71
	A -= 5
ENDIF
;男嫌い
IF TALENT:82 && TALENT:PLAYER:122
	A -= 12
ENDIF
;恋慕
IF TALENT:85 && ASSIPLAY == 0
	A += 8
	S = 5
ENDIF

;処女
IF TALENT:0
	A -= 30
ELSEIF EXP:0 < EXPLV:2
	A -= 10
ENDIF

;一線越えない（処女のみ）
IF TALENT:27 && TALENT:0
	A -= 20
ENDIF

;媚薬
IF TEQUIP:21
	A += 6
ENDIF

;童貞
IF TALENT:PLAYER:154
	A += 3
ENDIF

;ムードボーナス
B = 0
IF TFLAG:37 < 10
	B -= 12
ELSEIF TFLAG:37 < 30
	B -= 3
ELSEIF TFLAG:37 < 50
	B -= 1
ELSEIF TFLAG:37 < 100
	B += TFLAG:37 / 10
ELSEIF TFLAG:37 < 300
	B += TFLAG:37 / 5
ELSE
	B += TFLAG:37 / 4
ENDIF
A += B
IF B < 0
	B = B * -1
ELSE
ENDIF


;好感度
IF CFLAG:2 < 2000
	B = 0
	IF CFLAG:2 < 1000
		B = 50
	ELSEIF CFLAG:2 < 1300
		B = 20
	ELSEIF CFLAG:2 < 1700
		B = 10
	ELSE
		B = 5
	ENDIF
	A -= B
ENDIF
;好感度
IF CFLAG:2 >= 2500
	B = 0
	IF CFLAG:2 < 3000
		B = 5
	ELSE
		B = CFLAG:2 / 250
	ENDIF
	A += B
ENDIF

;合意
IF TALENT:152 == 0
	A -= 35
ENDIF

IF ((FLAG:23 & 1p27) == 0) && FLAG:2 != 1
	;ABL:欲望
	IF ABL:1
		SIF S
			PRINT  + 
		PRINTS ABLNAME:1
		PRINTV 'LV,ABL:1,'(,ABL:1 * 3,')
		S = 1
	ENDIF
	;ABL:Ｖ感覚
	IF ABL:4
		SIF S
			PRINT  + 
		PRINTS ABLNAME:4
		PRINTV 'LV,ABL:4,'(,ABL:4 * 2,')
		S = 1
	ENDIF
	;ABL:奉仕精神
	IF ABL:6
		SIF S
			PRINT  + 
		PRINTS ABLNAME:6
		PRINTV 'LV,ABL:6,'(,ABL:6 * 4,')
		S = 1
	ENDIF
	;ABL:精液中毒
	IF ABL:11
		SIF S
			PRINT  + 
		PRINTS ABLNAME:11
		PRINTV 'LV,ABL:11,'(,ABL:11 * 1,')
		S = 1
	ENDIF

	IF MARK:1
		SIF S
			PRINT  + 
		PRINTS MARKNAME:1
		PRINTV 'LV,MARK:1,'(,MARK:1 * 3,')
		S = 1
	ENDIF

	;PALAM:潤滑（の不足）
	IF PALAM:3 < PALAMLV:3
		PRINT  - 
		PRINT 潤滑不足
		PRINTV '(,5,')
		S = 1
	ENDIF
	;PALAM:欲情
	IF PALAM:5 < PALAMLV:1
		L = 0
	ELSEIF PALAM:5 < PALAMLV:2
		L = 1
	ELSEIF PALAM:5 < PALAMLV:3
		L = 2
	ELSEIF PALAM:5 < PALAMLV:4
		L = 3
	ELSEIF PALAM:5 < PALAMLV:5
		L = 4
	ELSE
		L = 5
	ENDIF
	IF L
		SIF S
			PRINT  + 
		PRINTS PALAMNAME:5
		PRINTV 'LV,L,'(,L * 3,')
		S = 1
	ENDIF

	;恥じらい
	IF TALENT:35
		PRINT  - 
		PRINTS TALENTNAME:35
		PRINTV '(,5,')
		S = 1
	ENDIF
	;快感の否定
	IF TALENT:71
		PRINT  - 
		PRINTS TALENTNAME:71
		PRINTV '(,5,')
		S = 1
	ENDIF
	;男嫌い
	IF TALENT:82 && TALENT:PLAYER:122
		PRINT  - 
		PRINTS TALENTNAME:82
		PRINTV '(,12,')
		S = 1
	ENDIF
	;恋慕
	IF TALENT:85 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:85
		PRINTV '(,8,')
		S = 5
	ENDIF

	;処女
	IF TALENT:0
		PRINT  - 
		PRINTS TALENTNAME:0
		PRINTV '(,30,')
		S = 1
	ELSEIF EXP:0 < EXPLV:2
		PRINT  - 
		PRINTS EXPNAME:0
		PRINT 不足
		PRINTV '(,10,')
		S = 1
	ENDIF

	;一線越えない（処女のみ）
	IF TALENT:27 && TALENT:0
		PRINT  - 
		PRINTS TALENTNAME:27
		PRINTV '(,20,')
		S = 1
	ENDIF

	;媚薬
	IF TEQUIP:21
		SIF S
			PRINT  + 
		PRINTS ITEMNAME:26
		PRINTV '(,6,')
		S = 1
	ENDIF

	;童貞
	IF TALENT:PLAYER:154
		SIF S
			PRINT  + 
		PRINTS TALENTNAME:154
		PRINTV '(,3,')
		S = 1
	ENDIF

	;ムードボーナス
	B = 0
	IF TFLAG:37 < 10
		B -= 12
	ELSEIF TFLAG:37 < 30
		B -= 3
	ELSEIF TFLAG:37 < 50
		B -= 1
	ELSEIF TFLAG:37 < 100
		B += TFLAG:37 / 10
	ELSEIF TFLAG:37 < 300
		B += TFLAG:37 / 5
	ELSE
		B += TFLAG:37 / 4
	ENDIF
	IF B < 0
		B = B * -1
		PRINT  - 
		PRINT ムード悪い
		PRINTV '(,B,')
		S = 1
	ELSE
		PRINT  + 
		PRINT ムード良い
		PRINTV '(,B,')
		S = 1
	ENDIF


	;好感度
	IF CFLAG:2 < 2000
		B = 0
		PRINT  - 
		IF CFLAG:2 < 1000
			B = 50
		ELSEIF CFLAG:2 < 1300
			B = 20
		ELSEIF CFLAG:2 < 1700
			B = 10
		ELSE
			B = 5
		ENDIF
		PRINT 好感度不足
		PRINTV '(,B,')
		S = 1
	ENDIF
	;好感度
	IF CFLAG:2 >= 2500
		B = 0
		SIF S
			PRINT  + 
		IF CFLAG:2 < 3000
			B = 5
		ELSE
			B = CFLAG:2 / 250
		ENDIF
		PRINT 好感度
		PRINTV '(,B,')
		S = 1
	ENDIF

	;合意
	IF TALENT:152 == 0
		PRINT  - 
		PRINTFORM %TALENTNAME:152%無し
		PRINTV '(,35,')
		S = 1
	ENDIF

	;合計を表示(160以上で実行)
	PRINT  = 
	PRINTV A

	SIF A < V
		PRINT  < 
	SIF A == V
		PRINT  = 
	SIF A > V
		PRINT  > 
	PRINT 実行値
	PRINTV V
	WAIT
ENDIF

