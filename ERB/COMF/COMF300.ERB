@COM300
;戦闘訓練

PRINTL 戦闘訓練
STR:0 = 戦闘訓練
CALL GET_SUCCESS_RATE
B = 95 + A / 10
SIF B > 99
	B = 99
A = RAND:100
IF A <= 9
	;10％で大成功
	TFLAG:18 = 1
ELSEIF A >= B
	;5～1％で失敗
	TFLAG:18 = -1
ELSE
	;残りは成功
	TFLAG:18 = 0
ENDIF

CALL TRAIN_MESSAGE_B

LOSEBASE:0 += 50
;戦闘経験で気力消費軽減
IF EXP:95 < 100
	LOSEBASE:1 += 100
ELSEIF EXP:95 < 500
	LOSEBASE:1 += 80
ELSE
	LOSEBASE:1 += 50
ENDIF

;固定で獲得するソース
SOURCE:18 = 100

;ABL:戦闘技能をみる
IF ABL:88 == 0
	SOURCE:5 = 0
ELSEIF ABL:88 == 1
	SOURCE:18 += 10
	SOURCE:5 = 50
ELSEIF ABL:88 == 2
	SOURCE:18 += 30
	SOURCE:5 = 100
ELSEIF ABL:88 == 3
	SOURCE:18 += 50
	SOURCE:5 = 150
ELSEIF ABL:88 == 4
	SOURCE:18 += 80
	SOURCE:5 = 200
ELSE
	SOURCE:18 += (ABL:88 * 20)
	SOURCE:5 = (ABL:88 * 50)
ENDIF

;ABL:従順をみる
IF ABL:0 <= 1
	SOURCE:18 += (ABL:0 * 50)
ELSEIF ABL:0 <= 3
	SOURCE:18 += 50 + (ABL:0 * 50)
ELSEIF ABL:0 <= 5
	SOURCE:18 += 100 + (ABL:0 * 50)
ELSEIF ABL:0 <= 8
	SOURCE:18 += 150 + (ABL:0 * 60)
ELSEIF ABL:0 <= 11
	SOURCE:18 += 200 + (ABL:0 * 60)
ELSE
	SOURCE:18 += 250 + (ABL:0 * 70)
ENDIF

;主導権をチェック
IF TFLAG:45 == 0
	;主導権が主人公側
	;ABL:マゾっ気をみる
	IF ABL:8 == 0
		SOURCE:21 = 300
	ELSEIF ABL:8 == 1
		SOURCE:21 = 600
	ELSEIF ABL:8 == 2
		SOURCE:21 = 1000
	ELSEIF ABL:8 == 3
		SOURCE:21 = 1500
	ELSEIF ABL:8 == 4
		SOURCE:21 = 2000
	ELSE
		SOURCE:21 = 1500 + (ABL:8 * 200)
	ENDIF
ELSE
	;主導権が相手側
	;ABL:サドっ気をみる
	IF ABL:15 == 0
		SOURCE:20 = 400
	ELSEIF ABL:15 == 1
		SOURCE:20 = 800
	ELSEIF ABL:15 == 2
		SOURCE:20 = 1200
	ELSEIF ABL:15 == 3
		SOURCE:20 = 1800
	ELSEIF ABL:15 == 4
		SOURCE:20 = 2700
	ELSE
		SOURCE:20 = 2500 + (ABL:15 * 200)
	ENDIF
ENDIF

;好感度ランダム変化
A = RAND:5 - 1
TFLAG:30 += A

A = RAND:10
IF A <= 5
	;60%で経験+1
	B = 1
ELSEIF A <= 8
	;30%で経験+2
	B = 2
ELSE
	;10%で経験+3
	B = 3
ENDIF

IF TFLAG:18 == -1
	TIMES SOURCE:5 , 0.50
	TIMES SOURCE:18 , 0.50
	TIMES SOURCE:20 , 0.50
	TIMES SOURCE:21 , 0.50
	TFLAG:30 -= 3
	B -= 1
ELSEIF TFLAG:18 == 0
	TIMES SOURCE:5 , 1.00
	TIMES SOURCE:18 , 1.00
	TIMES SOURCE:20 , 1.00
	TIMES SOURCE:21 , 1.00
ELSE
	TIMES SOURCE:5 , 2.00
	TIMES SOURCE:18 , 2.00
	TIMES SOURCE:20 , 2.00
	TIMES SOURCE:21 , 2.00
	TFLAG:30 += 1
	B += 1
ENDIF

PRINTFORML %EXPNAME:95%+{B}
PRINTFORMW %EXPNAME:95%（%CALLNAME:PLAYER%）+{B}
EXP:95 += B
EXP:PLAYER:95 += B

RETURN 1

;戦闘訓練
@TRAIN_MESSAGE_COM300
PRINTFORMW %CALLNAME:PLAYER%は%CALLNAME:TARGET%と戦闘訓練をした
IF TFLAG:18 == 1
	PRINTFORML 思っていたよりも良い特訓が出来た。これは良い経験になるだろう…
ELSEIF TFLAG:18 == -1
	PRINTFORML ……しかし、%CALLNAME:PLAYER%はあっさりとバテてしまった
ENDIF

