@LUNCH_EAT
;奉仕精神と料理技能が5以上必須で、親密10以上or好感度1000以上or恋人のとき
;パートナーといっしょに弁当を作って食べるオプション。条件を満たすとときどき食後の運動イベントが発生
;-------------------------------------------------
;K = 料理の種類
;L = 料理の値段,習得の珠取得量の補正を決定
;M = 料理の値段
;N = 料理の値段の基礎
;P = 固有料理の種類
;P:1 = 乱数処理
;Q = 習得の珠取得数
;R = 料理の出来栄え
;S = 奉仕快楽経験上昇値
;-------------------------------------------------
;料理関係の共通処理
CALL LUNCH_COMMON

;地の文カット
IF FLAG:6 == 1
	PRINTL 
	PRINTFORMW %CALLNAME:TARGET%に料理を作らせた
	PRINTL 
ENDIF

;一文字変数をローカル変数に保存
LOCAL:0 = K
LOCAL:1 = L
LOCAL:2 = M
LOCAL:3 = N
LOCAL:4 = Q
LOCAL:5 = R
LOCAL:6 = S
;弁当口上(LLのSHOP.ERBより拝借)
TFLAG:600 = 19
CALL KOJO_JUN
;ローカル変数から一文字変数復帰
K = LOCAL:0
L = LOCAL:1
M = LOCAL:2
N = LOCAL:3
Q = LOCAL:4
R = LOCAL:5
S = LOCAL:6

BASE:MASTER:0 = MAXBASE:MASTER:0
BASE:MASTER:4 = MAXBASE:MASTER:4
BASE:TARGET:0 = MAXBASE:TARGET:0
BASE:TARGET:4 = MAXBASE:TARGET:4

;地の文カット
SIF CFLAG:99 == 1 || FLAG:6 == 0
	GOTO LUNCH_EVALUATE

IF TALENT:153
	PRINTW   「♪～」
	PRINTW  「♪♪～」
	PRINTW 「♪～♪～」
	PRINTW      !
ELSE
	PRINTW      ･
	PRINTW      ･
	PRINTW      ･
	PRINTW      !
ENDIF
PRINTL 
PRINTFORM %CALLNAME:TARGET%は
SIF TALENT:153
	PRINT 愛情たっぷりの

IF R <= 2
	PRINT 微妙な
ELSEIF R <= 4
	PRINT 普通の
ELSEIF R <= 6
	PRINT 美味しい
ELSEIF R <= 8
	PRINT 非常に美味しい
ELSEIF R <= 10
	PRINT 最高の味がする
ENDIF
SIF K == 0 && TFLAG:14 == 1
	PRINT おいしそうな香りがする
PRINTFORMW %TSTR:1%を作った。
PRINTL      ･
PRINTL      ･
PRINTL      ･
PRINTL 
PRINTL さて……折角の料理だ。
PRINTW ありがたく頂くとしよう……
PRINTL      ･
PRINTL      ･
PRINTW      ･
PRINTFORM %CALLNAME:MASTER%と%CALLNAME:TARGET%は２人でお弁当を作って
IF TALENT:153
	PRINTL 食べさせあった
ELSE
	PRINTL 食べた
ENDIF

PRINTFORM %CALLNAME:TARGET%の料理は
IF R >= 10
	PRINTL これ以上にないほどの絶品だ。
	PRINTFORML %CALLNAME:MASTER%は貪るように出された料理を食べつくした。
	PRINTFORML あまりのがっつきっぷりに%CALLNAME:TARGET%は一瞬ビックリした表情を浮かべたが
	PRINTFORMW 自分の料理が気に入ってもらえたことに気づくと満足そうな笑みを浮かべ、おかわりが欲しいか尋ねてきた。
ELSEIF R >= 8 && R < 10
	PRINTL 素晴らしい逸品だ。
	PRINTFORML 気が付いたときにはすでに%CALLNAME:MASTER%の皿が空っぽになっていた。
	PRINTFORMW %CALLNAME:MASTER%の食べっぷりに%CALLNAME:TARGET%は満足そうな目つきでこちらを見つめている……
ELSEIF R >= 5 && R < 8
	PRINTL おいしい。
	PRINTFORMW 料理を食べ終えた%CALLNAME:MASTER%は満足げに腹をさすった。
ELSEIF R >= 3 && R < 5
	PRINTL 可も無く、不可もないというレベルだ。
	PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:TARGET%と談笑しながら料理を平らげた。
ELSEIF R < 3
	PRINTFORML おせじにも%CALLNAME:MASTER%の口に合わなかったみたいだ。
	PRINTFORMW しかしなんだかんだ言っても%CALLNAME:MASTER%は料理を完食することができた。
ENDIF
PRINTW      ･
PRINTW      ･
PRINTW      ･
PRINTFORML %CALLNAME:MASTER%と%CALLNAME:TARGET%は英気を養った
$LUNCH_EVALUATE
PRINTL 
IF L > 0
	IF ABL:90 >= 10 && TALENT:153
		JUEL:4 += L * 20
		JUEL:15 += L * 30
		PRINTFORML %PALAMNAME:4%の珠＋{L*20}
		PRINTFORML %PALAMNAME:15%の珠＋{L*30}
	ELSE
		JUEL:4 += L * 10
		JUEL:15 += L * 15
		PRINTFORML %PALAMNAME:4%の珠＋{L*10}
		PRINTFORML %PALAMNAME:15%の珠＋{L*15}
	ENDIF
ENDIF
PRINTFORML %PALAMNAME:7%の珠＋{Q}
JUEL:7 += Q
PRINTFORML %PALAMNAME:16%の珠＋{ABL:TARGET:90 * SQRT( ABL:TARGET:90 ) * R * 20}
JUEL:16 += ABL:TARGET:90 * SQRT( ABL:TARGET:90 ) * R * 20

;恋人持ちかつ食後のイベント発生確定時は愛情経験が入る
;また、食後のイベントで押し倒すことを選ぶと奉仕快楽経験が入るようになる
IF TFLAG:14 == 1 && TALENT:153
	PRINTFORML %EXPNAME:23% ＋2
	EXP:23 += 2
	TFLAG:14 = 2
;服従持ちのときは食後のイベントで押し倒すことを選ぶと奉仕快楽経験が入るようになる
ELSEIF TFLAG:14 == 1 && TALENT:160
	TFLAG:14 = 2
ENDIF

IF R >= 10
	LOCAL = 5
	SIF ABL:TARGET:90 > 99
		LOCAL = ABL:TARGET:90 / 20
	PRINTFORML %EXPNAME:90% ＋{LOCAL}
	EXP:90 += LOCAL
ELSEIF R >= 5
	PRINTFORML %EXPNAME:90% ＋4
	EXP:90 += 4
ELSE
	PRINTFORML %EXPNAME:90% ＋3
	EXP:90 += 3
ENDIF
;好感度増加（浪費癖/10をマイナスし、Aがマイナスになる場合好感度は増加しない）
A = RAND:101 + 150 - (ABL:97 / 10)
IF A > 0
	CFLAG:2 += A
	PRINTFORMW 好感度が {A} 増えた
ENDIF
WAIT

;食後の運動イベント
IF (TFLAG:14 >= 1 && TFLAG:14 <= 2) && (TALENT:152 || TALENT:153) && TALENT:MASTER:154 == 0
	CALL LUNCH_EXERCISE
ELSE
	TFLAG:14 = 0
ENDIF

;休憩フラグを立てる
FLAG:0 = 1
BEGIN TURNEND
RETURN 1

;-------------------------------------------------
;料理関係の共通処理
;-------------------------------------------------
@LUNCH_COMMON
K = RAND:16
L = 0
M = 0
N = 0

L = (ABL:MASTER:2 * 2 + ABL:MASTER:90 + ABL:TARGET:2 + ABL:TARGET:6 * 2 + ABL:TARGET:90 * 3 + 20) / 6

;献身的
SIF TALENT:63
	TIMES L , 1.10

;恋人
SIF TALENT:153
	TIMES L , 1.20

;料理の種類決定
;K = 料理の種類
;N = 料理の値段の基礎
;固有料理のパターンが口上で把握出来ないので先に呼ぶ
IF K == 0 && RAND:2 == 0
	TFLAG:14 = 1
ENDIF
CALL LUNCH_MENU

;料理素質で補正をかける
IF ABL:50 == 2
	TIMES L , 1.20
ELSEIF ABL:50 == 1
	TIMES L , 1.10
ELSEIF ABL:50 == -1
	TIMES L , 0.90
ENDIF

;謎の魅力
SIF TALENT:92
	TIMES N , 1.10

;魅力
SIF TALENT:113
	TIMES N , 1.10

;人気
SIF TALENT:126
	TIMES N , 1.30

;R = 料理の出来栄え
R = (RAND:100 + ABL:TARGET:90) / 10
SIF R <= 0
	R = 1
SIF R >= 10
	R = 10

;金額計算
M = L * N * R * 3
SIF TALENT:153
	TIMES M , 1.30

;獲得資金増加ボーナス
IF ABL:MASTER:86 == 1
	TIMES M , 1.50
ELSEIF ABL:MASTER:86 > 1
	M *= ABL:MASTER:86
ENDIF

;値段が高くなり過ぎないように補正をかける
;75000を超えた分は25%OFF
SIF M >= 75000
	M = 75000 + (M - 75000) / 4 * 3
;補正後、150000を超えていたらその分を更に50%OFF
SIF M >= 150000
	M = 150000 + (M - 150000) / 2
;補正後、300000を超えていたらその分を更に75%OFF
SIF M >= 300000
	M = 300000 + (M - 300000) / 4
;補正後、500000を超えていたらその分を更に90%OFF
SIF M >= 500000
	M = 500000 + (M - 500000) / 10
;補正後、1000000を超えていたらその分を更に90%OFF
SIF M >= 1000000
	M = 1000000 + (M - 1000000) / 10

;P:1 = 乱数処理
IF ABL:90 <= 10
	P:1 = ABL:90
ELSEIF ABL:90 <= 20
	P:1 = 10 + (ABL:90 / 4)
ELSEIF ABL:90 <= 30
	P:1 = 15 + (ABL:90 / 6)
ELSEIF ABL:90 <= 40
	P:1 = 20 + (ABL:90 / 8)
ELSEIF ABL:90 <= 50
	P:1 = 25 + (ABL:90 / 10)
ELSEIF ABL:90 <= 60
	P:1 = 30 + (ABL:90 / 12)
ELSEIF ABL:90 <= 70
	P:1 = 35 + (ABL:90 / 14)
ELSEIF ABL:90 <= 80
	P:1 = 40 + (ABL:90 / 16)
ELSEIF ABL:90 <= 90
	P:1 = 45 + (ABL:90 / 18)
ELSE
	P:1 = 50 + (ABL:90 / 20)
ENDIF
P:1 += RAND:4

;Q = 習得の珠取得数
IF R >= 10
	Q = L * (P:1+1) * 5
ELSEIF R >= 5
	Q = L * (P:1+1) * 4
ELSEIF R >= 3
	Q = L * (P:1+1) * 3
ELSE
	Q = L * (P:1+1) * 2
ENDIF
SIF ABL:90 >= 50 && TALENT:153
	Q *= 3

;S = 奉仕快楽経験上昇値
S = M / 20000
SIF S < 2
	S = 2
SIF S > 16
	S = 16

;親密でL(恭順・好意の珠取得数)を補正
L = L * ABL:0 / 15
;EXキャラ補正(恋慕有りだと効果UP、恋慕無しだと珠なし)
IF CFLAG:0 && TALENT:85
	TIMES L , 1.20
ELSEIF CFLAG:0 && TALENT:85 == 0
	L = 0
ENDIF

;-------------------------------------------------
;料理メニュー決定
;口上で判別出来る様にする為、その場で出力せずにTSTRへの格納処理とします
;料理追加の際は、他に習ってください
;eramakerだとSTRの中身が何なのか判別出来ない為、料理分岐にランダム生成を複数使う事は禁止
;最終的にpがユニークの値、若しくは口上から判定で料理名が特定出来る様にしてあること
;ランダム生成以外の分岐は口上書き殺しになりそうなので極力控えてください
;-------------------------------------------------
@LUNCH_MENU
;K = 料理の種類
IF K == 0 && ABL:90 >= 7
	;キャラ固有料理
	N = 300
ELSEIF K <= 1 || K == 15
	;蕎麦
	N = 190
ELSEIF K == 2 || K == 14
	;肉じゃが
	N = 230
ELSEIF K == 3 || K == 13
	;ケーキ
	N = 210
ELSEIF K == 4 || K == 12
	;素麺
	N = 170
ELSEIF K == 5 || K == 11
	;サラダ
	N = 130
ELSEIF K == 6 || K == 10
	;ハンバーグ
	N = 150
ELSE
	IF ITEM:26 > 0 && TALENT:76
		;甘い香りがする桜色のおにぎり
		N = 65
	ELSE
		;おにぎり
		N = 60
	ENDIF
ENDIF

IF K == 0
	;キャラ固有料理
	IF NO:TARGET == 0
		;あなた
		P = RAND:8
		IF P == 0
			TSTR:1 = 鯨の竜田揚げ
			N += 110
		ELSEIF P == 1
			TSTR:1 = すき焼き
			N += 100
		ELSEIF P == 2
			TSTR:1 = カツ丼
			N += 90
		ELSEIF P == 3
			TSTR:1 = 照焼きチキン
			N += 80
		ELSEIF P == 4
			TSTR:1 = ローストポーク
			N += 60
		ELSEIF P == 5
			TSTR:1 = マグロの刺身
			N += 50
		ELSEIF P == 6
			TSTR:1 = 目玉焼き
			N += 40
		ELSE
			TSTR:1 = おにぎり
			N += 30
		ENDIF
	
	;固有料理名
	ELSEIF NO:TARGET == 1
		;ショウタ
		TSTR:1 = カレー
		N += 50
	ELSEIF NO:TARGET == 2
		;アイ
		TSTR:1 = 目玉焼き定食
		N += 50
	ELSEIF NO:TARGET == 3
		;マキ
		TSTR:1 = ラザニア
		N += 50
	ELSEIF NO:TARGET == 4
		;ゆかり
		;マイクラの料理。
		P = RAND:5
		IF P == 0
			TSTR:1 = クッキー
			N += 10
		ELSEIF P == 1
			TSTR:1 = きのこシチュー
			N += 50
		ELSEIF P == 2
			TSTR:1 = ビートルートスープ
			N += 30
		ELSEIF P == 3
			TSTR:1 = パンプキンパイ
			N += 80
		ELSE
			TSTR:1 = ケーキ
			N += 100
		ENDIF
	ELSEIF NO:TARGET == 5
		;ずん子
		P = RAND:31
		IF P == 0
			;ずん子のイラストが入った実在するお土産。Amazonで購入可能。
			TSTR:1 = ずんだ温麺
			N += 30
		ELSEIF P == 1
			;こちらもずん子のイラストが入った実在するお土産。Amazonで購入可能。
			TSTR:1 = ずんだサイダー
			N += 10
		ELSEIF P == 2
			;ずん子とは関係なく実在する料理。レトルトのものと店で出されるものと2種類が存在する。
			TSTR:1 = ずんだカレー
			N += 40
		ELSEIF P == 3
			;こちらもずん子とは関係なく実在する店で食べられる料理。
			TSTR:1 = ずんだそば
			N += 20
		ELSEIF P == 4
			;こちらもずん子とは関係なく実在する店で食べられる料理。世の中どうなってんの。
			;麺にずんだが練り込まれているらしい。
			TSTR:1 = ずんだラーメン
			N += 20
		ELSEIF P == 5
			;以下、wikipedia情報。ちゃんとまともな食べ物だと思う
			TSTR:1 = ずんだ団子
			N += 100
		ELSEIF P == 6
			TSTR:1 = ずんだ大福
			N += 100
		ELSEIF P == 7
			TSTR:1 = ずんだまんじゅう
			N += 100
		ELSEIF P == 8
			TSTR:1 = ずんだ羊羹
			N += 100
		ELSEIF P == 9
			TSTR:1 = ずんだクレープ
			N += 80
		ELSEIF P == 10
			TSTR:1 = ずんだたい焼き
			N += 80
		ELSEIF P == 11
			TSTR:1 = ずんだモナカ
			N += 80
		ELSEIF P == 12
			;小学校の給食で出るらしい。
			TSTR:1 = ずんだ春巻き
			N += 30
		ELSEIF P == 13
			TSTR:1 = ずんだクリームあんみつ
			N += 100
		ELSEIF P == 14
			TSTR:1 = ずんだミルクパフェ
			N += 100
		ELSEIF P == 15
			TSTR:1 = ずんだクリームロールケーキ
			N += 100
		ELSEIF P == 16
			TSTR:1 = ずんだモンブラン
			N += 100
		ELSEIF P == 17
			;テレビでも紹介されたことのある一品。うまい。
			TSTR:1 = ずんだシェイク
			N += 200
		ELSEIF P == 18
			TSTR:1 = ずんだラテ
			N += 80
		ELSEIF P == 19
			;実在する商品。ハローキティのご当地土産。
			TSTR:1 = ずんだミルクオレ
			N += 100
		ELSEIF P == 20
			TSTR:1 = ずんだプリン
			N += 80
		ELSEIF P == 21
			TSTR:1 = ずんだサブレ
			N += 80
		ELSEIF P == 22
			TSTR:1 = ずんだチーズケーキ
			N += 80
		ELSEIF P == 23
			TSTR:1 = ずんだショートケーキ
			N += 80
		ELSEIF P == 24
			TSTR:1 = ずんだパン
			N += 40
		ELSEIF P == 25
			TSTR:1 = ずんだキャラメル
			N += 30
		ELSEIF P == 26
			TSTR:1 = ずんだアイスクリーム
			N += 80
		ELSEIF P == 27
			TSTR:1 = ずんだチョコレート
			N += 30
		ELSEIF P == 28
			TSTR:1 = ずんだ豆腐
			N += 10
		ELSEIF P == 29
			TSTR:1 = ずんだ煎餅
			N += 20
		ELSE
			TSTR:1 = 特製ずんだ餅
			N += 200
		ENDIF
	ELSEIF NO:TARGET == 6
		;琴葉葵
		P = RAND:2
		IF P == 0
			TSTR:1 = 特製ハンバーガー
			N += 50
		ELSE
			TSTR:1 = エビフライ定食
			N += 100
		ENDIF
	ELSEIF NO:TARGET == 7
		;琴葉葵
		P = RAND:3
		IF P == 0
			TSTR:1 = チョコミントアイス
			N += 100
		ELSEIF P == 1
			TSTR:1 = 特製ハンバーガー
			N += 50
		ELSE
			TSTR:1 = ナポリタン
			N += 80
		ENDIF
	ELSEIF NO:TARGET == 8
		;コウ
		TSTR:1 = シーフードカレー
		N += 50
	ELSEIF NO:TARGET == 9
		;セイカ
		;セイカさんのイラストが入った実在するお土産。
		TSTR:1 = いちごジェラート
		N += 100
	ELSEIF NO:TARGET == 10
		;きりたん
		P = RAND:2
		IF P == 0
			TSTR:1 = 特製ずんだ餅
			N += 50
		ELSE
			TSTR:1 = きりたんぽ鍋
			N += 100
		ENDIF
		
	ELSEIF NO:TARGET == 11
		;あかり
		TSTR:1 = 特大おにぎり
		N += 50
	ELSEIF NO:TARGET == 12
		;イタコ
		TSTR:1 = いなり寿司
		N += 50
	ELSEIF NO:TARGET == 13
		;そら
		P = RAND:2
		IF P == 0
			;井上喜久子の好きな食べ物
			TSTR:1 = 特製サラダ
			N += 50
		ELSE
			TSTR:1 = オリジナルパフェ
			N += 100
		ENDIF
	ELSEIF NO:TARGET == 14
		;ついな
		;公式の得意料理。
		TSTR:1 = 恵方巻き
		N += 50
		
	ELSEIF NO:TARGET == 50
		;ささら
		P = RAND:3
		IF P == 0
			TSTR:1 = チョコレートケーキ
			N += 50
		ELSEIF P == 1
			TSTR:1 = もんじゃ焼き
			N += 50
		ELSE
			TSTR:1 = 手作りアイス
			N += 50
		ENDIF
	ELSEIF NO:TARGET == 51
		;つづみ
		P = RAND:3
		IF P == 0
			TSTR:1 = モンブランケーキ
			N += 50
		ELSEIF P == 1
			TSTR:1 = 中華まん
			N += 50
		ELSE
			TSTR:1 = 久寿餅
			N += 100
		ENDIF
	ELSEIF NO:TARGET == 52
		;タカハシ
		TSTR:1 = ラーメン
		N += 50
	ELSEIF NO:TARGET == 53
		;ONE
		P = RAND:2
		IF P == 0
			TSTR:1 = オレンジシャーベット
			N += 50
		ELSE
			TSTR:1 = チャーハン
			N += 100
		ENDIF
		N += 50
	ELSEIF NO:TARGET == 54
		;IA
		TSTR:1 = オレンジゼリー
		N += 50
	ELSEIF NO:TARGET == 80
		;ギャラ子
		TSTR:1 = レインボーケーキ
		N += 30
	ELSEIF NO:TARGET == 81
		;ウナ
		TSTR:1 = うなぎの蒲焼き
		N += 70
	ELSEIF NO:TARGET == 82
		;ヒメ
		TSTR:1 = 日の丸弁当
		N += 70
	ELSEIF NO:TARGET == 83
		;ミコト
		TSTR:1 = 日の丸弁当
		N += 70
	ELSEIF NO:TARGET == 84
		;flower
		TSTR:1 = エディブルフラワーのサラダ
		N += 70
	
		
	;テンプレ用
;	ELSEIF NO:TARGET == 
;		;キャラ
;		TSTR:1 = (料理名)
;		N += 

	ELSE
		TSTR:1 = お弁当
		N += 50
	ENDIF
;固有料理の条件を満たさなくてもずん子はずんだ
ELSEIF NO:TARGET == 5
	TSTR:1 = ずんだ餅
	N += 50
	
ELSEIF K == 1 || K == 15
	TSTR:1 = 蕎麦
ELSEIF K == 2 || K == 14
	TSTR:1 = 肉じゃが
	SIF TALENT:153
		TFLAG:14 = 1
ELSEIF K == 3 || K == 13
	TSTR:1 = ケーキ
ELSEIF K == 4 || K == 12
	TSTR:1 = 素麺
ELSEIF K == 5 || K == 11
	TSTR:1 = サラダ
ELSEIF K == 6 || K == 10
	TSTR:1 = ハンバーグ
ELSE
	IF ITEM:26 > 0 && TALENT:76 == 1
		ITEM:26 -= 1
		TFLAG:14 = 1
		TSTR:1 = 甘い香りがする桜色のおにぎり
	ELSE
		TSTR:1 = おにぎり
	ENDIF
ENDIF
