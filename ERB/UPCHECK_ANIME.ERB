;-------------------------------------
;バーアニメ付き,UPCHECK
;-------------------------------------
@UPCHECK_ANIME, ARG = -1
#LOCALSIZE 3
;--------------------------------------
;数の定義が必要な変数
;--------------------------------------
;--------------------------------------
;以下はPALAMと同数だけ必要です
;Variablesize.csvの定義数あるいは表示量にあわせて調整してください
;Jまとめでは200なので200にしています
;--------------------------------------
#DIM 初期値, 200
#DIM 加算値, 200
#DIM 表示値, 200
#DIM バー速度, 200
#DIMS 表示順, 200
#DIM バー加速指定フラグ
;---------------------------------------
;数の定義が不必要な変数
;---------------------------------------
#DIM CHARA
#DIM COLOR
#DIM LINE
#DIM NEXTP
#DIM NOWP
#DIM MAXPLV
#DIM NOWPLV
#DIM PUP
#DIM MAXNAMELENS
#DIM MAXPLENS
#DIM MAXPUPLENS
#DIM MAXPLVLENS
#DIM BARLENS
#DIM MAXBAR
#DIM 増減があったPALAMのみ
#DIMS 除外PALAM
#DIMS 快系, 20
#DIMS 絶頂, 20
#DIM 絶頂強度, 20
#DIMS 絶頂名, 20
#DIM 潤滑最大値
#DIM 始色
#DIM 終色
;------------------------------------------
;使用変数の初期化
;------------------------------------------
VARSET LOCAL
VARSET 初期値
VARSET 加算値
VARSET 表示値
VARSET 絶頂強度, -1
MAXNAMELENS = 6
MAXPLENS = 6
MAXPUPLENS = 6

;------------------------------------------
;右クリック時動作指定フラグ
;オプションで使うフラグを指定してください
;バー加速指定フラグが真の時、右クリックでバーがスキップでなく
;加速されるようになります
;------------------------------------------
IF FLAG:23 & 1p29
	バー加速指定フラグ = 1
ELSE
	バー加速指定フラグ = -1
ENDIF

;----------------------------------------
;バー最長の長さの定義
;半角の数をいれてください
;デフォはとりあえず16字
;----------------------------------------
BARLENS = 16
MAXBAR = BARLENS * 8 - 1

;----------------------------------------
;これを1にすることで、増減がなかったPALAMは表示しなくなる
;----------------------------------------
増減があったPALAMのみ = 0

;------------------------------------------
;定義されているがJUEL専用などでUPに絡まないものは
;<◯◯>といった形で追加してください
;------------------------------------------
除外PALAM = <否定><酒気><情感>
SIF !(FLAG:62 & 4194304)
	除外PALAM += "<快Ｍ>"

;------------------------------------------
;快系PALAMはここで定義してください
;------------------------------------------
SPLIT "快Ｃ/快Ｖ/快Ａ/快Ｂ/快Ｍ", "/", 快系

;------------------------------------------
;絶頂強度の定義
;強度などがある場合はここで閾値を設定してください
;------------------------------------------
SPLIT "10000/20000/200000/1000000/2000000/20000000/100000000/200000000/2000000000/10000000000/15000000000/20000000000/30000000000/999999999999999999", "/", 絶頂名
FOR LOCAL, 0, RESULT
	絶頂強度:LOCAL = TOINT(絶頂名:LOCAL)
NEXT
;-------------------------------------------
;絶頂名の定義
;表示に使います
;-------------------------------------------
SPLIT "絶頂!/強絶頂!!/超強絶頂!!/最強絶頂!!/S強絶頂!!/S超強絶頂!!/S最強絶頂!!/SS強絶頂!!/SS超強絶頂!!/SS最強絶頂!!/EX強絶頂!!/EX超強絶頂!!/EX最強絶頂!!/EX最強絶頂!!", "/", 絶頂名

;------------------------------------------
;表示順を定義したい場合はここで
;そのままでいい場合はコメントアウトあるいは削除してください
;------------------------------------------
SPLIT "好意/生活/優越/快Ｃ/快Ｖ/快Ａ/快Ｂ/快Ｍ/潤滑/恭順/欲情/屈服/習得/恥情/苦痛/恐怖/反感/不快/抑鬱", "/", 表示順
FOR LOCAL, 0, VARSIZE("PALAM")
	SIF PALAMNAME:LOCAL == ""
		CONTINUE
	SIF STRFIND(除外PALAM, "<"+PALAMNAME:LOCAL+">") >= 0
		CONTINUE
	SIF FINDELEMENT(表示順, PALAMNAME:LOCAL,0, 50, 1) >= 0
		CONTINUE
	PRINTFORMW %PALAMNAME:LOCAL%が表示順から漏れています
NEXT

;------------------------------------------
;潤滑最大値
;定義してある場合、潤滑が一定値を超えた場合、MAXと表記されます
;------------------------------------------
潤滑最大値 = 0

;------------------------------------------
;PALAMLVの最大値を取得
;------------------------------------------
MAXPLV = FINDELEMENT(PALAMLV, MAXARRAY(PALAMLV))

;------------------------------------------
;回すキャラを設定
;------------------------------------------
CHARA = ARG == -1 ? TARGET # ARG
;CUP/COWNが採用されてないと思われる場合、UPを移す
IF !MAXARRAY(CUP:CHARA:0) && !MAXARRAY(CDOWN:CHARA:0)
	CHARA = TARGET
	FOR LOCAL, 0, VARSIZE("PALAM")
		CUP:CHARA:LOCAL = UP:LOCAL
		CDOWN:CHARA:LOCAL = DOWN:LOCAL
	NEXT
ENDIF

;行数と元々の色の確保
LINE = LINECOUNT
COLOR = GETCOLOR()

FOR LOCAL, 0, VARSIZE("PALAM")
	SIF PALAMNAME:LOCAL == ""
		CONTINUE
	SIF 増減があったPALAMのみ && CUP:CHARA:LOCAL == 0 && CDOWN:CHARA:LOCAL == 0
		CONTINUE
	SIF STRFIND(除外PALAM, "<"+PALAMNAME:LOCAL+">") >= 0
		CONTINUE
	;名前の長さを取る
	MAXNAMELENS = MAX(MAXNAMELENS, STRLENS(PALAMNAME:LOCAL))
	;--------------------------
	;初期値を取得
	;--------------------------
	;カンスト
	IF PALAM:CHARA:LOCAL >= PALAMLV:(MAXPLV)
		初期値:LOCAL = MAXBAR
	ELSE
		;現在のPALAM量と次のレベルアップに必要なPALAM量を取る
		NOWP = PALAM:CHARA:LOCAL - PALAMLV:GETPALAMLV(PALAM:CHARA:LOCAL, MAXPLV)
		NEXTP = PALAMLV:(GETPALAMLV(PALAM:CHARA:LOCAL, MAXPLV) + 1) - PALAMLV:GETPALAMLV(PALAM:CHARA:LOCAL, MAXPLV)
		初期値:LOCAL = NOWP * MAXBAR / NEXTP
	ENDIF
	;---------------------------
	;加算値を計算
	;---------------------------
	;計算用のPALAMLVをとっておく
	NOWPLV = GETPALAMLV(PALAM:CHARA:LOCAL, MAXPLV)
	;UPとDOWNをまとめとく ただし、快系はUPのみをとる
	PUP = FINDELEMENT(快系, PALAMNAME:LOCAL, 0, 20, 1) >= 0 ? CUP:CHARA:LOCAL # CUP:CHARA:LOCAL - CDOWN:CHARA:LOCAL
	;最大レベル到達時は加算がない
	SIF NOWPLV == MAXPLV
		PUP = 0
	SIF 潤滑最大値 > 0 && PALAMNAME:LOCAL == "潤滑" && (PUP + PALAM:CHARA:LOCAL > 潤滑最大値)
		PUP = 潤滑最大値 - PALAM:CHARA:LOCAL
	;結果PALAMと加算量の最大の長さを取っておく
	MAXPLENS = MAX(MAXPLENS, STRLENS(TOSTR(PALAM:CHARA:LOCAL+CUP:CHARA:LOCAL+CDOWN:CHARA:LOCAL)))
	MAXPUPLENS = MAX(MAXPUPLENS, STRLENS(TOSTR(ABS(PUP))))
	WHILE PUP
		;レベルアップする場合
		IF NEXTP <= (PUP + NOWP)
			PUP -= (NEXTP - NOWP)
			加算値:LOCAL += MAXBAR+1
			;レベルアップ一回目はバー一本分は加算しない
			IF NOWPLV == GETPALAMLV(PALAM:CHARA:LOCAL, MAXPLV)
				加算値:LOCAL -= 初期値:LOCAL
				NOWP = 0
			ENDIF
			;NOWPLVを増加 最大レベルならここで終了
			IF ++NOWPLV == MAXPLV
				PUP = 0
			ELSE
				;快系でLV4を超える場合特別措置
				IF FINDELEMENT(快系, PALAMNAME:LOCAL, 0, 20, 1) >= 0 && NOWPLV >= 4
					;最後に残る分量を加算
					加算値:LOCAL += (PALAM:CHARA:LOCAL + CUP:CHARA:LOCAL - CDOWN:CHARA:LOCAL) * MAXBAR / 10000
					;-----------------------------------------------
					;絶頂強度による追加
					;-----------------------------------------------
					FOR LOCAL:1, 1, 20
						SIF 絶頂強度:(LOCAL:1) <= 0
							BREAK
						SIF 絶頂強度:(LOCAL:1) > PALAM:CHARA:LOCAL + CUP:CHARA:LOCAL
							BREAK
						加算値:LOCAL += MAXBAR
						MAXPUPLENS = MAX(MAXPUPLENS, STRLENS(絶頂名:(LOCAL:1)))
					NEXT
					PUP = 0
				ELSE
					;次に必要な数値を更新
					NEXTP =  PALAMLV:(NOWPLV+1) - PALAMLV:(NOWPLV)
				ENDIF
			ENDIF
		ELSE
			加算値:LOCAL += MAXBAR * PUP / NEXTP
			PUP = 0
		ENDIF
	WEND
	表示値:LOCAL = 初期値:LOCAL
	バー速度:LOCAL = 1 + MIN(ABS(加算値:LOCAL)/18, 35)
NEXT
FOR LOCAL ,0, VARSIZE("PALAM")
	SIF PALAMNAME:LOCAL == ""
		CONTINUE
	SIF 増減があったPALAMのみ && CUP:CHARA:LOCAL == 0 && CDOWN:CHARA:LOCAL == 0
		CONTINUE
NEXT
;かくつく場合があるので抑制
REDRAW 0
;字数調整。付属の数値を加算しつつ、全角罫線にあわせて偶数化
MAXNAMELENS = (MAXNAMELENS + 1) / 2 * 2
;名前は(LvX)を含むのでそのぶん加算する
MAXPLVLENS = (STRLENS(TOSTR(MAXPLV)) + 1 + 4) / 2 * 2
MAXPLENS = (MAXPLENS+1) / 2 * 2
;+-の符号分増加
MAXPUPLENS = (MAXPUPLENS + 1 +1) / 2 * 2
LOCAL:2 = 0
;表示
DO
	SIF 増減があったPALAMのみ && !MAXARRAY(加算値) && !MINARRAY(加算値)
		BREAK
	CLEARLINE LINECOUNT -LINE
	PRINTFORM %"┌"+"─"*((MAXNAMELENS+MAXPLVLENS)/2)%
	PRINTFORM %"┬"+"─"*(BARLENS/2)%
	PRINTFORM %"┬"+"─"*(MAXPLENS/2)%
	PRINTFORML %"┬"+"─"*(MAXPUPLENS/2)+"┐"%
	
	PRINTFORM │%"PALAM",MAXNAMELENS+MAXPLVLENS,LEFT%
	PRINTFORM │%"BAR", BARLENS, LEFT%
	PRINTFORM │%"TOTAL", MAXPLENS, LEFT%
	PRINTFORML │%"GET", MAXPUPLENS, LEFT%│
	
	PRINTFORM %"├"+"─"*((MAXNAMELENS+MAXPLVLENS)/2)%
	PRINTFORM %"┼"+"─"*(BARLENS/2)%
	PRINTFORM %"┼"+"─"*(MAXPLENS/2)%
	PRINTFORML %"┼"+"─"*(MAXPUPLENS/2)+"┤"%
	FOR LOCAL:1 ,0, VARSIZE("PALAM")
		IF 表示順 == ""
			LOCAL = LOCAL:1
		ELSE
			SIF 表示順:(LOCAL:1) == ""
				CONTINUE
			LOCAL = GETNUM(PALAM, 表示順:(LOCAL:1))
		ENDIF
		SIF PALAMNAME:LOCAL == ""
			CONTINUE
		SIF 増減があったPALAMのみ && CUP:CHARA:LOCAL == 0 && CDOWN:CHARA:LOCAL == 0
			CONTINUE
		SIF STRFIND(除外PALAM, "<"+PALAMNAME:LOCAL+">") >= 0
			CONTINUE
		IF MESSKIP()
			;加速モード字は描画モードを変える
			IF バー加速指定フラグ >= 0
				REDRAW 1
			;スキップモード字は加算を一気に終わらせる
			ELSE
				表示値:LOCAL += 加算値:LOCAL
				加算値:LOCAL = 0
			ENDIF
		ENDIF
		;初回は数値加算をしない
		IF LOCAL:2
			IF 加算値:LOCAL >= 0
				表示値:LOCAL += MIN(加算値:LOCAL, バー速度:LOCAL)
				加算値:LOCAL -= MIN(加算値:LOCAL, バー速度:LOCAL)
			ELSE
				表示値:LOCAL += MAX(加算値:LOCAL, -バー速度:LOCAL)
				加算値:LOCAL -= MAX(加算値:LOCAL, -バー速度:LOCAL)
			ENDIF
		ENDIF
		;ドラムロール用に必要量を産出
		NOWPLV = MIN(GETPALAMLV(PALAM:CHARA:LOCAL, MAXPLV) + ((表示値:LOCAL-1) / MAXBAR), MAXPLV)
		NEXTP = PALAMLV:(NOWPLV+1) - PALAMLV:NOWPLV
		PRINTFORM │%PALAMNAME:LOCAL,MAXNAMELENS,LEFT%(Lv{NOWPLV,MAXPLVLENS-4})│
		SELECTCASE PALAMNAME:LOCAL
			;赤
			CASE "好意", "生活", "優越"
				始色 =  0xC07070
				終色 =  0xC0C0C0
			;青
			CASE "快Ｃ", "快Ｖ", "快Ａ", "快Ｂ", "快Ｍ"
				始色 =  0x7070C0
				終色 =  0xC0C0C0
			;緑
			CASE "恭順", "欲情", "屈服", "習得"
				始色 =  0x70C070
				終色 =  0xC0C0C0
			;紫
			CASE "反感", "不快", "抑鬱"
				始色 =  0xC070C0
				終色 =  0xC0C0C0
			;黄
			CASE "恐怖", "苦痛", "恥情"
				始色 =  0xC0C070
				終色 =  0xC0C0C0
			;青緑
			CASE "屈服", "抑鬱"
				始色 =  0x70C0C0
				終色 =  0xC0C0C0
			CASEELSE
				始色 =  0xC0C0C0
				終色 =  0xFFFFFF
		ENDSELECT
		CALL PRINT_EIGHT_BAR, 表示値:LOCAL, BARLENS, 始色, 終色, 1, 1
		;色を戻す
		SETCOLOR COLOR
		SIF BARLENS%2
			PRINT  
		PRINT │
		;最大レベル到達時
		IF NOWPLV == MAXPLV || (潤滑最大値 > 0 && PALAMNAME:LOCAL == "潤滑" && PALAMLV:NOWPLV + NEXTP * (表示値:LOCAL % (MAXBAR+1)) / MAXBAR >= 潤滑最大値)
			PRINTFORM %"MAX!!", MAXPLENS%│
			PRINTFORM %"MAX!!", MAXPUPLENS%
		;絶頂している場合は特殊表示
		ELSEIF FINDELEMENT(快系, PALAMNAME:LOCAL, 0, 20, 1) >= 0 && NOWPLV >= 4
			PRINTFORM {加算値:LOCAL == 0 ? PALAM:CHARA:LOCAL + CUP:CHARA:LOCAL - CDOWN:CHARA:LOCAL # 10000 * (表示値:LOCAL % (MAXBAR+1)) / MAXBAR, MAXPLENS}│
			PRINTFORM %絶頂名:(NOWPLV - 4),MAXPUPLENS%
		;加算し終わってる場合は正確な数値をだすために分岐
		ELSEIF 加算値:LOCAL == 0
			;結果PALAM
			PRINTFORM {PALAM:CHARA:LOCAL+CUP:CHARA:LOCAL+CDOWN:CHARA:LOCAL,MAXPLENS}│
			PRINTFORM 
			;増減量
			PRINTFORM %@"\@CUP:CHARA:LOCAL-CDOWN:CHARA:LOCAL >= 0 ? + # - \@{ABS(CUP:CHARA:LOCAL-CDOWN:CHARA:LOCAL)}",MAXPUPLENS%
		ELSE
			PRINTFORM {PALAMLV:NOWPLV + NEXTP * (表示値:LOCAL % (MAXBAR+1)) / MAXBAR, MAXPLENS}│
			PRINTFORM 
			PRINTFORM %@"\@加算値:LOCAL >= 0 ? + # - \@{MAX(PALAMLV:NOWPLV - PALAM:CHARA:LOCAL, 0) + NEXTP * (ABS(表示値:LOCAL - (表示値:LOCAL <= MAXBAR ? 初期値:LOCAL # 0)) % (MAXBAR+1)) / MAXBAR}", MAXPUPLENS%
		ENDIF
		PRINTL │
	NEXT
	PRINTFORM %"└"+"─"*((MAXNAMELENS+MAXPLVLENS)/2)%
	PRINTFORM %"┴"+"─"*(BARLENS/2)%
	PRINTFORM %"┴"+"─"*(MAXPLENS/2)%
	PRINTFORML %"┴"+"─"*(MAXPUPLENS/2)+"┘"%
	TWAIT 100, 0
	LOCAL:2++
LOOP MAXARRAY(加算値) || MINARRAY(加算値)
;PALAMへの加算
FOR LOCAL, 0, VARSIZE("PALAM")
	SIF PALAMNAME:LOCAL == ""
		CONTINUE
	PALAM:CHARA:LOCAL += (CUP:CHARA:LOCAL - CDOWN:CHARA:LOCAL)
NEXT
VARSET CUP:CHARA:0
VARSET CDOWN:CHARA:0
VARSET UP:0
VARSET DOWN:0

;-------------------------------------------------
;8分割カラーバー表示関数@PRINT_EIGHT_BAR
;	引数0：バーの長さ（半角1/8文字単位）
;	引数1：バー全体の長さ（半角1/8文字単位）
;		なお、バーの長さが最長の数値(ARG:1*8-1)を超えている場合、表示は0に戻る
;	引数2：バー始端の色（16進。RGBCOLOR関数使用可能。省略すると現在の文字色に）
;	引数3：バー終端の色（同上。省略すると単色バーに）
;	引数4：始色比率（始端の色が濃くなる）
;	引数5：終色比率（終端の色が濃くなる）
;
;独自形式の引数です。ご注意ください
;@PRINT_EIGHT_BARの引数が独自形式なので、従来のBARの形式でも呼び出せるように用意しました。
;半角の1/8刻みでバーを表示できますが、バーに使う文字を指定はできません。また、バーの背景色も使えません。
;引数3・4を指定することでグラデーションができますが、この場合色の変わる単位は半角１文字ごとになります。
;-------------------------------------------------
;@PRINT_EIGHT_BAR(ARG, ARG:1 = 32, ARG:2 = -1, ARG:3 = -1, ARG:4 = 1, ARG:5 = 1)
;#DIM COLOR
;#DIM RED, 3
;#DIM BLUE, 3
;#DIM GREEN, 3
;#DIM 始色比率
;#DIM 終色比率
;始色比率 = ARG:4
;終色比率 = ARG:5
;COLOR = GETCOLOR()
;IF ARG:2  >= 0 && ARG:3 >= 0
;	RED:1 = ARG:2 / 256 / 256
;	GREEN:1 = ARG:2 / 256 % 256
;	BLUE:1 = ARG:2 % 256
;	RED:2 = ARG:3 / 256 / 256
;	GREEN:2 = ARG:3 / 256 % 256
;	BLUE:2 = ARG:3 % 256
;ELSEIF ARG:2 >= 0
;	SETCOLOR ARG:2
;ENDIF
;FOR LOCAL, 0, ARG:1
;	IF ARG:2 >= 0 && ARG:3 >= 0
;		RED = (RED:1 * (ARG:1 - LOCAL) * 始色比率 + RED:2 * LOCAL * 終色比率) / ((ARG:1 -LOCAL) * 始色比率 + LOCAL * 終色比率)
;		GREEN = (GREEN:1 * (ARG:1 - LOCAL) * 始色比率 + GREEN:2 * LOCAL * 終色比率) / ((ARG:1 -LOCAL) * 始色比率 + LOCAL * 終色比率)
;		BLUE = (BLUE:1 * (ARG:1 - LOCAL) * 始色比率 + BLUE:2 * LOCAL * 終色比率) / ((ARG:1 -LOCAL) * 始色比率 + LOCAL * 終色比率)
;		SETCOLOR RED, GREEN, BLUE
;	ENDIF
;	IF LOCAL < MAX((ARG % (ARG:1 * 8) / 8),0)
;		PRINTFORM %UNICODE(0x2588)%
;	ELSEIF LOCAL == MAX((ARG % (ARG:1 * 8) / 8),0)
;		PRINTFORM \@ARG % (ARG:1 * 8) % 8 ? %UNICODE(0x2588 + 8 - (ARG % (ARG:1 * 8) % 8))%# %" "%\@
;	ELSE
;		PRINTFORM  
;	ENDIF
;NEXT
;SETCOLOR COLOR
;-----------------------------------
;ARGを16進表記にして返します
;-----------------------------------
@TO16, ARG
#FUNCTIONS
#LOCALSSIZE 3
VARSET LOCALS
LOCALS:2 = \@ARG < 0 ?- #\@
SIF ARG == 0
	RETURNF "0"
WHILE ARG
	SELECTCASE ABS(ARG) % 16
		CASE IS < 10
			LOCALS:1 = %TOSTR(ABS(ARG)%16)%
		CASE 10
			LOCALS:1 = a
		CASE 11
			LOCALS:1 = b
		CASE 12
			LOCALS:1 = c
		CASE 13
			LOCALS:1 = d
		CASE 14
			LOCALS:1 = e
		CASE 15
			LOCALS:1 = f
	ENDSELECT
	LOCALS = %LOCALS:1%%LOCALS%
	ARG /= 16
WEND
RETURNF LOCALS:2 + LOCALS

@GRADATIONTEST
#LOCALSIZE 5
#LOCALSSIZE 3
#DIM LINE
#DIM RED, 10
#DIM BLUE, 10
#DIM GREEN, 10
#DIM NUM1
#DIM NUM2
#DIMS COLORNUM1
#DIMS COLORNUM2
#DIMS COLOR
LINE = LINECOUNT
FOR LOCAL, 0, 5
	SELECTCASE LOCAL
		CASE 0
			LOCAL:1 = 0xff0000
			LOCAL:2 = 0xff7a7a
		CASE 1
			LOCAL:1 = 0x0000ff
			LOCAL:2 = 0x7a7aff
		CASE 2
			LOCAL:1 = 0x00ff00
			LOCAL:2 = 0x7aff7a
		CASE 3
			LOCAL:1 = 0x7f00ff
			LOCAL:2 = 0xbc7aff
		CASE 4
			LOCAL:1 = 0xffff00
			LOCAL:2 = 0xffff7a
	ENDSELECT
	RED:(LOCAL*2) = LOCAL:1 / 256 / 256
	RED:(LOCAL*2+1) = LOCAL:2 / 256 /256
	GREEN:(LOCAL*2) = LOCAL:1 / 256 % 256
	GREEN:(LOCAL*2+1) = LOCAL:2 / 256 % 256
	BLUE:(LOCAL*2) = LOCAL:1 % 256
	BLUE:(LOCAL*2+1) = LOCAL:2 % 256
NEXT
REDRAW 0
LOCAL:1 = RAND:5 * 2
$START
CLEARLINE LINECOUNT - LINE
DRAWLINE
FONTSTYLE 2
FOR LOCAL, 0, 16
	LOCAL:2 = (RED:(LOCAL:1) * (15- LOCAL) + RED:(LOCAL:1+1) * LOCAL) / 15
	LOCAL:3 = (GREEN:(LOCAL:1) * (15- LOCAL) + GREEN:(LOCAL:1+1) * LOCAL) / 15
	LOCAL:4 = (BLUE:(LOCAL:1) * (15- LOCAL) + BLUE:(LOCAL:1+1) * LOCAL) / 15
	SETCOLOR LOCAL:2, LOCAL:3, LOCAL:4
	PRINTFORM %SUBSTRINGU("　ＧＲＡＤＡＴＩＯＮ　ＴＥＳＴ", LOCAL, 1)%
	RESETCOLOR
NEXT
PRINTL 
FONTSTYLE 0
FOR LOCAL, 0, 5
	NUM1 = LOCAL * 2
	NUM2 = LOCAL * 2 + 1
	DRAWLINE
	PRINT □始色　
	SETCOLOR RED:NUM1*256*256+GREEN:NUM1*256+BLUE:NUM1
	PRINTFORM %UNICODE(0x2586)*20%
	RESETCOLOR
	PRINTFORM %" "*11%
	PRINT □終色　
	SETCOLOR RED:NUM2*256*256+GREEN:NUM2*256+BLUE:NUM2
	PRINTFORM %UNICODE(0x2586)*20%
	RESETCOLOR
	PRINTFORML %" "*11%
	FOR LOCAL:1, 0, 3
		IF LOCAL:1 == 0
			COLOR = RED
			COLORNUM1 = %TO16(RED:NUM1)%
			COLORNUM2 = %TO16(RED:NUM2)%
		ELSEIF LOCAL:1 == 1
			COLOR = GREEN
			COLORNUM1 = %TO16(GREEN:NUM1)%
			COLORNUM2 = %TO16(GREEN:NUM2)%
		ELSE
			COLOR = BLUE
			COLORNUM1 = %TO16(BLUE:NUM1)%
			COLORNUM2 = %TO16(BLUE:NUM2)%
		ENDIF
		PRINTBUTTON "[++]", @"%COLOR%/{NUM1}/++"
		PRINT 　
		PRINTBUTTON "[+]", @"%COLOR%/{NUM1}/+"
		PRINT 　
		PRINTFORM %COLOR,7,LEFT%<%COLORNUM1,2%>　
		PRINTBUTTON "[--]", @"%COLOR%/{NUM1}/--"
		PRINT 　
		PRINTBUTTON "[-]", @"%COLOR%/{NUM1}/-"
		PRINT 　　　
		PRINTBUTTON "[++]", @"%COLOR%/{NUM2}/++"
		PRINT 　
		PRINTBUTTON "[+]", @"%COLOR%/{NUM2}/+"
		PRINT 　
		PRINTFORM %COLOR,7,LEFT%<%COLORNUM2,2%>　
		PRINTBUTTON "[-]", @"%COLOR%/{NUM2}/-"
		PRINT 　
		PRINTBUTTON "[--]", @"%COLOR%/{NUM2}/--"
		PRINTL 　
	NEXT
	PRINT 
	CALL PRINT_EIGHT_BAR, 143, 16, RED:NUM1*256*256+GREEN:NUM1*256+BLUE:NUM1, RED:NUM2*256*256+GREEN:NUM2*256+BLUE:NUM2
	PRINT  　　　　　　　　　　　
	CALL PRINT_EIGHT_BAR, 287, 32, RED:NUM1*256*256+GREEN:NUM1*256+BLUE:NUM1, RED:NUM2*256*256+GREEN:NUM2*256+BLUE:NUM2
	PRINTL 
NEXT
INPUTS
SPLIT RESULTS, "/", LOCALS
LOCAL = TOINT(LOCALS:1)
SELECTCASE LOCALS
	CASE "RED"
		SELECTCASE LOCALS:2
			CASE "++"
				RED:LOCAL = MIN(RED:LOCAL+16, 255)
			CASE "+"
				RED:LOCAL = MIN(RED:LOCAL+1, 255)
			CASE "-"
				RED:LOCAL = MAX(RED:LOCAL-1, 0)
			CASE "--"
				RED:LOCAL = MAX(RED:LOCAL-16, 0)
		ENDSELECT
	CASE "GREEN"
		SELECTCASE LOCALS:2
			CASE "++"
				GREEN:LOCAL = MIN(GREEN:LOCAL+16, 255)
			CASE "+"
				GREEN:LOCAL = MIN(GREEN:LOCAL+1, 255)
			CASE "-"
				GREEN:LOCAL = MAX(GREEN:LOCAL-1, 0)
			CASE "--"
				GREEN:LOCAL = MAX(GREEN:LOCAL-16, 0)
		ENDSELECT
	CASE "BLUE"
		SELECTCASE LOCALS:2
			CASE "++"
				BLUE:LOCAL = MIN(BLUE:LOCAL+16, 255)
			CASE "+"
				BLUE:LOCAL = MIN(BLUE:LOCAL+1, 255)
			CASE "-"
				BLUE:LOCAL = MAX(BLUE:LOCAL-1, 0)
			CASE "--"
				BLUE:LOCAL = MAX(BLUE:LOCAL-16, 0)
		ENDSELECT
ENDSELECT
LOCAL:1 = (LOCAL / 2)*2
GOTO START

