;---------------------------------------------------------
;このファイルの中に入っている関数一覧
;GET_INITIATIVE_RATE		Tで指定されたキャラの主導権基準値をRに入れて返す
;				勝手に使う変数	A、I
;GET_BATTLE_POWER		Tで指定されたキャラの戦闘力をPに入れて返す
;	REPEAT使用アリ		勝手に使う変数	A
;GET_BATTLE_PRICE		Tで指定されたキャラの戦闘訓練に必要な金額をPに入れて返す
;				勝手に使う変数	Q
;GET_STUFF			Tで指定されたキャラ番号のキャラの、戦闘素質をSに入れて返す
;				勝手に使う変数	A
;GET_EXISTANCE			Tで指定されたキャラ番号のキャラが仲間にいる場合、Eに加入番号+1を入れて返す。居なければ0を返す
;	REPEAT使用アリ		勝手に使う変数	なし
;GET_EXISTANCE2			Tで指定されたキャラ番号のキャラが仲間にいる場合、Eに加入番号+1を入れて返す。居なければ0を返す。CFLAG:21が立っている、一時離別中キャラを居る扱いとする
;	REPEAT使用アリ		勝手に使う変数	なし
;GET_BOSS_NO			次のボスのNoを取得して、Bに入れて返す
;	REPEAT使用アリ		勝手に使う変数	A、C、D、E、F、G、T
;GET_CHARA_NAME			Tで指定されたキャラ番号のキャラの名前をSTR:Nに入れて返す
;				勝手に使う変数	A
;GET_JUN_VALUE			純愛系のコマンド実行値をAに入れて返す
;				勝手に使う変数	L、R
;ADD_YUKARISAMA			ゆかり様加入処理
;	REPEAT使用アリ		勝手に使う変数	A、E、P、T
;ADD_NEW_CHARA			倒したキャラの加入処理
;				勝手に使う変数	A、E、T
;PUSH_OVER			押し倒し判定。判定成功時はRESULT = 1
;				勝手に使う変数	A、B、L、R
;GET_FEAT			特殊能力の習得
;				勝手に使う変数	A
;GET_LOST_CHARA			霧に飲まれかけているキャラの番号をランダムで取得し、Cに入れて返す。誰もいなければ０を返す
;	REPEAT使用アリ		勝手に使う変数	A、B、C、D
;---------------------------------------------------------


@GET_INITIATIVE_RATE
;加筆開始
;TとPLAYERに対して元々の@GET_INITIATIVE_RATEを実行し、Tに対するRからPLAYERに対するRを引いた値をRとする
;よって、T=PLAYERの時は自動的に0になる

CALL GET_SUB_INITIATIVE_RATE
I = R
T = PLAYER
CALL GET_SUB_INITIATIVE_RATE
R = I - R

@GET_SUB_INITIATIVE_RATE
;加筆終了
;Tで指定されたキャラの主導権基準値をRに入れて返す

;大元の基準値を取得
A = CFLAG:T:12

;ABL:マゾっ気による影響
A -= ABL:T:8 * 5

;ABL:サドっ気による影響
A += ABL:T:15 * 5

;臆病　主人公側に片寄りやすい
IF TALENT:T:10
	IF A < 0
		TIMES A , 1.20
	ELSE
		TIMES A , 0.80
	ENDIF
ENDIF

;反抗的　相手側に片寄りやすい
IF TALENT:T:11
	IF A < 0
		TIMES A , 0.80
	ELSE
		TIMES A , 1.20
	ENDIF
ENDIF

;素直　どちらの影響も強くなる
IF TALENT:T:13
	IF A < 0
		TIMES A , 1.20
	ELSE
		TIMES A , 1.20
	ENDIF
ENDIF

;大人しい　主人公側に片寄りやすい
IF TALENT:T:14
	IF A < 0
		TIMES A , 1.20
	ELSE
		TIMES A , 0.60
	ENDIF
ENDIF

;プライド高い　相手側に片寄りやすい
IF TALENT:T:15
	IF A < 0
		TIMES A , 0.70
	ELSE
		TIMES A , 1.30
	ENDIF
ENDIF

;生意気　相手側に片寄りやすい
IF TALENT:T:16
	IF A < 0
		TIMES A , 0.90
	ELSE
		TIMES A , 1.30
	ENDIF
ENDIF

;プライド低い　主人公側に片寄りやすい
IF TALENT:T:17
	IF A < 0
		TIMES A , 1.30
	ELSE
		TIMES A , 0.70
	ENDIF
ENDIF

;自制心　主人公側に片寄りやすい
IF TALENT:T:20
	IF A < 0
		TIMES A , 1.20
	ELSE
		TIMES A , 0.80
	ENDIF
ENDIF

;無関心　どちらの影響も弱くなる
IF TALENT:T:21
	IF A < 0
		TIMES A , 0.80
	ELSE
		TIMES A , 0.80
	ENDIF
ENDIF

;感情乏しい　どちらの影響も弱くなる
IF TALENT:T:22
	IF A < 0
		TIMES A , 0.80
	ELSE
		TIMES A , 0.80
	ENDIF
ENDIF

;好奇心　相手側に片寄りやすい
IF TALENT:T:23
	IF A < 0
		TIMES A , 0.70
	ELSE
		TIMES A , 1.20
	ENDIF
ENDIF

;保守的　主人公側に片寄りやすい
IF TALENT:T:24
	IF A < 0
		TIMES A , 1.30
	ELSE
		TIMES A , 0.70
	ENDIF
ENDIF

;目立ちたがり　相手側に非常に片寄りやすい
IF TALENT:T:28
	IF A < 0
		TIMES A , 0.60
	ELSE
		TIMES A , 1.40
	ENDIF
ENDIF

;恥じらい　主人公側に片寄りやすい
IF TALENT:T:35
	IF A < 0
		TIMES A , 1.20
	ELSE
		TIMES A , 0.80
	ENDIF
ENDIF

;サド　相手側に非常に片寄りやすい
IF TALENT:T:83
	IF A < 0
		TIMES A , 0.50
	ELSE
		TIMES A , 2.00
	ENDIF
ENDIF

;マゾ　主人公側に非常に片寄りやすい
IF TALENT:T:79
	IF A < 0
		TIMES A , 2.00
	ELSE
		TIMES A , 0.50
	ENDIF
ENDIF

;小悪魔　相手側に片寄りやすい
IF TALENT:T:87
	IF A < 0
		TIMES A , 0.80
	ELSE
		TIMES A , 1.20
	ENDIF
ENDIF

;妖狐　相手側に片寄りやすい
IF TALENT:T:90
	IF A < 0
		TIMES A , 0.80
	ELSE
		TIMES A , 1.20
	ENDIF
ENDIF

;魅惑　どちらの影響も強くなる
IF TALENT:T:91
	IF A < 0
		TIMES A , 1.10
	ELSE
		TIMES A , 1.10
	ENDIF
ENDIF

;謎の魅力　どちらの影響も強くなる
IF TALENT:T:92
	IF A < 0
		TIMES A , 1.20
	ELSE
		TIMES A , 1.20
	ENDIF
ENDIF

;魅力　どちらの影響も強くなる
IF TALENT:T:113
	IF A < 0
		TIMES A , 1.10
	ELSE
		TIMES A , 1.10
	ENDIF
ENDIF

;鼓舞　特に主人公側に片寄りやすい
IF TALENT:T:118
	IF A < 0
		TIMES A , 1.50
	ELSE
		TIMES A , 0.80
	ENDIF
ENDIF

;服従　ほぼ主人公側になる
IF TALENT:T:180
	IF A < 0
		TIMES A , 2.00
	ELSE
		TIMES A , 0.01
	ENDIF
ENDIF

R = A

RETURN 1

@GET_BATTLE_POWER_LIMIT
;戦闘力計算途中のオーバーフロー防止
IF 0
ELSEIF P > 999999999999999999
	P /= 100000
	V *= 100000
ELSEIF P > 99999999999999999
	P /= 10000
	V *= 10000
ELSEIF P > 9999999999999999
	P /= 1000
	V *= 1000
ELSEIF P > 999999999999999
	P /= 100
	V *= 100
ELSEIF P > 99999999999999
	P /= 10
	V *= 10
ENDIF

@GET_BATTLE_POWER
;Tで指定されたキャラの戦闘力をPに入れて返す
P = 0
;カンストチェック用にV使用
V = 1

;好感度
IF CFLAG:T:2 <= 50
	P = CFLAG:T:2
ELSEIF CFLAG:T:2 <= 500
	P = 50 + (CFLAG:T:2 - 50) / 2
ELSEIF CFLAG:T:2 <= 5000
	P = 300 + (CFLAG:T:2 - 500) / 3
ELSEIF CFLAG:T:2 <= 50000
	P = 2000 + (CFLAG:T:2 - 5000) / 4
ELSE
	P = 15000 + (CFLAG:T:2 - 50000) / 5
ENDIF

CALL GET_BATTLE_POWER_LIMIT

;素質の無いキャラほど、好感度の影響を大きくする。愛だよ、愛
IF ABL:T:98 == 0
	;素質未設定はLv50とみなす＝倍率３倍
	TIMES P , 3.00
ELSEIF ABL:T:98 <= 10
	;最弱
	TIMES P , 10.00
ELSEIF ABL:T:98 <= 20
	;最弱クラス
	TIMES P , 7.00
ELSEIF ABL:T:98 <= 30
	;弱い方
	TIMES P , 5.00
ELSEIF ABL:T:98 <= 50
	;弱い方
	TIMES P , 3.00
ENDIF

CALL GET_BATTLE_POWER_LIMIT

;戦闘技能レベル
IF ABL:T:88 <= 1
	P += 50 + 50 * ABL:T:88
ELSEIF ABL:T:88 <= 3
	P += 100 + 75 * ABL:T:88
ELSEIF ABL:T:88 <= 5
	P += 300 + 100 * ABL:T:88
ELSEIF ABL:T:88 <= 8
	P += 500 + 120 * ABL:T:88
ELSEIF ABL:T:88 <= 11
	P += 800 + 150 * ABL:T:88
ELSEIF ABL:T:88 <= 25
	P += 800 + 200 * ABL:T:88
ELSE
	P += 1000 + 300 * ABL:T:88
ENDIF

CALL GET_BATTLE_POWER_LIMIT

;戦闘素質
IF ABL:T:98 == 0
	;素質未設定はLv50とみなす＝倍率55/25倍
	P = P * 55 / 25
ELSEIF ABL:T:98 <= 20
	;最弱クラス
	P = P * (ABL:T:98 + 10) / 30
ELSEIF ABL:T:98 <= 50
	;弱い方
	P = P * (ABL:T:98 + 5) / 25
ELSEIF ABL:T:98 < 80
	;強い方
	P = P * ABL:T:98 / 20
ELSE
	;最強クラス
	P = P * ABL:T:98 / 15
ENDIF

CALL GET_BATTLE_POWER_LIMIT

;魔力(魔力による戦闘力は素質の影響を受けない)
IF BASE:T:5 <= 100
	P += BASE:T:5
ELSEIF BASE:T:5 <= 200
	P += BASE:T:5 * 3
ELSEIF BASE:T:5 <= 400
	P += BASE:T:5 * 5
ELSEIF BASE:T:5 <= 800
	P += BASE:T:5 * 8
ELSEIF BASE:T:5 <= 5000
	P += BASE:T:5 * 12
ELSE
	IF BASE:T:5 >= 588235294117647
		P += 999999999999999999
	ELSE
		P += BASE:T:5 * 17
	ENDIF
ENDIF

CALL GET_BATTLE_POWER_LIMIT

;サドい人は戦闘力1.2倍
SIF TALENT:T:83
	TIMES P , 1.20

;生理中はイラだって戦闘力1.2倍
SIF TALENT:T:138 && (FLAG:62 & 1)
	TIMES P , 1.20

;母は強し
SIF TALENT:T:136
	TIMES P , 1.20

CALL GET_BATTLE_POWER_LIMIT

;アクセサリ関係
IF FLAG:62 & 4
	;主人公のアクセサリで1%補正
	SIF CFLAG:MASTER:24 == NO:T
		TIMES P , 1.01
	
	;キャラが装備中のアクセサリで補正
	;アクセサリから戦闘素質を算出
	IF CFLAG:T:23 > 0
		CSVABL CFLAG:T:23, 98, 0
		LOCAL = RESULT
		;例えば戦闘素質が50のキャラのアクセサリを付けていた場合1.050倍になる
		IF P >= 9999999999999
			P = P / 1000 * (1000 + LOCAL)
		ELSE
			P = P * (1000 + LOCAL) / 1000
		ENDIF
	ENDIF
	IF CFLAG:T:24 > 0
		CSVABL CFLAG:T:24, 98, 0
		LOCAL = RESULT
		;例えば戦闘素質が50のキャラのアクセサリを付けていた場合1.050倍になる
		IF P >= 9999999999999
			P = P / 1000 * (1000 + LOCAL)
		ELSE
			P = P * (1000 + LOCAL) / 1000
		ENDIF
	ENDIF
ENDIF

CALL GET_BATTLE_POWER_LIMIT

;Phモード限定の恋人ボーナス
IF FLAG:5 == 10 || TALENT:MASTER:恋人ボーナス || TALENT:MASTER:恋人ボーナス強化
	A = 0
	FOR LOCAL, 0, CHARANUM
		SIF TALENT:LOCAL:153
			A += 1
	NEXT
	SIF TALENT:MASTER:203
		TIMES A , 1.50
	P = P / 10 * (10 + A)
ENDIF

CALL GET_BATTLE_POWER_LIMIT

;Phモード限定の親愛ボーナス
IF FLAG:5 == 10 || TALENT:MASTER:親愛ボーナス || TALENT:MASTER:親愛ボーナス強化
	A = 0
	FOR LOCAL, 0, CHARANUM
		SIF TALENT:LOCAL:88
			A += 1
	NEXT
	SIF TALENT:MASTER:205
		TIMES A , 3.00
	P = P / 5 * (5 + A)
ENDIF

CALL GET_BATTLE_POWER_LIMIT

;戦闘技能レベルによる倍率
IF P < 1000000
	IF ABL:T:88 <= 1
		P = P * (ABL:T:88 * 10 + 50) / 100
	ELSEIF ABL:T:88 <= 3
		P = P * (ABL:T:88 * 10 + 50) / 100
	ELSEIF ABL:T:88 <= 5
		P = P * (ABL:T:88 * 7 + 60) / 100
	ELSEIF ABL:T:88 <= 8
		P = P * (ABL:T:88 * 5 + 70) / 100
	ELSEIF ABL:T:88 <= 11
		P = P * (ABL:T:88 * 4 + 80) / 100
	ELSEIF ABL:T:88 <= 25
		P = P * (ABL:T:88 * 4 + 90) / 100
	ELSE
		P = P * (ABL:T:88 * 5 + 70) / 100
	ENDIF
ELSE
	P /= 200
	IF ABL:T:88 <= 1
		P = P * (ABL:T:88 * 20 + 100)
	ELSEIF ABL:T:88 <= 3
		P = P * (ABL:T:88 * 20 + 100)
	ELSEIF ABL:T:88 <= 5
		P = P * (ABL:T:88 * 14 + 120)
	ELSEIF ABL:T:88 <= 8
		P = P * (ABL:T:88 * 10 + 140)
	ELSEIF ABL:T:88 <= 11
		P = P * (ABL:T:88 * 8 + 160)
	ELSEIF ABL:T:88 <= 25
		P = P * (ABL:T:88 * 8 + 180)
	ELSEIF ABL:T:88 <= 100
		P = P * (ABL:T:88 * 6 + 230)
	ELSEIF ABL:T:88 <= 1000
		P = P * (ABL:T:88 * 4 + 430)
	ELSE
		P = P * (ABL:T:88 + 3430)
	ENDIF
ENDIF

IF V > 1
	IF P >= 999999999999999999 / V
		P = 999999999999999999
	ELSE
		P *= V
	ENDIF
ENDIF

;カンストする場合。Emuera専用になったのでカンスト値上昇
SIF P >= 999999999999999999
	P = 999999999999999999

SIF TALENT:T:134
	P = P / 20 * (CFLAG:T:110 - DAY:7)

SIF P < 1
	P = 1

RETURN 1

;---------------------------------------------------------
;あなたの戦闘能力計算
;---------------------------------------------------------
@GET_BATTLE_POWER_MASTER
VARSET LOCAL, 0

;あなた自身の戦闘能力（LOCALに退避）
T = MASTER
CALL GET_BATTLE_POWER
LOCAL = P

;戦闘能力強化
IF ABL:MASTER:87 >= 1
	IF 999999999999999999 / ABL:MASTER:87 < LOCAL
		LOCAL = 999999999999999999
	ELSE
		IF ABL:MASTER:87 == 1
			TIMES LOCAL , 1.50
		ELSE
			LOCAL *= ABL:MASTER:87
		ENDIF
	ENDIF
ENDIF

;パートナーが居れば、パートナーの戦闘能力の1/3をあなたの戦闘能力に加える
IF TARGET>= 0
	T = TARGET
	CALL GET_BATTLE_POWER
	;パートナーの戦闘能力を退避
	LOCAL:1 = P / 3
ENDIF

;助手が居れば、助手の戦闘能力の半分をあなたの戦闘能力に加える
IF ASSI >= 0
	T = ASSI
	CALL GET_BATTLE_POWER
	LOCAL:2 = P / 2
ENDIF

;戦闘能力合計
P = LOCAL + LOCAL:1 + LOCAL:2



@GET_BATTLE_PRICE
;Tで指定されたキャラの戦闘訓練に必要な金額をPに入れて返す
P = 0
Q = 0

;戦闘技能レベル
IF ABL:T:88 <= 1
	P += 2000 + 2000 * ABL:T:88
	Q = 5
	IF EXP:T:95 >= (20 + 20 * ABL:T:88)
		P /= 2
	ELSEIF EXP:T:95 >= (15 + 15 * ABL:T:88)
		P = P * 3 / 4
	ENDIF
ELSEIF ABL:T:88 <= 3
	P += 4000 * ABL:T:88
	Q = 6
	IF EXP:T:95 >= (35 * ABL:T:88)
		P /= 2
	ELSEIF EXP:T:95 >= (25 * ABL:T:88)
		P = P * 3 / 4
	ENDIF
ELSEIF ABL:T:88 <= 5
	P += 8000 * ABL:T:88
	Q = 7
	IF EXP:T:95 >= (50 * ABL:T:88)
		P /= 2
	ELSEIF EXP:T:95 >= (35 * ABL:T:88)
		P = P * 3 / 4
	ENDIF
ELSEIF ABL:T:88 <= 8
	P += 12000 * ABL:T:88
	Q = 8
	IF EXP:T:95 >= (65 * ABL:T:88)
		P /= 2
	ELSEIF EXP:T:95 >= (45 * ABL:T:88)
		P = P * 3 / 4
	ENDIF
ELSEIF ABL:T:88 <= 11
	P += 18000 * ABL:T:88
	Q = 9
	IF EXP:T:95 >= (80 * ABL:T:88)
		P /= 2
	ELSEIF EXP:T:95 >= (55 * ABL:T:88)
		P = P * 3 / 4
	ENDIF
ELSEIF ABL:T:88 <= 25
	P += 30000 * ABL:T:88
	Q = 10
	IF EXP:T:95 >= (90 * ABL:T:88)
		P /= 2
	ELSEIF EXP:T:95 >= (65 * ABL:T:88)
		P = P * 3 / 4
	ENDIF
ELSE
	P += 50000 * ABL:T:88
	Q = 12
	IF EXP:T:95 >= (100 * ABL:T:88)
		P /= 2
	ELSEIF EXP:T:95 >= (80 * ABL:T:88)
		P = P * 3 / 4
	ENDIF
ENDIF


;戦闘経験で値引き
IF EXP:T:95 <= 100
	P -= EXP:T:95 * 3 * Q
ELSEIF EXP:T:95 <= 300
	P -= EXP:T:95 * 4 * Q
ELSEIF EXP:T:95 <= 500
	P -= EXP:T:95 * 5 * Q
ELSEIF EXP:T:95 <= 1000
	P -= EXP:T:95 * 6 * Q
ELSEIF EXP:T:95 <= 2000
	P -= EXP:T:95 * 7 * Q
ELSE
	P -= EXP:T:95 * 8 * Q
ENDIF

;戦闘素質で割引
IF ABL:T:98 == 0
	;Lv0＝未設定はLv50扱い＝１倍
ELSEIF ABL:T:98 <= 20
	;最弱級キャラは半額
	TIMES P , 0.50
ELSEIF ABL:T:98 <= 50
	;弱め～普通のキャラは１倍
ELSEIF ABL:T:98 < 80
	;強めのキャラは1.5倍
	TIMES P , 1.50
ELSE
	;強いキャラは２倍
	TIMES P , 2.00
ENDIF

SIF P < 10
	P = 10

RETURN 1


@BATTLE_PRICE_I
;使うお金が$11以上になるまで一気にレベルを上げる
LOCAL = ABL:T:88
M = 0
K = 0
L = 9999 - ABL:T:88
REPEAT L
	M += P
	K += 1
	ABL:T:88 += 1
	CALL GET_BATTLE_PRICE
	SIF P > 10
		BREAK
REND

ABL:T:88 = LOCAL

RETURN 1

@BATTLE_PRICE_G
;お金の限界までレベルを上げる
LOCAL = ABL:T:88
M = 0
K = 0
L = 9999 - ABL:T:88
REPEAT L
	M += P
	K += 1
	ABL:T:88 += 1
	CALL GET_BATTLE_PRICE
	SIF M + P > MONEY
		BREAK
REND

ABL:T:88 = LOCAL

RETURN 1

@BATTLE_PRICE_4(ARG)
LOCAL = ABL:T:88
;決めたレベルまで上げる
M = 0
K = 0
L = RESULT
REPEAT L
	M += P
	K += 1
	ABL:T:88 += 1
	CALL GET_BATTLE_PRICE
REND

ABL:T:88 = LOCAL

RETURN 1


@GET_STUFF
;Tで指定されたキャラ番号のキャラの、戦闘素質をSに入れて返す。こっそりS:1に浪費癖、S:2に主導権基準値も入れる

ADDCHARA T
A = CHARANUM - 1
S = ABL:A:98
S:1 = ABL:A:97
S:2 = CFLAG:A:12
DELCHARA A
;レベル0＝未設定はレベル50扱い
SIF S == 0
	S = 50
SIF S:1 == 0
	S:1 = 50

RETURN 1


@GET_EXISTANCE
;Tで指定されたキャラ番号のキャラが仲間にいる場合、Eにキャラの加入番号+1を入れて返す。居なければ0を返す
;例えば、主人公がそのキャラなら0+1＝1を返すし、最初のパートナーなら1+1＝2を返す

E = 0
FOR LOCAL, 0, CHARANUM
	SIF CFLAG:LOCAL:21 == 1
		CONTINUE
	SIF NO:LOCAL == T
		E = LOCAL + 1
NEXT

RETURN 1

@GET_EXISTANCE2
;Tで指定されたキャラ番号のキャラが仲間にいる場合、Eにキャラの加入番号+1を入れて返す。居なければ0を返す
;例えば、主人公がそのキャラなら0+1＝1を返すし、最初のパートナーなら1+1＝2を返す
;CFLAG:21が立っている一時離別中のキャラも居る扱いにする

E = 0
FOR LOCAL, 0, CHARANUM
	SIF NO:LOCAL == T
		E = LOCAL + 1
NEXT

RETURN 1


@GET_BOSS_NO
;次のボスのNoを取得して、Bに入れて返す
A = 0
B = 0
D = 0

;!!!
;ボスになる可能性のあるキャラを全部チェックし、F配列に追加していく
;ボスのランダム選択(完全にランダムで選ぶ)変更。変数Gを追加で使用。
FOR LOCAL, 0, 200
	;今のところあなたがボスになる予定は無し
	SIF LOCAL == 0
		CONTINUE
	
	;CSVが存在しない番号（存在すればRESULT = 1）は選択しない
	EXISTCSV LOCAL,0
	SIF RESULT == 0
		CONTINUE

	;追加キャラcsvは除外
	SIF LOCAL >= 87 && LOCAL <= 89
		CONTINUE
	
	;ひまわり妖精（汎用キャラ）はクリア済みの場合出てくる（クリア後のオマケみたいなもの）
	IF LOCAL == 99
		SIF (FLAG:28 & 1) == 0
			CONTINUE
	ENDIF
	
	;ランダム決定用に出現する可能性があるキャラを保存
	G:D = LOCAL
	D += 1
	
	;各モードのラスボスは出現させない
	;EASY,NORMAL,HARDの場合
	IF FLAG:3 == 0 || FLAG:3 == 1 || FLAG:3 == 2
		;ずん子は出さない
		SIF LOCAL == 5
			CONTINUE

		T = 5
		CALL GET_EXISTANCE
		;ずん子が既に居る(＝パートナー)場合、きりたんは出さない
		SIF E && LOCAL == 10
			CONTINUE
	ENDIF
	T = LOCAL
	CALL GET_EXISTANCE
	;既に仲間になっているキャラは出さない
	SIF E
		CONTINUE


	;ここまで残ったキャラはボス候補
	F:A = LOCAL
	A += 1
NEXT

;ボス候補が誰も居ない場合
IF A == 0 && RAND:20 == 0
	;5%でゆかり様の幻影が出てくる
	F:0 = 666
	A += 1
ENDIF

;ボス候補が誰もいない場合
IF A == 0
	;完全にランダムで選ぶ（幻影登場）
	A = RAND:D
	B = G:A
ELSE
	;誰か候補がいる場合
	;候補キャラの中からランダムで選ぶ
	D = RAND:A
	B = F:D
ENDIF

RETURN 1



@GET_CHARA_NAME
;Tで指定されたキャラ番号のキャラの名前をSTR:Nに入れて返す
;EXISTCSVは対応するキャラが定義されているかどうかをチェックし定義されていればRESULTに1、されていなければ0を返します
EXISTCSV T,0
IF RESULT == 1
	CSVCALLNAME T, 0
	STR:N = %RESULTS%
ELSE
	STR:N = 誰かの幻影
ENDIF

RETURN 1



@GET_JUN_VALUE
;純愛系のコマンド実行値をAに入れて返す
;COM_ORDERと違って詳細は表示しない。あくまで隠しコマンド用

;従順
A = ABL:0 * 5

;PALAM:恭順
IF PALAM:4 < PALAMLV:1
	L = 0
ELSEIF PALAM:4 < PALAMLV:2
	L = 1
ELSEIF PALAM:4 < PALAMLV:3
	L = 2
ELSEIF PALAM:4 < PALAMLV:4
	L = 3
ELSEIF PALAM:4 < PALAMLV:5
	L = 4
ELSE
	L = 5
ENDIF
SIF L
	A += L * 3

;反抗的
SIF TALENT:11
	A -= 5

;気丈
SIF TALENT:12
	A -= 5

;素直
SIF TALENT:13
	A += 5

;プライド高い
SIF TALENT:15
	A -= 15

;プライド低い
SIF TALENT:17
	A += 5

;目立ちたがり
SIF TALENT:28
	A += 2

;抑圧
SIF TALENT:32
	A -= 10

;抵抗
SIF TALENT:34
	A -= 10

;即落ち
SIF TALENT:73
	A += 10

;男嫌い
SIF TALENT:82 && TALENT:PLAYER:122
	A -= 7

;調教者の素質
;魅惑
SIF TALENT:PLAYER:91
	A += 6
;謎の魅力
SIF TALENT:PLAYER:92
	A += 6

;相性チェック
R = NO:PLAYER
IF RELATION:R > 0 && RELATION:R < 30
	A -= 10
ELSEIF RELATION:R > 0 && RELATION:R < 70
	A -= 6
ELSEIF RELATION:R > 0 && RELATION:R < 100
	A -= 3
ELSEIF RELATION:R >= 100 && RELATION:R < 130
	A += 3
ELSEIF RELATION:R >= 100 && RELATION:R < 170
	A += 6
ELSEIF RELATION:R >= 100 && RELATION:R >= 170
	A += 10
ENDIF


;ほろ酔いボーナス
IF TFLAG:47 > 0
	A += TFLAG:47 / 5
ENDIF


RETURN 1


@GET_SUCCESS_RATE
;コマンド成功率の基準値をAに入れて返す

;従順
A = ABL:0 * 5

;PALAM:恭順
IF PALAM:4 < PALAMLV:1
	L = 0
ELSEIF PALAM:4 < PALAMLV:2
	L = 1
ELSEIF PALAM:4 < PALAMLV:3
	L = 2
ELSEIF PALAM:4 < PALAMLV:4
	L = 3
ELSEIF PALAM:4 < PALAMLV:5
	L = 4
ELSE
	L = 5
ENDIF
SIF L
	A += L * 3

;反抗的
SIF TALENT:11
	A -= 5

;気丈
SIF TALENT:12
	A -= 5

;素直
SIF TALENT:13
	A += 5

;プライド高い
SIF TALENT:15
	A -= 15

;プライド低い
SIF TALENT:17
	A += 5

;目立ちたがり
SIF TALENT:28
	A += 2

;抑圧
SIF TALENT:32
	A -= 10

;抵抗
SIF TALENT:34
	A -= 10

;即落ち
SIF TALENT:73
	A += 10

;男嫌い
SIF TALENT:82 && TALENT:PLAYER:122
	A -= 7

;調教者の素質
;魅惑
SIF TALENT:PLAYER:91
	A += 6
;謎の魅力
SIF TALENT:PLAYER:92
	A += 6

;相性チェック
R = NO:PLAYER
IF RELATION:R > 0 && RELATION:R < 30
	A -= 10
ELSEIF RELATION:R > 0 && RELATION:R < 70
	A -= 6
ELSEIF RELATION:R > 0 && RELATION:R < 100
	A -= 3
ELSEIF RELATION:R >= 100 && RELATION:R < 130
	A += 3
ELSEIF RELATION:R >= 100 && RELATION:R < 170
	A += 6
ELSEIF RELATION:R >= 100 && RELATION:R >= 170
	A += 10
ENDIF

;好感度
A += CFLAG:2 / 50

;恋慕
SIF TALENT:85
	A += 20

;恋人
SIF TALENT:153
	A += 20

;親愛
SIF TALENT:88
	A += 40

SIF A < 0
	A = 0

RETURN 1

@ADD_NEW_CHARA
;倒したキャラの加入処理
CALL ADD_NEW_CHARA_COMMON
;従者判定
CALL ATTENDANT
;従者加入　W:2～に数値が入っていれば追加。
FOR LOCAL, 2, 10
	IF W:LOCAL > 0
		FLAG:25 = W:LOCAL
		CALL ADD_NEW_CHARA_COMMON
	ENDIF
NEXT

RETURN 1

@ADD_NEW_CHARA_COMMON
;FLAG:25のキャラを追加
T = FLAG:25
CALL GET_EXISTANCE
IF E == 0
	T = FLAG:25
	FOR LOCAL, 0, CHARANUM
		SIF CFLAG:LOCAL:21 == 1 && NO:LOCAL == T
			E = LOCAL
	NEXT
	IF E != 0
		T = E
		CFLAG:T:21 = 0
		E = TARGET
		LOCAL:1 = TARGET
		TARGET = T
		T = FLAG:25
		TFLAG:600 = 36
		;口上の条件はTFLAG:600 == 36にしてください
		CALL KOJO_JUN
		TARGET = LOCAL:1
		TFLAG:600 = 0
	ELSE
		ADDCHARA FLAG:25
		A = CHARANUM - 1
		;キャラ加入時の共通設定
		CALL NEWCHARA_INIT
		
		;エクストラ構文呼び出し
		TFLAG:600 = 0
		Z = TARGET
		LOCAL:1 = TARGET
		TARGET = A
		CALL KOJO_JUN
		
		A = CHARANUM - 1
		
		;仲間になった場合FLAG:1000～に2を代入し、一度仲間になった判定に使用する
		FLAG:(999+ T) = 2
		
		;FLAG:23 & 4194304がONなら休憩所で休ませる
		SIF FLAG:23 & 4194304
			CFLAG:A:16 = 1
		;仲間にしたキャラのコマンドフィルタを自動的に共通設定にする
		SIF FLAG:61 & 1p10
			CALL INVERT_COM_FILTERING(1001, A)
		
		TARGET = LOCAL:1
	ENDIF
	E = 0
	;「あなた」はサポート対象外
	SIF FLAG:25 == 0
		TALENT:A:210 = 1
ENDIF

RETURN 1

;-------------------------------------------------
;キャラ加入時の共通設定
;AはCHARANUM - 1
;-------------------------------------------------
@NEWCHARA_INIT
;汎用キャラのランダム素質付加処理
SIF NO:A == 99
	CALL CHARA_RANDOM_STATE

;キス未経験付加
TALENT:A:151 = 1
;初期異常経験を記憶
CFLAG:A:22 = EXP:A:50
;初期噴乳経験を記録
SIF TALENT:A:130 == 0
	CFLAG:A:25 = EXP:A:54
;初期好感度を決定
CFLAG:A:2 = RAND:50 - 40
;初期戦闘技術を決定
ABL:A:88 += FLAG:24 / 2
;仲間になった日
CFLAG:A:75 = DAY:1
CFLAG:A:76 = DAY:2

;パートナーのキャラID設定
CFLAG:A:81 = FLAG:998
FLAG:998 += 1

;種族設定

L = NO:A
CALL GET_STRAINNAME
CFLAG:A:14 = RESULT

;初期月経周期を設定
L = A
CALL EVENT_ADD_CHARA_MENSTRUATION

;キャラ加入時の陰毛設定
CALL GET_BASE8, A

;デフォルトのコマンドを指示
CFLAG:A:13 = CFLAG:MASTER:13

;初期の耐力と魔力を計算
;耐力＝体力を基準値として、絶頂経験で増加
MAXBASE:A:4 = MAXBASE:A:0 * (100 + EXP:A:2) / 100
;魔力＝気力の10%を基準値として、精液経験で増加
MAXBASE:A:5 = MAXBASE:A:1 / 10 * (50 + EXP:A:20) / 50
BASE:A:4 = MAXBASE:A:4
BASE:A:5 = MAXBASE:A:5

;SPキャラ加入時、通常状態のSPキャラにエンゲージリングを贈っていた場合、
;SPキャラとして加入後エンゲージリングが贈れなくなってしまう為初期化
IF CFLAG:A:0 && FLAG:29 == NO:A
	FLAG:29 = 0
	TIME:9 = 0
	TIME:10 = 0
ENDIF

;-------------------------------------------------
;キャラ加入時の陰毛設定
;-------------------------------------------------
@GET_BASE8(ARG)
;陰毛なし
;ショウタ、アイ、きりたん、ウナ、ヒメ、ミコト
IF CFLAG:ARG:0 == 0 && GROUPMATCH(NO:ARG, 1, 2, 10, 81, 82, 83)
	BASE:ARG:8 = 0
	MAXBASE:ARG:8 = 0
	TALENT:ARG:65 = 1
;陰毛通常
;コウ、セイカ、イタコ、そら、タカハシ、ギャラ子
ELSEIF CFLAG:ARG:0 == 0 && GROUPMATCH(NO:ARG, 8, 9, 12, 13, 52, 80)
	BASE:ARG:8 = 1000
	MAXBASE:ARG:8 = 1000
	TALENT:ARG:65 = 0
;陰毛少なめ
;マキ、ゆかり、ずん子、茜、葵、あかり、ついな、ささら、つづみ、ＯＮＥ、ＩＡ、flower
ELSEIF CFLAG:ARG:0 == 0 && GROUPMATCH(NO:ARG, 3, 4, 5, 6, 7, 11, 14, 50, 51, 53, 54, 84)
	BASE:ARG:8 = 300
	MAXBASE:ARG:8 = 300
	TALENT:ARG:65 = 0
;マキ様, ゆかり様
ELSEIF CFLAG:ARG:0 && GROUPMATCH(NO:ARG, 3, 4)
	BASE:ARG:8 = 300
	MAXBASE:ARG:8 = 300
	TALENT:ARG:65 = 0
;その他のキャラ
ELSE
	;小人体型は陰毛上限0、[パイパン]
	IF TALENT:ARG:149
		BASE:ARG:8 = 0
		MAXBASE:ARG:8 = 0
		TALENT:ARG:65 = 1
	;小柄体型は陰毛上限300
	ELSEIF TALENT:ARG:100
		BASE:ARG:8 = 300
		MAXBASE:ARG:8 = 300
		TALENT:ARG:65 = 0
	;それ以外は陰毛上限1000
	ELSE
		BASE:ARG:8 = 1000
		MAXBASE:ARG:8 = 1000
		TALENT:ARG:65 = 0
	ENDIF
ENDIF

;-------------------------------------------------
;押し倒され判定
;-------------------------------------------------
@PUSH_OVER
;IF ASSIPLAY == 0 && (ABL:1 >= 7 || PALAM:3 >= 3000 || PALAM:5 >= 3000 || CFLAG:2 >= 10000)
;この条件で呼び出されているため、助手プレイ中は押し倒されない
;また、欲望Lv7以上、潤滑3000以上、欲情3000以上、好感度10000以上のどれかを満たす必要あり

;既に押し倒されてたら無視
;TFLAG:48は酔ってるお陰で服脱がせが成功したら１になるフラグ
;	または酔ってる勢いで押し倒されたら１になるフラグ
;TFLAG:51は押し倒されたら（押し倒され判定をクリアしたら）１になるフラグ
SIF TFLAG:48 || TFLAG:51
	RETURN 0

;既に脱いでたら無視
SIF TFLAG:44
	RETURN 0

;押し倒され数値判定。Aを使用
CALL PUSH_OVER_RATE

;合意有
IF TALENT:TARGET:152
	;ムード300突破で襲われる
	IF TFLAG:37 >= 300
		;個別の口上があれば口上表示
		TRYCCALLFORM KOJO_PUSH_OVER_{NO:TARGET}
		CATCH
			;個別ないなら共通の地の文
			PRINTFORMW %CALLNAME:MASTER%が%CALLNAME:TARGET%と一旦別れようとして振り返ると、突然%CALLNAME:TARGET%に押し倒されてしまった
			PRINTFORMW 焦る%CALLNAME:MASTER%の服を%CALLNAME:TARGET%は上気した顔で次々と脱がしていく……
			PRINTFORMW そして完全に%CALLNAME:MASTER%の服を脱がした%CALLNAME:TARGET%は、自らも一糸纏わぬ姿になった
			PRINTFORMW ……どうやらこれは覚悟を決めねばならないようだ
		ENDCATCH
		
		;気力半分回復
		BASE:1 += MAXBASE:1 / 2
		SIF BASE:1 > MAXBASE:1
			BASE:1 = MAXBASE:1
		
		;ウフフ開始
		TFLAG:44 = 1
		;映画館から追い出される
		CALL GET_OUT_MOVIE
		;押し倒されフラグON
		TFLAG:51 = 1
		;押し倒されたなら待機強制
		FORCEWAIT
		RETURN 1
	;酔い300突破で酔った勢いで襲われる
	ELSEIF TFLAG:47 >= 300
		TFLAG:48 = 1
		;個別の口上があれば口上表示
		TRYCCALLFORM KOJO_PUSH_OVER_{NO:TARGET}
		CATCH
			;個別ないなら共通の地の文
			PRINTFORMW %CALLNAME:MASTER%が%CALLNAME:TARGET%と一旦別れようとして振り返ると、突然%CALLNAME:TARGET%に押し倒されてしまった
			PRINTFORMW 焦る%CALLNAME:MASTER%の服を%CALLNAME:TARGET%は上気した顔で次々と脱がしていく……
			PRINTFORMW そして完全に%CALLNAME:MASTER%の服を脱がした%CALLNAME:TARGET%は、自らも一糸纏わぬ姿になった
			IF TFLAG:48 == 1
				PRINTFORMW その間何度も呼びかけてみたが、%CALLNAME:TARGET%の返事はどうも要領を得ない
				PRINTFORM 少し飲ませすぎてしまっただろうか
			ENDIF
			PRINTFORMW ……どうやらこれは覚悟を決めねばならないようだ
		ENDCATCH
		
		;気力半分回復
		BASE:1 += MAXBASE:1 / 2
		SIF BASE:1 > MAXBASE:1
			BASE:1 = MAXBASE:1
		
		;ウフフ開始
		TFLAG:44 = 1
		;映画館から追い出される
		CALL GET_OUT_MOVIE
		;押し倒されフラグON
		TFLAG:51 = 1
		;押し倒されたなら待機強制
		FORCEWAIT
		RETURN 1
	ENDIF
;合意なし
ELSE
	;220突破で襲われる
	IF A >= 220
		TFLAG:48 = 0
		TFLAG:600 = 27
		CALL KOJO_JUN
		PRINTL  

		IF ASSIPLAY == 0 && TALENT:152 == 0
			PRINTFORMW %CALLNAME:TARGET%の【合意】を得た
			TALENT:152 = 1
		ENDIF
		;気力全快
		BASE:1 = MAXBASE:1

		TFLAG:44 = 1

		;映画館から追い出される
		CALL GET_OUT_MOVIE
		;既成事実２に相当
		TFLAG:200 = 2
		CALL MARK_GOT_CHECK
		;押し倒されたなら待機強制
		FORCEWAIT
		RETURN 1
	;酔った勢いで襲われる
	ELSEIF A >= 220 - TFLAG:47 / 2
		TFLAG:48 = 1
		TFLAG:600 = 27
		CALL KOJO_JUN
		PRINTL  

		;気力半分回復
		BASE:1 += MAXBASE:1 / 2
		SIF BASE:1 > MAXBASE:1
			BASE:1 = MAXBASE:1

		TFLAG:44 = 1
		;映画館から追い出される
		CALL GET_OUT_MOVIE
		;既成事実２に相当
		TFLAG:200 = 2
		CALL MARK_GOT_CHECK
		;押し倒されたなら待機強制
		FORCEWAIT
		RETURN 1
	ENDIF
ENDIF

RETURN 0

;-------------------------------------------------
;押し倒され数値判定
;-------------------------------------------------
@PUSH_OVER_RATE
A = 0
CALL GET_JUN_VALUE

;ABL:欲望
IF ABL:1
	A += ABL:1 * 4
ENDIF

;PALAM:潤滑
IF PALAM:3 >= 3000
	A += PALAM:3 / 1000
ENDIF

;PALAM:欲情
IF PALAM:5 >= 3000
	A += PALAM:5 / 1000
ENDIF

;ABL:奉仕精神
IF ABL:6
	A += ABL:6 * 3
ENDIF
;男嫌い
IF TALENT:82 && TALENT:PLAYER:122
	A -= 7
ENDIF
;貞操観念
IF TALENT:TARGET:30
	A -= 10
ENDIF
;恥じらい
IF TALENT:TARGET:35
	A -= 10
ENDIF
;献身的
IF TALENT:TARGET:63
	A += 10
ENDIF

;合意と恋慕がある場合
IF TALENT:TARGET:152 && TALENT:TARGET:85
	;相手にサド
	IF TALENT:TARGET:83
		A += 1
	ENDIF
	;親愛&淫乱　取得順序を間違えると最初からやり直さないといけないので達成時に補正多め
	IF TALENT:TARGET:88 && TALENT:TARGET:76
		;相愛がある場合
		IF TALENT:TARGET:89
			A += 22
		;相愛がない場合
		ELSE
			A += 13
		ENDIF
	ENDIF
	;親愛と淫乱がなく、相愛のみある場合
	IF TALENT:TARGET:88 == 0 && TALENT:TARGET:76 == 0 && TALENT:TARGET:89
		A += 9
	ENDIF
;合意がある
ELSEIF TALENT:TARGET:152
	;恋慕と恋人を両方持つ
	IF TALENT:85 && TALENT:TARGET:152
		A += 22
	;恋慕か恋人がある
	ELSEIF TALENT:85 || TALENT:TARGET:152
		A += 10
	;どちらもない
	ELSE
		A += 1
	ENDIF
	;素直
	IF TALENT:TARGET:13
		;ツンデレがあると更に補正あり
		IF TALENT:TARGET:18
			A += 2
		ELSE
			A += 1
		ENDIF
	ENDIF
;合意がない
ELSEIF TALENT:TARGET:152 == 0
	;恋慕と恋人を両方持つ
	IF TALENT:85 && TALENT:TARGET:152
		A += 2
	;恋慕か恋人がある
	ELSEIF TALENT:85 || TALENT:TARGET:152
		A += 1
	ENDIF
	;素直
	IF TALENT:TARGET:13
		A += 1
	ENDIF
ENDIF

;ムードボーナス
B = 0
IF TFLAG:37 < 10
	B -= 20
ELSEIF TFLAG:37 < 30
	B -= 5
ELSEIF TFLAG:37 < 50
	B -= 1
ELSEIF TFLAG:37 < 100
	B += TFLAG:37 / 10
ELSEIF TFLAG:37 < 300
	B += TFLAG:37 / 7
ELSE
	B += TFLAG:37 / 5
ENDIF
A += B

;好感度
IF CFLAG:2 >= 3000
	B = CFLAG:2 / 1000
	A += B
ENDIF
;キス未経験
IF TALENT:151
	A -= 20
ENDIF

;一線越えない
IF TALENT:27
	A -= 20
ENDIF

;相手が主導権
IF TFLAG:45
	A += 5
ENDIF

;相手が非処女で、こちらが童貞
IF TALENT:0 == 0 && TALENT:MASTER:154
	A += 5
ENDIF

;桃色の秘薬使用中
IF TEQUIP:28 >= 5
	A += 5
ELSEIF TEQUIP:28
	A += TEQUIP:28
ENDIF

;-------------------------------------------------
;特殊能力の取得
;-------------------------------------------------
@GET_FEAT
;特殊能力の取得

CALL GET_FEAT_CHECK
IF RESULT
	CALL GET_FEAT2
	RETURN 0
ENDIF

;初期化
VARSET A, 0

PRINTL 
PRINTFORML 好きな特殊能力を身に付けることが出来ます。選んでください
IF ABL:MASTER:81 < 99
	PRINTFORML [  0] 好感度ボーナス Lv{ABL:MASTER:81 + 1}
	A:0 = 1
ENDIF
IF TALENT:MASTER:113 == 0
	PRINTFORML [  1] 【魅力】追加
	A:1 = 1
ENDIF
IF TALENT:MASTER:202 == 0
	PRINTFORML [  2] 脱衣制限解除
	A:2 = 1
ENDIF
IF ABL:MASTER:82 < 99
	PRINTFORML [  3] 生活費軽減 Lv{ABL:MASTER:82 + 1}
	A:3 = 1
ENDIF
IF ABL:MASTER:83 < 99
	PRINTFORML [  4] 戦闘経験ボーナス Lv{ABL:MASTER:83 + 1}
	A:4 = 1
ENDIF
IF ABL:MASTER:84 < 99 && FLAG:5 == 10
	PRINTFORML [  5] 霧無効化能力強化 Lv{ABL:MASTER:84 + 1}
	A:5 = 1
ENDIF
IF ABL:MASTER:85 < 99 && FLAG:5 == 10
	PRINTFORML [  6] 霧浄化能力強化 Lv{ABL:MASTER:85 + 1}
	A:6 = 1
ENDIF
IF TALENT:MASTER:203 == 0 && FLAG:5 == 10
	PRINTFORML [  7] 恋人ボーナス強化
	A:7 = 1
ENDIF
IF TALENT:MASTER:204 == 0 && FLAG:5 == 10
	PRINTFORML [  8] 恋慕ボーナス強化
	A:8 = 1
ENDIF
IF TALENT:MASTER:205 == 0 && FLAG:5 == 10
	PRINTFORML [  9] 親愛ボーナス強化
	A:9 = 1
ENDIF
IF TALENT:MASTER:206 == 0 && FLAG:5 == 10
	PRINTFORML [ 10] 淫乱ボーナス強化
	A:10 = 1
ENDIF
IF ABL:MASTER:86 < 99
	PRINTFORML [ 11] 獲得資金増加 Lv{ABL:MASTER:86 + 1}
	A:11 = 1
ENDIF
IF ABL:MASTER:87 < 99
	PRINTFORML [ 12] 戦闘能力強化 Lv{ABL:MASTER:87 + 1}
	A:12 = 1
ENDIF
IF TALENT:MASTER:207 == 0
	PRINTFORML [ 13] 逃走能力強化
	A:13 = 1
ENDIF
IF TALENT:MASTER:208 == 0 && FLAG:5 == 10
	PRINTFORML [ 14] 後戻りできる程度の能力
	A:14 = 1
ENDIF
IF TALENT:MASTER:209 == 0 && NO:MASTER == 0 && FLAG:5 == 10
	PRINTFORML [ 15] 野菜を育てる程度の能力
	A:15 = 1
ENDIF
IF MAXBASE:MASTER:体力 < 9999
	PRINTFORML [ 16] %CALLNAME:MASTER%の最大体力 + 500 （現在の最大体力{MAXBASE:MASTER:体力}）
	A:16 = 1
ENDIF
IF MAXBASE:MASTER:気力 < 9999
	PRINTFORML [ 17] %CALLNAME:MASTER%の最大気力 + 500 （現在の最大気力{MAXBASE:MASTER:気力}）
	A:17 = 1
ENDIF

PRINTFORML [100] 何も取得しない

$INPUT_LOOP
INPUT
B = RESULT

IF RESULT == 100
	PRINTFORMW 何も取得しませんでした
	RETURN 1
ELSEIF A:RESULT == 0
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
ELSEIF RESULT == 0
	PRINTFORML コマンド実行時の好感度上昇量が+{ABL:MASTER:81 + 1}されます
ELSEIF RESULT == 1
	PRINTFORML コマンド実行時のムード上昇量が+1されます
ELSEIF RESULT == 2
	PRINTFORML 「服を脱がせる」が必ず成功するようになり、戦闘中は最初から服を脱ぐようになります
ELSEIF RESULT == 3
	PRINTFORML 生活費が{ABL:MASTER:82 + 1}％引きになります
ELSEIF RESULT == 4
	PRINTFORML Stage攻略の際に得られる戦闘経験が+{ABL:MASTER:83 + 1}されます
ELSEIF RESULT == 5
	PRINTFORML 仲間が霧に飲まれかけるまでの期限が約{(ABL:MASTER:84 + 1) * 2}日伸びます
ELSEIF RESULT == 6
	PRINTFORML 仲間が霧に飲まれかけても大丈夫な回数が{ABL:MASTER:85 + 1}回増えます
ELSEIF RESULT == 7
	PRINTFORML 恋人の人数に応じて得られる戦闘能力ボーナスが1.5倍になります
ELSEIF RESULT == 8
	PRINTFORML 恋慕の人数に応じて得られる防御力ボーナスが2倍になります
ELSEIF RESULT == 9
	PRINTFORML 親愛の人数に応じて得られる戦闘能力ボーナスが3倍になります
ELSEIF RESULT == 10
	PRINTFORML 淫乱の人数に応じて得られる耐力・魔力ボーナスが3倍になります
ELSEIF RESULT == 11
	IF ABL:MASTER:86 == 0
		PRINTFORML アルバイトやモンスター退治で得られる資金が1.5倍になります
	ELSE
		PRINTFORML アルバイトやモンスター退治で得られる資金が{ABL:MASTER:86 + 1}倍になります
	ENDIF
ELSEIF RESULT == 12
	IF ABL:MASTER:82 == 0
		PRINTFORML %CALLNAME:MASTER%の戦闘能力が1.5倍になります
	ELSE
		PRINTFORML %CALLNAME:MASTER%の戦闘能力が{ABL:MASTER:87 + 1}倍になります
	ENDIF
ELSEIF RESULT == 13
	PRINTFORML 撤退が一部の例外を除いて必ず成功します
ELSEIF RESULT == 14
	PRINTFORML 前のStageに戻れるようになります
ELSEIF RESULT == 15
	PRINTFORML 畑仕事で得られる資金が増加します
ELSEIF RESULT == 16
	PRINTFORML %CALLNAME:MASTER%の最大体力が上昇します（最高9999まで）
ELSEIF RESULT == 17
	PRINTFORML %CALLNAME:MASTER%の最大気力が上昇します（最高9999まで）
ELSE
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
ENDIF

PRINTFORML この能力を取得して良いですか？
PRINTFORML [0] はい
PRINTFORML [1] いいえ

$INPUT_LOOP_2
INPUT

IF (RESULT < 0 || RESULT > 1)
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP_2
ELSEIF RESULT == 1
	RESTART
ELSEIF B == 0
	ABL:MASTER:81 += 1
	PRINTFORMW 好感度ボーナス Lv{ABL:MASTER:81} を取得しました
ELSEIF B == 1
	TALENT:MASTER:113 = 1
	PRINTFORMW 【魅力】を取得しました
ELSEIF B == 2
	TALENT:MASTER:202 = 1
	PRINTFORMW 脱衣制限解除 を取得しました
ELSEIF B == 3
	ABL:MASTER:82 += 1
	PRINTFORMW 生活費軽減 Lv{ABL:MASTER:82} を取得しました
ELSEIF B == 4
	ABL:MASTER:83 += 1
	PRINTFORMW 戦闘経験ボーナス Lv{ABL:MASTER:83} を取得しました
ELSEIF B == 5
	ABL:MASTER:84 += 1
	PRINTFORMW 霧無効化能力強化 Lv{ABL:MASTER:84} を取得しました
ELSEIF B == 6
	ABL:MASTER:85 += 1
	PRINTFORMW 霧浄化能力強化 Lv{ABL:MASTER:85} を取得しました
ELSEIF B == 7
	TALENT:MASTER:203 = 1
	PRINTFORMW 恋人ボーナス強化 を取得しました
ELSEIF B == 8
	TALENT:MASTER:204 = 1
	PRINTFORMW 恋慕ボーナス強化 を取得しました
ELSEIF B == 9
	TALENT:MASTER:205 = 1
	PRINTFORMW 親愛ボーナス強化 を取得しました
ELSEIF B == 10
	TALENT:MASTER:206 = 1
	PRINTFORMW 淫乱ボーナス強化 を取得しました
ELSEIF B == 11
	ABL:MASTER:86 += 1
	PRINTFORMW 獲得資金増加 Lv{ABL:MASTER:86} を取得しました
ELSEIF B == 12
	ABL:MASTER:87 += 1
	PRINTFORMW 戦闘能力強化 Lv{ABL:MASTER:87} を取得しました
ELSEIF B == 13
	TALENT:MASTER:207 = 1
	PRINTFORMW 逃走能力強化 を取得しました
ELSEIF B == 14
	TALENT:MASTER:208 = 1
	PRINTFORMW 後戻りできる程度の能力 を取得しました
ELSEIF B == 15
	TALENT:MASTER:209 = 1
	PRINTFORMW 野菜を育てる程度の能力 を取得しました
ELSEIF B == 16
	MAXBASE:MASTER:体力 += 500
	BASE:MASTER:体力 += 500
	PRINTFORMW %CALLNAME:MASTER%の最大体力が上昇しました
ELSEIF B == 17
	MAXBASE:MASTER:気力 += 500
	BASE:MASTER:気力 += 500
	PRINTFORMW %CALLNAME:MASTER%の最大気力が上昇しました
ENDIF
RETURN 0

;-------------------------------------------------
;特殊能力の取得判定
;-------------------------------------------------
@GET_FEAT_CHECK
;初期化
VARSET A, 0
VARSET LOCAL, 0

IF ABL:MASTER:81 < 99
	;PRINTFORML [  0] 好感度ボーナス Lv{ABL:MASTER:81 + 1}
	A:0 = 1
ENDIF
IF TALENT:MASTER:113 == 0
	;PRINTFORML [  1] 【魅力】追加
	A:1 = 1
ENDIF
IF TALENT:MASTER:202 == 0
	;PRINTFORML [  2] 脱衣制限解除
	A:2 = 1
ENDIF
IF ABL:MASTER:82 < 99
	;PRINTFORML [  3] 生活費軽減 Lv{ABL:MASTER:82 + 1}
	A:3 = 1
ENDIF
IF ABL:MASTER:83 < 99
	;PRINTFORML [  4] 戦闘経験ボーナス Lv{ABL:MASTER:83 + 1}
	A:4 = 1
ENDIF
IF ABL:MASTER:84 < 99 && FLAG:5 == 10
	;PRINTFORML [  5] 霧無効化能力強化 Lv{ABL:MASTER:84 + 1}
	A:5 = 1
ENDIF
IF ABL:MASTER:85 < 99 && FLAG:5 == 10
	;PRINTFORML [  6] 霧浄化能力強化 Lv{ABL:MASTER:85 + 1}
	A:6 = 1
ENDIF
IF TALENT:MASTER:203 == 0 && FLAG:5 == 10
	;PRINTFORML [  7] 恋人ボーナス強化
	A:7 = 1
ENDIF
IF TALENT:MASTER:204 == 0 && FLAG:5 == 10
	;PRINTFORML [  8] 恋慕ボーナス強化
	A:8 = 1
ENDIF
IF TALENT:MASTER:205 == 0 && FLAG:5 == 10
	;PRINTFORML [  9] 親愛ボーナス強化
	A:9 = 1
ENDIF
IF TALENT:MASTER:206 == 0 && FLAG:5 == 10
	;PRINTFORML [ 10] 淫乱ボーナス強化
	A:10 = 1
ENDIF
IF ABL:MASTER:86 < 99
	;PRINTFORML [ 11] 獲得資金増加 Lv{ABL:MASTER:86 + 1}
	A:11 = 1
ENDIF
IF ABL:MASTER:87 < 99
	;PRINTFORML [ 12] 戦闘能力強化 Lv{ABL:MASTER:87 + 1}
	A:12 = 1
ENDIF
IF TALENT:MASTER:207 == 0
	;PRINTFORML [ 13] 逃走能力強化
	A:13 = 1
ENDIF
IF TALENT:MASTER:208 == 0 && FLAG:5 == 10
	;PRINTFORML [ 14] 後戻りできる程度の能力
	A:14 = 1
ENDIF
IF TALENT:MASTER:209 == 0 && NO:MASTER == 0 && FLAG:5 == 10
	;PRINTFORML [ 15] 野菜を育てる程度の能力
	A:15 = 1
ENDIF
IF MAXBASE:MASTER:体力 < 9999
	;PRINTFORML [ 16] %CALLNAME:MASTER%の最大体力 + 500 （現在の最大体力{MAXBASE:MASTER:体力}）
	A:16 = 1
ENDIF
IF MAXBASE:MASTER:気力 < 9999
	;PRINTFORML [ 17] %CALLNAME:MASTER%の最大気力 + 500 （現在の最大気力{MAXBASE:MASTER:気力}）
	A:17 = 1
ENDIF

FOR LOCAL, 0, 20
	SIF A:LOCAL
		LOCAL:1 = 1
NEXT

IF LOCAL:1 == 0
	RETURN 1
ELSE
	RETURN 0
ENDIF

;-------------------------------------------------
;特殊能力代わりのボーナス取得
;-------------------------------------------------
@GET_FEAT2
;初期化
VARSET A, 0

PRINTL 
PRINTFORML 好きなボーナスを取得出来ます。選んでください
PRINTFORML [  0] 全調合素材×1
PRINTFORML [  1] 全宝探しアイテム×1
PRINTFORML [  2] 所持金2倍

PRINTFORML [100] 何も取得しない

$INPUT_LOOP
INPUT
B = RESULT

IF RESULT == 100
	PRINTFORMW 何も取得しませんでした
	RETURN 1
ELSEIF RESULT == 0
	PRINTFORML 全調合素材を１つずつ入手できます
ELSEIF RESULT == 1
	PRINTFORML 宝探しで入手できるアイテムを全種類１つずつ入手できます
ELSEIF RESULT == 2
	PRINTFORML 所持金が2倍になります
ELSE
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
ENDIF

PRINTFORML このボーナスを取得して良いですか？
PRINTFORML [0] はい
PRINTFORML [1] いいえ

$INPUT_LOOP_2
INPUT

IF (RESULT < 0 || RESULT > 1)
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP_2
ELSEIF RESULT == 1
	RESTART
ELSEIF B == 0
	FOR LOCAL, 900, 998
		SIF DISP_ITEM(LOCAL) == 0
			CONTINUE
		ITEM:LOCAL += 1
	NEXT
	PRINTFORMW 全調合素材を１つずつ入手しました
ELSEIF B == 1
	FOR LOCAL, 500, 600
		SIF DISP_ITEM(LOCAL) == 0
			CONTINUE
		ITEM:LOCAL += 1
	NEXT
	PRINTFORMW 宝探しで入手できるアイテムを全種類１つずつ入手しました
ELSEIF B == 2
	MONEY *= 2
	PRINTFORMW 所持金が2倍になりました
ENDIF
RETURN 0


;-------------------------------------------------
;霧に飲まれかけているキャラ取得
;-------------------------------------------------
@GET_LOST_CHARA
;霧に飲まれかけているキャラの番号をランダムで取得し、Cに入れて返す。誰もいなければ０を返す

A = 0
B = 0
C = 0
D = 0

FOR LOCAL, 0, CHARANUM
	IF LOCAL == 0
		CONTINUE
	ELSEIF CFLAG:LOCAL:17
		A:B = LOCAL
		B += 1
	ENDIF
NEXT

IF B == 0
	C = 0
ELSE
	D = RAND:B
	C = A:D
ENDIF

RETURN 0


@SET_TMP_PARTNER
;暫定パートナーを選択
;好感度が一番高いキャラを暫定パートナーに
;臨月、育児中のキャラは回避
;別れているキャラは回避

A = 0
B = 0
FOR LOCAL, 0, CHARANUM
	SIF LOCAL == NO:MASTER
		CONTINUE
	SIF (FLAG:62 & 1) && (MARK:LOCAL:90 == 1)
		CONTINUE
	SIF CFLAG:LOCAL:21
		CONTINUE
	IF CFLAG:LOCAL:17 == 0
		IF CFLAG:LOCAL:2 > A
			A = CFLAG:LOCAL:2
			B = LOCAL
		ENDIF
	ENDIF
NEXT

IF B != 0
	SIF ASSI == B
		ASSI = -1
	TARGET = B
	SIF CFLAG:16 == 1
		CFLAG:16 = 0
	PRINTFORMW %CALLNAME:TARGET%をパートナーに選びました
	RETURN 1
ENDIF

RETURN 0


@CHECK_FEAT
;特殊能力獲得

;Phモード以外は無視
SIF FLAG:5 != 10
	RETURN 0

IF FLAG:48 == 1 || FLAG:48 == 3 || FLAG:48 == 5 || FLAG:48 == 8 || FLAG:48 == 11 || (FLAG:48 >= 15 && (FLAG:48 % 5 == 0))
	PRINTFORMW 恋人の延べ人数が{FLAG:48}人を超えたので、特殊能力を１つ得る事ができます
	CALL GET_FEAT
ENDIF

RETURN 0

;-------------------------------------------------
;【恋人】取得
;-------------------------------------------------
@GET_LOVERS
;TにMASTERと恋人になる人

PRINTFORMW %CALLNAME:T%と【恋人】になりました
FLAG:48 += 1
CALL CHECK_FEAT
TFLAG:39 = 1

;既成事実LV2
TFLAG:200 = 2

;-------------------------------------------------
;CSVの名前取得関数
;-------------------------------------------------
@NAME_CSV(ARG, ARG:1)
#FUNCTIONS
;CSVが存在するか判定
;CSVが存在しない場合（存在すればRESULT = 1）は空白
EXISTCSV ARG, ARG:1
SIF RESULT == 0
	RETURNF ""

CSVNAME ARG, ARG:1
RETURNF RESULTS

;----------------------------------------------------------------
;ARGSで放り込んだ文字列を一文字刻みで区切り文字(ARGS:1)で区切って返す関数
;適当に作ったけど没……にするのも惜しいから適当に利用
;----------------------------------------------------------------
@SPLIT_EASY(ARGS, ARGS:1 = "/")
#FUNCTIONS
FOR LOCAL, 0, STRLENSU(ARGS)
	LOCALS += CHARATU(ARGS, LOCAL) + ARGS:1
NEXT

RETURNF LOCALS

;----------------------------------------------------------------
;キャラが居るか、別れてるか、それとも仲間にしてないのかを判定する式中関数
;居ない = 0, 居る = 1, 別れてる = 2
;----------------------------------------------------------------
@CHARA_NOW(ARG)
#FUNCTION
GETCHARA ARG
;居るまたはデータのみ居る
IF RESULT > -1
	;別れている
	SIF CFLAG:RESULT:別れ
		RETURNF 2
	;別れてない
	RETURNF 1
ENDIF
;居ない
RETURNF 0

