;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/
;_/
;_/待機中指示関連
;_/
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/

;-------------------------------------------------
;待機中指示変更
;-------------------------------------------------
@CHANGE_ACTION
X = 0
REPEAT CHARANUM
	SIF CFLAG:COUNT:16 == 1
		X = 1
REND

;臨月、育児中キャラは強制的に[何もしない]
REPEAT CHARANUM
	SIF MARK:COUNT:90 == 1
		CFLAG:COUNT:13 = 0
REND

PRINTL 誰に指示を出しますか？
CALL ACTION_LIST
IF CHARANUM <= 100
	PRINTL [100] 戻る
ELSE
	PRINTL [1000][-1] 戻る
ENDIF
IF X == 1 && U == 0
	PRINTFORML [200] 休憩中のキャラも表示する
ELSEIF X == 1 && U == 1
	PRINTFORML [200] 休憩中のキャラを非表示にする
ENDIF
SIF CHARANUM >= 3
	PRINTFORML [300] 全員に指示を出す


$INPUT_LOOP
INPUT
IF RESULT == -1 || (CHARANUM <= 100 && RESULT == 100) || RESULT == 1000
	RETURN 0
ELSEIF RESULT == 200 && X == 1
	IF U == 0
		U = 1
		RESTART
	ELSE
		U = 0
		RESTART
	ENDIF
ELSEIF RESULT == 300 && CHARANUM >= 3
	;全員に指示を出す
	CALL SHOP_ACT_LIST
	$INPUT_LOOP_ACT_TOTAL
	INPUT
	IF RESULT == 100
		RESTART
	ELSEIF RESULT < 0 || RESULT > 100
		PRINTFORML 正しい値を入力してください
		GOTO INPUT_LOOP_ACT_TOTAL
	ELSEIF C:RESULT == 0
		PRINTFORML 正しい値を入力してください
		GOTO INPUT_LOOP_ACT_TOTAL
	ELSE
		;選択された待機中行動を保存
		D = RESULT
		IF RESULT == 20
			;[13] リフレッシュ(耐力○％以下)
			PRINTFORML 耐力が○％以下の仲間にリフレッシュを指示します。
			PRINTFORML 何％以下の仲間に指示しますか？ （1-100、0で戻る）
			$INPUT_LOOP_REFRESH2
			INPUT
			IF RESULT == 0
				CALL SHOP_ACT_LIST
				GOTO INPUT_LOOP_ACT_TOTAL
			ELSEIF RESULT < 0 || RESULT > 100
				PRINTFORML 正しい値を入力してください
				GOTO INPUT_LOOP_REFRESH2
			ELSE
				;キャラごとに選択された待機中行動を判定し、可能なキャラは変更する
				FOR A, 0, CHARANUM
					;MASTERは変更できない
					SIF A == MASTER
						CONTINUE
					;U == 0の時（休憩中のキャラが表示されていない時）は休憩中のキャラの行動を変えない
					SIF U == 0 && CFLAG:A:16
						CONTINUE
					
					;耐力が○(選択された値)％以下ならリフレッシュに変更
					SIF (BASE:A:耐力 <= MAXBASE:A:耐力 * RESULT / 100)
						CFLAG:A:13 = 2
				NEXT
			ENDIF
		ELSE
			;キャラごとに選択された待機中行動を判定し、可能なキャラは変更する
			FOR A, 0, CHARANUM
				;MASTERは変更できない
				SIF A == MASTER
					CONTINUE
				;U == 0の時（休憩中のキャラが表示されていない時）は休憩中のキャラの行動を変えない
				SIF U == 0 && CFLAG:A:16
					CONTINUE
				
				CALL SHOP_ACT_ABLE
				;D（選択された待機中行動）が可能（B:D = 1）なら変更
				SIF B:D
					CFLAG:A:13 = D
			NEXT
		ENDIF
		PRINTW 全員に指示しました
		CALL ACTION_LIST
		CALL SHOP_ACT_LIST
		GOTO INPUT_LOOP_ACT_TOTAL
	ENDIF
	RESTART
ELSEIF RESULT < 0 || RESULT >= CHARANUM
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
ELSEIF CFLAG:RESULT:16 == 1 && U == 0
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
ELSEIF CFLAG:RESULT:21 == 1
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
ELSEIF FLAG:62 & 1 && (MARK:RESULT:90 == 1)
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
;ELSEIF RESULT == ASSI
	;GOTO INPUT_LOOP
ENDIF

IF CFLAG:RESULT:16 == 0 || U == 1
;内容を指示
	;RESULT退避
	A = RESULT

	IF RESULT == MASTER
		PRINT 新たに加わった仲間へのデフォルトとして
	ENDIF
	PRINTFORML 何を指示しますか？
	
	CALL SHOP_ACT_NAME
	FOR LOCAL, 0, 15
		CALL SHOP_ACT_ABLE
		SIF B:LOCAL
			PRINTFORML [{LOCAL, 3}] %TSTR:LOCAL,16,LEFT% ：%TSTR:(100+LOCAL)%
	NEXT
	PRINTFORML [100] キャンセル

	$INPUT_LOOP_ACT
	INPUT
	IF RESULT == 100
		RESTART
	ELSEIF RESULT < 0 || RESULT > 100
		PRINTFORML 正しい値を入力してください
		GOTO INPUT_LOOP_ACT
	ELSEIF B:RESULT == 0
		PRINTFORML 正しい値を入力してください
		GOTO INPUT_LOOP_ACT
	ELSE
		CFLAG:A:13 = RESULT
		PRINTFORMW 指示を変更しました
	ENDIF

	RESTART
ENDIF

@SHOP_ACT_LIST
IF U == 0
	PRINTFORML 何を指示しますか？(休憩中のキャラには指示を出しません)
ELSEIF U == 1
	PRINTFORML 何を指示しますか？(休憩中のキャラも含めて全員に指示を出します)
ENDIF
;全キャラに対して指示可能な待機中行動(B:1～)を判定し、指示可能なキャラが1人でもいる待機中行動(C:1～に記録)を表示する
VARSET C, 0
FOR A, 0, CHARANUM
	CALL SHOP_ACT_ABLE
	FOR LOCAL, 0, 15
		SIF B:LOCAL
			C:LOCAL = 1
	NEXT
NEXT
CALL SHOP_ACT_NAME
FOR LOCAL, 0, 15
	SIF C:LOCAL
		PRINTFORML [{LOCAL, 3}] %TSTR:LOCAL,16,LEFT% ：%TSTR:(100+LOCAL)%
NEXT
PRINTFORML [ 20] リフレッシュ(耐力○％以下)
C:13 = 1
PRINTFORML [100] キャンセル

@SHOP_ACT_NAME
VARSET TSTR, ""
TSTR:0  = 何もさせない
TSTR:1  = 修行
TSTR:2  = リフレッシュ
TSTR:3  = 自給自足
TSTR:4  = 連携を確認する
TSTR:5  = 家事
TSTR:6  = モンスター退治
TSTR:7  = イチャイチャする
TSTR:8  = 朝フェラ
TSTR:9  = 自慰
TSTR:10 = レズ行為
TSTR:11 = 性行為
TSTR:12 = 宝探し
;TSTR:20 = リフレッシュ(耐力○％以下)
TSTR:100 = 変化なし
TSTR:101 = 回復量↓   生活費↓   戦闘経験↑
TSTR:102 = 回復量↑↑ 生活費↑↑
TSTR:103 = 回復量↓↓ 生活費↓↓ 宝珠↑
TSTR:104 = 回復量↓   生活費↑   戦闘経験↑ 好感度↑
TSTR:105 = 回復量↓   生活費↓   宝珠↑
TSTR:106 = 回復量０   耐力  ↓   戦闘経験↑ 金↑
TSTR:107 = 生活費↑↑ 好感度↑
TSTR:108 = 回復量↓   経験  ↑   宝珠↑
TSTR:109 = 回復量↓   生活費↑   経験↑ 宝珠↑
TSTR:110 = 回復量↓   生活費↑   経験↑ 宝珠↑
TSTR:111 = 回復量↓   生活費↑   経験↑ 宝珠↑ 好感度↑
TSTR:112 = 回復量↓   生活費↑   アイテム発見

@SHOP_ACT_ABLE
;事前にAにTARGET等キャラを指定しておき、Aが各待機中行動をできるか判定する
;各待機中行動が可能な場合B:○が1になる
VARSET B, 0
;[0] 何もさせない
B:0 = 1
;[1] 修行
B:1 = 1
;[2] リフレッシュ
B:2 = 1
;[3] 自給自足
;ゆかり様には親密30以上ないと自給自足を指示できない
SIF TALENT:A:eraVoiceroidJも支配する程度の能力 == 0 || ABL:A:親密 >= 30 || A == MASTER
	B:3 = 1
;[4] 連携を確認する
SIF A != MASTER && (ABL:A:親密 >= 5 || CFLAG:A:2 >= 500 || TALENT:A:恋人)
	B:4 = 1
;[5] 家事
;ゆかり様には親密20以上ないと指示できない
SIF A != MASTER && (TALENT:A:eraVoiceroidJも支配する程度の能力 == 0 && (ABL:A:親密 >= 5 || CFLAG:A:2 >= 500 || TALENT:A:恋人) || ABL:A:親密 >= 20)
	B:5 = 1
;[6] モンスター退治
SIF A != MASTER && A != TARGET && A != ASSI && BASE:A:耐力 >= 500 && (ABL:A:親密 >= 5 || CFLAG:A:2 >= 500 || TALENT:A:恋人)
	B:6 = 1
;[7] イチャイチャする
SIF A != MASTER && TALENT:A:恋人
	B:7 = 1
;[8] 朝フェラ
SIF TALENT:A:猫舌 == 0 && A != MASTER && (TALENT:A:合意 || TALENT:A:恋人) && (ABL:A:親密 >= 10 || ABL:A:欲望 >= 10 || ABL:A:奉仕精神 >= 10 || ABL:A:精液中毒 >= 1) && (TALENT:MASTER:半人半妖 || TALENT:MASTER:ふたなり || TALENT:MASTER:オトコ)
	B:8 = 1
;[9] 自慰
SIF A != MASTER && (TALENT:A:合意 || TALENT:A:恋人) && (ABL:A:親密 >= 10 || ABL:A:欲望 >= 10 || ABL:A:自慰中毒 >= 1)
	B:9 = 1
;[10] レズ行為
SIF A != MASTER && ABL:A:欲望 >= 5 && ABL:A:レズっ気 >= 3 && (ABL:A:親密 >= 10 || ABL:A:欲望 >= 10 || ABL:A:レズっ気 >= 10 || ABL:A:レズ中毒 >= 1) && TALENT:MASTER:オトコ == 0 && TALENT:A:オトコ == 0
	B:10 = 1
;[11] 性行為
SIF A != MASTER && TALENT:MASTER:童貞 == 0 && TALENT:A:親愛 && TALENT:A:処女 == 0 && (ABL:A:親密 + ABL:A:欲望 + ABL:A:Ｖ感覚 + ABL:A:奉仕精神 >= 55) && (TALENT:MASTER:半人半妖 || TALENT:MASTER:ふたなり || TALENT:MASTER:オトコ)
	B:11 = 1
;[12] 宝探し
B:12 = 1

@ALL_ACT_ABLE
;全キャラ指定中の待機中行動をできるか判定し、できなくなっている場合は何もさせないに変更
FOR A, 0, CHARANUM
	CALL SHOP_ACT_ABLE
	SIF B:(CFLAG:A:13) == 0
		CFLAG:A:13 = 0
NEXT

@ACTION_LIST
DRAWLINE
REPEAT CHARANUM
	;主人公は特別表示
	SIF COUNT == MASTER
		PRINTFORM                                     [ 0]デフォルト指示    
	;預かり中のキャラは非表示だと選べない
	SIF CFLAG:COUNT:16 == 1 && U == 0
		CONTINUE
	;別れたキャラも選べない
	SIF CFLAG:COUNT:21
		CONTINUE
	;臨月、育児中のキャラも選べない
	SIF FLAG:62 & 1 && (MARK:COUNT:90 == 1)
		CONTINUE
	IF COUNT != MASTER
		PRINT 体力
		A = BASE:COUNT:体力
		SIF BASE:COUNT:体力 < 0
			A = 0
		CALL PRINT_BAR_0, A, MAXBASE:COUNT:体力, 16
		PRINTFORM ({BASE:COUNT:体力,4}/{MAXBASE:COUNT:体力,4})
		IF COUNT == TARGET
			PRINT  ☆
		ELSEIF COUNT == ASSI
			PRINT  ★
		ELSE
			PRINT  　
		ENDIF
		PRINTFORM [{COUNT,2}]%NAME:COUNT,16,LEFT%　
		;CALL ARRANGE_CHARANAME
	ENDIF

	CALL SHOP_ACT_NAME
	PRINTFORML （%TSTR:(CFLAG:COUNT:13)%）
REND
PRINTL 　　　　　　☆：パートナー　★：助手　を表しています

;-------------------------------------------------
;待機中行動
;-------------------------------------------------
@TURNEND_COM
VARSET LOCAL, 0
;SHOP.ERBのREPEAT CHARANUM中
;待機中指示コマンド実行
IF COUNT != MASTER && CFLAG:COUNT:17 == 0
	IF CFLAG:COUNT:13 == 0
		;何もしない
		;実行したコマンドを保存
		Y:COUNT = 0
	ELSEIF CFLAG:COUNT:13 == 1
		;修行
		TIMES A , 0.70
		TIMES B , 0.70
		TIMES C , 0.75
		LOCAL = RAND:3 + 1
		EXP:COUNT:戦闘経験 += LOCAL
		CALL TURNEND_COM_PRINT, "EXP", 95, LOCAL
		Y:COUNT = 1
	ELSEIF CFLAG:COUNT:13 == 2
		;リフレッシュ
		A *= 2
		B *= 2
		C *= 3
		LOCAL = RAND:3 + 1
		CFLAG:COUNT:2 += LOCAL
		CALL TURNEND_COM_PRINT, "CFLAG", 2, LOCAL
		;実行したコマンドを保存
		Y:COUNT = 2
	ELSEIF CFLAG:COUNT:13 == 3
		;自給自足
		TIMES A , 0.10
		TIMES B , 0.10
		TIMES C , 0.10
		LOCAL = (ABL:COUNT:技巧 * ABL:COUNT:奉仕精神 + 5) * (RAND:10 + 5) / 10
		JUEL:COUNT:生活 += LOCAL
		CALL TURNEND_COM_PRINT, "JUEL", 16, LOCAL
		;実行したコマンドを保存
		Y:COUNT = 3
	ELSEIF CFLAG:COUNT:13 == 4 && H == 1
		;連携を確認する
		TIMES A , 0.80
		TIMES B , 0.80
		C *= 2
		LOCAL = RAND:7 + 5
		EXP:COUNT:戦闘経験 += LOCAL
		CALL TURNEND_COM_PRINT, "EXP", 95, LOCAL
		LOCAL = RAND:21 + 20
		CFLAG:COUNT:2 += LOCAL
		CALL TURNEND_COM_PRINT, "CFLAG", 2, LOCAL
		BASE:MASTER:耐力 -= MAXBASE:MASTER:耐力 / 100
		;実行したコマンドを保存
		Y:COUNT = 4
	ELSEIF CFLAG:COUNT:13 == 5
		;家事
		TIMES A , 0.80
		TIMES B , 0.80
		C /= 2
		LOCAL = SQRT(ABL:COUNT:奉仕精神 + ABL:COUNT:料理技能) + RAND:6 / 2 + 1
		EXP:COUNT:家事経験 += LOCAL
		CALL TURNEND_COM_PRINT, "EXP", 90, LOCAL
		LOCAL = (ABL:COUNT:奉仕精神 * SQRT(ABL:COUNT:奉仕精神) * (ABL:COUNT:料理技能 * 2 + ABL:COUNT:技巧) / 3 + 5) * (RAND:10 + 5) / 10
		JUEL:COUNT:生活 += LOCAL
		CALL TURNEND_COM_PRINT, "JUEL", 16, LOCAL
		LOCAL = RAND:6 + 5
		CFLAG:COUNT:2 += LOCAL
		CALL TURNEND_COM_PRINT, "CFLAG", 2, LOCAL
		;実行したコマンドを保存
		Y:COUNT = 5
	ELSEIF CFLAG:COUNT:13 == 6
		;モンスター退治
		IF BASE:COUNT:耐力 >= 500
			LOCAL = RAND:5 + 5
			EXP:COUNT:戦闘経験 += LOCAL
			CALL TURNEND_COM_PRINT, "EXP", 95, LOCAL
			LOCAL = RAND:10 + 6
			CFLAG:COUNT:2 -= LOCAL
			CALL TURNEND_COM_PRINT, "CFLAG", 2, -LOCAL
			BASE:COUNT:耐力 -= RAND:11 * 30 + 200
			T = COUNT
			Z = COUNT
			CALL GET_BATTLE_POWER
			COUNT = Z

			A = 0
			B = 0

			D = P / 10 * (RAND:10 + 5)

			;稼ぎが高くなり過ぎないように補正をかける
			;7500を超えた分は25%OFF
			SIF D >= 7500
				D = 7500 + (D - 7500) / 4 * 3
			;補正後、15000を超えていたらその分を更に50%OFF
			SIF D >= 15000
				D = 15000 + (D - 15000) / 2
			;補正後、30000を超えていたらその分を更に75%OFF
			SIF D >= 30000
				D = 30000 + (D - 30000) / 4
			;補正後、50000を超えていたらその分を更に90%OFF
			SIF D >= 50000
				D = 50000 + (D - 50000) / 10
			;補正後、100000を超えていたらその分を更に90%OFF
			SIF D >= 100000
				D = 100000 + (D - 100000) / 10

			;実行したコマンドを保存
			Y:COUNT = 6
		ELSE
			PRINTFORML %CALLNAME:COUNT%はかなり疲労しているようだ…
		ENDIF
	ELSEIF CFLAG:COUNT:13 == 7 && H == 1
		;イチャイチャする
		C *= 3
		LOCAL = (ABL:COUNT:親密 + 5) * (ABL:COUNT:奉仕精神 + 3) * (RAND:10 + 10) / 10
		JUEL:COUNT:好意 += LOCAL
		CALL TURNEND_COM_PRINT, "JUEL", 15, LOCAL
		LOCAL = RAND:51 + 50
		CFLAG:COUNT:2 += LOCAL
		CALL TURNEND_COM_PRINT, "CFLAG", 2, LOCAL
		BASE:MASTER:耐力 -= MAXBASE:MASTER:耐力 / 200
		;実行したコマンドを保存
		Y:COUNT = 7
	ELSEIF CFLAG:COUNT:13 == 8 && H == 1
		;朝フェラ 朝じゃなくてもやってくれます。昼寝の寝起き？
		TIMES A , 0.90
		TIMES B , 0.90
		LOCAL = RAND:11 + 10
		CFLAG:COUNT:2 += LOCAL
		CALL TURNEND_COM_PRINT, "CFLAG", 2, LOCAL
		BASE:MASTER:耐力 -= MAXBASE:MASTER:耐力 / 200

		G = 0
		IF ABL:COUNT:精液中毒 == 1
			G = 1
		ELSEIF ABL:COUNT:精液中毒 == 2
			G = 2
		ELSEIF ABL:COUNT:精液中毒 == 3
			G = 3
		ELSEIF ABL:COUNT:精液中毒 == 4
			G = 4
		ELSEIF ABL:COUNT:精液中毒 >= 5
			G = 3 + ABL:COUNT:精液中毒 / 2
		ENDIF

		;汚臭鈍感
		SIF TALENT:COUNT:汚臭鈍感
			G += 1
		;汚臭敏感
		SIF TALENT:COUNT:汚臭敏感
			G -= 1
		;献身的
		SIF TALENT:COUNT:献身的
			G += 1
		;恋慕
		SIF TALENT:COUNT:淫乱
			G += 1
		;淫乱
		SIF TALENT:COUNT:恋慕
			G += 1
		;親愛
		SIF TALENT:COUNT:親愛
			G += 3
		;恋人
		SIF TALENT:COUNT:恋人
			G += 1

		;言われてやってるので普段より多い
		G += 1

		SIF G <= 0
			G = 1

		EXP:COUNT:フェラ経験 += G
		CALL TURNEND_COM_PRINT, "EXP", 22, G
		EXP:COUNT:精液経験 += G*2
		CALL TURNEND_COM_PRINT, "EXP", 20, G*2
		JUEL:COUNT:恭順 += G*100
		CALL TURNEND_COM_PRINT, "JUEL", 4, G*100
		JUEL:COUNT:屈服 += G*30
		CALL TURNEND_COM_PRINT, "JUEL", 6, G*30
		JUEL:COUNT:習得 += G*40
		CALL TURNEND_COM_PRINT, "JUEL", 7, G*40
		JUEL:COUNT:好意 += G*100
		CALL TURNEND_COM_PRINT, "JUEL", 15, G*100
		
		;実行したコマンドを保存
		Y:COUNT = 8
	ELSEIF CFLAG:COUNT:13 == 9
		;自慰
		TIMES A , 0.80
		TIMES B , 0.80
		TIMES C , 1.30
		LOCAL = RAND:11 + 10
		CFLAG:COUNT:2 += LOCAL
		CALL TURNEND_COM_PRINT, "CFLAG", 2, LOCAL

		G = 0

		IF ABL:COUNT:自慰中毒 == 1
			G = 1
		ELSEIF ABL:COUNT:自慰中毒 == 2
			G = 2
		ELSEIF ABL:COUNT:自慰中毒 == 3
			G = 4
		ELSEIF ABL:COUNT:自慰中毒 == 4
			G = 6
		ELSEIF ABL:COUNT:自慰中毒 >= 5
			G = (ABL:COUNT:自慰中毒 * 2) - 1
		ENDIF

		;言われてやってるので普段より多い
		G += 1

		IF ABL:COUNT:欲望 == 4 && ABL:COUNT:Ｃ感覚 >= 4
			G += 1
		ELSEIF ABL:COUNT:欲望 >= 5 && ABL:COUNT:Ｃ感覚 >= 4
			G += ABL:COUNT:欲望
		ENDIF
		
		;自慰しやすい
		IF TALENT:COUNT:自慰しやすい
			G *= 2
			;欲望ＬＶ３でもGに+1する
			SIF ABL:COUNT:欲望 >= 3 && ABL:COUNT:Ｃ感覚 >= 3
				G += 1
		ENDIF
		
		;快感に素直、快感の否定、淫乱化
		IF TALENT:COUNT:快感に素直
			G *= 3/2
		ELSEIF TALENT:COUNT:快感の否定
			G /= 2
		ENDIF
		IF TALENT:COUNT:淫乱
			G *= 2
		ENDIF
		
		SIF G <= 0
			G = 1

		EXP:COUNT:自慰経験 += G
		CALL TURNEND_COM_PRINT, "EXP", 10, G
		EXP:COUNT:絶頂経験 += G / 5
		CALL TURNEND_COM_PRINT, "EXP", 2, G/5
		JUEL:COUNT:快Ｃ += G*500
		CALL TURNEND_COM_PRINT, "JUEL", 0, G*500
		JUEL:COUNT:恭順 += G*100
		CALL TURNEND_COM_PRINT, "JUEL", 4, G*100
		JUEL:COUNT:欲情 += G*250
		CALL TURNEND_COM_PRINT, "JUEL", 5, G*250
		
		IF ABL:COUNT:親密+ABL:COUNT:露出癖+ABL:COUNT:マゾっ気 >= 10
			JUEL:COUNT:恥情 += G*200
			CALL TURNEND_COM_PRINT, "JUEL", 8, G*200
		ENDIF
		
		;実行したコマンドを保存
		Y:COUNT = 9
	ELSEIF CFLAG:COUNT:13 == 10
		;レズ行為
		TIMES A , 0.80
		TIMES B , 0.80
		TIMES C , 1.30

		N = 0

		IF ABL:COUNT:レズ中毒 == 1
			N += 1
		ELSEIF ABL:COUNT:レズ中毒 == 2
			N += 2
		ELSEIF ABL:COUNT:レズ中毒 == 3
			N += 3
		ELSEIF ABL:COUNT:レズ中毒 == 4
			N += 5
		ELSEIF ABL:COUNT:レズ中毒 >= 5
			N += 2 + ABL:COUNT:レズ中毒
		ENDIF
		G = RAND:100 + 1
		IF G <= 30
			N += 1
		ELSEIF G <= 65
			N += 2
		ELSEIF G <= 85
			N += 5
		ELSEIF G <= 95
			N += 8
		ELSE
			N += 15
		ENDIF
		
		R = RAND:76 + 75
		N *= R
		N /= 100

		EXP:COUNT:レズ経験 += N*20
		CALL TURNEND_COM_PRINT, "EXP", 40, N*20
		EXP:COUNT:絶頂経験 += N*100*ABL:COUNT:親密/500
		CALL TURNEND_COM_PRINT, "EXP", 2, N*100*ABL:COUNT:親密/500
		
		JUEL:COUNT:屈服 += N*75*(ABL:COUNT:技巧+ABL:COUNT:奉仕精神)
		CALL TURNEND_COM_PRINT, "JUEL", 6, N*75*(ABL:COUNT:技巧+ABL:COUNT:奉仕精神)
		JUEL:COUNT:習得 += N*75*(ABL:COUNT:技巧+ABL:COUNT:奉仕精神)
		CALL TURNEND_COM_PRINT, "JUEL", 7, N*75*(ABL:COUNT:技巧+ABL:COUNT:奉仕精神)
		IF TALENT:COUNT:ふたなり
			EXP:COUNT:射精経験 += N
			CALL TURNEND_COM_PRINT, "EXP", 3, N
			JUEL:COUNT:恥情 += N*100
			CALL TURNEND_COM_PRINT, "JUEL", 8, N*100
		ENDIF
		
		;実行したコマンドを保存
		Y:COUNT = 10
	ELSEIF CFLAG:COUNT:13 == 11 && H == 1
		;性行為
		TIMES A , 0.70
		TIMES B , 0.70
		TIMES C , 1.50
		LOCAL = RAND:41 + 30
		CFLAG:COUNT:2 += LOCAL
		BASE:MASTER:耐力 -= MAXBASE:MASTER:耐力 / 50

		S = 1
		;V感覚
		IF ABL:COUNT:Ｖ感覚 == 1
			S += 1
		ELSEIF ABL:COUNT:Ｖ感覚 == 2
			S += 2
		ELSEIF ABL:COUNT:Ｖ感覚 == 3
			S += 2
		ELSEIF ABL:COUNT:Ｖ感覚 == 4
			S += 3
		ELSEIF ABL:COUNT:Ｖ感覚 >= 5
			S += 1 + (ABL:COUNT:Ｖ感覚 * 3/4)
		ENDIF
		
		;欲望と奉仕精神によるボーナス
		IF ABL:COUNT:欲望 == 4 && ABL:COUNT:奉仕精神 >= 4
			S += 1
		ELSEIF ABL:COUNT:欲望 >= 5 && ABL:COUNT:奉仕精神 >= 4
			S += ABL:COUNT:欲望
		ENDIF
		
		;V感覚が充分にあれば、欲望ＬＶ３でもSに+1する
		IF ABL:COUNT:欲望 >= 3 && ABL:COUNT:Ｖ感覚 >= 3
			S += 1
		ENDIF
		
		;快感に素直、快感の否定、淫乱化
		IF TALENT:COUNT:快感に素直
			S *= 3/2
		ELSEIF TALENT:COUNT:快感の否定
			S /= 2
		ENDIF
		
		IF TALENT:COUNT:淫乱
			S *= 2
		ENDIF
	
		SIF S <= 0
			S = 1
		EXP:COUNT:Ｖ経験 += S
		CALL TURNEND_COM_PRINT, "EXP", 0, S
		EXP:COUNT:絶頂経験 += S
		CALL TURNEND_COM_PRINT, "EXP", 2, S
		CFLAG:MASTER:2 += S
		CFLAG:COUNT:2 += S*5
		CALL TURNEND_COM_PRINT, "CFLAG", 2, LOCAL+S*5
		IF TALENT:MASTER:ふたなり || TALENT:MASTER:オトコ
			EXP:COUNT:精液経験 += S * 10
			CALL TURNEND_COM_PRINT, "EXP", 20, S*10
		ENDIF
		JUEL:COUNT:快Ｖ += S*800
		CALL TURNEND_COM_PRINT, "JUEL", 1, S*800
		JUEL:COUNT:恭順 += S*500
		CALL TURNEND_COM_PRINT, "JUEL", 4, S*500
		JUEL:COUNT:欲情 += S*500
		CALL TURNEND_COM_PRINT, "JUEL", 5, S*500
		JUEL:COUNT:好意 += S*500
		CALL TURNEND_COM_PRINT, "JUEL", 15, S*500
		
		;実行したコマンドを保存
		Y:COUNT = 11
	ELSEIF CFLAG:COUNT:13 == 12
		;宝探し
		TIMES A , 0.70
		TIMES B , 0.70
		TIMES C , 1.50
		CALL TREASURE
		
		;実行したコマンドを保存
		Y:COUNT = 12
	ENDIF
	;リング資金稼ぎ
	CALL TURNEND_EARN_MONEY_TM
ENDIF

@TURNEND_COM_PRINT, ARGS, ARG, ARG:1
;待機行動中に入手した経験や宝珠や好感度を表示させる関数
;当該CONFIGがONのときのみ。
SIF (FLAG:23 & 1p25) == 0
	RETURN 0
SIF ARG:1 == 0
	RETURN 0
SELECTCASE ARGS
	CASE "CFLAG"
		PRINTFORML %CALLNAME:COUNT%は%CFLAGNAME:ARG%を\@(ARG:1 > 0) ? {ARG:1}だけ得ることができた。 # {-ARG:1}だけ減らした。\@
	CASE "EXP"
		PRINTFORML %CALLNAME:COUNT%は%EXPNAME:ARG%を{ARG:1}だけ得ることができた。
	CASE "JUEL"
		PRINTFORML %CALLNAME:COUNT%は%PALAMNAME:ARG%の宝珠を{ARG:1}だけ得ることができた。
ENDSELECT
RETURN 1
