@LUNCH_EXERCISE
;SELF.ERBを基に作成。

;TFLAG:15を借りますよ
TFLAG:15 = 0

;ターゲットに挿入
SIF (TALENT:MASTER:ふたなり || TALENT:MASTER:オトコ) && TALENT:処女 == 0 && EXP:Ｖ経験 > 0
	TFLAG:15 |= 1
;マスターに挿入
SIF (TALENT:ふたなり || TALENT:オトコ) && TALENT:MASTER:処女 == 0 && EXP:MASTER:Ｖ経験 > 0
	TFLAG:15 |= 2
;両方出来るならランダムで決定
SIF TFLAG:15 == 3
	TFLAG:15 -= 1 + RAND:2

S = 0

;ターゲットに挿入する場合はV感覚
IF TFLAG:15 & 1
	A = 4
;ターゲットが挿入、もしくは非挿入の場合はC感覚
ELSE
	A = 3
ENDIF

;C/V感覚
IF ABL:A == 1
	S = 1
ELSEIF ABL:A == 2
	S = 2
ELSEIF ABL:A == 3
	S = 2
ELSEIF ABL:A == 4
	S = 3
ELSEIF ABL:A >= 5
	S = 1 + (ABL:A / 2)
ENDIF

;欲望と奉仕精神によるボーナス
IF ABL:欲望 == 4 && ABL:奉仕精神 >= 4
	S += 2
ELSEIF ABL:欲望 >= 5 && ABL:奉仕精神 >= 4
	S += 3
ENDIF

;欲望ＬＶ３でもSに+1する
IF ABL:欲望 == 3 && ABL:Ｖ感覚 >= 3 && ABL:奉仕精神 >= 5
	S += 1
ENDIF

;快感に素直、快感の否定、淫乱化
IF TALENT:快感に素直 && S
	S += 1
ELSEIF TALENT:快感の否定 && S
	S -= 2
ENDIF

IF TALENT:淫乱 && S
	S += 1
ENDIF

;昼の場合は夜までｷｬｯｷｬｳﾌﾌ
IF TIME == 0 && S > 3
	TIME += 1
	S += 2
ENDIF

;反発感情があると実行しにくくなる。また、恋慕なし反発3では発生せず
IF MARK:反発感情 && S
	IF MARK:反発感情 >= 3 && TALENT:恋慕 == 0
		S = 0
	ELSE
		S /= 1+MARK:反発感情
		S -= MARK:反発感情
	ENDIF
ENDIF

IF S > 0
	DRAWLINE
	PRINTFORML 料理を食べた%CALLNAME:MASTER%と%CALLNAME:TARGET%はなんとも言えない感覚に襲われた
	PRINTL 
	PRINTL どうする？
	PRINTL [0] - 今日はやめておく
	PRINTFORML [1] - %CALLNAME:TARGET%を押し倒す
	DRAWLINE
	$SELECT_POINT_EXERCISE
	INPUT
	IF RESULT == 0
		CALL LUNCH_EXERCISE_0
	ELSEIF RESULT == 1
		CALL LUNCH_EXERCISE_1
	ELSE
		GOTO SELECT_POINT_EXERCISE
	ENDIF
ENDIF
TFLAG:14 = 0
TFLAG:15 = 0
RETURN 1

;---------------------------------------------------------
;今日はやめておく場合
;---------------------------------------------------------
@LUNCH_EXERCISE_0
PRINTFORML %CALLNAME:MASTER%は押し倒したいという衝動を抑え込み、%CALLNAME:TARGET%に別れを告げた……
PRINTL 
WAIT
;押し倒されチェック
A = 0
;反抗的
SIF TALENT:反抗的
	A += 1
;生意気
SIF TALENT:生意気
	A += 1
;好奇心
SIF TALENT:好奇心
	A += 1
;楽観的
SIF TALENT:楽観的
	A += 1
;目立ちたがり
SIF TALENT:目立ちたがり
	A += 1
;貞操無頓着
SIF TALENT:貞操無頓着
	A += 1
;解放
SIF TALENT:抑圧
	A += 1
;恥薄い
SIF TALENT:恥薄い
	A += 1
;快感に素直
SIF TALENT:快感に素直
	A += 1
;淫乱
SIF TALENT:淫乱
	A += 1
;サド
SIF TALENT:サド
	A += 1
;小悪魔
SIF TALENT:小悪魔
	A += 1

;臆病
SIF TALENT:臆病
	A -= 1
;気丈
SIF TALENT:気丈
	A -= 1
;素直
SIF TALENT:素直
	A -= 1
;自制心
SIF TALENT:自制心
	A -= 1
;無関心
SIF TALENT:無関心
	A -= 1
;保守的
SIF TALENT:保守的
	A -= 1
;悲観的
SIF TALENT:悲観的
	A -= 1
;一線越えない
SIF TALENT:一線越えない
	A -= 1
;貞操観念
SIF TALENT:貞操観念
	A -= 1
;抑圧
SIF TALENT:抑圧
	A -= 1
;恥じらい
SIF TALENT:恥じらい
	A -= 1
;快感の否定
SIF TALENT:快感の否定
	A -= 1

;中毒系：男主人なら精液、女主人ならレズを見る
IF TALENT:MASTER:オトコ
	SIF ABL:精液中毒 >= 5
		A += 1
ELSE
	SIF ABL:レズ中毒 >= 5
		A += 1
ENDIF

;素質に応じた確率で押し倒される
IF RAND:3 < A
	SIF TFLAG:14 == 2
		S += 2
	;カットフラグを退避
	LOCAL:100 = CFLAG:99
	;口上開始前の地の文をカットするか
	TFLAG:13 = 3
	CALL MAEKOJO_LUNCH_EXERCISE
	;地の文章カット
	IF FLAG:6 && (CFLAG:99 & 1) == 0
		PRINTFORMW そして、部屋を後にしようとしたが、肩を強く掴まれ歩みが止まる
		PRINTFORMW %CALLNAME:MASTER%が振り返ると、目が据わった%CALLNAME:TARGET%に睨まれていた
		PRINTFORMW ……どうやらこれは覚悟を決めねばならないようだ
		PRINTL  
		PRINTFORM %CALLNAME:MASTER%は%CALLNAME:TARGET%にベッドへ引きずり込まれ、そこで
		PRINTV S
		PRINTL 回まぐわった…
	ENDIF
	;素敵な食後の運動
	CALL KOJO_LUNCH_EXERCISE
	;経験値の取得
	CALL GETEXP_L_EXERCISE
	
	;ターゲットが挿入されるときはV
	IF TFLAG:15 & 1
		PRINTFORML %PALAMNAME:1%の珠+{S*200}
		JUEL:快Ｖ += S*200
	;ターゲットが挿入する時はC
	ELSEIF TFLAG:15 & 2
		PRINTFORML %PALAMNAME:0%の珠+{S*200}
		JUEL:快Ｃ += S*200
	;非挿入の時はBC
	ELSE
		PRINTFORML %PALAMNAME:0%の珠+{S*150}
		JUEL:快Ｃ += S*150
		PRINTFORML %PALAMNAME:14%の珠+{S*100}
		JUEL:快Ｂ += S*100
	ENDIF
	PRINTFORML %PALAMNAME:4%の珠+{S*100}
	PRINTFORML %PALAMNAME:5%の珠+{S*300}
	T = TARGET
	PLAYER = MASTER
	CALL GET_INITIATIVE_RATE
	;強気だと優越の珠
	IF R > 0
		PRINTFORML %PALAMNAME:17%の珠+{S*100}
		JUEL:優越 += S*200
	;そうでなければ恥情の珠
	ELSE
		PRINTFORML %PALAMNAME:8%の珠+{S*100}
		JUEL:恥情 += S*200
	ENDIF
	JUEL:恭順 += S*100
	JUEL:欲情 += S*300
	WAIT
	IF ABL:親密+ABL:Ｖ感覚+ABL:奉仕精神 >= 13 && FLAG:6 && (CFLAG:99 & 1) == 0
		PRINTFORMW やがて%CALLNAME:MASTER%と%CALLNAME:TARGET%はどちらからとも無く体を離し、互いに微笑みあった
		PRINTFORMW いつまでも留まるわけにもいかず、%CALLNAME:MASTER%は名残惜しむかのように部屋を後にする…
	ENDIF
	;カットフラグを戻す
	CFLAG:99 = LOCAL:100
ELSE
	;欲望
	SIF ABL:欲望
		S += ABL:欲望
	;自慰中毒
	SIF ABL:自慰中毒
		S += ABL:自慰中毒
	;淫乱
	SIF TALENT:淫乱
		S += 1
	;自制心
	SIF TALENT:自制心
		S -= 2
	;快楽の否定
	SIF TALENT:快感の否定
		S -= 2
	;反発感情
	SIF MARK:反発感情
		S -= MARK:反発感情
	S /= 5
	S -= 3
	SIF TFLAG:14 == 2
		S += 5
	IF S > 0
		;カットフラグを退避
		LOCAL:100 = CFLAG:99
		;口上開始前の地の文をカットするか
		TFLAG:13 = 2
		CALL MAEKOJO_LUNCH_EXERCISE
		;地の文章をカット
		IF FLAG:6 && (CFLAG:99 & 1) == 0
			PRINTFORML 部屋に一人残された%CALLNAME:TARGET%は%CALLNAME:MASTER%への思いを抑えきれず
			PRINTV S
			PRINTL 回自慰をしたようだ。
		ENDIF
		;素敵な食後の運動
		CALL KOJO_LUNCH_EXERCISE
		PRINTFORML %EXPNAME:10%+{S}
		PRINTFORML %PALAMNAME:0%の珠+{S*100}
		PRINTFORML %PALAMNAME:4%の珠+{S*50}
		PRINTFORMW %PALAMNAME:5%の珠+{S*100}
		EXP:自慰経験 += S
		JUEL:快Ｃ += S*100
		JUEL:恭順 += S*50
		JUEL:欲情 += S*100
		
		;カットフラグを戻す
		CFLAG:99 = LOCAL:100
	ENDIF
ENDIF

;---------------------------------------------------------
;押し倒す場合
;---------------------------------------------------------
@LUNCH_EXERCISE_1
SIF TFLAG:14 == 2
	S += 2
;カットフラグを退避
LOCAL:100 = CFLAG:99
;口上開始前の地の文をカットするか
;ターゲットが挿入されるなら、元々の食後の運動を呼び出す
IF TFLAG:15 & 1
	TFLAG:13 = 4
;そうでないなら、独自仕様なので、別の番号を呼び出す。
ELSE
	TFLAG:13 = 1
ENDIF
CALL MAEKOJO_LUNCH_EXERCISE
;地の文章をカット
IF FLAG:6 && (CFLAG:99 & 1) == 0
	PRINTFORM 二人は熱く火照った体でベッドに潜り込み、そこで
	PRINTV S
	PRINTL 回まぐわった…
ENDIF
;素敵な食後の運動
CALL KOJO_LUNCH_EXERCISE
;経験値の取得
CALL GETEXP_L_EXERCISE

;ターゲットが挿入されるときはV
IF TFLAG:15 & 1
	PRINTFORML %PALAMNAME:1%の珠+{S*200}
	JUEL:快Ｖ += S*200
;ターゲットが挿入する時はC
ELSEIF TFLAG:15 & 2
	PRINTFORML %PALAMNAME:0%の珠+{S*200}
	JUEL:快Ｃ += S*200
;非挿入の時はBC
ELSE
	PRINTFORML %PALAMNAME:0%の珠+{S*150}
	JUEL:快Ｃ += S*150
	PRINTFORML %PALAMNAME:14%の珠+{S*100}
	JUEL:快Ｂ += S*100
ENDIF
PRINTFORML %PALAMNAME:4%の珠+{S*100}
PRINTFORML %PALAMNAME:5%の珠+{S*300}
IF TALENT:オトコ || TALENT:ふたなり
	T = TARGET
	PLAYER = MASTER
	CALL GET_INITIATIVE_RATE
	;ターゲットが挿入し、強気だと優越の珠
	IF TFLAG:15 & 2 && R > 0
		PRINTFORML %PALAMNAME:17%の珠+{S*100}
		JUEL:優越 += S*100
	;そうでなければ恥情の珠
	ELSE
		PRINTFORML %PALAMNAME:8%の珠+{S*100}
		JUEL:恥情 += S*100
	ENDIF
ENDIF
JUEL:恭順 += S*100
JUEL:欲情 += S*300
WAIT
IF ABL:親密+ABL:Ｖ感覚+ABL:奉仕精神 >= 13 && FLAG:6 && (CFLAG:99 & 1) == 0
	PRINTFORMW やがて%CALLNAME:MASTER%と%CALLNAME:TARGET%はどちらからとも無く体を離し、互いに微笑みあった
	PRINTFORMW いつまでも留まるわけにもいかず、%CALLNAME:MASTER%は名残惜しむかのように部屋を後にする…
ENDIF
;カットフラグを戻す
CFLAG:99 = LOCAL:100

;---------------------------------------------------------
;経験値の取得
;---------------------------------------------------------
@GETEXP_L_EXERCISE
;ターゲットが挿入されるならV経験が付く
IF TFLAG:15 & 1
	PRINTFORML %EXPNAME:0%+{S}
	EXP:Ｖ経験 += S
ENDIF
;主人がオトコかふたなり
IF TALENT:MASTER:ふたなり || TALENT:MASTER:オトコ
	;精液経験
	PRINTFORML %EXPNAME:20%+{S*5}
	EXP:精液経験 += S * 5
	;射精経験
	PRINTFORML %EXPNAME:3%+{S}(%CALLNAME:MASTER%)
	EXP:MASTER:射精経験 += S
ENDIF
;パートナーがオトコかふたなり
IF TALENT:オトコ || TALENT:ふたなり
	;精液経験
	PRINTFORML %EXPNAME:20%+{S*5}(%CALLNAME:MASTER%)
	EXP:MASTER:精液経験 += S * 5
	;射精経験
	PRINTFORML %EXPNAME:3%+{S}
	EXP:射精経験 += S
ENDIF

PRINTFORML %EXPNAME:2%+{S}
EXP:絶頂経験 += S

;レズ経験
IF TALENT:MASTER:オトコ == 0 && TALENT:オトコ == 0
	EXP:レズ経験 += S * 20
	EXP:MASTER:レズ経験 += S * 20
	PRINTFORML %EXPNAME:40%+{S*20}
;BL経験
ELSEIF TALENT:MASTER:オトコ && TALENT:オトコ
	EXP:ＢＬ経験 += S * 20
	EXP:MASTER:ＢＬ経験 += S * 20
	PRINTFORML %EXPNAME:41%+{S*20}
ENDIF

;恋人を持っていた場合奉仕快楽経験が入る
IF TFLAG:14 == 2
	PRINTFORML %EXPNAME:21%+2
	EXP:奉仕快楽経験 += 2
ENDIF

RETURN 0
