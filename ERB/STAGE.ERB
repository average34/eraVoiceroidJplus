;---------------------------------------------------------
;ステージ攻略処理

;ステージ攻略選択
	;@STAGE_CAPTURE
;戦闘用の情報を表示
	;@SHOW_BATTLE_STATUS
	;@GET_BATTLE_POWER_MASTER(あなたの戦闘能力計算)
	;@SHOW_ENEMY_INFO
;どの戦闘か（雑魚敵、ボス戦闘、仲間救出等）
	;@BATTLE_NORMAL
	;@BATTLE_BOSS
		;@INPUT_ADD_NEW_CHARA
	;@BATTLE_ANGELIA
	;@BATTLE_LOST_CHARA
;戦闘方法選択
	;@BATTLE_COMMON_CHOICE(ARG)
;どの戦闘方法か（オートバトル、コマンドバトル等）
	;@AUTO_BATTLE
	;@COMMAND_BATTLE
		;@ENEMY_ATTACK
;戦闘に関する処理（ダメージ補正、回復量補正等）
	;@DAMAGE_CAP
	;@HEAL_CAP
;従者判定
	;@ATTENDANT
	;@ATTENDANT_2
	;@ADD_ATTENDANT
;戦闘終了時の共通処理
	;@BATTLE_COMMON_END
	;@BATTLE_COMMON_EM
;特殊攻撃等の処理はSPECIAL_ATACK.ERBへ移動
;---------------------------------------------------------
;使用している一文字変数
;K:1→加速時に戦闘したい回数記録、K:2→最大耐力/4を保存し撤退の判断に使用
;K:3→PH時に10の倍数のとき出現する固定キャラ戦かの判断に使用、K:4→PH時のボス一時記録に使用
;M→報酬
;P→こちらの戦闘力
;R:1～→ライオットドールの耐力
;S:1～→ライオットドールの行動制御
;T:1→ライオットドールの戦闘力
;V:4→TARGET脱衣、V:5→MASTER脱衣、V:6→庇う
;W:1～→敵キャラのNO代入
;X(X:0)→敵の数
;X:1～→敵の最大耐力
;Y:1～→敵の耐力
;Z:1～→敵の戦闘力
;---------------------------------------------------------
@STAGE_CAPTURE
;ステージ攻略処理
TFLAG:46 = 0
;加速フラグ
K:1 = 0
K:2 = 0

VARSET R, 0

$START_POINT
K:3 = 0

;自動撤退判定
IF TARGET >= 0 && FLAG:23 & 4 && BASE:耐力 <= MAXBASE:耐力 / 4
	PRINTFORML  
	PRINTFORMW %CALLNAME:TARGET%が霧に抵抗できなくなってきたので、%CALLNAME:MASTER%達は早めに撤退しました
	TFLAG:600 = 35
	CALL KOJO_JUN
	RETURN 1
ELSEIF FLAG:23 & 4 && BASE:MASTER:耐力 <= MAXBASE:MASTER:耐力 / 4
	PRINTFORML  
	PRINTFORM %CALLNAME:MASTER%はかなりの疲労を感じるようになってきたので、
	SIF TARGET >= 0
		PRINTFORM %CALLNAME:TARGET%と一緒に
	PRINTW 早めに撤退しました
	TFLAG:600 = 35
	CALL KOJO_JUN
	RETURN 1
ENDIF

;所持金の上限設定
SIF MONEY >= 999999999999999999
	MONEY = 999999999999999999

;ドールフラグ
TFLAG:852 = FLAG:44

PRINTL  
PRINTFORML 現在Stage{FLAG:24}攻略中  所持金：${MONEY}

CALL SHOW_BATTLE_STATUS

;千里眼持ちがいれば常に中ボス・ボスの居場所が分かる
IF TALENT:MASTER:千里眼 || (TARGET >= 0 && TALENT:千里眼) || (ASSI >= 0 && TALENT:ASSI:千里眼)
	IF FLAG:27 <= 10
		FLAG:27 = 10
	ELSEIF FLAG:27 >= 11 && FLAG:27 <= 20
		FLAG:27 = 20
	ENDIF
ENDIF

;初期化
;V:4（TARGET脱衣）、V:5（MASTER脱衣）、V:6（庇う）、V:7（ドール庇う）
VARSET V, 0
VARSET W, 0
VARSET X, 0
VARSET Y, 0
VARSET Z, 0
VARSET LOCAL, 0
X = 1
N = 1
;脱衣制限解除有ならV:4（TARGET脱衣）、V:5（MASTER脱衣）を1にする
IF TALENT:MASTER:脱衣制限解除
	V:4 = 1
	V:5 = 1
ENDIF

IF K:1 == 0
	PRINTFORML どうしますか？
	PRINTFORML [0] 道中攻略
	IF FLAG:27 == 10
		PRINTFORML [1] 中ボス攻略
		LOCAL:1 = 1
	ELSEIF FLAG:27 >= 20
		PRINTFORML [1] ボス攻略
		LOCAL:1 = 1
	ENDIF
	PRINTFORML [2] 撤退
	;Extraかクリア済みならゆかり様に挑める
	IF FLAG:5 == 9 || FLAG:5 == 10 || (FLAG:28 & 1)
		PRINTFORML [3] ？？？  ({FLAG:45+1}回目)
		LOCAL:3 = 1
	ENDIF
	;霧に飲まれかけているキャラが居れば救出できる
	CALL GET_LOST_CHARA
	IF C != 0 && FLAG:5 == 10
		PRINTFORML [4] 霧に飲まれかけている仲間を助ける
		LOCAL:4 = 1
	ENDIF
	IF ITEM:852
		PRINTFORML [5] ドール設定
		LOCAL:5 = 1
	ENDIF
	PRINTFORML [8] 周囲の探索（指定回数だけ自動で戦闘を繰り返します）
	PRINTFORML [9] 周囲の探索（消耗するまで自動で戦闘を繰り返します）

	$INPUT_LOOP
	INPUT
	IF RESULT == 0
		;Phモードは10の倍数のとき固定キャラ出現
		IF FLAG:5 == 10 && (TFLAG:46 % 10) == 0 && TFLAG:46 > 0
			K:3 = 1
			K:4 = 0
			SIF FLAG:25 != 0
				K:4 = FLAG:25
			FLAG:25 = 0
			CALL BATTLE_BOSS
			FLAG:25 = K:4
			TFLAG:46 += 1
		ELSE
			CALL BATTLE_NORMAL
			TFLAG:46 += 1
		ENDIF
	ELSEIF RESULT == 1 && LOCAL:1
		CALL BATTLE_BOSS
		;強制終了の値が帰ってきてたら、強制的に攻略終了
		SIF RESULT == 2
			RETURN 1
		SIF (TARGET >= 0 && BASE:耐力 <= 0) || BASE:MASTER:耐力 <= 0
			RETURN 1
	ELSEIF RESULT == 2
		IF TARGET >= 0
			PRINTFORMW %CALLNAME:MASTER%と%CALLNAME:TARGET%は攻略を諦め、一旦引き返すことにしました
		ELSE
			PRINTFORMW %CALLNAME:MASTER%は攻略を諦め、一旦引き返すことにしました
		ENDIF
		TFLAG:600 = 35
		CALL KOJO_JUN
		RETURN 1
	ELSEIF RESULT == 3 && LOCAL:3
		CALL BATTLE_ANGELIA
	ELSEIF RESULT == 4 && LOCAL:4
		K:4 = 0
		SIF FLAG:25 != 0
			K:4 = FLAG:25
		CALL BATTLE_LOST_CHARA
		FLAG:25 = K:4
	ELSEIF RESULT == 5 && LOCAL:5
		;ドール設定
		CALL RIOT_DOLL
		GOTO END_POINT
	ELSEIF RESULT == 8
		PRINTFORML 戦闘したい回数を入力してください
		INPUT
		SIF RESULT <= 0
			RESULT = 1
		K:1 = RESULT + 1
		IF TARGET >= 0
			K:2 = MAXBASE:耐力 / 4
		ELSE
			K:2 = MAXBASE:MASTER:耐力 / 4
		ENDIF
		PRINTFORMW %CALLNAME:MASTER%達は周囲の探索を開始した……
	ELSEIF RESULT == 9
		K:1 = 1
		IF TARGET >= 0
			K:2 = MAXBASE:耐力 / 4
		ELSE
			K:2 = MAXBASE:MASTER:耐力 / 4
		ENDIF
		PRINTFORMW %CALLNAME:MASTER%達は周囲の探索を開始した……
		CALL BATTLE_NORMAL
		TFLAG:46 += 1
	ELSE
		PRINTFORML 正しい値を入力してください
		GOTO INPUT_LOOP
	ENDIF
	
	SIF (TARGET >= 0 && BASE:耐力 <= 0) || BASE:MASTER:耐力 <= 0
		RETURN 1
	
	GOTO KASOKU_END
ELSE
	;加速中
	IF (TARGET >= 0 && (BASE:耐力 <= K:2 || BASE:魔力 <= 0)) || (TARGET < 0 && BASE:MASTER:耐力 <= K:2)
		K:1 = 0
		PRINTFORMW 消耗が激しくなってきたので、周囲の探索を終了します
	ELSE
		;Phモードは10の倍数のとき固定キャラ出現
		IF FLAG:5 == 10 && (TFLAG:46 % 10) == 0 && TFLAG:46 > 0
			K:3 = 1
			K:4 = 0
			SIF FLAG:25 != 0
				K:4 = FLAG:25
			CALL GET_BOSS_NO
			FLAG:25 = B
			CALL BATTLE_BOSS
			FLAG:25 = K:4
			TFLAG:46 += 1
		ELSE
			CALL BATTLE_NORMAL
			TFLAG:46 += 1
		ENDIF
		SIF (TARGET >= 0 && BASE:耐力 <= 0) || BASE:MASTER:耐力 <= 0
			RETURN 1
		IF K:1 > 1
			K:1 -= 1
			SIF K:1 == 1
				K:1 = 0
		ENDIF
	ENDIF
	;加速分岐終了
ENDIF

$KASOKU_END

;ボス未出現の場合
IF FLAG:27 < 10 || (FLAG:27 > 10 && FLAG:27 < 20)
	;ボス出現判定を行う
	A = RAND:100
	IF FLAG:27 < 10 && ((FLAG:5 != 4 && FLAG:27 >= 3) || (FLAG:5 == 4 && FLAG:27 >= 5))
		;3回以上戦闘していれば、探索進度*15％で発見
		;S-Lunaは5回以上必要かつ発見率が*10％
		IF (FLAG:5 != 4 && A <= FLAG:27 * 15) || (FLAG:5 == 4 && A <= FLAG:27 * 10)
			FLAG:27 = 10
			PRINTFORMW 中ボスの居場所を突き止めました！
		ENDIF
	ELSEIF FLAG:27 < 20 && ((FLAG:5 != 4 && FLAG:27 >= 13) || (FLAG:5 == 4 && FLAG:27 >= 15))
		;中ボス後2回以上戦闘していれば、探索進度*11％で発見
		;S-Lunaは4回以上必要かつ発見率が*7％
		IF (FLAG:5 != 4 && A <= (FLAG:27 - 10) * 11) || (FLAG:5 == 4 && A <= (FLAG:27 - 10) * 7)
			FLAG:27 = 20
			PRINTFORMW ボスの居場所を突き止めました！
		ENDIF
	ENDIF
ENDIF

$END_POINT
GOTO START_POINT

RETURN 1

;---------------------------------------------------------
;戦闘用の情報を表示
;---------------------------------------------------------
@SHOW_BATTLE_STATUS
DRAWLINE

IF TARGET>= 0
	T = TARGET
	CALL GET_BATTLE_POWER
	LOCALS:2 = 
	IF P >= 99999999
		CALL VALUE_FORM, P
		LOCALS:2 = (%RESULTS%)
	ENDIF
	PRINTFORML (%CALLNAME:TARGET%)  戦闘能力：{P}%LOCALS:2%
	PRINT 耐力
	A = BASE:耐力
	SIF BASE:耐力 < 0
		A = 0
	CALL PRINT_BAR_4, A, MAXBASE:耐力, 32
	PRINTFORML ({A}/{MAXBASE:耐力})
	PRINT 魔力
	A = BASE:魔力
	SIF BASE:魔力 < 0
		A = 0
	IF (A * 100 / MAXBASE:魔力) > 34
		CALL PRINT_BAR, A, MAXBASE:魔力, 32, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("緑"), RESULT:1
	ELSE
		CALL PRINT_BAR, A, MAXBASE:魔力, 32, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("赤"), RESULT:1
	ENDIF
	PRINTFORML ({A}/{MAXBASE:魔力})
ENDIF

;あなたの戦闘能力計算
CALL GET_BATTLE_POWER_MASTER
LOCALS:2 = 
IF P >= 99999999
	CALL VALUE_FORM, P
	LOCALS:2 = (%RESULTS%)
ENDIF
PRINTFORML (%CALLNAME:MASTER%)  戦闘能力：{P}%LOCALS:2%
PRINT 耐力
A = BASE:MASTER:耐力
SIF BASE:MASTER:耐力 < 0
	A = 0
CALL PRINT_BAR_4, A, MAXBASE:MASTER:耐力, 32
PRINTFORML ({A}/{MAXBASE:MASTER:耐力})
PRINT 魔力
A = BASE:MASTER:魔力
SIF BASE:MASTER:魔力 < 0
	A = 0
IF (A * 100 / MAXBASE:MASTER:魔力) > 34
	CALL PRINT_BAR, A, MAXBASE:MASTER:魔力, 32, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("緑"), RESULT:1
ELSE
	CALL PRINT_BAR, A, MAXBASE:MASTER:魔力, 32, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("赤"), RESULT:1
ENDIF
PRINTFORML ({A}/{MAXBASE:MASTER:魔力})

;ライオットドールの表示（連れて行っていて、所持かつ復活待ちでない）
IF ITEM:852 > 0 && ITEM:852 < 51 && TFLAG:852 < 99
	;最大耐力の設定（MASTERの1/3 * レベル補正）
	R:1 = MAXBASE:MASTER:耐力 / 3
	R:1 *= (100 + ITEM:852) / 100
	;サーキットあり
	;冥界サーキット（耐力2倍）
	IF TFLAG:852 == 2
		TIMES R:1 , 2.00
	;聖輦サーキット（耐力1.5倍）
	ELSEIF TFLAG:852 == 7
		TIMES R:1 , 1.50
	ENDIF

	;現在の耐力（R:2は受けたダメージ）
	R:3 = R:1 - R:2
	;耐力が0以下かつ神霊サーキットを装着していない
	IF R:3 <= 0 && TFLAG:852 != 8
		;永夜サーキット装着（復活待ち処理へ）
		IF TFLAG:852 == 3 && ITEM:852 < 51
			ITEM:852 += 50
		;消失判定（最大耐力以上のオーバーダメージで消失）
		ELSEIF R:1 + R:3 < 0
			ITEM:852 = 0
			;装着サーキットも消失
			IF TFLAG:852
				ITEM:(853 + TFLAG:852) -= 1
				TFLAG:852 = 0
			ENDIF
			FLAG:44 = 0
		;壊れたドールに置換（レベル保持）
		ELSE
			ITEM:853 = ITEM:852
			ITEM:852 = 0
			SIF TFLAG:852
				TFLAG:852 = 0
		ENDIF
	;耐力あり
	ELSE
		;戦闘能力計算（MASTERの1/3 * レベル補正）
		T:1 = P / 3
		T:1 *= (100 + ITEM:852) / 100
		;冥界サーキット
		IF TFLAG:852 == 2
			TIMES T:1 , 1.30
		;守矢サーキット
		ELSEIF TFLAG:852 == 5
			TIMES T:1 , 1.20
		ENDIF
		PRINTFORML 
		PRINTFORM ※
		SELECTCASE TFLAG:852
			CASE 1
				PRINTFORM [紅]
			CASE 2
				PRINTFORM [冥]
			CASE 3
				PRINTFORM [永]
			CASE 4
				PRINTFORM [博]
			CASE 5
				PRINTFORM [守]
			CASE 6
				PRINTFORM [地]
			CASE 7
				PRINTFORM [聖]
			CASE 8
				PRINTFORM [神]
		ENDSELECT
		IF R:2 > R:1 && TFLAG:852 == 8
			SETCOLOR 255, 0, 0
			PRINTFORM %ITEMNAME:852%(LV{ITEM:852})
			RESETCOLOR
		ELSE
			PRINTFORM %ITEMNAME:852%(LV{ITEM:852})
		ENDIF
		PRINTFORML   戦闘能力：{T:1}
	ENDIF
ENDIF
DRAWLINE

RETURN 1

;---------------------------------------------------------
;敵の情報を表示
;---------------------------------------------------------
@SHOW_ENEMY_INFO
DRAWLINE
FOR LOCAL, 1, X+1
	;S-Lunaとインビジブルモードは情報を隠す
	;地霊サーキットでは見れるように
	IF (FLAG:5 == 4 || FLAG:2) && TFLAG:852 != 6
		IF Y:LOCAL > 0
			PRINTFORML %STR:LOCAL%  戦闘能力：？
			PRINTFORM 耐力
			CALL PRINT_BAR_4, Y:LOCAL, X:LOCAL, 32
			PRINTFORML ？
		ELSE
			PRINTFORML %STR:LOCAL%  戦闘能力：？  戦闘不能
		ENDIF
	ELSE
		IF X:LOCAL > 0
			PRINTFORML %STR:LOCAL%  戦闘能力：{Z:LOCAL}
			PRINTFORM 耐力
			CALL PRINT_BAR_4, Y:LOCAL, X:LOCAL, 32
			PRINTFORML ({Y:LOCAL}/{X:LOCAL})
		ELSE
			PRINTFORML %STR:LOCAL%  戦闘能力：{Z:LOCAL}  耐力：{Y:LOCAL}
		ENDIF
	ENDIF
NEXT
DRAWLINE

RETURN 1

;-------------------------------------------------
;ライオットドール関連
;使用変数:A,,TFLAG:852
;攻略時に使用する変数R,S,Tはここで初期化
;-------------------------------------------------
@RIOT_DOLL
TFLAG:852 = 0

PRINTFORML Stage攻略にライオットドールを連れて行く事が出来ます
PRINTFORML また機能拡張サーキットを一つ選択して装着できます。
PRINTFORML ライオットドールの設定を変更しますか？
PRINTFORM 現在の設定：
IF FLAG:44 == 0
	PRINTL 「連れて行く（サーキット装着無し）」
ELSEIF FLAG:44 == 99
	PRINTL 「連れて行かない」
ELSE
	PRINTFORML 「%ITEMNAME:(853+FLAG:44)%装着」
ENDIF
PRINTL 
PRINTFORML [0] 連れて行く（サーキット装着無し）
;所持しているサーキットを表示（紅魔、冥界、永夜、博麗、守矢、地霊、聖輦、神霊の順）
SIF ITEM:854
	PRINTFORML [1] %ITEMNAME:854%装着　効果：特殊攻撃1.5倍、通常攻撃時ダメージの1/4を回復
SIF ITEM:855
	PRINTFORML [2] %ITEMNAME:855%装着　効果：耐力2倍、戦闘能力1.3倍
SIF ITEM:856
	PRINTFORML [3] %ITEMNAME:856%装着　効果：攻略終了後復活、ターン回復、警戒時味方回復
SIF ITEM:857
	PRINTFORML [4] %ITEMNAME:857%装着　効果：オートバトル時戦闘能力3倍
SIF ITEM:858
	PRINTFORML [5] %ITEMNAME:858%装着　効果：戦闘力1.2倍、被ダメージの1/3を味方魔力に転換
SIF ITEM:859
	PRINTFORML [6] %ITEMNAME:859%装着　効果：20％の確率で敵の攻撃を回避する（させる）
SIF ITEM:860
	PRINTFORML [7] %ITEMNAME:860%装着　効果：耐力1.5倍、庇う能力アップ
SIF ITEM:861
	PRINTFORML [8] %ITEMNAME:861%装着　効果：耐力が0になっても戦い続ける、攻略終了後復活
PRINTFORML [99] 連れて行かない

$INPUT_RIOT
INPUT
IF RESULT == 99
	PRINTFORML %ITEMNAME:852%を置いて行くことにしました
ELSEIF RESULT == 0
	PRINTFORML %ITEMNAME:852%を連れて行くことにしました
ELSEIF ITEM:(RESULT+853) > 0
	PRINTFORMW %ITEMNAME:852%に[%ITEMNAME:(RESULT+853)%]を装着しました
ELSE
	PRINTW 正しい数値を入力してください
	GOTO INPUT_RIOT
ENDIF
FLAG:44 = RESULT
TFLAG:852 = FLAG:44

;攻略時使用する変数を初期化
VARSET R, 0
VARSET S, 0
VARSET T, 0

;---------------------------------------------------------
;雑魚敵との戦闘
;変数予約	P→こちらの戦闘力	X→敵の数	Y→敵の耐力	Z→敵の戦闘力	M→報酬
;---------------------------------------------------------
@BATTLE_NORMAL
;出現数決定
A = RAND:10
IF A <= 2
	;30%で1匹
	X = 1
ELSEIF A <= 7
	;50%で2匹
	X = 2
ELSE
	;20%で3匹
	X = 3
ENDIF

;敵の種類・能力を決定
VARSET Y, 0
VARSET Z, 0
M = 0
REPEAT X
	A = COUNT + 1
	B = RAND:100 + 1
	IF B <= 5
		STR:A = 謎の娘
		Y:A = 100
		Z:A = 30
		M += 50
	ELSEIF B <= 20
		STR:A = ずん子鳥
		Y:A = 180
		Z:A = 70
		M += 80
	ELSEIF B <= 40
		STR:A = けだマキマキ
		Y:A = 200
		Z:A = 100
		M += 90
	ELSEIF B <= 60
		STR:A = 発情した町娘
		Y:A = 220
		Z:A = 110
		M += 100
	ELSEIF B <= 80
		STR:A = ゆっくりれいむ
		Y:A = 250
		Z:A = 120
		M += 110
	ELSEIF B <= 95
		STR:A = ゆっくりまりさ
		Y:A = 300
		Z:A = 160
		M += 150
	ELSE
		STR:A = ニコニコテレビちゃん
		Y:A = 500
		Z:A = 250
		M += 200
	ENDIF


	;Stageによる強化
	Y:A = Y:A * (9 + FLAG:24 * FLAG:24 * FLAG:24) / 10
	Z:A = Z:A * (9 + FLAG:24 * FLAG:24 * FLAG:24) / 10


	;戦いすぎによる強化
	IF TFLAG:46 >= 60
		IF 999999999999999999 / TFLAG:46 < Y:A
			Y:A = 999999999999999999
		ELSE
			Y:A = Y:A * TFLAG:46
		ENDIF
		IF 999999999999999999 / TFLAG:46 < Z:A
			Z:A = 999999999999999999
		ELSE
			Z:A = Z:A * TFLAG:46
		ENDIF
	ELSEIF TFLAG:46 >= 40
		Y:A = Y:A / 2 * TFLAG:46
		Z:A = Z:A / 2 * TFLAG:46
	ELSEIF TFLAG:46 >= 20
		Y:A = Y:A * TFLAG:46 / 5
		Z:A = Z:A * TFLAG:46 / 5
	ELSEIF TFLAG:46 >= 10
		Y:A = Y:A * (TFLAG:46 + 3) / 8
		Z:A = Z:A * (TFLAG:46 + 3) / 8
	ELSEIF TFLAG:46 >= 5
		Y:A = Y:A * (TFLAG:46 + 5) / 10
		Z:A = Z:A * (TFLAG:46 + 5) / 10
	ENDIF


	;難易度による強化
	IF FLAG:5 == 0
		TIMES Y:A , 0.70
		TIMES Z:A , 0.70
	ELSEIF FLAG:5 == 2
		TIMES Y:A , 1.10
		TIMES Z:A , 1.10
	ELSEIF FLAG:5 == 3
		TIMES Y:A , 1.30
		TIMES Z:A , 1.30
	ELSEIF FLAG:5 == 4
		TIMES Y:A , 2.00
		TIMES Z:A , 2.00
	ELSEIF FLAG:5 == 10
		TIMES Y:A , 1.30
		TIMES Z:A , 1.30
	ENDIF

	;ある程度ランダムで上下させる
	B = RAND:61 + 70
	IF Y:A < 10000000
		Y:A = Y:A * B / 100
	ELSE
		Y:A = Y:A / 100 * B
	ENDIF
	IF Z:A < 10000000
		Z:A = Z:A * B / 100
	ELSE
		Z:A = Z:A / 100 * B
	ENDIF
	SIF Y:A < 0
		Y:A = 2000000000
	SIF Z:A < 0
		Y:A = 2000000000
	
	;最大耐力保存
	X:A = Y:A
REND

PRINTFORML 敵が出現！！
CALL SHOW_ENEMY_INFO

;!!!
;詳細なコマンド選択は未実装
;PRINTFORML どうしますか？
;PRINTFORML [0] 通常攻撃
;PRINTFORML [1] 特殊攻撃
;PRINTFORML [2] 撤退

;確認ONの場合、逃げるかどうか聞く
IF FLAG:23 & 32
	PRINTFORML どうしますか？
	PRINTFORML [0] もちろん倒す
	PRINTFORML [1] 撤退する

	$INPUT_ESCAPE
	INPUT
	IF RESULT < 0 || RESULT > 1
		GOTO INPUT_ESCAPE
	;逃げる
	ELSEIF RESULT == 1
		A = RAND:100 + 5
		;10%で逃げるの失敗。あなたが囮になって好感度UP
		IF A <= 15 && TALENT:MASTER:逃走能力強化 == 0
			PRINTFORMW %CALLNAME:MASTER%達は逃げ出した！！
			PRINTFORMW しかし%STR:1%に回り込まれてしまった！！
			PRINTL  
			PRINTFORML 仕方なく、%CALLNAME:MASTER%は自分の身を呈して%CALLNAME:TARGET%を逃がす事にした
			PRINTFORML %CALLNAME:MASTER%の耐力が0になりました
			PRINTFORMW %CALLNAME:TARGET%の好感度が{A}上がりました
			TFLAG:600 = 39
			CALL KOJO_JUN
			CFLAG:2 += A
			BASE:MASTER:耐力 = 0
			RETURN 1
		ELSE
			PRINTFORMW %CALLNAME:MASTER%達は逃げ出した！！
			TFLAG:600 = 35
			CALL KOJO_JUN
			RETURN 1
		ENDIF
	ENDIF
	;入力ミスが１回以上あるとENDIFで怒られるので、GOTO使用orz
	GOTO END_ESCAPE
ENDIF

$END_ESCAPE

SIF K:1 == 0
	WAIT

CALL AUTO_BATTLE

;戦闘終了時の共通処理
CALL BATTLE_COMMON_END

IF (TARGET >= 0 && BASE:耐力 > 0 && BASE:MASTER:耐力 > 0) || (TARGET < 0 && BASE:MASTER:耐力 > 0)
	;勝利
	IF TARGET >= 0 && K:1 == 0
		TFLAG:600 = 8
		CALL KOJO_JUN
	ENDIF
	IF K:1 == 0
		PRINTFORMW 勝利！！
	ELSE
		PRINTFORML 勝利！！
	ENDIF
	;戦闘経験取得と報酬の計算
	M = M * (FLAG:24 + 3) * (RAND:41 + 40) / 100
	CALL BATTLE_COMMON_EM
	IF K:1 == 0
		IF TARGET < 0 && ASSI < 0
			PRINTFORMW %CALLNAME:MASTER%はモンスター退治の報酬として${M}を手に入れた
		ELSE
			PRINTFORMW %CALLNAME:MASTER%達はモンスター退治の報酬として${M}を手に入れた
		ENDIF
	ELSE
		;こっそりオートだと収入を減らす
		TIMES M , 0.80
		PRINTFORML 報酬として${M}を手に入れた
	ENDIF

	MONEY += M
	IF FLAG:27 < 9 || (FLAG:27 > 10 && FLAG:27 < 19)
		FLAG:27 += 1
	ENDIF

	;オートでも、勝利時だけは一旦止める
	SIF K:1
		WAIT
ENDIF

RETURN 1

;---------------------------------------------------------
;ボスとの戦闘
;---------------------------------------------------------
@BATTLE_BOSS
;ボスがまだ決まっていない場合、ボスを選択
IF FLAG:25 == 0
	;ラスボスの場合
	IF FLAG:24 == 6 && FLAG:27 >= 20 && FLAG:3 != 9
		;通常の場合
		IF FLAG:3 == 0 || FLAG:3 == 1 || FLAG:3 == 2 || FLAG:3 == 0
			;ずん子
			FLAG:25 = 5

			T = 5
			CALL GET_EXISTANCE
			;ずん子が既に居る場合、きりたん
			SIF E
				FLAG:25 = 10
		ENDIF

	;通常ボスの場合
	ELSE
		CALL GET_BOSS_NO
		FLAG:25 = B
	ENDIF
ENDIF

;対ゆかり様の場合
IF FLAG:25 == 666
	CALL BATTLE_ANGELIA
	IF BASE:耐力 > 0 && BASE:MASTER:耐力 > 0
		IF FLAG:27 == 10 && K:3 == 0
			FLAG:27 += 1
		ELSEIF FLAG:27 >= 20 && K:3 == 0
			PRINTFORMW 次のステージに進みます
			FLAG:27 = 0
			FLAG:24 += 1
			SIF FLAG:24 > 99
				FLAG:24 = 99

			SIF FLAG:5 == 10
				CFLAG:20 += 5
		ENDIF
	ENDIF
	FLAG:25 = 0
	RETURN 1
ENDIF

;口上用に先に名前を取得
N = 1
T = FLAG:25
CALL GET_CHARA_NAME


;特殊掛け合い、ボス戦開始口上
;口上内でTFLAG:120を1にすることで、ボスとして出現、ラスボス登場、ボス戦開始とのバッティングを避けます。
TFLAG:120 = 0
TFLAG:600 = 40
CALL KOJO_JUN

;ボスの台詞を表示
IF FLAG:24 == 6 && FLAG:27 == 20 && FLAG:5 != 9 && FLAG:5 != 10
	IF TFLAG:120 == 0
		TFLAG:600 = 31
		CALL KOJO_JUN
	ENDIF
	FLAG:27 = 21
ELSEIF TFLAG:120 == 0
	;TARGETをZ（口上用）とローカル変数で記録
	VARSET LOCAL, 0
	Z = TARGET
	LOCAL = TARGET
	;パートナーがいない時は口上のエラー防止にZにMASTERを記録
	IF TARGET <= 0
		Z = MASTER
		LOCAL:1 = 1
	ENDIF
	ADDCHARA FLAG:25
	TARGET = CHARANUM - 1
	TFLAG:600 = 32
	CALL KOJO_JUN
	DELCHARA TARGET
	TARGET = LOCAL
	IF LOCAL:1
		TARGET = -1
	ENDIF
ENDIF

;ラスボス以外はボス戦開始口上を表示
IF FLAG:27 != 21
	TFLAG:600 = 10
	CALL KOJO_JUN
ENDIF

T = FLAG:25
;W:1～に敵キャラのNO代入
W:1 = FLAG:25
;口上で変更されてるかもしれないので、再度名前を取得
CALL GET_CHARA_NAME
CALL GET_STUFF
Y:1 = 1000 + S * 10
Z:1 = 300 + S * 3

;従者判定
CALL ATTENDANT
;仲間にならない従者判定（幻影等）
CALL ATTENDANT_2

REPEAT X
	A = COUNT + 1

	;Stageによる強化
	Y:A = Y:A * (9 + FLAG:24 * FLAG:24 * FLAG:24) / 10
	Z:A = Z:A * (9 + FLAG:24 * FLAG:24 * FLAG:24) / 10

	SIF Y:A > 1000000000
		Y:A = 1000000000
	SIF Z:A > 1000000000
		Z:A = 1000000000

	;難易度による強化
	IF FLAG:5 == 0
		TIMES Y:A , 0.70
		TIMES Z:A , 0.70
	ELSEIF FLAG:5 == 2
		TIMES Y:A , 1.10
		TIMES Z:A , 1.10
	ELSEIF FLAG:5 == 3
		TIMES Y:A , 1.30
		TIMES Z:A , 1.30
	ELSEIF FLAG:5 == 4
		TIMES Y:A , 3.00
		TIMES Z:A , 2.00
	ELSEIF FLAG:5 == 10
		TIMES Y:A , 1.30
		TIMES Z:A , 1.30
	ENDIF

	;大ボスの方は能力2割増し
	IF FLAG:27 >= 20 && K:3 == 0
		TIMES Y:A , 1.20
		TIMES Z:A , 1.20
	ENDIF

	;S-Luna6ボスにはもう少し強くなってもらう
	IF FLAG:24 == 6 && FLAG:27 >= 20 && FLAG:5 == 4
		TIMES Y:A , 1.50
		TIMES Z:A , 1.20
	ENDIF

	;Ph6ボスにはもう少し強くなってもらう
	IF FLAG:24 == 6 && FLAG:27 >= 20 && FLAG:5 == 10
		TIMES Y:A , 5.00
		TIMES Z:A , 2.50
	ENDIF


	;ある程度ランダムで上下させる
	B = RAND:21 + 90
	IF Y:A < 10000000
		Y:A = Y:A * B / 100
	ELSE
		Y:A = Y:A / 100 * B
	ENDIF
	IF Z:A < 10000000
		Z:A = Z:A * B / 100
	ELSE
		Z:A = Z:A / 100 * B
	ENDIF

	SIF Y:A < 0
		Y:A = 2000000000
	SIF Z:A < 0
		Y:A = 2000000000

	;最大耐力保存
	X:A = Y:A
REND

PRINTFORML %STR:1%と遭遇した！！
CALL SHOW_ENEMY_INFO

;バトル選択
CALL BATTLE_COMMON_CHOICE(0)
SIF RESULT
	RETURN 1

;戦闘終了時の共通処理
CALL BATTLE_COMMON_END

IF (TARGET >= 0 && BASE:耐力 > 0 && BASE:MASTER:耐力 > 0) || (TARGET < 0 && BASE:MASTER:耐力 > 0)
	;勝利
	IF TARGET >= 0
		;特殊掛け合い、ボス戦勝利口上
		;口上内でTFLAG:120を1にすることで、ボスに勝利、ボスとして敗北とのバッティングを避けます。
		TFLAG:120 = 0
		TFLAG:600 = 41
		CALL KOJO_JUN

		IF TFLAG:120 == 0
			;TARGETをZ（口上用）とローカル変数で記録
			VARSET LOCAL, 0
			Z = TARGET
			LOCAL = TARGET
			;パートナーがいない時は口上のエラー防止にZにMASTERを記録
			IF TARGET <= 0
				Z = MASTER
				LOCAL:1 = 1
			ENDIF
			ADDCHARA FLAG:25
			TARGET = CHARANUM - 1
			TFLAG:600 = 33
			CALL KOJO_JUN
			DELCHARA TARGET
			TARGET = LOCAL
			IF LOCAL:1
				TARGET = -1
			ENDIF
		ENDIF

		IF TFLAG:120 == 0
			TFLAG:600 = 11
			CALL KOJO_JUN
		ENDIF
	ENDIF
	PRINTFORMW 勝利！！
	;戦闘経験取得と報酬の計算
	M = X * (FLAG:24 + 3) * (RAND:41 + 40) * 20
	X *= 10
	CALL BATTLE_COMMON_EM
	IF TARGET < 0 && ASSI < 0
		PRINTFORM %CALLNAME:MASTER%は報酬として${M}
	ELSE
		PRINTFORM %CALLNAME:MASTER%達は報酬として${M}
	ENDIF
	;素材を得る(COMF309.ERB)
	;TRYCALL ADD_SOZAI_ITEM
	PRINTW を手に入れました
	MONEY += M
	T = FLAG:25
	CALL GET_EXISTANCE
	IF E
		;幻影戦
		PRINTFORMW 気がつくと相手の%STR:1%は消えていた。どうやら幻影だったようだ
	ELSE
		;仲間にするかしないか
		LOCAL = 0
		IF FLAG:23 & 65536
			;自動的に仲間にしない場合LOCAL = 1
			LOCAL = 1
			;仲間にするか選択
			IF FLAG:23 & 131072
				CALL INPUT_ADD_NEW_CHARA
				LOCAL = RESULT
			ENDIF
		ENDIF
		IF LOCAL
			;仲間にしない
			IF X <= 10
				PRINTFORMW 正気を取り戻した%STR:1%と別れました
			ELSE
				PRINTFORMW 正気を取り戻した%STR:1%達と別れました
			ENDIF
		ELSE
			IF X <= 10
				PRINTFORMW 正気を取り戻した%STR:1%を、%CALLNAME:MASTER%達は仲間に加える事にした
			ELSE
				PRINTFORMW 正気を取り戻した%STR:1%達を、%CALLNAME:MASTER%達は仲間に加える事にした
			ENDIF

			CALL ADD_NEW_CHARA
		ENDIF
	ENDIF
	FLAG:25 = 0
	SIF FLAG:5 == 10 && TARGET >= 0
		CFLAG:20 += 3

	;全キャラ収集チェック
	CALL ADDITIONAL_ENDING_2

	IF FLAG:27 == 10 && K:3 == 0
		FLAG:27 += 1
	ELSEIF FLAG:27 >= 20 && K:3 == 0
		PRINTFORMW 次のステージに進みます
		
		;現在のStage数が最高到達Stage数を超えたら記録
		;最高到達Stage数はStage99クリア時の100が上限
		SIF FLAG:24 + 1 > FLAG:42
			FLAG:42 = FLAG:24 + 1
		
		;!!!
		IF FLAG:24 == 3
			IF FLAG:5 == 9 || FLAG:5 == 10
				PRINTFORMW Stage3をクリアしたので「服を脱がせる」が必ず成功するようになりました
			ELSE
				TFLAG:600 = 30
				CALL KOJO_JUN
				FLAG:27 = 0
				FLAG:24 += 1
				RETURN 2
			ENDIF
		ELSEIF FLAG:24 == 6 && FLAG:5 != 9 && FLAG:5 != 10
			CALL GAME_CONTINUE
			;クリアフラグをビットで管理
			SIF FLAG:5 != 9 && (FLAG:28 & 1) == 0
				FLAG:28 |= 1
			FLAG:27 = 0
			FLAG:24 += 1
			RETURN 2
		ENDIF

		FLAG:27 = 0
		FLAG:24 += 1
		SIF FLAG:24 > 99
			FLAG:24 = 99

		SIF FLAG:5 == 10 && TARGET >= 0
			CFLAG:20 += 5
	ENDIF
ENDIF

RETURN 1

;---------------------------------------------------------
;仲間にするか選択
;---------------------------------------------------------
@INPUT_ADD_NEW_CHARA
IF X <= 10
	PRINTFORMW 正気を取り戻した%STR:1%を仲間に加えますか？
ELSE
	PRINTFORMW 正気を取り戻した%STR:1%達を仲間に加えますか？
ENDIF
PRINTFORML [0] はい
PRINTFORML [1] いいえ
$INPUT_ADD_NEW_CHARA
INPUT
IF RESULT == 0
	RETURN 0
ELSEIF RESULT == 1
	RETURN 1
ELSE
	GOTO INPUT_ADD_NEW_CHARA
ENDIF

;---------------------------------------------------------
;ゆかり様と戦闘
;---------------------------------------------------------
@BATTLE_ANGELIA
;ボス指定を保存し、一時的に1へ変更。@BATTLE_ANGELIA終了時にボス指定を戻す
LOCAL = FLAG:25
FLAG:25 = 1

;通常はW:Nに敵キャラのNO代入。ゆかり様なので1
W:1 = 1
STR:1 = ゆかり様
;戦闘能力決定
Y:1 = 1000000 * (FLAG:45 + 1)
Z:1 = 800000 * (FLAG:45 + 1)


;ボス戦開始
TFLAG:600 = 10
CALL KOJO_JUN

;難易度による強化
IF FLAG:5 == 0
	TIMES Y:1 , 0.70
	TIMES Z:1 , 0.70
ELSEIF FLAG:5 == 2
	TIMES Y:1 , 1.20
	TIMES Z:1 , 1.20
ELSEIF FLAG:5 == 3
	TIMES Y:1 , 1.50
	TIMES Z:1 , 1.50
ELSEIF FLAG:5 == 4
	TIMES Y:1 , 2.00
	TIMES Z:1 , 2.00
ELSEIF FLAG:5 == 10
	TIMES Y:1 , 1.50
	TIMES Z:1 , 1.50
ENDIF

;ある程度ランダムで上下させる
B = RAND:21 + 90
Y:1 = Y:1 / 100 * B
Z:1 = Z:1 / 100 * B

SIF Y:1 < 0
	Y:1 = 2000000000
SIF Z:1 < 0
	Z:1 = 2000000000

;一応カンストいれておく
SIF Y:1 >= 999999999999999999
	Y:1 = 999999999999999999
SIF Z:1 >= 999999999999999999
	Z:1 = 999999999999999999

;ゆかり様も分身する（最高2人までデコイもとい幻影分身）
LOCAL:1 = FLAG:45 / 5
SIF LOCAL:1 >= 2
	LOCAL:1 = 2
IF LOCAL:1
	REPEAT LOCAL:1
		X += 1
		N += 1
		;通常はW:Nに敵キャラのNO代入。ゆかり様なので1
		W:(COUNT+2) = 1
		Y:(COUNT+2) = Y:1 / 200 * (RAND:21 + 90)
		Z:(COUNT+2) = Z:1 / 200 * (RAND:21 + 90)
		STR:(COUNT+2) = ゆかり様(幻影)
	REND
ENDIF

;最大耐力保存
REPEAT X
	X:(COUNT+1) = Y:(COUNT+1)
REND

PRINTFORML %STR:1%と遭遇した！！
CALL SHOW_ENEMY_INFO

;バトル選択
CALL BATTLE_COMMON_CHOICE(666)
SIF RESULT
	RETURN 1

;戦闘終了時の共通処理
CALL BATTLE_COMMON_END

CALL BATTLE_ANGELIA_V

;ボス指定を戻す
FLAG:25 = LOCAL

RETURN 1

;---------------------------------------------------------
;ゆかり様に勝利
;---------------------------------------------------------
@BATTLE_ANGELIA_V
IF (TARGET >= 0 && BASE:耐力 > 0 && BASE:MASTER:耐力 > 0) || (TARGET < 0 && BASE:MASTER:耐力 > 0)
	;勝利
	TFLAG:600 = 11
	CALL KOJO_JUN
	PRINTFORMW 勝利！！
	;戦闘経験取得と報酬の計算
	IF FLAG:45 <= 96
		M = 5000 * (FLAG:45 + 4)
	ELSE
		M = 5000000
	ENDIF
	X *= 25 * (FLAG:45 + 4)
	CALL BATTLE_COMMON_EM
	PRINTFORMW %CALLNAME:MASTER%達は報酬として${M}を手に入れた
	MONEY += M
	
	FLAG:45 += 1
	SIF FLAG:45 > 9999
		FLAG:45 = 9999
	
	T = 1
	CALL GET_EXISTANCE
	IF FLAG:5 == 9 && FLAG:28 == 0
		PRINTFORMW ついに%CALLNAME:MASTER%達は%STR:1%を倒す事に成功した！！
		PRINTFORMW 強くてニューゲームが解禁されました。
		FLAG:28 = 1
	ELSEIF FLAG:5 == 10 && E == 0
		PRINTFORMW 正気を取り戻した%STR:1%を、%CALLNAME:MASTER%達は仲間に加える事にした
		ADDSPCHARA 1
		A = CHARANUM - 1
		;キャラ加入時の共通設定
		CALL NEWCHARA_INIT
	ELSE
		PRINTFORMW 気がつくと%STR:1%は消えていた。どうやら幻影だったようだ
	ENDIF
ELSEIF TARGET >= 0 && BASE:耐力 <= 0 && FLAG:5 == 10
	;Phモード限定でパートナーがやられた時
	;ゆかり様に負けると霧に飲まれる影響が大きい
	CFLAG:20 += 5
ENDIF

@BATTLE_ANGELIA_AUTO
;初期化
;V:4（TARGET脱衣）、V:5（MASTER脱衣）、V:6（庇う）、V:7（ドール庇う）
VARSET V, 0
VARSET W, 0
VARSET X, 0
VARSET Y, 0
VARSET Z, 0
VARSET LOCAL, 0
X = 1
N = 1
;脱衣制限解除有ならV:4（TARGET脱衣）、V:5（MASTER脱衣）を1にする
IF TALENT:MASTER:脱衣制限解除
	V:4 = 1
	V:5 = 1
ENDIF

;通常はW:Nに敵キャラのNO代入。ゆかり様なので54
W:1 = 54
STR:1 = ゆかり様
;戦闘能力決定
Y:1 = 1000000 * (FLAG:45 + 1)
Z:1 = 800000 * (FLAG:45 + 1)

;難易度による強化
IF FLAG:5 == 0
	TIMES Y:1 , 0.70
	TIMES Z:1 , 0.70
ELSEIF FLAG:5 == 2
	TIMES Y:1 , 1.20
	TIMES Z:1 , 1.20
ELSEIF FLAG:5 == 3
	TIMES Y:1 , 1.50
	TIMES Z:1 , 1.50
ELSEIF FLAG:5 == 4
	TIMES Y:1 , 2.00
	TIMES Z:1 , 2.00
ELSEIF FLAG:5 == 10
	TIMES Y:1 , 1.50
	TIMES Z:1 , 1.50
ENDIF

;ある程度ランダムで上下させる
B = RAND:21 + 90
Y:1 = Y:1 / 100 * B
Z:1 = Z:1 / 100 * B

SIF Y:1 < 0
	Y:1 = 2000000000
SIF Z:1 < 0
	Z:1 = 2000000000

;一応カンストいれておく
SIF Y:1 >= 999999999999999999
	Y:1 = 999999999999999999
SIF Z:1 >= 999999999999999999
	Z:1 = 999999999999999999

;ゆかり様も分身する（最高2人までデコイもとい幻影分身）
LOCAL:1 = FLAG:45 / 5
SIF LOCAL:1 >= 2
	LOCAL:1 = 2
IF LOCAL:1
	REPEAT LOCAL:1
		X += 1
		N += 1
		;通常はW:Nに敵キャラのNO代入。ゆかり様なので1
		W:(COUNT+2) = 1
		Y:(COUNT+2) = Y:1 / 200 * (RAND:21 + 90)
		Z:(COUNT+2) = Z:1 / 200 * (RAND:21 + 90)
		STR:(COUNT+2) = ゆかり様(幻影)
	REND
ENDIF

;最大耐力保存
REPEAT X
	X:(COUNT+1) = Y:(COUNT+1)
REND


;---------------------------------------------------------
;霧に飲まれかけている仲間を助ける
;---------------------------------------------------------
@BATTLE_LOST_CHARA
;相手を選択
CALL GET_LOST_CHARA
FLAG:25 = C
T = NO:C
CALL GET_CHARA_NAME
T = C
CALL GET_BATTLE_POWER
Y:1 = MAXBASE:C:耐力
Z:1 = P

REPEAT X
	A = COUNT + 1

	;Stageによる強化
	IF Y:A / 10000 * FLAG:24 * FLAG:24 > 100000
		Y:A = 1000000000
	ELSE
		Y:A = Y:A * (9 + FLAG:24 * FLAG:24) / 10
	ENDIF

	SIF Y:A > 1000000000
		Y:A = 1000000000
	SIF Z:A > 1000000000
		Z:A = 1000000000

	;ある程度ランダムで上下させる
	B = RAND:21 + 90
	IF Y:A < 10000000
		Y:A = Y:A * B / 100
	ELSE
		Y:A = Y:A / 100 * B
	ENDIF
	IF Z:A < 10000000
		Z:A = Z:A * B / 100
	ELSE
		Z:A = Z:A / 100 * B
	ENDIF

	;最大耐力保存
	X:A = Y:A
REND

PRINTFORML %STR:1%の救出を試みた！！
CALL SHOW_ENEMY_INFO

;バトル選択
CALL BATTLE_COMMON_CHOICE(1)
SIF RESULT
	RETURN 1

;戦闘終了時の共通処理
CALL BATTLE_COMMON_END

IF (TARGET >= 0 && BASE:耐力 > 0 && BASE:MASTER:耐力 > 0) || (TARGET < 0 && BASE:MASTER:耐力 > 0)
	;勝利
	PRINTFORMW 勝利！！
	;戦闘経験取得と報酬の計算
	X *= 10
	CALL BATTLE_COMMON_EM
	PRINTFORMW 正気を取り戻した%STR:1%を、%CALLNAME:MASTER%達は再び仲間に加える事にした
	C = FLAG:25
	CFLAG:C:17 = 0
	CFLAG:C:18 = 0
	CFLAG:C:20 = 0
	FLAG:25 = 0
ENDIF

RETURN 1

;---------------------------------------------------------
;バトル選択
;---------------------------------------------------------
@BATTLE_COMMON_CHOICE(ARG)
;確認ONの場合、逃げるかどうか聞く
IF FLAG:23 & 64
	PRINTFORML どうしますか？
	PRINTFORML [0] オートバトルを挑む
	PRINTFORML [1] コマンドバトルを挑む
	PRINTFORML [2] 撤退する
	IF ARG == 666
		PRINTFORML [3] オートバトルを挑む（指定回数だけ自動で戦闘を繰り返します）
		PRINTFORML [4] オートバトルを挑む（消耗するまで自動で戦闘を繰り返します）
	ENDIF

	$INPUT_ESCAPE
	INPUT
	;オート
	IF RESULT == 0
		CALL AUTO_BATTLE
	;コマンド
	ELSEIF RESULT == 1
		CALL COMMAND_BATTLE
	;逃げる
	ELSEIF RESULT == 2
		;ゆかり様からは逃げられない
		IF ARG == 666
			PRINTFORMW %STR:1%からは逃げられない！！
			RESTART
		ENDIF
		A = RAND:100 + 5
		;10%で逃げるの失敗。あなたが囮になって好感度UP
		IF A <= 15 && TALENT:MASTER:逃走能力強化 == 0
			PRINTFORMW %CALLNAME:MASTER%達は逃げ出した！！
			PRINTFORMW しかし%STR:1%に回り込まれてしまった！！
			PRINTL  
			PRINTFORML 仕方なく、%CALLNAME:MASTER%は自分の身を呈して%CALLNAME:TARGET%を逃がす事にした
			PRINTFORML %CALLNAME:MASTER%の体力が０になりました
			PRINTFORMW %CALLNAME:TARGET%の好感度が{A}上がりました
			TFLAG:600 = 39
			CALL KOJO_JUN
			CFLAG:2 += A
			BASE:MASTER:耐力 = 0
			RETURN 1
		ELSE
			PRINTFORMW %CALLNAME:MASTER%達は逃げ出した！！
			;@BATTLE_BOSS時はStage攻略中断（撤退）口上表示
			IF ARG == 0
				TFLAG:600 = 35
				CALL KOJO_JUN
			ENDIF
			RETURN 1
		ENDIF
	ELSEIF RESULT == 3 && ARG == 666
		PRINTFORML 戦闘したい回数を入力してください(1-99、0で戻る)
		INPUT
		IF RESULT == 0
			RESTART
		ELSEIF RESULT >= 1 && RESULT <= 99
			LOCAL:1 = RESULT
			FOR LOCAL, 0, LOCAL:1
				IF (TARGET >= 0 && (BASE:耐力 <= MAXBASE:耐力 / 4 || BASE:魔力 <= 0)) || (TARGET < 0 && BASE:MASTER:耐力 <= MAXBASE:MASTER:耐力 / 4)
					DRAWLINE
					PRINTFORMW 消耗が激しくなってきたので、戦闘を終了します
					RETURN 1
				ENDIF
				IF LOCAL != 0
					CALL SHOW_BATTLE_STATUS
					PRINTFORML %STR:1%と遭遇した！！
					CALL SHOW_ENEMY_INFO
				ENDIF
				CALL AUTO_BATTLE
				;戦闘終了時の共通処理
				CALL BATTLE_COMMON_END
				CALL BATTLE_ANGELIA_V
				CALL BATTLE_ANGELIA_AUTO
			NEXT
		ELSE
			RESTART
		ENDIF
	ELSEIF RESULT == 4 && ARG == 666
		FOR LOCAL, 0, 999
			IF (TARGET >= 0 && (BASE:耐力 <= MAXBASE:耐力 / 4 || BASE:魔力 <= 0)) || (TARGET < 0 && BASE:MASTER:耐力 <= MAXBASE:MASTER:耐力 / 4)
				DRAWLINE
				PRINTFORMW 消耗が激しくなってきたので、戦闘を終了します
				RETURN 1
			ENDIF
			IF LOCAL != 0
				CALL SHOW_BATTLE_STATUS
				PRINTFORML %STR:1%と遭遇した！！
				CALL SHOW_ENEMY_INFO
			ENDIF
			CALL AUTO_BATTLE
			;戦闘終了時の共通処理
			CALL BATTLE_COMMON_END
			CALL BATTLE_ANGELIA_V
			CALL BATTLE_ANGELIA_AUTO
		NEXT
	ELSE
		GOTO INPUT_ESCAPE
	ENDIF
	GOTO END_SELECT
ELSE
	WAIT
	CALL AUTO_BATTLE
ENDIF

$END_SELECT
RETURN 0

;---------------------------------------------------------
;オートバトル
;---------------------------------------------------------
@AUTO_BATTLE
$START_POINT
;パートナーの攻撃
IF TARGET >= 0
	T = TARGET
	CALL GET_BATTLE_POWER
	;魔力が切れている場合、戦闘力1/10
	SIF BASE:魔力 <= 0
		P /= 10

	REPEAT X
		LOCAL = X - COUNT
		IF Y:LOCAL > 0
			;敵に攻撃
			A = P / 5
			A -= Z:LOCAL / 100
			SIF A < 10
				A = 10
			Y:LOCAL -= A
			BREAK
		ENDIF
	REND

	IF Y:1 <= 0
		;撃退
		RETURN 1
	ENDIF
	;既に魔力が切れている場合、耐力を少し消費
	SIF BASE:魔力 <= 0
		BASE:耐力 -= MAXBASE:耐力 / 200 + 1
	;攻撃により魔力を消費
	BASE:魔力 -= MAXBASE:魔力 / 100 + 1
	;S-Lunaは更に消費
	SIF FLAG:5 == 4
		BASE:魔力 -= MAXBASE:魔力 / 100 + 1
	SIF BASE:魔力 < 0
		BASE:魔力 = 0
ENDIF

;パートナーの戦闘能力を退避
P:1 = P

;あなたの戦闘能力計算
CALL GET_BATTLE_POWER_MASTER
;ライオットドールの戦闘能力の半分をあなたに上乗せ
IF ITEM:852 > 0  && TFLAG:852 < 99
	;戦闘能力計算（MASTERの1/3＋レベル補正）
	T:1 = P / 3
	T:1 *= (100 * ITEM:852) / 100
	;冥界サーキット（1.3倍）
	SIF TFLAG:852 == 2
		TIMES T:1 , 1.30
	;博麗サーキット（3.0倍）
	SIF TFLAG:852 == 4
		TIMES T:1 , 3.00
	;守矢サーキット（1.2倍）
	SIF TFLAG:852 == 5
		TIMES T:1 , 1.20
	;パートナーが居る場合、半減
	SIF TARGET >= 0
		 T:1 /= 2

	;あなたに加算
	P += T:1
ENDIF
;あなたの攻撃
REPEAT X
	LOCAL = X - COUNT
	IF Y:LOCAL > 0
		;敵に攻撃
		A = P / 5
		A -= Z:LOCAL / 100
		SIF A < 10
			A = 10
		Y:LOCAL -= A
		BREAK
	ENDIF
REND

IF Y:1 <= 0
	;撃退
	RETURN 1
ENDIF

;退避させておいたパートナーの戦闘能力を元に戻す
P = P:1

B = 0
;Phモード限定の恋慕ボーナス
IF FLAG:5 == 10 || TALENT:MASTER:恋慕ボーナス || TALENT:MASTER:恋慕ボーナス強化
	REPEAT CHARANUM
		SIF TALENT:COUNT:恋慕
			B += 1
	REND
	SIF TALENT:MASTER:恋慕ボーナス強化
		TIMES B , 2.00
ENDIF

REPEAT X
	;敵の攻撃
	IF Y:(COUNT+1) > 0
		A = Z:(COUNT+1) / 5
		A -= P / 100 * (1 + B)
		SIF A < 1
			A = 1
		CALL DAMAGE_CAP
		;パートナーがいればパートナーに攻撃
		SIF TARGET >= 0
			BASE:耐力 -= A
		;25%で主人公にも攻撃（パートナーがいなければ必ず攻撃）
		SIF RAND:4 == 0 || TARGET < 0
			BASE:MASTER:耐力 -= A
	ENDIF
REND

;減少好感度記憶用
A:2 = 0

;パートナーの耐力0以下
IF TARGET >= 0
	IF BASE:耐力 <= 0
		;15%で逃げるのに失敗して主人公もやられる
		IF RAND:100 < 15
			BASE:MASTER:耐力 = 0
		ENDIF
		
		;脱がされて素質発覚
		SIF FLAG:2 == 1 && (CFLAG:60 & 32) == 0
			CFLAG:60 |= 32

		;好感度低下処理
		IF BASE:MASTER:耐力 <= 0
			EXP:絶頂経験 += RAND:11 + 10
			IF CFLAG:2 > 0
				IF CFLAG:2 > 20000
					A:2 += 2000
				ELSE
					A:2 += CFLAG:2 / 10
				ENDIF
			ENDIF
			A:2 += RAND:61 + 40
		ELSE
			EXP:絶頂経験 += RAND:3 + 3
			IF CFLAG:2 > 0
				IF CFLAG:2 > 30000
					A:2 += 750
				ELSE
					A:2 += CFLAG:2 / 40
				ENDIF
			ENDIF
			A:2 += RAND:21 + 20
		ENDIF

		CFLAG:2 -= A:2
		PRINTFORMW %CALLNAME:TARGET%の好感度が{A:2}下がった

		RETURN 1
	ENDIF

	;主人公の体力0以下
	IF BASE:MASTER:耐力 <= 0
		IF CFLAG:2 > 0
			IF CFLAG:2 > 20000
				A:2 += 1000
			ELSE
				A:2 += CFLAG:2 / 20
			ENDIF
		ENDIF
		A:2 += RAND:31 + 30

		CFLAG:2 -= A:2
		PRINTFORMW %CALLNAME:TARGET%の好感度が{A:2}下がった

		RETURN 1
	ENDIF
ELSE
	;主人公のみの場合
	IF BASE:MASTER:耐力 <= 0
		RETURN 1
	ENDIF
ENDIF

GOTO START_POINT

RETURN 1

;---------------------------------------------------------
;コマンドバトル
;---------------------------------------------------------
@COMMAND_BATTLE
$START_POINT

CALL SHOW_BATTLE_STATUS
CALL SHOW_ENEMY_INFO

;パートナーの攻撃
IF TARGET >= 0
	T = TARGET
	CALL GET_BATTLE_POWER
	;魔力が切れている場合、戦闘力1/10
	SIF BASE:魔力 <= 0
		P /= 10

	;攻撃力基準値を算出
	A = P / 5

	;S-Luna用の必要能力補正
	A:1 = 0
	SIF FLAG:5 == 4
		A:1 = 1

	;ローカル変数をクリア
	VARSET LOCAL, 0
	
	REPEAT X
		LOCAL = X - COUNT
		;現在耐力が最大耐力の1/2より下の敵がいれば愛撫するの条件の一つを満たす
		SIF (Y:LOCAL > 0 && Y:LOCAL < X:LOCAL / 2)
			LOCAL:103 = 1
		;現在耐力が最大耐力の1/4より下の敵がいれば霧の効果を吸い出すの条件の一つを満たす
		SIF (Y:LOCAL > 0 && Y:LOCAL < X:LOCAL / 4)
			LOCAL:104 = 1
	REND
	
	PRINTFORML %CALLNAME:TARGET%のターンです。どうしますか？
	PRINTFORML [0] 通常攻撃
	LOCAL:0 = 1
	IF BASE:魔力 > MAXBASE:魔力 / 3
		PRINTFORML [1] 特殊攻撃
		LOCAL:1 = 1
	ENDIF
	IF BASE:魔力 < MAXBASE:魔力
		PRINTFORML [2] 魔力転換
		LOCAL:2 = 1
	ENDIF
	IF (TALENT:MASTER:脱衣制限解除 || FLAG:24 > 3) && (LOCAL:103 == 1 || V:4 == 1)
		PRINTFORML [3] 愛撫する
		LOCAL:3 = 1
	ENDIF
	IF (TALENT:MASTER:脱衣制限解除 || FLAG:24 > 3) && ABL:欲望 > (5 + A:1 * 2) && (LOCAL:104 == 1 || V:4 == 1)
		PRINTFORML [4] 霧の効果を吸い出す
		LOCAL:4 = 1
	ENDIF
	IF (TALENT:MASTER:脱衣制限解除 || FLAG:24 > 3) && ABL:欲望 > (8 + A:1 * 3) && V:4 == 1
		PRINTFORML [5] 絡み合う
		LOCAL:5 = 1
	ENDIF
	IF (TALENT:MASTER:脱衣制限解除 || FLAG:24 > 3) && V:4 == 0
		PRINTFORML [9] 自分も脱ぐ
		LOCAL:9 = 1
	ENDIF
	INPUT

	;どのコマンドを使ったかLOCAL:100に保存
	LOCAL:100 = RESULT
	;ライオットの行動制御用に保存
	SIF ITEM:852 > 0
		S:1 = LOCAL:100
	;戦闘コマンドの処理はSPECIAL_ATACK.ERBに移動
	IF LOCAL:RESULT == 1
		T = TARGET
		CALLFORM COMMAND_BATTLE_ATACK_{RESULT}
	ELSE
		RESTART
	ENDIF

	SIF V:4 == 1
		TIMES A , 1.20

	;特殊攻撃時敵の数が2人以上なら一人当たりのダメージ減少
	SIF LOCAL:100 == 1 && X >= 2
		A = A / X  * (X - X/2)
	;A（ダメージ）保存
	LOCAL:101 = A
	;LOCAL使用
	LOCAL = 0
	REPEAT X
		LOCAL = X - COUNT
		IF Y:LOCAL > 0 && A >= 0
			;敵に攻撃(全体攻撃時のためにLOCAL:101からA復帰)
			A = LOCAL:101
			A -= Z:LOCAL / 100
			SIF A < 10
				A = 10
			Y:LOCAL -= A
			PRINTFORMW %STR:LOCAL%の耐力を{A}減少させた！
			IF Y:LOCAL <= 0
				Y:LOCAL = 0
				PRINTFORMW %STR:LOCAL%は正気に戻った！
			ENDIF
			;特殊攻撃は全体攻撃に
			SIF LOCAL:100 != 1
				BREAK
		ENDIF
	REND
	IF Y:1 <= 0
		;撃退
		RETURN 1
	ENDIF
	;既に魔力が切れている場合、耐力を少し消費
	SIF BASE:魔力 <= 0
		BASE:耐力 -= MAXBASE:耐力 / 200 + 1
	;攻撃により魔力を消費
	BASE:魔力 -= MAXBASE:魔力 / 100 + 1
	;S-Lunaは更に消費
	SIF FLAG:5 == 4
		BASE:魔力 -= MAXBASE:魔力 / 100 + 1
	SIF BASE:魔力 < 0
		BASE:魔力 = 0
	
	CALL SHOW_BATTLE_STATUS
	CALL SHOW_ENEMY_INFO
	
ENDIF

;パートナーの戦闘能力を退避
P:1 = P

;あなたの戦闘能力計算
CALL GET_BATTLE_POWER_MASTER

PRINTL  

;攻撃力基準値を算出
A = P / 5

;ローカル変数をクリア
VARSET LOCAL, 0

REPEAT X
	LOCAL = X - COUNT
	;現在耐力が最大耐力の1/2より下の敵がいれば愛撫するの条件の一つを満たす
	SIF (Y:LOCAL > 0 && Y:LOCAL < X:LOCAL / 2)
		LOCAL:103 = 1
	;現在耐力が最大耐力の1/4より下の敵がいれば霧の効果を吸い出すの条件の一つを満たす
	SIF (Y:LOCAL > 0 && Y:LOCAL < X:LOCAL / 4)
		LOCAL:104 = 1
REND

PRINTFORML %CALLNAME:MASTER%のターンです。どうしますか？
PRINTFORML [0] 通常攻撃
LOCAL:0 = 1
IF TARGET >= 0
	PRINTFORML [1] %CALLNAME:TARGET%を庇う
	LOCAL:1 = 1
	PRINTFORML [2] %CALLNAME:TARGET%を回復させる
	LOCAL:2 = 1
ELSE
	IF BASE:MASTER:魔力 > MAXBASE:MASTER:魔力 / 3
		PRINTFORML [1] 特殊攻撃
		LOCAL:1 = 1
	ENDIF
	IF BASE:MASTER:魔力 < MAXBASE:MASTER:魔力
		PRINTFORML [2] 魔力転換
		LOCAL:2 = 1
	ENDIF
ENDIF
IF (TALENT:MASTER:脱衣制限解除 || FLAG:24 > 3) && (LOCAL:103 == 1 || V:5 == 1)
	PRINTFORML [3] 愛撫する
	LOCAL:3 = 1
ENDIF
IF (TALENT:MASTER:脱衣制限解除 || FLAG:24 > 3) && ABL:MASTER:技巧 > (3 + A:1 * 2) && (LOCAL:104 == 1 || V:5 == 1)
	PRINTFORML [4] 霧の効果を吸い出す
	LOCAL:4 = 1
ENDIF
IF (TALENT:MASTER:脱衣制限解除 || FLAG:24 > 3) && ABL:MASTER:技巧 > (5 + A:1 * 2) && V:5 == 1
	PRINTFORML [5] 絡み合う
	LOCAL:5 = 1
ENDIF
IF (TALENT:MASTER:脱衣制限解除 || FLAG:24 > 3) && V:5 == 0
	PRINTFORML [9] 自分も脱ぐ
	LOCAL:9 = 1
ENDIF

$INPUT_ANATA
INPUT

;どのコマンドを使ったかLOCAL:100に保存
LOCAL:100 = RESULT
;ライオットの行動制御用に保存
SIF ITEM:852 > 0
	S:2 = LOCAL:100
;戦闘コマンドの処理はSPECIAL_ATACK.ERBに移動
IF LOCAL:RESULT == 1
	T = MASTER
	CALLFORM COMMAND_BATTLE_ATACK_{RESULT}
ELSE
	GOTO INPUT_ANATA
ENDIF



SIF V:5 == 1
	TIMES A , 1.10

;特殊攻撃時敵の数が2人以上なら一人当たりのダメージ減少
SIF TARGET < 0 && LOCAL:100 == 1 && X >= 2
	A = A / X  * (X - X/2)
;A（ダメージ）保存
LOCAL:101 = A
;LOCAL使用
LOCAL = 0
;あなたの攻撃
REPEAT X
	LOCAL = X - COUNT
	IF Y:LOCAL > 0 && A >= 0
		;敵に攻撃(全体攻撃時のためにLOCAL:101からA復帰)
		A = LOCAL:101
		A -= Z:LOCAL / 100
		SIF A < 10
			A = 10
		Y:LOCAL -= A
		PRINTFORMW %STR:LOCAL%の耐力を{A}減少させた！
		IF Y:LOCAL <= 0
			Y:LOCAL = 0
			PRINTFORMW %STR:LOCAL%は正気に戻った！
		ENDIF
		;特殊攻撃は全体攻撃に
		SIF LOCAL:100 != 1
			BREAK
	ENDIF
REND
IF Y:1 <= 0
	;撃退
	RETURN 1
ENDIF



PRINTL  
;ライオットドールの行動
;耐力が残っている時OR神霊サーキット時
IF ITEM:852 > 0 && (R:3 > 0 || TFLAG:852 == 8) && TFLAG:852 < 99
	PRINTFORML %ITEMNAME:852%のターンです。
	;攻撃力基準値を算出
	A = T:1 / 5

	;どのコマンドを使うかS:1、S:2で制御（通常、特攻:攻撃:防御＝1:6:3）
	S = RAND:10
	IF TARGET >= 0
		;攻撃寄り要素（パートナー）
		IF S:1 == 1
			S -= 2
			;同時にあなたも攻撃寄り
			SIF S:2 == 0 || S:2 == 9
				S -= 1
		ENDIF
		;攻撃寄り要素（あなた）
		SIF S:2 == 0 || S:2 == 1 || S:2 == 9
				S -= 1
		;警戒寄り要素（パートナー）
		IF S:1 == 2 || S:1 == 9
			S += 1
			;魔力転換は更に
			SIF S:1 == 2
				S += 1
		ENDIF
		;警戒寄り要素（あなた）
		IF S:2 == 2
			S += 1
			;同時にパートナーも警戒寄り
			SIF S:1 == 2 || S:1 == 9
				S += 1
		ENDIF
	;パートナーなし
	ELSE
		;攻撃寄り要素
		IF S:2 == 1
			S -= 4
		;警戒寄り
		ELSEIF S:2 == 2
			S += 5
		ENDIF
	ENDIF
	;行動決定
	;[1] 一斉掃射
	IF S < 1
		PRINTFORML %ITEMNAME:852%の一斉掃射！
		;無制限のため威力弱め
		TIMES A , 1.35
		;紅魔サーキット
		SIF TFLAG:852 == 1
			TIMES A , 1.50
		S:3 = 1
	;[0]通常攻撃
	ELSEIF S < 7
		PRINTFORML %ITEMNAME:852%は相手に攻撃した！
		S:3 = 2
	;[2]防御
	ELSE
		IF TARGET >= 0
			PRINTFORMW %ITEMNAME:852%は%CALLNAME:TARGET%への攻撃に対し警戒している
			;永夜サーキット
			IF TFLAG:852 == 3
				;パートナーの耐力があなたより低い（かつダメージを受けている）
				IF BASE:MASTER:耐力 > BASE:耐力 && MAXBASE:耐力 > BASE:耐力
					PRINTFORML ヒーリング効果発動！
					PRINTFORML %CALLNAME:TARGET%の身体を侵していた霧が効力を失い徐々に抜けていく……
					H = MAXBASE:耐力 / 15 + 1
					CALL HEAL_CAP
					PRINTFORMW %CALLNAME:TARGET%の耐力が{H}回復した！
					BASE:耐力 += H
					SIF BASE:耐力 > MAXBASE:耐力
						BASE:耐力 = MAXBASE:耐力
				;あなたの耐力がパートナーより低い（かつダメージを受けている）
				ELSEIF BASE:MASTER:耐力 < BASE:耐力 && MAXBASE:MASTER:耐力 > BASE:MASTER:耐力
					PRINTFORML ヒーリング効果発動！
					PRINTFORML %CALLNAME:MASTER%の身体を侵していた霧が効力を失い徐々に抜けていく……
					H = MAXBASE:MASTER:耐力 / 15 + 1
					CALL HEAL_CAP
					PRINTFORMW %CALLNAME:MASTER%の耐力が{H}回復した！
					BASE:MASTER:耐力 += H
					SIF BASE:MASTER:耐力 > MAXBASE:MASTER:耐力
						BASE:MASTER:耐力 = MAXBASE:MASTER:耐力
				ENDIF
			ENDIF
		;パートナーなし
		ELSE
			PRINTFORMW %ITEMNAME:852%は%CALLNAME:MASTER%への攻撃に対し警戒している
			;永夜サーキット
			IF TFLAG:852 == 3 && MAXBASE:MASTER:耐力 > BASE:MASTER:耐力
				PRINTFORML ヒーリング効果発動！
				PRINTFORML %CALLNAME:MASTER%の身体を侵していた霧が効力を失い徐々に抜けていく……
				H = MAXBASE:MASTER:耐力 / 15 + 1
				CALL HEAL_CAP
				PRINTFORMW %CALLNAME:MASTER%の耐力が{H}回復した！
				BASE:MASTER:耐力 += H
				SIF BASE:MASTER:耐力 > MAXBASE:MASTER:耐力
					BASE:MASTER:耐力 = MAXBASE:MASTER:耐力
			ENDIF
		ENDIF
		S:3 = 3
		A = -1
		V:7 = 1
	ENDIF
	
	;特殊攻撃時敵の数が2人以上なら一人当たりのダメージ減少
	SIF S:3 == 1 && X >= 2
		A = A / X  * (X - X/2)
	;A（ダメージ）保存
	LOCAL:101 = A
	;LOCAL使用
	LOCAL = 0
	REPEAT X
		LOCAL = X - COUNT
		IF Y:LOCAL > 0 && A >= 0
			;敵に攻撃(全体攻撃時のためにLOCAL:101からA復帰)
			A = LOCAL:101
			A -= Z:LOCAL / 100
			SIF A < 10
				A = 10
			Y:LOCAL -= A
			PRINTFORMW %STR:LOCAL%の耐力を{A}減少させた！
			;紅魔サーキット通常攻撃時
			IF S:3 == 2 && TFLAG:852 == 1 && R:2 > 0
				A /= 4
				R:2 -= A
				PRINTFORML %ITEMNAME:852%の耐力が{A}回復した！
				SIF R:2 < 0
					R:2 = 0
			ENDIF
			IF Y:LOCAL <= 0
				Y:LOCAL = 0
				PRINTFORMW %STR:LOCAL%は正気に戻った！
			ENDIF
			;特殊攻撃は全体攻撃に
			SIF S:3 != 1
				BREAK
		ENDIF
	REND
	IF Y:1 <= 0
		;撃退
		RETURN 1
	ENDIF
ENDIF

;行動をリセット
S:3 = 0
;退避させておいたパートナーの戦闘能力を元に戻す
P = P:1

PRINTL  

;敵の攻撃
REPEAT X
	;Cに何人目の敵か保存
	C = COUNT + 1
	IF Y:C > 0 && BASE:MASTER:耐力 > 0 && (TARGET < 0 || (TARGET >= 0 && BASE:耐力 > 0))
		A = Z:C / 5
		CALL ENEMY_ATTACK
		PRINTL  
	ENDIF
REND

;ライオットドール
;ダメージが最大耐力を上回った時（神霊サーキット以外）
SIF R:2 > R:1 && TFLAG:852 != 8 && TFLAG:852 < 99 && R:3 > 0
	PRINTFORMW %ITEMNAME:852%は動かなくなった
;ライオットドールの表示（連れて行っていて、所持かつ復活待ちでない）
IF ITEM:852 > 0 && ITEM:852 < 51 && TFLAG:852 < 99
	;最大耐力の設定（MASTERの1/3 * レベル補正）
	R:1 = MAXBASE:MASTER:耐力 / 3
	R:1 *= (100 + ITEM:852) / 100
	;サーキットあり
	;冥界サーキット（耐力2倍）
	IF TFLAG:852 == 2
		TIMES R:1 , 2.00
	;聖輦サーキット（耐力1.5倍）
	ELSEIF TFLAG:852 == 7
		TIMES R:1 , 1.50
	ENDIF

	;現在の耐力（R:2は受けたダメージ）
	R:3 = R:1 - R:2
	;耐力が0以下かつ神霊サーキットを装着していない
	IF R:3 <= 0 && TFLAG:852 != 8
		;永夜サーキット装着（復活待ち処理へ）
		IF TFLAG:852 == 3 && ITEM:852 < 51
			ITEM:852 += 50
		;消失判定（最大耐力以上のオーバーダメージで消失）
		ELSEIF R:1 + R:3 < 0
			ITEM:852 = 0
			;装着サーキットも消失
			IF TFLAG:852
				ITEM:(853 + TFLAG:852) -= 1
				TFLAG:852 = 0
			ENDIF
			FLAG:44 = 0
		;壊れたドールに置換（レベル保持）
		ELSE
			ITEM:853 = ITEM:852
			ITEM:852 = 0
			SIF TFLAG:852
				TFLAG:852 = 0
		ENDIF
	ENDIF
ENDIF
;永夜サーキット
IF TFLAG:852 == 3 && R:2 < R:1 && R:2 > 0
	A = RAND:666 + 1
	PRINTFORMW %ITEMNAME:852%は永夜サーキットの機能で耐力が{A}回復した
	R:2 -= A
	SIF R:2 < 0
		R:2 = 0
ENDIF
SIF V:7 > 0
	V:7 = 0


;減少好感度記憶用
A:2 = 0

IF TARGET >= 0
	;パートナーの耐力0以下
	IF BASE:耐力 <= 0
		;15%で逃げるのに失敗して主人公もやられる
		IF RAND:100 < 15
			BASE:MASTER:耐力 = 0
		ENDIF

		;好感度低下処理
		IF BASE:MASTER:耐力 <= 0
			EXP:絶頂経験 += RAND:11 + 10
			IF CFLAG:2 > 0
				IF CFLAG:2 > 20000
					A:2 += 2000
				ELSE
					A:2 += CFLAG:2 / 10
				ENDIF
			ENDIF
			A:2 += RAND:61 + 40
		ELSE
			EXP:絶頂経験 += RAND:3 + 3
			IF CFLAG:2 > 0
				IF CFLAG:2 > 30000
					A:2 += 750
				ELSE
					A:2 += CFLAG:2 / 40
				ENDIF
			ENDIF
			A:2 += RAND:21 + 20
		ENDIF

		CFLAG:2 -= A:2
		PRINTFORMW %CALLNAME:TARGET%の好感度が{A:2}下がった

		RETURN 1
	ENDIF

	;主人公の体力0以下
	IF BASE:MASTER:耐力 <= 0
		IF CFLAG:2 > 0
			IF CFLAG:2 > 20000
				A:2 += 1000
			ELSE
				A:2 += CFLAG:2 / 20
			ENDIF
		ENDIF
		A:2 += RAND:31 + 30

		CFLAG:2 -= A:2
		PRINTFORMW %CALLNAME:TARGET%の好感度が{A:2}下がった

		RETURN 1
	ENDIF
ELSE
	;主人公の体力0以下
	IF BASE:MASTER:耐力 <= 0
		RETURN 1
	ENDIF
ENDIF

GOTO START_POINT

RETURN 1

;---------------------------------------------------------
;敵の攻撃
;---------------------------------------------------------
@ENEMY_ATTACK
;25%で主人公が強制的に庇う
;ライオットの警戒が無い場合
SIF RAND:4 == 0 && V:7 == 0
	V:6 |= 2

R = RAND:7
;S-Lunaは攻撃が激しくなる
SIF FLAG:5 == 4
	R += 1
IF R <= 5
	IF TARGET >= 0
		PRINTFORM %STR:C%は%CALLNAME:TARGET%
	ELSE
		PRINTFORM %STR:C%は%CALLNAME:MASTER%
	ENDIF
	IF R == 0
		PRINTFORML へと向けて、霧の力を込めた魔力弾を飛ばしてきた！
		TIMES A , 0.90
	ELSEIF R == 1
		PRINTFORML に直接触れて、霧の力を流し込んできた！
		TIMES A , 0.95
	ELSEIF R == 2
		PRINTFORML の全身を愛撫してきた！
		TIMES A , 1.00
	ELSEIF R == 3
		PRINTFORML の胸を揉みしだいてきた！
		TIMES A , 1.05
	ELSEIF R == 4
		PRINTFORML の秘所に触れ、強く刺激してきた！
		TIMES A , 1.10
	ELSEIF R == 5
		;S-Lunaはいつでもクリティカル
		IF FLAG:5 == 4
			IF V:4 == 1
				PRINTFORML の陰核を噛んできた！
			ELSE
				PRINTFORM の秘芯を下着の上から捏ねまわした！
			ENDIF
			TIMES A , 1.50
		;脱いでると、20%でクリティカル
		ELSEIF RAND:100 < 20 && V:4 == 1
			PRINTFORML の陰核を噛んできた！
			TIMES A , 1.50
		ELSE
			PRINTFORM の秘芯を
			SIF V:4 == 0
				PRINTFORM 下着の上から
			PRINTFORML 捏ねまわした！
			TIMES A , 1.15
		ENDIF
	ENDIF
ELSE
	;霊撃かスペルカードか判断し、スペルカードなら地の文表示
	CALL SPELL_CARD_NAME_1
	IF TSTR:(W:C) != ""
		IF TARGET >= 0
			PRINTFORML %STR:C%は%CALLNAME:TARGET%へ向けて、必殺技を繰り出した！
		ELSE
			PRINTFORML %STR:C%は%CALLNAME:MASTER%へ向けて、必殺技を繰り出した！
		ENDIF
	ENDIF
	;敵キャラのスペルカード発動（ARGは使用者で、0がMASTER、1がTARGET、2が敵キャラ）
	CALL SPELL_CARD_ATACK, 2
	TIMES A , 5.00
ENDIF

;生理中の場合被ダメージアップ
SIF TARGET >= 0 && TALENT:生理 && (FLAG:62 & 1)
	TIMES A , 1.20

B = 0
;Phモード限定の恋慕ボーナス
IF FLAG:5 == 10 || TALENT:MASTER:恋慕ボーナス || TALENT:MASTER:恋慕ボーナス強化
	REPEAT CHARANUM
		SIF TALENT:COUNT:恋慕
			B += 1
	REND
	SIF TALENT:MASTER:恋慕ボーナス強化
		TIMES B , 2.00
ENDIF

;戦闘力のダメージ減少への影響にキャップをかける
LOCAL = P / 100 * (1 + B)
;100000を超えた分は25%OFF
SIF LOCAL >= 100000
	LOCAL = 100000 + (LOCAL - 100000) * 3 / 4
;補正後、1000000を超えていたらその分を更に50%OFF
SIF LOCAL >= 1000000
	LOCAL = 1000000 + (LOCAL - 1000000) / 2
;補正後、10000000を超えていたらその分を更に75%OFF
SIF LOCAL >= 10000000
	LOCAL = 10000000 + (LOCAL - 10000000) / 4
;補正後、100000000を超えていたらその分を更に50%OFF
SIF LOCAL >= 100000000
	LOCAL = 100000000 + (LOCAL - 100000000) / 2
;補正後、1000000000を超えていたらその分を更に90%OFF
SIF LOCAL >= 1000000000
	LOCAL = 1000000000 + (LOCAL - 1000000000) / 10

A -= LOCAL
SIF A < 1
	A = 10 + RAND:10
CALL DAMAGE_CAP


;主人公が庇った場合のダメージ減少を先に処理
IF V:6 & 1
	TIMES A , 0.50
ELSEIF V:6
	TIMES A , 0.75
ENDIF

;地霊サーキットは1/5の確率で避けさせる
IF TFLAG:852 == 6 && RAND:5 < 1 && R:2 < R:1
	PRINTFORML %ITEMNAME:852%は%STR:C%の行動を読んだ！
	IF TARGET >= 0
		IF V:7 == 0
			PRINTFORM %CALLNAME:TARGET%は
		ELSE
			PRINTFORM %ITEMNAME:852%は
		ENDIF
	ELSE
		PRINTFORM %CALLNAME:MASTER%は
	ENDIF
	PRINTFORMW %STR:C%の攻撃を避けきった！
ELSEIF TARGET >= 0
	;聖輦サーキットかつピンチ時（ピンチ以外に、耐久が0になるダメージの場合は50%の確率）
	IF TFLAG:852 == 7 && (BASE:耐力 < MAXBASE:耐力 / 4 || (BASE:耐力 < A && RAND:2 < 1))
		SIF V:7 == 1
			TIMES A , 0.75
		PRINTFORML 聖輦サーキット発動！
		PRINTFORML %ITEMNAME:852%は%CALLNAME:TARGET%を庇った！
		R:2 += A
		PRINTFORMW %ITEMNAME:852%の耐力が{A}減少した！
	;主人公が庇った場合
	ELSEIF V:6 && V:7 == 0
		PRINTFORML %CALLNAME:MASTER%は%CALLNAME:TARGET%を庇って、影響を少し軽減させた！
		BASE:耐力 -= A
		BASE:MASTER:耐力 -= A
		PRINTFORMW %CALLNAME:MASTER%と%CALLNAME:TARGET%の耐力が{A}減少した！
	;ライオットドールが警戒した場合（耐力が残っている時OR神霊サーキット時）
	ELSEIF V:7 == 1 && (R:2 < R:1 || TFLAG:852 == 8)
		PRINTFORML %ITEMNAME:852%は%CALLNAME:TARGET%を庇った！
		R:2 += A
		PRINTFORMW %ITEMNAME:852%の耐力が{A}減少した！
		;守矢サーキット
		IF TFLAG:852 == 5 && BASE:魔力 < MAXBASE:魔力
			A /= 3
			;最大回復率は50%
			SIF A > MAXBASE:魔力 / 2
				A = MAXBASE:魔力 / 2
			PRINTFORML 守矢サーキット展開！
			PRINTFORMW %ITEMNAME:852%は受けた霧を転換し、%CALLNAME:TARGET%の魔力を{A}回復させた！
			;魔力を回復
			BASE:魔力 += A
			SIF BASE:魔力 > MAXBASE:魔力
				BASE:魔力 = MAXBASE:魔力
		ENDIF
		;神霊サーキットの場合
		IF R:2 > R:1 && TFLAG:852 == 8
			PRINTFORMW %ITEMNAME:852%は倒れた
			PRINTFORMW が、吊り上げられたかのようにユラリと立ち上がった
		ENDIF
	;それ以外
	ELSE
		BASE:耐力 -= A
		PRINTFORMW %CALLNAME:TARGET%の耐力が{A}減少した！
	ENDIF

;	;ゆかり様は魔力も削ってくる
;	IF FLAG:25 == 666 && BASE:魔力 > 0 && (BASE:耐力 > 0 && BASE:MASTER:耐力 > 0)
;		A += P / (100 + RAND:10)
;		CALL DAMAGE_CAP
;		BASE:魔力 -= A
;		SIF BASE:魔力 <= 0
;			BASE:魔力 = 0
;		PRINTFORMW %CALLNAME:TARGET%の魔力が{A}減少した！
;	ENDIF
;パートナーなし
ELSE
	;聖輦サーキット
	IF TFLAG:852 == 7 && BASE:MASTER:耐力 < MAXBASE:MASTER:耐力 / 4
		SIF V:7 == 1
		TIMES A , 0.75
		PRINTFORML 聖輦サーキット発動！
		PRINTFORML %ITEMNAME:852%は%CALLNAME:MASTER%を庇った！
		R:2 += A
		PRINTFORMW %ITEMNAME:852%の耐力が{A}減少した！
	;庇った場合
	ELSEIF V:7 == 1 && (R:2 < R:1 || TFLAG:852 == 8)
		PRINTFORML %ITEMNAME:852%は%CALLNAME:MASTER%を庇った！
		R:2 += A
		PRINTFORMW %ITEMNAME:852%の耐力が{A}減少した！
		;守矢サーキット
		IF TFLAG:852 == 5 && BASE:MASTER:魔力 < MAXBASE:MASTER:魔力
			A /= 3
			;最大回復率は50%
			SIF A > MAXBASE:魔力 / 2
				A = MAXBASE:魔力 / 2
			PRINTFORML 守矢サーキット展開！
			PRINTFORMW %ITEMNAME:852%は受けた霧を転換し、%CALLNAME:MASTER%の魔力を{A}回復させた！
			;魔力を回復
			BASE:MASTER:魔力 += A
			SIF BASE:MASTER:魔力 > MAXBASE:MASTER:魔力
				BASE:MASTER:魔力 = MAXBASE:MASTER:魔力
		ENDIF
		;神霊サーキットの場合
		IF R:2 > R:1 && TFLAG:852 == 8
			PRINTFORMW %ITEMNAME:852%は倒れた
			PRINTFORMW が、吊り上げられたかのようにユラリと立ち上がった
		ENDIF
	ELSEIF R:2 < R:1 && RAND:2 < 1 && TFLAG:852 < 99
		PRINTFORML %ITEMNAME:852%は%CALLNAME:MASTER%を庇って、影響を少し軽減させた！
		TIMES A , 0.75
		R:2 += A
		BASE:MASTER:耐力 -= A
		PRINTFORMW %CALLNAME:MASTER%と%ITEMNAME:852%の耐力が{A}減少した！
		;守矢サーキット
		IF TFLAG:852 == 5 && BASE:MASTER:魔力 < MAXBASE:MASTER:魔力
			A /= 3
			;最大回復率は50%
			SIF A > MAXBASE:魔力 / 2
				A = MAXBASE:魔力 / 2
			PRINTFORML 守矢サーキット展開！
			PRINTFORMW %ITEMNAME:852%は受けた霧を転換し、%CALLNAME:MASTER%の魔力を{A}回復させた！
			;魔力を回復
			BASE:MASTER:魔力 += A
			SIF BASE:MASTER:魔力 > MAXBASE:MASTER:魔力
				BASE:MASTER:魔力 = MAXBASE:MASTER:魔力
		ENDIF
		;神霊サーキットの場合
		IF R:2 > R:1 && TFLAG:852 == 8
			PRINTFORMW %ITEMNAME:852%は倒れた
			PRINTFORMW が、吊り上げられたかのようにユラリと立ち上がった
		ENDIF
	ELSE
		BASE:MASTER:耐力 -= A
		PRINTFORMW %CALLNAME:MASTER%の耐力が{A}減少した！
	ENDIF
ENDIF

SIF V:6 > 1
	V:6 -= 2
RETURN 1

;---------------------------------------------------------
;被ダメにキャップをかける。戦闘能力のインフレ対策
;ダメージ予定値はAに入っているはずなので、その値を補正
;---------------------------------------------------------
@DAMAGE_CAP
;S-LunaのStage3以降は補正を緩めに（＝被ダメ増加）
IF FLAG:5 == 4 && FLAG:24 > 3
	;400を超えた分は20%OFF
	SIF A >= 400
		A = 400 + (A - 400) * 4 / 5
	;補正後、800を超えていたらその分を更に33%OFF
	SIF A >= 800
		A = 800 + (A - 800) * 2 / 3
	;補正後、1500を超えていたらその分を更に50%OFF
	SIF A >= 1500
		A = 1500 + (A - 1500) / 2
	;補正後、3000を超えていたらその分を更に50%OFF
	SIF A >= 3000
		A = 3000 + (A - 3000) / 2
	;補正後、10000を超えていたらその分を更に90%OFF
	SIF A >= 10000
		A = 10000 + (A - 10000) / 10
ELSE
	;400を超えた分は25%OFF
	SIF A >= 400
		A = 400 + (A - 400) * 3 / 4
	;補正後、800を超えていたらその分を更に50%OFF
	SIF A >= 800
		A = 800 + (A - 800) / 2
	;補正後、1500を超えていたらその分を更に75%OFF
	SIF A >= 1500
		A = 1500 + (A - 1500) / 4
	;補正後、3000を超えていたらその分を更に50%OFF
	SIF A >= 3000
		A = 3000 + (A - 3000) / 2
	;補正後、10000を超えていたらその分を更に90%OFF
	SIF A >= 10000
		A = 10000 + (A - 10000) / 10
ENDIF

RETURN 1

;---------------------------------------------------------
;回復量にキャップをかける
;回復予定値をHに入れておき、その値を補正
;---------------------------------------------------------
@HEAL_CAP
;400を超えた分は25%OFF
SIF H >= 400
	H = 400 + (H - 400) * 3 / 4
;補正後、800を超えていたらその分を更に50%OFF
SIF H >= 800
	H = 800 + (H - 800) / 2
;補正後、1500を超えていたらその分を更に75%OFF
SIF H >= 1500
	H = 1500 + (H - 1500) / 4
;補正後、3000を超えていたらその分を更に50%OFF
SIF H >= 3000
	H = 3000 + (H - 3000) / 2
;補正後、10000を超えていたらその分を更に90%OFF
SIF H >= 10000
	H = 10000 + (H - 10000) / 10

RETURN 1

;---------------------------------------------------------
;従者判定
;---------------------------------------------------------
@ATTENDANT
;W:2から順に従者の番号を代入していく処理。
;組み合わせが決まっている2人組、3人組の処理にLOCAL使用
VARSET LOCAL, 0
IF FLAG:25 == 1
	;ショウタにはアイがついてくる
	W:2 = 2
ENDIF

;従者追加　W:2～に数値が入っていれば追加。
FOR LOCAL, 2, 10
	IF W:LOCAL > 0
		T = W:LOCAL
		CALL ADD_ATTENDANT
	ENDIF
NEXT

;---------------------------------------------------------
;仲間にならない従者判定（幻影等）
;---------------------------------------------------------
@ATTENDANT_2
IF FLAG:5 == 4 && FLAG:24 == 6 && FLAG:27 >= 20 && FLAG:25 == 1
	;S-Lunaラスボスゆかりは分身する
	T = 1
	CALL ADD_ATTENDANT
	T = 1
	CALL ADD_ATTENDANT
	STR:2 = ゆかり(幻影)
	STR:3 = ゆかり(幻影)
ELSEIF FLAG:5 == 4 && FLAG:24 == 6 && FLAG:27 >= 20 && FLAG:25 == 2
	;S-Lunaラスボスマキは分身する
	T = 2
	CALL ADD_ATTENDANT
	T = 2
	CALL ADD_ATTENDANT
	STR:2 = マキ(幻影)
	STR:3 = マキ(幻影)
ENDIF


;---------------------------------------------------------
;従者追加
;---------------------------------------------------------
@ADD_ATTENDANT
;Tの従者追加
CALL GET_EXISTANCE
IF E
	;何もしない
ELSE
	X += 1
	N += 1
	CALL GET_CHARA_NAME
	CALL GET_STUFF
	Y:N = 500 + S * 5
	Z:N = 200 + S * 2
ENDIF

RETURN 1

;---------------------------------------------------------
;戦闘終了時の共通処理
;---------------------------------------------------------
@BATTLE_COMMON_END
IF (TARGET >= 0 && BASE:耐力 > 0 && BASE:MASTER:耐力 > 0) || (TARGET < 0 && BASE:MASTER:耐力 > 0)
	;勝利時の共通部分があればここで処理
	;雑魚敵撃破数記録
	FLAG:43 += X
ELSEIF TARGET < 0 && BASE:MASTER:耐力 <= 0
	;主人公だけで敗北
	PRINTFORMW %CALLNAME:MASTER%は%STR:1%に予想外の攻撃を受け、気絶してしまった！
	PRINTFORMW %CALLNAME:MASTER%が意識を失った事で、霧の影響が一気に強くなり始める……
	PRINTFORML  
	PRINTFORMW  
	PRINTFORMW 数時間後、%STR:1%達が完全に満足してようやく解放された%CALLNAME:MASTER%は、ヘトヘトになりながら家路についた……
ELSEIF BASE:耐力 <= 0 && BASE:MASTER:耐力 <= 0
	;全滅
	TFLAG:600 = 14
	CALL KOJO_JUN
	TFLAG:600 = 12
	CALL KOJO_JUN
	;Phモード限定
	IF FLAG:5 == 10
		PRINTFORMW %CALLNAME:TARGET%は霧に飲まれかけているようだ…
		CFLAG:17 = 1
		CFLAG:19 += 1
	ENDIF
ELSEIF BASE:耐力 <= 0
	;パートナーがヤられた
	TFLAG:600 = 13
	CALL KOJO_JUN
	TFLAG:600 = 12
	CALL KOJO_JUN
	;Phモード限定
	IF FLAG:5 == 10 && RAND:100 <= 85 + (TALENT:キス未経験 * 5) - (TALENT:恋人 * 10) - (TALENT:恋慕 * 10) - (TALENT:親愛 * 15)
		PRINTFORMW %CALLNAME:TARGET%は霧に飲まれかけているようだ…
		CFLAG:17 = 1
		CFLAG:19 += 1
	ELSEIF FLAG:5 == 10
		CFLAG:20 += 10
	ENDIF
ELSEIF BASE:MASTER:耐力 <= 0
	;主人公がやられた
	TFLAG:600 = 15
	CALL KOJO_JUN
	TFLAG:600 = 12
	CALL KOJO_JUN
ENDIF

;---------------------------------------------------------
;戦闘経験と報酬の補正共通処理
;CALL前にM（報酬の基本値）とX（元は敵の数。戦闘経験の基本値）を設定しておく
;Xは何も設定しない場合は敵の数のまま
;---------------------------------------------------------
@BATTLE_COMMON_EM
;戦闘経験の補正と取得
SIF ABL:MASTER:戦闘経験ボーナス > 0
	X += ABL:MASTER:戦闘経験ボーナス
PRINTFORML %EXPNAME:95%+{X}
SIF TARGET >= 0
	EXP:戦闘経験 += X
;主人公にも経験だけは入る
EXP:MASTER:戦闘経験 += X
;助手にも経験だけは入る
SIF ASSI >= 0
	EXP:ASSI:戦闘経験 += X

;報酬の補正
;獲得資金増加ボーナス
IF ABL:MASTER:獲得資金増加 == 1
	TIMES M , 1.50
ELSEIF ABL:MASTER:獲得資金増加 > 1
	M *= ABL:MASTER:獲得資金増加
ENDIF

