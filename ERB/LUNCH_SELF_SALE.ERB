@LUNCH_SELF_SALE
;主人が自分で弁当を作って売るオプション。
;-------------------------------------------------
;K = 料理の種類
;L = 料理の値段の補正を決定
;M = 料理の値段
;N = 料理の値段の基礎
;O = 習得の珠増加量
;P = 乱数処理用
;R = 料理の評価
;-------------------------------------------------
K = RAND:16
L = 0
M = 0
N = 0
O = 0
P = 0


L = (ABL:MASTER:技巧 + ABL:MASTER:料理技能 * 4 + 30) / 6

;EXP:調理経験をみる
IF EXP:MASTER:90 < EXPLV:1
	TIMES L , 0.80
ELSEIF EXP:MASTER:90 < EXPLV:2
	TIMES L , 1.00
ELSEIF EXP:MASTER:90 < EXPLV:3
	TIMES L , 1.20
ELSEIF EXP:MASTER:90 < EXPLV:4
	TIMES L , 1.40
ELSEIF EXP:MASTER:90 < EXPLV:5
	TIMES L , 1.70
ELSE
	TIMES L , 2.00
ENDIF

;献身的
SIF TALENT:MASTER:63
	TIMES L , 1.20

;料理の種類決定
LOCAL:0 = MASTER
LOCAL:1 = TARGET
TARGET = MASTER
CALL LUNCH_MENU
MASTER = LOCAL:0
TARGET = LOCAL:1

;料理素質で補正をかける
IF ABL:MASTER:料理素質 == 2
	TIMES N , 1.75
ELSEIF ABL:MASTER:料理素質 == 1
	TIMES N , 1.50
ELSEIF ABL:MASTER:料理素質 == -1
	TIMES N , 0.75
ENDIF

;謎の魅力
SIF TALENT:MASTER:92
	TIMES N , 1.10

;魅力
SIF TALENT:MASTER:113
	TIMES N , 1.10

;人気
SIF TALENT:MASTER:126
	TIMES N , 1.30

;オトコ
SIF TALENT:MASTER:122 && ABL:MASTER:料理技能 < 3
	TIMES N , 0.75

BASE:MASTER:4 -= 400

;R = 料理の出来栄え
R = (RAND:100 + ABL:MASTER:料理技能 + 1) / 10
SIF R <= 0
	R = 1
SIF R >= 10
	R = 10

M = L * N * R * 3

;獲得資金増加ボーナス
IF ABL:MASTER:獲得資金増加 == 1
	TIMES M , 1.50
ELSEIF ABL:MASTER:獲得資金増加 > 1
	M *= ABL:MASTER:獲得資金増加
ENDIF

;値段が高くなり過ぎないように補正をかける
;75000を超えた分は25%OFF
SIF M >= 75000
	M = 75000 + (M - 75000) / 4 * 3
;補正後、150000を超えていたらその分を更に50%OFF
SIF M >= 150000
	M = 150000 + (M - 150000) / 2
;補正後、300000を超えていたらその分を更に75%OFF
SIF M >= 300000
	M = 300000 + (M - 300000) / 4
;補正後、500000を超えていたらその分を更に90%OFF
SIF M >= 500000
	M = 500000 + (M - 500000) / 10
;補正後、1000000を超えていたらその分を更に90%OFF
SIF M >= 1000000
	M = 1000000 + (M - 1000000) / 10

;地の文カット
SIF CFLAG:99 == 1 || FLAG:6 == 0
	GOTO LUNCH_EVALUATE
PRINTL 
PRINTFORMW %CALLNAME:MASTER%は自分で料理を作った
PRINTL 
PRINTW      ･
PRINTW      ･
PRINTW      ･
PRINTW      !
PRINTL 
PRINTFORM %CALLNAME:MASTER%は
SIF TALENT:153
	PRINTFORM 愛情たっぷりの
IF R <= 2
	PRINT 微妙な
ELSEIF R <= 4
	PRINT 普通の
ELSEIF R <= 6
	PRINT 美味しい
ELSEIF R <= 8
	PRINT 非常に美味しい
ELSEIF R <= 10
	PRINT 最高の味がする
ENDIF
PRINTFORMW %TSTR:1%を作った。
PRINTL      ･
PRINTL      ･
PRINTL      ･
PRINTL 

PRINTFORML %CALLNAME:MASTER%が作った料理には{M}の買値が付いた。
$LUNCH_EVALUATE
MONEY += M
PRINTFORML 所持金 {M}増加


IF R >= 10
	PRINTFORML %EXPNAME:90% ＋5
	EXP:MASTER:90 += 5
ELSEIF R >= 5
	PRINTFORML %EXPNAME:90% ＋4
	EXP:MASTER:90 += 4
ELSE
	PRINTFORML %EXPNAME:90% ＋3
	EXP:MASTER:90 += 3
ENDIF
;好感度増加（浪費癖/10をマイナスし、Aがマイナスになる場合好感度は増加しない）
A = RAND:11 + 15 - (ABL:浪費癖 / 10)
IF A > 0
	CFLAG:2 += A
	PRINTFORMW 好感度が {A} 増えた
ENDIF
O = ABL:MASTER:料理技能 + (1 + RAND:5)
SIF ABL:MASTER:料理技能 < 9999
	FLAG:4 += O + R

IF ABL:MASTER:料理技能 < 5 && FLAG:4 >= 25
	TFLAG:14 = 1
ELSEIF ABL:MASTER:料理技能 < 20 && FLAG:4 >= 50
	TFLAG:14 = 1
ELSEIF ABL:MASTER:料理技能 < 50 && FLAG:4 >= 100
	TFLAG:14 = 1
ELSEIF ABL:MASTER:料理技能 < 60 && FLAG:4 >= 150
	TFLAG:14 = 1
ELSEIF ABL:MASTER:料理技能 < 70 && FLAG:4 >= 250
	TFLAG:14 = 1
ELSEIF ABL:MASTER:料理技能 < 90 && FLAG:4 >= 500
	TFLAG:14 = 1
ELSEIF ABL:MASTER:料理技能 < 1000 && FLAG:4 >= 1000
	TFLAG:14 += 10
ELSEIF ABL:MASTER:料理技能 >= 1000 && FLAG:4 >= 10000
	TFLAG:14 += 10
ENDIF

IF TFLAG:14 == 1 && ABL:MASTER:料理技能 < 9999
	ABL:MASTER:料理技能 += 1
	PRINTL  
	PRINTFORML %CALLNAME:MASTER%の%ABLNAME:90%のレベルが{ABL:MASTER:料理技能}になりました。
	FLAG:4 = 0
	TFLAG:14 = 0
ENDIF
WAIT

;休憩フラグを立てる
FLAG:0 = 1
BEGIN TURNEND
RETURN 1
