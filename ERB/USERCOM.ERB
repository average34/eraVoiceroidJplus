@CALLTRAINEND
TFLAG:CALLTRAIN = 0

@SHOW_USERCOM
VARSET Q, -1
VARSET C, 0
;CALLTRAIN時は表示しない
SIF TFLAG:CALLTRAIN
	RETURN 0
;パートナーが[親愛]を持っている場合、異常経験が付くコマンドを表示するコンフィグがONの場合
;パートナーが[親愛]を持っていたら異常経験フィルタを一時的にOFFにする
LOCAL:2 = 0
IF TALENT:親愛 && (FLAG:61 & 2)
	;異常経験フィルタが元々OFFかLOCAL:2に記憶。コマンド表示後戻す
	SIF (FLAG:61 & 1) == 0
		LOCAL:2 = 1
	FLAG:61 |= 1
ENDIF

LOCAL:1 = -1
;コマンドを全てチェック
FOR LOCAL, 0, 1000
	;名前の長さが0の場合、ないものとみなす
	SIF STRLENS(TRAINNAME:LOCAL) == 0
		CONTINUE
	;戦闘訓練(非ウフフモード時の1番目のコマンド)の初期位置を変える処理
	;デフォは100、コンフィグで設定している場合、0になる
	IF LOCAL == 300 && TFLAG:44 == 0
		IF FLAG:23 & 8388608
			LOCAL:1 = -1
		ELSE
			LOCAL:1 = 99
		ENDIF
	ENDIF
	LOCAL:1 += 1
	;異常経験フィルタがONの時、異常経験をとるかどうか判定し、通ったら潰す
	IF (FLAG:61 & 1) == 0
		SIF ABNOMALCOMABLE(LOCAL)
			CONTINUE
	ENDIF
	;コマンドフィルタ
	;共通設定フラグONの時
	IF GET_COM_FILTERING(1001, TARGET)
		SIF GET_COM_FILTERING(LOCAL, MASTER)
			CONTINUE
	ELSE
		SIF GET_COM_FILTERING(LOCAL, TARGET)
			CONTINUE
	ENDIF
	;警告用
	TFLAG:DOTRAIN = 0
	TRYCALLFORM COM_ABLE{LOCAL}
	IF RESULT == 1
		PRINTFORML %TRAINNAME:LOCAL%は@COM_ABLEで、COM_ABLE強制終了フラグが設定されていません
		PRINTFORML 他のCOM_ABLEを参考にして、以下の記述を追加してください
		PRINTFORML SIF TFLAG:DOTRAIN == 0 && TFLAG:CALLTRAIN == 0
		PRINTFORMW 		RETURN 0
	ENDIF
	;DOTRAINを使うので1に
	TFLAG:DOTRAIN = 1
	;COM_ABLE共通判定のためSELECTCOMにLOCALを代入
	SELECTCOM = LOCAL
	TRYCALLFORM COM_ABLE{LOCAL}
	SIF RESULT == 0
		CONTINUE
	;Q配列に実行可能コマンドを記録していく
	Q:(LOCAL:1) = LOCAL
	;ウフフ独自コマンド
	IF LOCAL >= 280 && LOCAL <= 284
		PRINTFORMC %CSTR:(LOCAL-200)%[{LOCAL:1,3}]
	;純愛独自コマンド
	ELSEIF LOCAL >= 410 && LOCAL <= 414
		PRINTFORMC %CSTR:(LOCAL-325)%[{LOCAL:1,3}]
	ELSE
		;予め実行判定を調べ実行不可能ならばグレー表示にする
		;例外＝口上独自コマンドおよび[316]髪を梳いて貰う
		LOCAL:3 = 0
		SKIPDISP 1
		CALL PRE_COMORDER, LOCAL
		LOCAL:3 = RESULT
		SKIPDISP 0
		SIF LOCAL:3
			SETCOLOR 105, 105, 105
		;コマンド名呼び出し
		CALL CUSTOM_COMMAND_NAME, LOCAL, LOCAL:1
		SIF LOCAL:3
			RESETCOLOR
	ENDIF
	;改行処理
	CALL NEWLINE
NEXT
;USERCOM表示の前に一度、改行する
IF (C % PRINTCPERLINE())
	C = 0
	PRINTL 
ENDIF

;異常経験フィルタをコマンド表示後元の設定に戻す
SIF LOCAL:2
	FLAG:61 -= 1

PRINTC 履歴表示[800]
CALL NEWLINE
PRINTC 汚れ表示[801]
CALL NEWLINE
PRINTC 能力表示[802]
CALL NEWLINE
A = 0
REPEAT 100
	SIF ITEM:(500 + COUNT)
		A = 1
REND
IF A
	PRINTC アイテム使用[810]
	CALL NEWLINE
ENDIF
;服を脱がせる＝通称ウフフモード突入
IF TFLAG:44 == 0 && TFLAG:34 == 0
	PRINTC 服を脱がせる[850]
	CALL NEWLINE
ENDIF
IF TFLAG:34 == 0 && (FLAG:23 & 8)
	PRINTC 主導権を決める[851]
	CALL NEWLINE
ENDIF

PRINTC コンフィグ[860]
CALL NEWLINE

;口上独自USERCOM表示　CFLAGは利用設定フラグ
IF CFLAG:TARGET:独自コマンド使用判定
	FOR LOCAL, 10, 19
		IF GETBIT(CFLAG:TARGET:独自コマンド使用判定, LOCAL)
			PRINTFORMC %CSTR:TARGET:(80+LOCAL)%[{860 + LOCAL}]
			CALL NEWLINE
		ENDIF
	NEXT
ENDIF
IF ASSI > 0
	IF ASSIPLAY && TEQUIP:90 == 0 && TEQUIP:2 == 0 && (TFLAG:44 == 0 || TALENT:合意)
		PRINTC マスターと交代[900]
	ELSEIF TEQUIP:90 == 0 && TEQUIP:2 == 0 && (TFLAG:44 == 0 || TALENT:合意)
		PRINTC 助手と交代[900]
	ENDIF
	CALL NEWLINE
ENDIF

IF ASSIPLAY 
	PRINTC 能力表示(助手)[902]
ELSE
	PRINTC 能力表示(マスター)[902]
ENDIF
CALL NEWLINE
PRINTC トロフィー[910]
CALL NEWLINE
PRINTC リセット[950]
CALL NEWLINE
IF TALENT:MASTER:Debug
	PRINTC デバッグメニュー[998]
	CALL NEWLINE
ENDIF
PRINTC 行動終了[999]
IF PREVCOM >= 0
	PRINTL 
	PRINTFORM < 前回実行コマンド : %STR:0% 
	SIF TFLAG:50
		PRINT (助手) 
	PRINT >
ENDIF

;---------------------------------------------------------
;改行処理
;---------------------------------------------------------
@NEWLINE
C += 1
SIF (C % PRINTCPERLINE()) == 0
	PRINTL 

;---------------------------------------------------------
;実行判定を呼び出してコマンドごとにチェックする処理
;TRYCCALLFORMを使うので式中関数にはできない
;---------------------------------------------------------
@PRE_COMORDER, ARG
;[316]髪を梳いて貰うコマンドだけは途中でINPUTあるので処理しない。
SIF ARG == 316
	RETURN 0
TRYCCALLFORM COM_ORDER_{ARG}
	;実行できない場合
	IF A < V
		RETURN 1
	;実行できる場合
	ELSE
		RETURN 0
	ENDIF
CATCH
	;元々実行判定のないコマンドの場合
	RETURN 0
ENDCATCH

;---------------------------------------------------------------------------------------------
;コマンド名変更処理
;元はPRINTFORMC %TRAINNAME:LOCAL%[{LOCAL:1,3}]でTrain.csvのコマンド名通りに表示されていた処理
;ARG 元処理のLOCAL(選択されているコマンド番号)
;ARG:1 元処理のLOCAL:1(表示番号)
;---------------------------------------------------------------------------------------------
@CUSTOM_COMMAND_NAME, ARG, ARG:1
;LOCALSに元のコマンド名を設定
LOCALS = %TRAINNAME:ARG%

;パートナーに主導権がある時、パートナー側がプレイヤーへ行動するコマンドの名前変更
;スパンキング
SIF SELECTCOM == 40 && TFLAG:45
	LOCALS = スパンキングされる
;鞭
SIF SELECTCOM == 41 && TFLAG:45
	LOCALS = 鞭で打たれる
;針
SIF SELECTCOM == 42 && TFLAG:45
	LOCALS = 針で刺される
;言葉責め
SIF SELECTCOM == 75 && TFLAG:45
	LOCALS = 言葉責めされる

;剃毛プレイ
IF SELECTCOM == 257
	;PLAYER主導権時の実行判定
	CALL COM_ABLE257_PLAYER_MAIN
	LOCAL:0 = RESULT

	;TARGET主導権時の実行判定
	CALL COM_ABLE257_TARGET_MAIN
	LOCAL:1 = RESULT
	
	;TARGET主導権時の「剃毛される」を実行可能で、TARGETが主導権をもっている場合名前変更
	;またPLAYER主導権時の「剃毛プレイ」を実行できない場合も「剃毛される」になるため名前変更
	SIF LOCAL:1 && (TFLAG:45 || LOCAL:0 == 0)
		LOCALS = 剃毛される
ENDIF

;浣腸使用前
SIF SELECTCOM == 46 && TEQUIP:46 == 0
	LOCALS = 浣腸
;浣腸・触手浣腸使用後
SIF (SELECTCOM == 46 || SELECTCOM == 107) && TEQUIP:46
	LOCALS = 排泄

;コマンド名表示
PRINTFORMC %LOCALS%[{ARG:1,3}]



@USERCOM
;コマンド入力ミスフラグ初期化
TFLAG:54 = 0

A = 0
REPEAT 100
	SIF ITEM:(500 + COUNT)
		A = 1
REND

IF RESULT == 800
	CALL SHOW_EQUIP
	RETURN 1
ELSEIF RESULT == 801
	CALL STAIN_INFO
	RETURN 1
ELSEIF RESULT == 802
	;新型表示
	DRAWLINE
	PRINTFORM ■ %NAME:TARGET%(パートナー)のステータス 
	SIF ((FLAG:23 & 1p26) == 0) && FLAG:2 != 1
		PRINTFORM (好感度:{CFLAG:2})
	PRINTL 
	CALL SHOW_INFO
	RETURN 1
ELSEIF RESULT == 810 && A
	;アイテム使用
	CALL USE_TREASURE_ITEM
ELSEIF RESULT == 850 && TFLAG:44 == 0 && TFLAG:34 == 0
	IF FLAG:23 & 2
		PRINTFORML 服を脱がせて(Ｈな事をしようとして)もよろしいですか？
		PRINTFORML [0] ダメ。手が滑った
		PRINTFORML [1] ＯＫ。早く楽しませろ！

		INPUT
		IF (RESULT < 0 || RESULT > 1)
			RESTART
		ELSEIF RESULT == 0
			RETURN 1
		ENDIF
	ENDIF

	;[合意]持ちなら判定全スルー
	IF TALENT:合意
		TFLAG:44 = 1
		TFLAG:600 = 7
		CALL KOJO_JUN
		IF TFLAG:44 == 1
			;映画館から追い出される
			CALL GET_OUT_MOVIE
			RETURN 1
		ELSE
			RETURN 0
		ENDIF
	;実行判定
	ELSE
		A = 0
		S = 0
		V = 70
		W = 100
		;Stage3クリア後は実行値スルー
		SIF  FLAG:24 > 3 || TALENT:MASTER:脱衣制限解除
			V = -9999

		;すべての命令に共通の要素を考慮
		;(従順が高いと命令に従いやすいなど)
		CALL COM_ORDER

		;ABL:欲望
		IF ABL:欲望
			A += ABL:欲望 * 4
		ENDIF
		;ABL:奉仕精神
		IF ABL:奉仕精神
			A += ABL:奉仕精神 * 3
		ENDIF
		;男嫌い
		IF TALENT:男嫌い && TALENT:PLAYER:オトコ
			A -= 7
		ENDIF


		;恋慕
		IF TALENT:恋慕 && ASSIPLAY == 0
			A += 10
		ENDIF

		;ムードボーナス
		B = 0
		IF TFLAG:37 < 10
			B -= 20
		ELSEIF TFLAG:37 < 30
			B -= 5
		ELSEIF TFLAG:37 < 50
			B -= 1
		ELSEIF TFLAG:37 < 100
			B += TFLAG:37 / 10
		ELSEIF TFLAG:37 < 300
			B += TFLAG:37 / 7
		ELSE
			B += TFLAG:37 / 5
		ENDIF
		A += B
		IF B < 0
			B = B * -1
		ELSE
		ENDIF


		;好感度
		IF CFLAG:2 < 700
			B = 0
			IF CFLAG:2 < 50
				B = 50
			ELSEIF CFLAG:2 < 300
				B = 20
			ELSEIF CFLAG:2 < 500
				B = 10
			ELSE
				B = 5
			ENDIF
			A -= B
		ENDIF

		;好感度
		IF CFLAG:2 >= 700
			B = 0
			IF CFLAG:2 < 1000
				B = 5
			ELSE
				B = CFLAG:2 / 100
			ENDIF
			A += B
		ENDIF
		;キス未経験
		IF TALENT:キス未経験
			A -= 20
		ENDIF

		;一線越えない
		IF TALENT:一線越えない
			A -= 15
		ENDIF

		;酔った勢い
		IF TFLAG:47 >= 3
			A += TFLAG:47 / 3
		ENDIF

		IF ((FLAG:23 & 1p27) == 0) && FLAG:2 != 1
			;ABL:欲望
			IF ABL:欲望
				SIF S
					PRINT  + 
				PRINTS ABLNAME:1
				PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 4,')
				S = 1
			ENDIF
			;ABL:奉仕精神
			IF ABL:奉仕精神
				SIF S
					PRINT  + 
				PRINTS ABLNAME:6
				PRINTV 'LV,ABL:奉仕精神,'(,ABL:奉仕精神 * 3,')
				S = 1
			ENDIF
			;男嫌い
			IF TALENT:男嫌い && TALENT:PLAYER:オトコ
				PRINT  - 
				PRINTS TALENTNAME:82
				PRINTV '(,7,')
				S = 1
			ENDIF


			;恋慕
			IF TALENT:恋慕 && ASSIPLAY == 0
				SIF S
					PRINT  + 
				PRINTS TALENTNAME:85
				PRINTV '(,10,')
				S = 1
			ENDIF

			;ムードボーナス
			B = 0
			IF TFLAG:37 < 10
				B -= 20
			ELSEIF TFLAG:37 < 30
				B -= 5
			ELSEIF TFLAG:37 < 50
				B -= 1
			ELSEIF TFLAG:37 < 100
				B += TFLAG:37 / 10
			ELSEIF TFLAG:37 < 300
				B += TFLAG:37 / 7
			ELSE
				B += TFLAG:37 / 5
			ENDIF
			IF B < 0
				B = B * -1
				PRINT  - 
				PRINT ムード悪い
				PRINTV '(,B,')
				S = 1
			ELSE
				PRINT  + 
				PRINT ムード良い
				PRINTV '(,B,')
				S = 1
			ENDIF


			;好感度
			IF CFLAG:2 < 700
				B = 0
				PRINT  - 
				IF CFLAG:2 < 50
					B = 50
				ELSEIF CFLAG:2 < 300
					B = 20
				ELSEIF CFLAG:2 < 500
					B = 10
				ELSE
					B = 5
				ENDIF
				PRINT 好感度不足
				PRINTV '(,B,')
				S = 1
			ENDIF

			;好感度
			IF CFLAG:2 >= 700
				B = 0
				SIF S
					PRINT  + 
				IF CFLAG:2 < 1000
					B = 5
				ELSE
					B = CFLAG:2 / 100
				ENDIF
				PRINT 好感度
				PRINTV '(,B,')
				S = 1
			ENDIF
			;キス未経験
			IF TALENT:キス未経験
				PRINT  - 
				PRINTS TALENTNAME:151
				PRINTV '(,20,')
				S = 1
			ENDIF

			;一線越えない
			IF TALENT:一線越えない
				PRINT  - 
				PRINTS TALENTNAME:27
				PRINTV '(,15,')
				S = 1
			ENDIF

			;酔った勢い
			IF TFLAG:47 >= 3
				PRINT  + 
				PRINT 酔った勢い
				PRINTV '(,TFLAG:47 / 3,')
				S = 1
			ENDIF

			;合計を表示(70以上で実行、100以上で合意)
			PRINT  = 
			PRINTV A

			IF A < V
				PRINT  < 
				PRINT 実行値
				PRINTV V
			ELSEIF A == V
				PRINT  = 
				PRINT 実行値
				PRINTV V
			ELSEIF A > V && A < W
				PRINT  < 
				PRINT 合意値
				PRINTV W
			ELSEIF A == W
				PRINT  = 
				PRINT 合意値
				PRINTV W
			ELSEIF A > W
				PRINT  > 
				PRINT 合意値
				PRINTV W
			ENDIF
			WAIT
		ENDIF

		;実行できない場合
		IF A < V
			TFLAG:600 = 6
			CALL KOJO_JUN
			IF TALENT:恋慕 == 0
				PRINTFORMW %CALLNAME:TARGET%の好感度が50下がりました
				CFLAG:2 -= 50
			ENDIF
			RETURN 1
		ELSEIF A < W
			TFLAG:44 = 1
			TFLAG:600 = 4
			CALL KOJO_JUN
			IF TFLAG:44 == 1
				;映画館から追い出される
				CALL GET_OUT_MOVIE
				RETURN 1
			ELSE
				RETURN 0
			ENDIF
		ELSEIF A >= W
			;酔ってるお陰で成功の場合
			IF A < W + TFLAG:47 / 3
				TFLAG:48 = 1
			ELSE
				TFLAG:48 = 0
			ENDIF
			TFLAG:600 = 5
			TFLAG:44 = 1
			CALL KOJO_JUN
			IF TFLAG:44 == 1
				IF ASSIPLAY == 0 && TFLAG:48 == 0
					PRINTFORMW %CALLNAME:TARGET%の【合意】を得た
					TALENT:合意 = 1
				ENDIF
				;映画館から追い出される
				CALL GET_OUT_MOVIE
				;既成事実２に相当
				TFLAG:200 = 2
				CALL MARK_GOT_CHECK
				RETURN 1
			ELSE
				RETURN 0
			ENDIF
		ENDIF
	ENDIF

	RETURN 1
ELSEIF RESULT == 851 && (FLAG:23 & 8) && TFLAG:34 == 0
	PRINT どちらが主導権を握りますか？  現在は
	IF CFLAG:15 == 0
		PRINTL 成り行きに任せています
	ELSEIF CFLAG:15 == 1
		PRINTL 自分で握っています
	ELSEIF CFLAG:15 == 2
		SIF TFLAG:34
			PRINT 完全に
		PRINTFORML %CALLNAME:TARGET%に握らせています
	ENDIF
	PRINTL [0] - 成り行きに任せる
	SIF ABL:親密 >= 3 || CFLAG:2 >= 300 || TALENT:素直 || TALENT:大人しい || TALENT:プライド低い || TALENT:恋人
		PRINTL [1] - 自分で握る
	SIF ABL:親密 >= 3 || CFLAG:2 >= 300 || TALENT:サド || TALENT:恋人
		PRINTFORML [2] - %CALLNAME:TARGET%に握らせる
	SIF TALENT:恋慕 && TALENT:恋人
		PRINTFORML [3] - 完全に%CALLNAME:TARGET%に握らせる

	INPUT
	IF RESULT == 0
		CFLAG:15 = 0
	ELSEIF RESULT == 1 && (ABL:親密 >= 3 || CFLAG:2 >= 300 || TALENT:素直 || TALENT:大人しい || TALENT:プライド低い || TALENT:恋人)
		CFLAG:15 = 1
		TFLAG:45 = 0
	ELSEIF RESULT == 2 && (ABL:親密 >= 3 || CFLAG:2 >= 300 || TALENT:サド || TALENT:恋人)
		CFLAG:15 = 2
		TFLAG:45 = 1
	ELSEIF RESULT == 3 && (TALENT:恋慕 && TALENT:恋人)
		CFLAG:15 = 2
		TFLAG:45 = 1
		;REVMODEフラグON
		TFLAG:34 = 1
	ELSE
		RESTART
	ENDIF
	
	RETURN 1
ELSEIF RESULT == 860
	;コンフィグ
	CALL START_CONFIGURE
ELSEIF (RESULT >= 870 && RESULT < 880) && GETBIT(CFLAG:TARGET:独自コマンド使用判定, (RESULT-860))
	TRYCALLFORM KOJO_USERCOM_{NO:TARGET}_{RESULT}
ELSEIF RESULT == 900 && TEQUIP:90 == 0 && ASSI > 0 && TEQUIP:2 == 0 && (TFLAG:44 == 0 || TALENT:合意)
	CALL KOJO_MESSAGE_PLAYERCHANGE
	IF ASSIPLAY
		ASSIPLAY = 0
		PLAYER = MASTER
	ELSE
		ASSIPLAY = 1
		PLAYER = ASSI
	ENDIF
	RETURN 1
ELSEIF RESULT == 902
	;新型表示
	W = PLAYER
	DRAWLINE
	IF ASSIPLAY
		PRINTFORML ■ %NAME:PLAYER%(助手)のステータス
	ELSE
		PRINTFORML ■ %NAME:PLAYER%(マスター)のステータス
	ENDIF
	DRAWLINE
	PRINTL □ 経験
	CALL NEW_PRINT_EXP
	DRAWLINE
	PRINTL □ 能力
	CALL NEW_PRINT_ABL
	DRAWLINE
	PRINTL □ 素質
	CALL NEW_PRINT_TALENT
	WAIT
	RETURN 1
ELSEIF RESULT == 910
	;実績表示
	CALL TROPHY
ELSEIF RESULT == 998 && TALENT:MASTER:Debug
	;デバッグモード用メニュー
	PRINTL デバッグメニューを呼び出します
	CALL DEBUG_MENU_TRAIN
	RETURN 1
ELSEIF RESULT == 999
	;襲われ判定
	IF ASSIPLAY == 0 && (ABL:欲望 >= 7 || PALAM:潤滑 >= 3000 || PALAM:欲情 >= 3000 || CFLAG:2 >= 10000)
		CALL PUSH_OVER
		SIF RESULT
			RETURN 1
	ENDIF

	BEGIN AFTERTRAIN
	RETURN 1
ELSEIF RESULT == 950
	PRINTL リセットしますがよろしいですか？
	PRINTL [0] はい
	PRINTL [1] いいえ
	PRINTL [2] オートセーブをロードする
	INPUT
	IF RESULT == 2
		CHKDATA 99
		IF RESULT == 0
			PRINTFORMW ロードします
			;ロード前に変数を破壊しておく
			RESETDATA
			;自動セーブは99のはずなんで99に
			LOADDATA 99
		ELSE
			PRINTFORMW %RESULTS%
			RETURN 1
		ENDIF
	ELSEIF RESULT
		RETURN 1
	ELSE
		PRINTFORMW タイトル画面に戻ります
		RESETDATA
		BEGIN TITLE
	ENDIF
;コマンド及び独自ユーティリティ実行
ELSEIF RESULT >= 0 && VARSIZE("Q") > RESULT && Q:RESULT >= 0
	DOTRAIN Q:RESULT
;無効な数字入力がなされた場合、フラグをたてる
;誘うコマンドの、行動前処理が無効な入力によって何度も表示される不具合を回避します
ELSE
	TFLAG:54 = 1
ENDIF
RETURN 0
@SHOW_EQUIP
;新型表示
W = TARGET
DRAWLINE
PRINTL □ 経験
CALL NEW_PRINT_EXP
PRINTL ･･･････････････････････････････････････････････････････････････････････････････
PRINTL □ 珠
CALL NEW_PRINT_JUEL
IF PREVCOM >= 0
	PRINT 前回実行コマンド : 
	PRINTS STR:0
	SIF TFLAG:50
		PRINT (助手)
	;PRINT (COMF:
	;PRINTV PREVCOM
	;PRINTL )
	PRINTL  
ENDIF
;日常用
SIF TEQUIP:7
	PRINTL [炬燵使用中]
SIF TEQUIP:36
	PRINTL [ローター装着中]

;ウフフ時用
SIF TEQUIP:21
	PRINTL [媚薬効果発揮中]
SIF TEQUIP:22
	PRINTL [利尿剤効果発揮中]
SIF TEQUIP:74
	PRINTL [鈴蘭の毒効果発揮中]
SIF TEQUIP:75
	PRINTL [昆虫採集セットの薬効果発揮中]

SIF TEQUIP:53
	PRINTFORML [ビデオ撮影中({TFLAG:70 - 1}/10)]
SIF TEQUIP:54
	PRINTL [野外プレイ中]
SIF TEQUIP:57
	PRINTL [羞恥プレイ中]
SIF TEQUIP:58
	PRINTL [お風呂場プレイ中]
SIF TEQUIP:59
	PRINTL [新妻プレイ中]
SIF TEQUIP:60
	PRINTL [スク水プレイ中]
SIF TEQUIP:90
	PRINTL [触手調教中]

IF TEQUIP:11 && TEQUIP:90
	PRINTL [触手挿入]
ELSEIF TEQUIP:11
	PRINTL [バイブ装備中]
ENDIF

IF TEQUIP:13 && TEQUIP:90
	PRINTL [触手アナル挿入]
ELSEIF TEQUIP:13
	PRINTL [アナルバイブ装備中]
ENDIF

IF TEQUIP:14 && TEQUIP:90
	PRINTL [触手クリ責め]
ELSEIF TEQUIP:14
	PRINTL [クリキャップ装着中]
ENDIF

IF TEQUIP:15 && TEQUIP:90
	PRINTL [触手乳首責め]
ELSEIF TEQUIP:15
	PRINTL [二プルキャップ装着中]
ENDIF

IF TEQUIP:16 && TEQUIP:90
	PRINTL [触手搾乳]
ELSEIF TEQUIP:16
	PRINTL [搾乳器装備中]
ENDIF

SIF TEQUIP:76
	PRINTL [サラシ装着中]

IF TEQUIP:17 && TEQUIP:90
	PRINTL [触手ペニス責め]
ELSEIF TEQUIP:17
	PRINTL [オナホール装備中]
ENDIF

IF TEQUIP:18
	PRINTL [シャワー使用中]
ENDIF

IF TEQUIP:44 && TEQUIP:90
	PRINTL [触手緊縛]
ELSEIF TEQUIP:44 && TEQUIP:72 == 0 && TEQUIP:73 == 0
	PRINTL [縄で緊縛]
ELSEIF TEQUIP:44 && TEQUIP:72 && TEQUIP:73 == 0
	PRINTL [荊で緊縛]
ELSEIF TEQUIP:44 && TEQUIP:73 && TEQUIP:72 == 0
	PRINTL [蜘蛛の糸で緊縛]
ENDIF

IF TEQUIP:46 && TEQUIP:90
	PRINTL [触手浣腸]
ELSEIF TEQUIP:46
	PRINTL [浣腸＋アナルプラグ装備中]
ENDIF

SIF TEQUIP:98
	PRINTL [触手口辱中]

SIF TEQUIP:43 && TEQUIP:73 == 0
	PRINTL [アイマスク装備中]
SIF TEQUIP:45
	PRINTL [ボールギャグ装備中]
SIF TEQUIP:19
	PRINTL [アナルビーズ使用中]
SIF TEQUIP:47
	PRINTL [空気浣腸中]
SIF TEQUIP:48
	PRINTL [拡張バルーン挿入中]
SIF TEQUIP:49
	PRINTL [アナル電極挿入中]
SIF TEQUIP:61
	PRINTL [クスコ挿入中]
SIF TEQUIP:62
	PRINTL [お医者プレイ中]
SIF TEQUIP:69
	PRINTL [女体盛りプレイ中]
SIF TEQUIP:71
	PRINTL [焼芋挿入中]
SIF TEQUIP:70
	PRINTL [巨峰挿入中]
SIF TEQUIP:PLAYER:43
	PRINTFORML [アイマスク装備中(%CALLNAME:PLAYER%)]
SIF TEQUIP:PLAYER:44
	PRINTFORML [縄で緊縛(%CALLNAME:PLAYER%)]
SIF TEQUIP:PLAYER:45
	PRINTFORML [ボールギャグ装備中(%CALLNAME:PLAYER%)]
IF TEQUIP:79
	SIF TEQUIP:79 == 1 || TEQUIP:79 == 2
		PRINTL [氷柱挿入中]
	SIF TEQUIP:79 == 3
		PRINTL [極太氷柱挿入中]
	SIF TEQUIP:79 >= 4
		PRINTL [氷塊挿入中]
ENDIF
SIF TEQUIP:80
	PRINTL [暗闇展開中]
SIF TEQUIP:92
	PRINTL [蟲風呂入浴中]
SIF TEQUIP:78
	PRINTL [鰻風呂入浴中]

SIF EX:Ｃ絶頂
	PRINTFORM C絶頂:{EX:Ｃ絶頂}回 
SIF EX:Ｖ絶頂
	PRINTFORM V絶頂:{EX:Ｖ絶頂}回 
SIF EX:Ａ絶頂
	PRINTFORM A絶頂:{EX:Ａ絶頂}回 
SIF EX:Ｂ絶頂
	PRINTFORM B絶頂:{EX:Ｂ絶頂}回 
SIF EX:Ｍ絶頂
	PRINTFORM M絶頂:{EX:Ｍ絶頂}回 
SIF EX:噴乳
	PRINTFORM 噴乳:{EX:噴乳}回 
SIF EX:射精
	PRINTFORM 射精:{EX:射精}回 
SIF EX:Ｃ絶頂 || EX:Ｖ絶頂 || EX:Ａ絶頂 || EX:Ｂ絶頂 || EX:噴乳 || EX:射精 || EX:Ｍ絶頂
	PRINTL 
WAIT
