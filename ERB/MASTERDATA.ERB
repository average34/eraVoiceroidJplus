@MASTER_TALENT_SET
;共通コンフィグ互換
;グローバル変数読み込み
LOADGLOBAL

PRINTFORML %CALLNAME:MASTER%の素質等を設定出来ます。
PRINTL 変更したい項目を選んでください。

CALL MASTER_TALENT_SET_SHOW

$INPUT_LOOP_01
INPUT

IF RESULT == 0
	;プレイヤーの名前変更
	A = MASTER
	CALL NAME_CONVERT
ELSEIF RESULT == 1
	;女の子→ふたなり/オトコ/童貞をOFF
	TALENT:MASTER:ふたなり = 0
	TALENT:MASTER:オトコ = 0
	TALENT:MASTER:童貞 = 0
ELSEIF RESULT == 2
	;ふたなり→ふたなりをON。オトコをOFF
	TALENT:MASTER:ふたなり = 1
	TALENT:MASTER:オトコ = 0
ELSEIF RESULT == 3
	;オトコ→オトコをON。Ｖ敏感/Ｖ鈍感/貧乳/巨乳/ふたなり/処女をOFF
	TALENT:MASTER:ふたなり = 0
	TALENT:MASTER:オトコ = 1
	TALENT:MASTER:Ｖ鈍感 = 0
	TALENT:MASTER:Ｖ敏感 = 0
	TALENT:MASTER:貧乳 = 0
	TALENT:MASTER:巨乳 = 0
	TALENT:MASTER:処女 = 0
ELSEIF RESULT < 200
	IF X:RESULT == 0
		PRINTL 選択できません
		GOTO INPUT_LOOP_01
	ELSEIF TALENT:MASTER:オトコ && (RESULT == 103 || RESULT == 104 || RESULT == 109 || RESULT == 110)
		PRINTL 選択できません
		GOTO INPUT_LOOP_01
	ELSEIF TALENT:MASTER:RESULT
		TALENT:MASTER:RESULT = 0
		;反抗的消去時はツンデレも消去
		SIF RESULT == 11
			TALENT:MASTER:ツンデレ = 0
	ELSE
		TALENT:MASTER:RESULT = 1
		CALL TALENT_CONFLICT_CHECK
	ENDIF
ELSEIF RESULT == 200
	;処女
	IF TALENT:MASTER:オトコ == 0
		IF TALENT:MASTER:処女 == 0
			TALENT:MASTER:処女 = 1
		ELSE
			TALENT:MASTER:処女 = 0
		ENDIF
	ELSE
		PRINTL 選択できません
		GOTO INPUT_LOOP_01
	ENDIF
ELSEIF RESULT == 201
	;童貞
	IF TALENT:MASTER:ふたなり || TALENT:MASTER:オトコ
		IF TALENT:MASTER:童貞 == 0
			TALENT:MASTER:童貞 = 1
		ELSE
			TALENT:MASTER:童貞 = 0
		ENDIF
	ELSE
		PRINTL 選択できません
		GOTO INPUT_LOOP_01
	ENDIF
ELSEIF RESULT == 202
	;キス未経験
	IF TALENT:MASTER:キス未経験 == 0
		TALENT:MASTER:キス未経験 = 1
	ELSE
		TALENT:MASTER:キス未経験 = 0
	ENDIF
ELSEIF RESULT == 300
	;誕生日　DAYS.ERBの@BIRTHDAY
	CALL BIRTHDAY
ELSEIF RESULT == 400
	;開始時の季節
	CALL MASTER_TALENT_SET_400
ELSEIF RESULT == 500
	;共有している主人公の設定をロードする
	CALL LOAD_GLOBAL_TALENT_SET
ELSEIF RESULT == 501
	;デフォルトに戻す
	CALL MASTER_TALENT_SET_DEFAULT
ELSEIF RESULT == 502
	;現在の設定を共有設定として保存する
	CALL STORE_GLOBAL_TALENT_SET
ELSEIF RESULT == 600
	;設定完了
	RETURN 0
ELSE
	PRINTL 選択できません
	GOTO INPUT_LOOP_01
ENDIF

RESTART

;---------------------------------------------------------
;同時に設定できない素質をチェック・消去
;---------------------------------------------------------
@TALENT_CONFLICT_CHECK
IF RESULT == 10
	;臆病→気丈をOFF
	TALENT:MASTER:気丈 = 0
ELSEIF RESULT == 11
	;反抗的→素直をOFF
	TALENT:MASTER:素直 = 0
ELSEIF RESULT == 12
	;気丈→臆病をOFF
	TALENT:MASTER:臆病 = 0
ELSEIF RESULT == 13
	;素直→反抗的/ツンデレをOFF
	TALENT:MASTER:反抗的 = 0
	TALENT:MASTER:ツンデレ = 0
ELSEIF RESULT == 14
	;大人しい→生意気をOFF
	TALENT:MASTER:生意気 = 0
ELSEIF RESULT == 15
	;プライド高い→プライド低いをOFF
	TALENT:MASTER:プライド低い = 0
ELSEIF RESULT == 16
	;生意気→素直をOFF
	TALENT:MASTER:素直 = 0
ELSEIF RESULT == 17
	;プライド低い→プライド高いをOFF
	TALENT:MASTER:プライド高い = 0
ELSEIF RESULT == 18
	;ツンデレ→反抗的をON 素直をOFF
	TALENT:MASTER:反抗的 = 1
	TALENT:MASTER:素直 = 0
ELSEIF RESULT == 20
	;自制心→をOFF
	;TALENT:MASTER: = 0
ELSEIF RESULT == 21
	;無関心→好奇心をOFF
	TALENT:MASTER:好奇心 = 0
ELSEIF RESULT == 22
	;感情乏しい→目立ちたがりをOFF
	TALENT:MASTER:目立ちたがり = 0
ELSEIF RESULT == 23
	;好奇心→無関心をOFF
	TALENT:MASTER:無関心 = 0
ELSEIF RESULT == 24
	;保守的→をOFF
	;TALENT:MASTER: = 0
ELSEIF RESULT == 25
	;楽観的→悲観的をOFF
	TALENT:MASTER:悲観的 = 0
ELSEIF RESULT == 26
	;悲観的→楽観的をOFF
	TALENT:MASTER:楽観的 = 0
ELSEIF RESULT == 27
	;一線越えない→をOFF
	;TALENT:MASTER: = 0
ELSEIF RESULT == 28
	;目立ちたがり→感情乏しいをOFF
	TALENT:MASTER:感情乏しい = 0
ELSEIF RESULT == 30
	;貞操観念→貞操無頓着をOFF
	TALENT:MASTER:貞操無頓着 = 0
ELSEIF RESULT == 31
	;貞操無頓着→貞操観念をOFF
	TALENT:MASTER:貞操観念 = 0
ELSEIF RESULT == 32
	;抑圧→解放をOFF
	TALENT:MASTER:解放 = 0
ELSEIF RESULT == 33
	;解放→抑圧をOFF
	TALENT:MASTER:抑圧 = 0
ELSEIF RESULT == 34
	;抵抗→をOFF
	;TALENT:MASTER: = 0
ELSEIF RESULT == 35
	;恥じらい→恥薄いをOFF
	TALENT:MASTER:恥薄い = 0
ELSEIF RESULT == 36
	;恥薄い→恥じらいをOFF
	TALENT:MASTER:恥じらい = 0
ELSEIF RESULT == 40
	;痛みに弱い→痛みに強いをOFF
	TALENT:MASTER:痛みに強い = 0
ELSEIF RESULT == 41
	;痛みに強い→痛みに弱いをOFF
	TALENT:MASTER:痛みに弱い = 0
ELSEIF RESULT == 42
	;濡れやすい→濡れにくいをOFF
	TALENT:MASTER:濡れにくい = 0
ELSEIF RESULT == 43
	;濡れにくい→濡れやすいをOFF
	TALENT:MASTER:濡れやすい = 0
ELSEIF RESULT == 50
	;習得早い→習得遅いをOFF
	TALENT:MASTER:習得遅い = 0
ELSEIF RESULT == 51
	;習得遅い→習得早いをOFF
	TALENT:MASTER:習得早い = 0
ELSEIF RESULT == 60
	;自慰しやすい→をOFF
	;TALENT:MASTER: = 0
ELSEIF RESULT == 61
	;汚臭鈍感→汚臭敏感をOFF
	TALENT:MASTER:汚臭敏感 = 0
ELSEIF RESULT == 62
	;汚臭敏感→汚臭鈍感をOFF
	TALENT:MASTER:汚臭鈍感 = 0
ELSEIF RESULT == 63
	;献身的→をOFF
	;TALENT:MASTER: = 0
ELSEIF RESULT == 70
	;快感に素直→快感の否定をOFF
	TALENT:MASTER:快感の否定 = 0
ELSEIF RESULT == 71
	;快感の否定→快感に素直をOFF
	TALENT:MASTER:快感に素直 = 0
ELSEIF RESULT == 72
	;中毒しやすい→をOFF
	;TALENT:MASTER: = 0
ELSEIF RESULT == 79
	;マゾ→をOFF
	;TALENT:MASTER: = 0
ELSEIF RESULT == 80
	;倒錯的→をOFF
	;TALENT:MASTER: = 0
ELSEIF RESULT == 81
	;両刀→男嫌いをOFF
	TALENT:MASTER:男嫌い = 0
ELSEIF RESULT == 82
	;男嫌い→両刀をOFF
	TALENT:MASTER:両刀 = 0
ELSEIF RESULT == 83
	;サド→をOFF
	;TALENT:MASTER: = 0
ELSEIF RESULT == 84
	;嫉妬→をOFF
	;TALENT:MASTER: = 0
ELSEIF RESULT == 87
	;小悪魔→をOFF
	;TALENT:MASTER: = 0
ELSEIF RESULT == 100
	;小柄体型→をOFF
	;TALENT:MASTER: = 0
ELSEIF RESULT == 101
	;Ｃ鈍感→Ｃ敏感をOFF
	TALENT:MASTER:Ｃ敏感 = 0
ELSEIF RESULT == 102
	;Ｃ敏感→Ｃ鈍感をOFF
	TALENT:MASTER:Ｃ鈍感 = 0
ELSEIF RESULT == 103
	;Ｖ鈍感→Ｖ敏感をOFF
	TALENT:MASTER:Ｖ敏感 = 0
ELSEIF RESULT == 104
	;Ｖ敏感→Ｖ鈍感をOFF
	TALENT:MASTER:Ｖ鈍感 = 0
ELSEIF RESULT == 105
	;Ａ鈍感→Ａ敏感をOFF
	TALENT:MASTER:Ａ敏感 = 0
ELSEIF RESULT == 106
	;Ａ敏感→Ａ鈍感をOFF
	TALENT:MASTER:Ａ鈍感 = 0
ELSEIF RESULT == 107
	;Ｂ鈍感→Ｂ敏感をOFF
	TALENT:MASTER:Ｂ敏感 = 0
ELSEIF RESULT == 108
	;Ｂ敏感→Ｂ鈍感をOFF
	TALENT:MASTER:Ｂ鈍感 = 0
ELSEIF RESULT == 109
	;貧乳→巨乳をOFF
	TALENT:MASTER:巨乳 = 0
ELSEIF RESULT == 110
	;巨乳→貧乳をOFF
	TALENT:MASTER:貧乳 = 0
ELSEIF RESULT == 111
	;回復早い→回復遅いをOFF
	TALENT:MASTER:回復遅い = 0
ELSEIF RESULT == 112
	;回復遅い→回復早いをOFF
	TALENT:MASTER:回復早い = 0
ENDIF

;---------------------------------------------------------
;開始時の季節
;---------------------------------------------------------
@MASTER_TALENT_SET_400
PRINTFORML 開始時の季節を選択してください
PRINTL [0]春
PRINTL [1]夏
PRINTL [2]秋
PRINTL [3]冬

$INPUT_LOOP_6
INPUT

IF (RESULT < 0 || RESULT > 3)
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP_6
ELSE
	PRINT 開始時の季節を
	IF RESULT == 0
		PRINT 春
	ELSEIF RESULT == 1
		PRINT 夏
	ELSEIF RESULT == 2
		PRINT 秋
	ELSE
		PRINT 冬
	ENDIF
	PRINTFORMW にしました
	PRINTL 
	DAY:6 = RESULT
ENDIF

;---------------------------------------------------------
;デフォルト設定
;---------------------------------------------------------
@MASTER_TALENT_SET_DEFAULT
REPEAT 113
	TALENT:MASTER:COUNT = 0
REND

TALENT:MASTER:オトコ = 1
TALENT:MASTER:童貞 = 1
TALENT:MASTER:キス未経験 = 1

CSVNAME NO:MASTER, 0
NAME:MASTER = %RESULTS%
CSVCALLNAME NO:MASTER, 0
CALLNAME:MASTER = %RESULTS%

TIME:2 = 1
TIME:3 = 0
DAY:6 = 0

;---------------------------------------------------------
;素質設定表示
;---------------------------------------------------------
@MASTER_TALENT_SET_SHOW
DRAWLINE
PRINTFORML 名前： [  0] %CALLNAME:MASTER%
DRAWLINE

PRINT 性別： 
REPEAT 3
	IF COUNT == 0
		PRINTFORM [  1][♀]
		STR:N = ♀
		IF TALENT:MASTER:ふたなり == 0 && TALENT:MASTER:オトコ == 0
			PRINT  ○
		ELSE
			PRINT  ×
		ENDIF
	ELSEIF COUNT == 1
		PRINTFORM [  2][%TALENTNAME:121%]
		STR:N = %TALENTNAME:121%
		IF TALENT:MASTER:ふたなり
			PRINT  ○
		ELSE
			PRINT  ×
		ENDIF
	ELSEIF COUNT == 2
		PRINTFORM [  3][%TALENTNAME:122%]
		STR:N = %TALENTNAME:122%
		IF TALENT:MASTER:オトコ
			PRINT  ○
		ELSE
			PRINT  ×
		ENDIF
	ENDIF
	CALL ARRANGE_TALENTNAME
REND
PRINTL 
DRAWLINE

PRINT 素質： 
REPEAT 133
	SIF COUNT == 0 || COUNT == 37 || COUNT == 52 || COUNT == 53 || COUNT == 54 || COUNT == 55 || COUNT == 56 || COUNT == 57 || COUNT == 64 || COUNT == 73 || COUNT == 74 || COUNT == 75 || COUNT == 76 || COUNT == 77 || COUNT == 78 || COUNT == 85 || COUNT == 86 || COUNT == 88 || COUNT == 89 || COUNT == 90 || COUNT == 91 || COUNT == 92 || COUNT == 93
		CONTINUE
	
	SIF COUNT >= 113 && COUNT <= 131
		CONTINUE

	STR:N = %TALENTNAME:COUNT%
	STRLENS STR:N
	
	SIF RESULT == 0
		CONTINUE

	PRINTFORM [{COUNT,3}][%TALENTNAME:COUNT%]
	IF TALENT:MASTER:COUNT
		PRINT  ○
	ELSE
		PRINT  ×
	ENDIF
	
	CALL ARRANGE_TALENTNAME

	;選択時に表示された素質だけ選択できるようにX:COUNTを立てておく
	X:COUNT = 1

	LOCAL:0 += 1
	
	IF LOCAL:0 % 3 == 0
		PRINTL 
		PRINT  　　　
	ENDIF
REND
LOCAL:0 = 0
PRINTL 
DRAWLINE

PRINT 経験： 
REPEAT 3
	IF COUNT == 0
		PRINTFORM [200][%TALENTNAME:0%]
		STR:N = %TALENTNAME:0%
		IF TALENT:MASTER:処女
			PRINT  ○
		ELSE
			PRINT  ×
		ENDIF
	ELSEIF COUNT == 1
		PRINTFORM [201][%TALENTNAME:154%]
		STR:N = %TALENTNAME:154%
		IF TALENT:MASTER:童貞
			PRINT  ○
		ELSE
			PRINT  ×
		ENDIF
	ELSEIF COUNT == 2
		PRINTFORM [202][%TALENTNAME:151%]
		STR:N = %TALENTNAME:151%
		IF TALENT:MASTER:キス未経験
			PRINT  ○
		ELSE
			PRINT  ×
		ENDIF
	ENDIF
	CALL ARRANGE_TALENTNAME
REND
PRINTL 
DRAWLINE

PRINT 誕生： 
PRINTFORM [300] 誕生日：
IF TIME:3 == 0
	PRINT 春
ELSEIF TIME:3 == 1
	PRINT 夏
ELSEIF TIME:3 == 2
	PRINT 秋
ELSEIF TIME:3 == 3
	PRINT 冬
ENDIF
PRINT の
PRINTFORM {TIME:2}
PRINTL 日
DRAWLINE

PRINT 季節： 
PRINTFORM [400] 
IF DAY:6 == 0
	PRINT 春
ELSEIF DAY:6 == 1
	PRINT 夏
ELSEIF DAY:6 == 2
	PRINT 秋
ELSEIF DAY:6 == 3
	PRINT 冬
ENDIF
PRINTL 
DRAWLINE
PRINTL [500] - 共有している主人公の設定をロードする
PRINTL [501] - デフォルトに戻す
PRINTL [502] - 現在の設定を共有設定として保存する
PRINTL [600] - 設定完了

;---------------------------------------------------------
;@MASTER_TALENT_SET用位置合わせ
;---------------------------------------------------------
@ARRANGE_TALENTNAME
STRLENS STR:N
IF RESULT:0 == 2
	PRINT  　　　　　
ELSEIF RESULT:0 == 4
	PRINT  　　　　
ELSEIF RESULT:0 == 6
	PRINT  　　　
ELSEIF RESULT:0 == 8
	PRINT  　　
ELSEIF RESULT:0 == 10
	PRINT  　
ELSEIF RESULT:0 == 12
	PRINT  
ELSE
	PRINT  
ENDIF

;-------------------------------------------------
;共通設定読み込み
;-------------------------------------------------
@LOAD_GLOBAL_TALENT_SET
;グローバル変数読み込み
LOADGLOBAL

;GLOBAL:0-154,900-902を使用（素質と900-902に誕生日、季節を記録）
REPEAT 155
	TALENT:MASTER:COUNT = GLOBAL:COUNT
REND
TIME:2 = GLOBAL:900
TIME:3 = GLOBAL:901
DAY:6 = GLOBAL:902

NAME:MASTER = %GLOBALS:0%
CALLNAME:MASTER = %GLOBALS:1%

PRINTW 共有している主人公の設定をロードしました

;-------------------------------------------------
;共通設定保存
;-------------------------------------------------
@STORE_GLOBAL_TALENT_SET
PRINTFORML 現在のプレイヤーの設定を保存しますか？
PRINTFORML 保存すると次のゲーム開始時に利用できます
PRINTL [0] はい
PRINTL [1] いいえ

$INPUT_LOOP_S
INPUT

IF RESULT == 0
	PRINTW 現在のプレイヤーの設定を保存しました
ELSEIF RESULT == 1
	PRINTW 現在のプレイヤーの設定を保存しませんでした
	RETURN 0
ELSE
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP_S
ENDIF

;グローバル変数読み込み
LOADGLOBAL

;GLOBAL:0-154,900-902を使用（素質と900-902に誕生日、季節を記録）
;GLOBAL:0-154については、素質で使っている番号(例:0,処女、10,臆病etc）以外は使用可能
REPEAT 155
	;素質で使っていない番号は共通設定保存に使用しない
	STRLENS TALENTNAME:COUNT
	SIF RESULT < 1
		CONTINUE
	;素質番号と同じ番号のGLOBALに現在のプレイヤーの素質を保存
	GLOBAL:COUNT = TALENT:MASTER:COUNT
REND
GLOBAL:900 = TIME:2
GLOBAL:901 = TIME:3
GLOBAL:902 = DAY:6

GLOBALS:0 = %NAME:MASTER%
GLOBALS:1 = %CALLNAME:MASTER%

SAVEGLOBAL



;-------------------------------------------------
;質問形式
;-------------------------------------------------
@MASTER_TALENT_SET_Q
;逆調教用の構文を流用する為
TARGET = MASTER

PRINTL 
PRINTFORML %NAME:TARGET%の性別を教えてください
PRINTL [0]♂
PRINTL [1]♀
;PRINTL [2]ふたなり
$INPUT_LOOP_01
INPUT
IF RESULT == 0
	TALENT:処女 = 0
	TALENT:貧乳 = 0
	TALENT:巨乳 = 0
	TALENT:ふたなり = 0
	TALENT:オトコ = 1
	TALENT:140 = 0
	TALENT:141 = 0
	ABL:レズっ気 = 0
	EXP:Ｖ経験 = 0
	EXP:レズ経験 = 0
	MAXBASE:射精 = 10000
ELSEIF RESULT == 1
	TALENT:オトコ = 0
	TALENT:ふたなり = 0
	MAXBASE:射精 = 0
;ELSEIF RESULT == 2
;	TALENT:ふたなり = 1
;	MAXBASE:射精 = 10000
ELSE
	GOTO INPUT_LOOP_01
ENDIF

;趣味的素質付加をスキップするかどうか
PRINTL ちょっと質問が長くなりますが、付き合いますか？
PRINTL [0]どんと来い
PRINTL [1]やめとく
$INPUT_LOOP_6
INPUT
SIF RESULT != 0 && RESULT != 1
	GOTO INPUT_LOOP_6
SIF RESULT == 1
	GOTO TALENT_SET_EX

IF TALENT:オトコ == 1
	CALL TALENT_SET_M
	GOTO TALENT_SET_COMMON
ENDIF

;女性用の質問
PRINTL 
PRINTFORML %NAME:TARGET%は…
PRINTL [0]両方いける
PRINTL [1]そこまででもない
PRINTL [2]むしろオトコはイヤ
$INPUT_LOOP_02
INPUT
IF RESULT == 0
	TALENT:両刀 = 1
ELSEIF RESULT == 1
	TALENT:両刀 = 0
ELSEIF RESULT == 2
	CALL TALENT_SET_F
	TALENT:男嫌い = 1
ELSE
	GOTO INPUT_LOOP_02
ENDIF
SIF ABL:レズっ気 < 3
	ABL:レズっ気 = 3

PRINTL 処女ですか？
PRINTL [0]はい
PRINTL [1]実は違います
PRINTL [2]経験豊富です
$INPUT_LOOP_1
INPUT
IF RESULT == 0
	TALENT:MASTER:処女 = 1
	EXP:MASTER:Ｖ経験 = 0
ELSEIF RESULT == 1
	TALENT:MASTER:処女 = 0
	SIF EXP:MASTER:Ｖ経験 == 0
		EXP:MASTER:Ｖ経験 += RAND:2 + 1
ELSEIF RESULT == 2
	TALENT:MASTER:処女 = 0
	SIF EXP:MASTER:Ｖ経験 == 0
		EXP:MASTER:Ｖ経験 += RAND:91 + 30
ELSE
	GOTO INPUT_LOOP_1
ENDIF

TALENT:MASTER:貧乳 = 0
TALENT:MASTER:巨乳 = 0
TALENT:MASTER:140 = 0
TALENT:MASTER:141 = 0
PRINTL 胸の大きさは？
PRINTL [0]自信あるよ
PRINTL [1]普通かな
PRINTL [2]……………
$INPUT_LOOP_2
INPUT
IF RESULT == 0
	TALENT:MASTER:巨乳 = 1
ELSEIF RESULT == 2
	TALENT:MASTER:貧乳 = 1
ELSEIF RESULT != 1
	GOTO INPUT_LOOP_2
ENDIF
$TALENT_SET_COMMON

;小柄体型
PRINTL 体型は……
PRINTL [0]普通だよ
PRINTL [1]ちっちゃいってゆーな！
$INPUT_LOOP_5
INPUT
SIF RESULT != 0 && RESULT != 1
	GOTO INPUT_LOOP_5
TALENT:小柄体型 = RESULT

;臆病
PRINTL 知らない人に話しかけられたら……
PRINTL [0]受け答えする
PRINTL [1]とりあえず逃げる
$INPUT_LOOP_7
INPUT
SIF RESULT != 0 && RESULT != 1
	GOTO INPUT_LOOP_7
TALENT:臆病 = RESULT

;反抗的とツンデレ
PRINTL ツンデレですか？
PRINTL [0]それはない
PRINTL [1]デレはない
IF TALENT:MASTER:オトコ
	PRINTL [2]そ、そんなバカな事！
ELSE
	PRINTL [2]そ、そんなことないわよ！
ENDIF
$INPUT_LOOP_8
INPUT
SIF RESULT != 0 && RESULT != 1 && RESULT != 2
	GOTO INPUT_LOOP_8
TALENT:反抗的 = RESULT != 0
TALENT:ツンデレ = (RESULT & 2) == 2

;素直
SIF TALENT:反抗的
	GOTO JUMP_1
PRINTL じゃ、素直って事だね？
PRINTL [0]短絡的すぎるよ
PRINTL [1]はい
$INPUT_LOOP_9
INPUT
SIF RESULT != 0 && RESULT != 1
	GOTO INPUT_LOOP_9
TALENT:素直 = RESULT
$JUMP_1

;気丈
;PRINTL 挫けぬ心は……
;PRINTL [0]兵士に回収されました
;PRINTL [1]この胸に
PRINTL 何事にも屈しない？
PRINTL [0]折れがち
PRINTL [1]そうあるつもり
$INPUT_LOOP_10
INPUT
SIF RESULT != 0 && RESULT != 1
	GOTO INPUT_LOOP_10
TALENT:気丈 = RESULT

;大人しい
PRINTL 大人しいって言われる？
PRINTL [0]そんなには
PRINTL [1]よく言われる
$INPUT_LOOP_11
INPUT
SIF RESULT != 0 && RESULT != 1
	GOTO INPUT_LOOP_11
TALENT:大人しい = RESULT

;生意気
PRINTL 生意気ってのは？
PRINTL [0]そんなには
PRINTL [1]よく言われる
$INPUT_LOOP_11_2
INPUT
SIF RESULT != 0 && RESULT != 1
	GOTO INPUT_LOOP_11_2
TALENT:生意気 = RESULT

;自制心
PRINTL 自分を自分で止められる？
PRINTL [0]そんなのできない
PRINTL [1]できる
$INPUT_LOOP_11_3
INPUT
SIF RESULT != 0 && RESULT != 1
	GOTO INPUT_LOOP_11_3
TALENT:自制心 = RESULT

;感情乏しい
PRINTL 感情の表現は……
PRINTL [0]難なく
PRINTL [1]うまく伝わらない
$INPUT_LOOP_11_4
INPUT
SIF RESULT != 0 && RESULT != 1
	GOTO INPUT_LOOP_11_4
TALENT:感情乏しい = RESULT

;プライド高い/低い
PRINTL 自分のプライドは……
PRINTL [0]ほどほどに
PRINTL [1]一番大事
PRINTL [2]犬に食わせろ
$INPUT_LOOP_12
INPUT
SIF RESULT != 0 && RESULT != 1 && RESULT != 2
	GOTO INPUT_LOOP_12
TALENT:プライド高い = RESULT & 1
TALENT:プライド低い = (RESULT & 2) == 2

;保守的と好奇心と無関心
PRINTL 真新しいものには……
PRINTL [0]気が向いたら
PRINTL [1]なんでも手を出す
PRINTL [2]手を出さない
PRINTL [3]そんなのどうでもいい
$INPUT_LOOP_13
INPUT
IF RESULT == 1
	TALENT:好奇心 = 1
ELSEIF RESULT == 2
	TALENT:保守的 = 1
ELSEIF RESULT == 3
	TALENT:無関心 = 1
ELSEIF RESULT != 0
	GOTO INPUT_LOOP_13
ENDIF

;楽観的と悲観的
PRINTL 恋愛映画を見ていると……
PRINTL [0]先が気になる
PRINTL [1]ハッピーエンドになるよ
PRINTL [2]どうせバッドエンドだよ
$INPUT_LOOP_13_2
INPUT
SIF RESULT != 0 && RESULT != 1 && RESULT != 2
	GOTO INPUT_LOOP_13_2
TALENT:楽観的 = RESULT & 1
TALENT:悲観的 = (RESULT & 2) == 2

;一線越えない
PRINTL 人との関係は……
PRINTL [0]気にしない
PRINTL [1]距離を置いてしまう
$INPUT_LOOP_14
INPUT
SIF RESULT != 0 && RESULT != 1
	GOTO INPUT_LOOP_14
TALENT:一線越えない = RESULT

;目立ちたがり
PRINTL 目立つ事は……
PRINTL [0]イヤではない
PRINTL [1]○○ちゃーん見てるー？
$INPUT_LOOP_15
INPUT
SIF RESULT != 0 && RESULT != 1
	GOTO INPUT_LOOP_15
TALENT:目立ちたがり = RESULT

;貞操観念と貞操無頓着
PRINTL エッチな事は……
PRINTL [0]誰彼かまわずやるのは問題
PRINTL [1]結婚初夜で
PRINTL [2]ヤりたい時にヤる
$INPUT_LOOP_16
INPUT
SIF RESULT != 0 && RESULT != 1 && RESULT != 2
	GOTO INPUT_LOOP_16
TALENT:貞操観念 = RESULT & 1
TALENT:貞操無頓着 = (RESULT & 2) == 2

;抑圧と解放
PRINTL 自分の気持ちは……
PRINTL [0]楽になれる程度に
PRINTL [1]しまっておく
PRINTL [2]筒抜け
$INPUT_LOOP_17
INPUT
SIF RESULT != 0 && RESULT != 1 && RESULT != 2
	GOTO INPUT_LOOP_17
TALENT:抑圧 = RESULT & 1
TALENT:解放 = (RESULT & 2) == 2

;抵抗
PRINTL 諦めは……
PRINTL [0]肝心
PRINTL [1]悪いよ
$INPUT_LOOP_18
INPUT
SIF RESULT != 0 && RESULT != 1
	GOTO INPUT_LOOP_18
TALENT:抵抗 = RESULT

;恥じらいと恥薄い
PRINTL 人前で脱いだりとかは……
PRINTL [0]出来なくもない
PRINTL [1]恥ずかしくて無理
PRINTL [2]平気
$INPUT_LOOP_19
INPUT
SIF RESULT != 0 && RESULT != 1 && RESULT != 2
	GOTO INPUT_LOOP_19
TALENT:恥じらい = RESULT & 1
TALENT:恥薄い = (RESULT & 2) == 2

;痛みに弱い/強い
PRINTL 痛いのは……
PRINTL [0]好きではないよ
PRINTL [1]勘弁して欲しいです
PRINTL [2]慣れてます
$INPUT_LOOP_20
INPUT
SIF RESULT != 0 && RESULT != 1 && RESULT != 2
	GOTO INPUT_LOOP_20
TALENT:痛みに弱い = RESULT & 1
TALENT:痛みに強い = (RESULT & 2) == 2

;濡れやすい/にくい
PRINTL 濡れやすい？
PRINTL [0]普通だと思う
PRINTL [1]そんな気がする
PRINTL [2]むしろ逆かな
$INPUT_LOOP_21
INPUT
SIF RESULT != 0 && RESULT != 1 && RESULT != 2
	GOTO INPUT_LOOP_21
TALENT:濡れやすい = RESULT & 1
TALENT:濡れにくい = (RESULT & 2) == 2

;習得早い/遅い
PRINTL 物覚えは……
PRINTL [0]普通だと思う
PRINTL [1]良い
PRINTL [2]あれ？昨日何やったっけ？
$INPUT_LOOP_22
INPUT
SIF RESULT != 0 && RESULT != 1 && RESULT != 2
	GOTO INPUT_LOOP_22
TALENT:習得早い = RESULT & 1
TALENT:習得遅い = (RESULT & 2) == 2

;汚臭鈍感/敏感
PRINTL 部屋の掃除は……
PRINTL [0]適度に
PRINTL [1]サボりがち
PRINTL [2]こまめに
$INPUT_LOOP_23
INPUT
SIF RESULT != 0 && RESULT != 1 && RESULT != 2
	GOTO INPUT_LOOP_23
TALENT:汚臭鈍感 = RESULT & 1
TALENT:汚臭敏感 = (RESULT & 2) == 2

;快感に素直/の否定
PRINTL 気持ち良い事は……
PRINTL [0]良いも悪いも無い
PRINTL [1]良い事
PRINTL [2]えっちなのはいけないと思います！
$INPUT_LOOP_24
INPUT
SIF RESULT != 0 && RESULT != 1 && RESULT != 2
	GOTO INPUT_LOOP_24
TALENT:快感に素直 = RESULT & 1
TALENT:快感の否定 = (RESULT & 2) == 2

;自慰しやすい
PRINTL 自分ですることは……
PRINTL [0]人並み程度には
PRINTL [1]多い……かも
$INPUT_LOOP_25
INPUT
SIF RESULT != 0 && RESULT != 1
	GOTO INPUT_LOOP_25
TALENT:自慰しやすい = RESULT
IF RESULT == 1
	EXP:MASTER:自慰経験 += RAND:11 + 30
ELSE
	EXP:MASTER:自慰経験 += RAND:10 + 1
ENDIF

;中毒しやすい
PRINTL 何かにはまってしまったら……
PRINTL [0]それでも節度は守る
PRINTL [1]やめられない、とまらない
$INPUT_LOOP_26
INPUT
SIF RESULT != 0 && RESULT != 1
	GOTO INPUT_LOOP_26
TALENT:中毒しやすい = RESULT

;倒錯的
PRINTL 普通の事には興味ない？
PRINTL [0]そんな事ない
PRINTL [1]もちろん
$INPUT_LOOP_27
INPUT
SIF RESULT != 0 && RESULT != 1
	GOTO INPUT_LOOP_27
TALENT:倒錯的 = RESULT

$TALENT_SET_EX
;ゲームバランスにかかわる素質をスキップするか
PRINTL 残りの質問は難易度変わっちゃうけど……
PRINTL [0]どんと来い
PRINTL [1]やめとく
$INPUT_LOOP_31
INPUT
SIF RESULT != 0 && RESULT != 1
	GOTO INPUT_LOOP_31
SIF RESULT == 1
	GOTO TALENT_SET_END

;サド/小悪魔/マゾ
PRINTL いじめるのは……
PRINTL [0]悪いことです
PRINTL [1]大好きです
PRINTL [2]むしろいじめてください
$INPUT_LOOP_28
INPUT
SIF RESULT != 0 && RESULT != 1 && RESULT != 2
	GOTO INPUT_LOOP_28
SIF RESULT == 1
	CALL TALENT_SET_S
TALENT:マゾ = (RESULT & 2) == 2

;幼稚
PRINTL 子供っぽい性格？
PRINTL [0]まさか
PRINTL [1]うにゅ？
$INPUT_LOOP_29
INPUT
SIF RESULT != 0 && RESULT != 1
	GOTO INPUT_LOOP_29
;TALENT:小悪魔 = RESULT & 1
TALENT:幼稚 = RESULT & 1

;回復早い/遅い
PRINTL 自分の体力について……
PRINTL [0]平均的
PRINTL [1]元気だと思う
PRINTL [2]病弱かも
$INPUT_LOOP_30
INPUT
SIF RESULT != 0 && RESULT != 1 && RESULT != 2
	GOTO INPUT_LOOP_30
TALENT:回復早い = RESULT & 1
TALENT:回復遅い = (RESULT & 2) == 2

;c/v/a/b鈍感/敏感はどうしようか

$TALENT_SET_END
PRINTL 
;多分問題ないと思うけど、念のためターゲットを初期化
TARGET = -1

RETURN 1

;-------------------------------------------------
;質問形式の分岐関数
;-------------------------------------------------
@TALENT_SET_M
;[オトコ]用両刀と男嫌い
PRINTFORML 両方いける？
PRINTL [0] マテ、何の話だ
PRINTL [1] どんと来い
PRINTL [2] 無理！　絶対に無理！
$INPUT_LOOP_1
INPUT
SIF RESULT != 0 && RESULT != 1 && RESULT != 2
	GOTO INPUT_LOOP_1
TALENT:両刀 = RESULT & 1
TALENT:男嫌い = (RESULT & 2) == 2

@TALENT_SET_F
;女の子用両刀
PRINTFORML それじゃ女の子が好きってこと？
PRINTL [0] そう言う訳じゃ……
PRINTL [1] 大好きです
$INPUT_LOOP_1
INPUT
SIF RESULT != 0 && RESULT != 1
	GOTO INPUT_LOOP_1
TALENT:両刀 = RESULT & 1

@TALENT_SET_S
;サド/小悪魔
PRINTFORML 怖がらせて、痛めつけるのと
PRINTFORML トロトロに蕩けさせて、可愛く鳴かせるのと
PRINTFORML どっちが好き？
PRINTL [0] 出直します
PRINTL [1] 痛めつけたい
PRINTL [2] 鳴かせたい
PRINTL [3] 両方！　両方！
$INPUT_LOOP_1
INPUT
SIF RESULT != 0 && RESULT != 1 && RESULT != 2 && RESULT != 3
	GOTO INPUT_LOOP_1
TALENT:サド = RESULT & 1
TALENT:小悪魔 = (RESULT & 2) == 2

;-------------------------------------------------
;質問形式の分岐関数（SYSTEM.ERBから移動した質問）
;-------------------------------------------------
@TALENT_SET_J
PRINTFORML %CALLNAME:MASTER%のキス経験はどうしますか？
PRINTL [0] キス位は経験済み
PRINTL [1] ファーストキスも済んでません

$INPUT_LOOP2
INPUT

IF RESULT != 0 && RESULT != 1
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP2
ENDIF
IF RESULT == 0
	PRINTFORMW %CALLNAME:MASTER%にキス経験を持たせました
	EXP:MASTER:キス経験 = 5
ELSE
	PRINTFORMW %CALLNAME:MASTER%をキス未経験にしました
	EXP:MASTER:キス経験 = 0
	TALENT:MASTER:キス未経験 = 1
ENDIF

IF TALENT:MASTER:オトコ == 0 && TALENT:MASTER:ふたなり == 0
	PRINTFORML 最初から%CALLNAME:MASTER%をふたなり化させることが出来ます。どうしますか？
	PRINTL [0] ふたなり化する
	PRINTL [1] そんな事はしない

	$INPUT_LOOP3
	INPUT

	IF RESULT != 0 && RESULT != 1
		PRINTFORML 正しい値を入力してください
		GOTO INPUT_LOOP3
	ENDIF
	IF RESULT == 0
		PRINTFORMW %CALLNAME:MASTER%をふたなりにしました
		TALENT:MASTER:ふたなり = 1
	ENDIF
	GOTO DOUTEI
ENDIF

$DOUTEI
IF TALENT:MASTER:オトコ
	PRINTFORML %CALLNAME:MASTER%を童貞にすることが出来ます。どうしますか？
	PRINTL [0] 童貞にする
	PRINTL [1] 童貞じゃないよ

	$INPUT_LOOP4
	INPUT

	IF RESULT != 0 && RESULT != 1
		PRINTFORML 正しい値を入力してください
		GOTO INPUT_LOOP4
	ENDIF
	IF RESULT == 0
		PRINTFORMW %CALLNAME:MASTER%を童貞にしました
		TALENT:MASTER:童貞 = 1
	ENDIF
ENDIF



