@CONCERT
;奴隷に歌を歌わせてチケットを売るオプション。
;ここでは野外ライブ成功判定を行う
;-------------------------------------------------
;成功判定
;-------------------------------------------------
H = 0
L = 0
P = 0

;EXP:歌唱経験
IF EXP:歌唱経験 < EXPLV:1
	L = 1
ELSEIF EXP:歌唱経験 < EXPLV:2
	L = 2
ELSEIF EXP:歌唱経験 < EXPLV:3
	L = 5
ELSEIF EXP:歌唱経験 < EXPLV:4
	L = 10
ELSEIF EXP:歌唱経験 < EXPLV:5
	L = 15
ELSE
	L = 20
ENDIF

;ABL:露出癖
IF ABL:歌唱技能 < 1
	L += 1
ELSEIF ABL:露出癖 < 9
	L += ABL:露出癖
ELSE
	L += 10
ENDIF

;ABL:歌唱技能
IF ABL:歌唱技能 < 1
	L += 1
ELSEIF ABL:歌唱技能 < 15
	L += ABL:歌唱技能 + 1
ELSE
	L += 15
ENDIF

;臆病
SIF TALENT:臆病
	L -= 2
;恥じらい
SIF TALENT:恥じらい
	L -= 1

;自制心
SIF TALENT:自制心
	L += 1
;好奇心
SIF TALENT:好奇心
	L += 1
;目立ちたがり
SIF TALENT:目立ちたがり
	L += 2
;解放
SIF TALENT:解放
	L += 1
;恥薄い
SIF TALENT:恥薄い
	L += 2

SIF L <= 0
	L = 0
;鼓舞
IF TALENT:鼓舞
	L += 1 + RAND:15
ELSE
	L += 1 + RAND:10
ENDIF

P = 20
;親愛
IF TALENT:親愛
	P += RAND:16
;恋人or恋慕
ELSEIF TALENT:恋人 || TALENT:恋慕
	P += RAND:21
ELSE
	P += RAND:26
ENDIF

;歌唱素質によって難易度変化
SIF ABL:歌唱素質 >= 3
	P -= 5
IF ASSI > 0
	;助手の歌唱素質によって難易度低下
	IF ABL:歌唱素質 >= 3
		P -= 3
	;助手が鼓舞持ちまたは歌唱技能Lv20以上だと難易度低下
	ELSEIF TALENT:ASSI:鼓舞 || ABL:ASSI:歌唱技能 >= 20
		P -= 2
	ENDIF
ENDIF
;歌苦手
SIF ABL:歌唱素質 <= -2
	P += 3

IF L < P
	;失敗
	CALL CONCERT_FAILURE
ELSE
	;成功
	CALL CONCERT_SUCCESS
ENDIF

TFLAG:79 = 1
;休憩フラグを立てる
FLAG:0 = 1
BEGIN TURNEND

RETURN 1

@CONCERT_FAILURE
;野外ライブ失敗

L = 0
M = 0
N = 10
P = 0

;ABL:歌唱技能をみる
IF ABL:歌唱技能 == 0
	L = 1
ELSEIF ABL:歌唱技能 < 10
	L = ABL:歌唱技能
ELSEIF ABL:歌唱技能 <= 20
	L = 5 + (ABL:歌唱技能 / 2)
ELSEIF ABL:歌唱技能 <= 40
	L = 15 + (ABL:歌唱技能 / 5)
ELSEIF ABL:歌唱技能 <= 64
	L = 23 + (ABL:歌唱技能 / 8)
ELSEIF ABL:歌唱技能 <= 90
	L = 31 + (ABL:歌唱技能 / 10)
ELSE
	L = 40 + (ABL:歌唱技能 / 20)
ENDIF

;ABL:露出癖をみる
IF ABL:露出癖 <= 5
	TIMES L , 0.70
ELSEIF ABL:露出癖 < 10
	TIMES L , 0.90
ELSEIF ABL:露出癖 < 20
	TIMES L , 1.00
ELSEIF ABL:露出癖 < 30
	TIMES L , 1.10
ELSEIF ABL:露出癖 < 40
	TIMES L , 1.30
ELSEIF ABL:露出癖 < 50
	TIMES L , 1.40
ELSE
	TIMES L , 1.50
ENDIF

;EXP:歌唱経験をみる
IF EXP:歌唱経験 < EXPLV:1
	TIMES L , 0.80
ELSEIF EXP:歌唱経験 < EXPLV:2
	TIMES L , 1.00
ELSEIF EXP:歌唱経験 < EXPLV:3
	TIMES L , 1.20
ELSEIF EXP:歌唱経験 < EXPLV:4
	TIMES L , 1.40
ELSEIF EXP:歌唱経験 < EXPLV:5
	TIMES L , 1.70
ELSE
	TIMES L , 2.00
ENDIF

;臆病
SIF TALENT:臆病
	TIMES L , 0.80

;目立ちたがり
SIF TALENT:目立ちたがり
	TIMES L , 1.20

;献身的
SIF TALENT:献身的
	TIMES L , 1.20

;恋慕
SIF TALENT:恋慕
	TIMES L , 2.00

;歌の上手そうな人ボーナス
CALL SINGER_BONUS

;カットフラグを退避
Z:302 = CFLAG:99
;!;LOCAL:100 = CFLAG:99
;口上開始前の地の文をカットするか
TFLAG:13 = 17
CALL SELF_MAEKOJO
;地の文章カット
IF (CFLAG:99 & 1) == 0 && FLAG:6
	PRINTL 
	PRINTFORMW %CALLNAME:TARGET%は野外ライブを開いた
	PRINTL 　
	PRINTW   「♪～」
	PRINTW  「♪♪～」
	PRINTW 「♪～♪～」
	PRINTW 「…………」
	PRINTW ………………
	PRINTL 　
	PRINTFORML 緊張しすぎたのか、歌っている最中に%CALLNAME:TARGET%は固まってしまった。
	PRINTW どうやら今回のライブは失敗したようだ……
	PRINTW      ･
	PRINTW      ･
	PRINTW      ･
ENDIF
;イベント口上野外ライブ失敗用
;TFLAG:13 = 17
H = 0
CALL SELF_KOJO
;地の文章カット
SIF (CFLAG:99 & 1) == 0 && FLAG:6
	PRINTFORMW 部屋に戻った%CALLNAME:MASTER%は泣いている%CALLNAME:TARGET%の頭を抱き寄せるとそっと撫でてやった……
PRINTL 　
P = ABL:歌唱技能
SIF P > 6
	P = 5
O = (P + RAND:6) / 2
SIF O <= 1
	O = 1
JUEL:恭順 += L * 20
PRINTFORML %PALAMNAME:4%の珠+{L*20}
IF P > 0
	JUEL:習得 += L * P * (ABL:歌唱技能 / 4 + 1)
	PRINTFORML %PALAMNAME:7%の珠+{L * P * (ABL:歌唱技能 / 4 + 1)}
ENDIF
JUEL:恥情 += N * (P+15)
PRINTFORML %PALAMNAME:8%の珠+{N * (P+15)}
EXP:歌唱経験 += O
PRINTFORML %EXPNAME:93% +{O}
SIF ASSI > 0
	EXP:ASSI:歌唱経験 += 1 + RAND:O
PRINTL 
WAIT
;カットフラグを戻す
CFLAG:99 = Z:302
;!;CFLAG:99 = LOCAL:100

RETURN 1

@CONCERT_SUCCESS
;野外ライブ成功
;-------------------------------------------------
;K = 歌の種類
;L = 音楽チケットの値段の補正を決定
;M = 音楽チケットの値段
;N = 音楽チケットの値段の基礎
;O = 習得の珠増加量
;P = 乱数処理用
;-------------------------------------------------

;K = RAND:21
K = 0
L = 0
M = 0
N = 0
P = 0

;ABL:歌唱技能
SIF ABL:歌唱技能 < 5
	K = RAND:21

;ABL:歌唱技能
IF ABL:歌唱技能 <= 10
	L += ABL:歌唱技能
ELSEIF ABL:歌唱技能 <= 20
	L += 20 + ABL:歌唱技能 / 2
ELSEIF ABL:歌唱技能 <= 40
	L += 30 + ABL:歌唱技能 / 4
ELSEIF ABL:歌唱技能 <= 60
	L += 40 + ABL:歌唱技能 / 6
ELSEIF ABL:歌唱技能 <= 80
	L += 50 + ABL:歌唱技能 / 8
ELSE
	L += 60 + ABL:歌唱技能 / 10
ENDIF

;ABL:露出癖をみる
IF ABL:露出癖 <= 5
	TIMES L , 0.70
ELSEIF ABL:露出癖 < 10
	TIMES L , 0.90
ELSEIF ABL:露出癖 < 20
	TIMES L , 1.00
ELSEIF ABL:露出癖 < 30
	TIMES L , 1.10
ELSEIF ABL:露出癖 < 40
	TIMES L , 1.30
ELSEIF ABL:露出癖 < 50
	TIMES L , 1.40
ELSE
	TIMES L , 1.50
ENDIF

;EXP:歌唱経験をみる
IF EXP:歌唱経験 < EXPLV:1
	TIMES L , 0.80
ELSEIF EXP:歌唱経験 < EXPLV:2
	TIMES L , 1.00
ELSEIF EXP:歌唱経験 < EXPLV:3
	TIMES L , 1.20
ELSEIF EXP:歌唱経験 < EXPLV:4
	TIMES L , 1.40
ELSEIF EXP:歌唱経験 < EXPLV:5
	TIMES L , 1.70
ELSE
	TIMES L , 2.00
ENDIF

;臆病
SIF TALENT:臆病
	TIMES L , 0.80

;目立ちたがり
SIF TALENT:目立ちたがり
	TIMES L , 1.20

;献身的
SIF TALENT:献身的
	TIMES L , 1.20

;恋慕
SIF TALENT:恋慕
	TIMES L , 2.00

;クリア後なら技能補正で安定させる
IF (FLAG:28 & 1) != 0
	;K = 歌の種類
	IF K == 0 || ABL:歌唱技能 >= 5
		;キャラの持ち歌
		N += 100
	ELSEIF K == 1 || K == 2
		N += 90 + ABL:歌唱技能 / 10
	ELSEIF K >= 3 && K < 6
		N += 70 + ABL:歌唱技能 / 8
	ELSEIF K >= 6 && K < 10
		N += 50 + ABL:歌唱技能 / 6
	ELSEIF K >= 10 && K < 15
		N += 30 + ABL:歌唱技能 / 4
	ELSE
		N += 10 + ABL:歌唱技能 / 2
	ENDIF
ELSE
	;K = 歌の種類
	IF K == 0 && ABL:歌唱技能 >= 5
		;キャラの持ち歌
		N += 100
	ELSEIF K == 1 || K == 2
		N += 90
	ELSEIF K >= 3 && K < 6
		N += 70
	ELSEIF K >= 6 && K < 10
		N += 50
	ELSEIF K >= 10 && K < 15
		N += 30
	ELSE
		N += 10
	ENDIF
ENDIF

;歌うのが上手そうな人ボーナス
CALL SINGER_BONUS

;アイドル補正
;謎の魅力
SIF TALENT:謎の魅力
	TIMES N , 1.10
;魅力
SIF TALENT:魅力
	TIMES N , 1.10
;鼓舞
SIF TALENT:鼓舞
	TIMES N , 1.30
;人気
SIF TALENT:人気
	TIMES N , 1.25

;カットフラグを退避
Z:302 = CFLAG:99
;!;LOCAL:100 = CFLAG:99
;口上開始前の地の文をカットするか
TFLAG:13 = 17
CALL SELF_MAEKOJO

;キャラ固有の持ち歌
SIF K == 0 && ABL:歌唱技能 >= 5
	CALL SP_SONG_CONCERT
SIF N <= 0
	N = 0
N += L
;地の文章カット
IF (CFLAG:99 & 1) == 0 && FLAG:6
	PRINTL 
	PRINTFORMW %CALLNAME:TARGET%は野外ライブを開いた
	PRINTL 　
	PRINTW   「♪～」
	PRINTW  「♪♪～」
	PRINTW 「♪～♪～」
	PRINTW ………………
	PRINTW      !
	PRINTL 　
	PRINTFORM %CALLNAME:TARGET%
	IF K == 0 && ABL:歌唱技能 >= 5
		PRINTFORM が披露したとっておきの持ち歌%TSTR:1%
	ELSE
		PRINT のライブ
	ENDIF
	PRINT は
	IF N >= 180
		PRINTW 大成功だ。
		PRINTFORML %CALLNAME:TARGET%の魅力的な歌声に、会場に集まった観客は熱狂的な歓声を上げている。
		PRINTFORMW 一通り歌い終わると%CALLNAME:TARGET%は達成感溢れる笑みを観客、そして%CALLNAME:MASTER%へと送った……
	ELSEIF N >= 120 && N < 180
		PRINTW 成功だ。
		PRINTFORML 歌い終え、%CALLNAME:TARGET%が去った後も歓声が聞こえてくる。
		PRINTFORMW タオルで顔に付いた汗を拭きとると、%CALLNAME:TARGET%は満足そうな笑顔をこちらに向けた
	ELSEIF N >= 60 && N < 120
		PRINTW いい感じに成功したようだ。
		PRINTFORMW %CALLNAME:TARGET%は歌い終えた後も気分が高揚しているのか、さっきからずっと鼻歌を歌っている……
	ELSEIF N < 60 && EXP:歌唱経験 == 0
		PRINTFORMW まだまだ完璧には程遠い。
		PRINTL しかしどうやらファンを作ることには成功したようだ。
		PRINTFORMW 練習を積めば%CALLNAME:TARGET%の人気は今以上に出るかもしれない……
	ELSE
		PRINTW 失敗せずに済んだようだ。
		PRINTFORMW %CALLNAME:TARGET%は緊張していたのか、舞台を降りるとほっとした表情を浮かべている。
	ENDIF
	PRINTW      ･
	PRINTW      ･
	PRINTW      ･
ENDIF
;イベント口上野外ライブ成功用
;TFLAG:13 = 17
H = 1
CALL SELF_KOJO
IF ABL:歌唱技能 <= 10
	P = ABL:歌唱技能
ELSEIF ABL:歌唱技能 <= 20
	P = 10 + (ABL:歌唱技能 / 4)
ELSEIF ABL:歌唱技能 <= 30
	P = 15 + (ABL:歌唱技能 / 6)
ELSEIF ABL:歌唱技能 <= 40
	P = 20 + (ABL:歌唱技能 / 8)
ELSEIF ABL:歌唱技能 <= 50
	P = 25 + (ABL:歌唱技能 / 10)
ELSEIF ABL:歌唱技能 <= 60
	P = 30 + (ABL:歌唱技能 / 12)
ELSEIF ABL:歌唱技能 <= 70
	P = 35 + (ABL:歌唱技能 / 14)
ELSEIF ABL:歌唱技能 <= 80
	P = 40 + (ABL:歌唱技能 / 16)
ELSEIF ABL:歌唱技能 <= 90
	P = 45 + (ABL:歌唱技能 / 18)
ELSE
	P = 50 + (ABL:歌唱技能 / 20)
ENDIF
O = 5 + RAND:16
JUEL:恭順 += L * 10
PRINTFORML %PALAMNAME:4%の珠+{L*10}
JUEL:習得 += L * O
PRINTFORML %PALAMNAME:7%の珠+{L * O}
JUEL:恥情 += N * (P+5) / 5
PRINTFORML %PALAMNAME:8%の珠+{N * (P+5) / 5}
IF ABL:歌唱技能 <= 5
	O = 2
ELSEIF ABL:歌唱技能 <= 30
	O = P / 2
ELSEIF ABL:歌唱技能 <= 50
	O = P / 3
ELSEIF ABL:歌唱技能 <= 70
	O = P / 4
ELSE
	O = P / 5
ENDIF
EXP:歌唱経験 += O
PRINTFORML %EXPNAME:93% +{O}
SIF ASSI > 0
	EXP:ASSI:歌唱経験 += 1 + RAND:O
PRINTL 

M = L + N
;クリア後インフレ
IF (FLAG:28 & 1) != 0
	IF DAY >= 120
		M += RAND:(1 + (EXP:歌唱経験 / 10))
	ELSEIF DAY >= 90
;		M = L + N
	ELSEIF DAY >= 60
		M /= 5
	ELSEIF DAY >= 48
		M /= 10
	ELSEIF DAY >= 30
		M /= 15
	ELSE
		M /= 20
	ENDIF
ELSE
	M /= 25
ENDIF

;地の文章カット
IF (CFLAG:99 & 1) == 0 && FLAG:6
	PRINTFORM %CALLNAME:TARGET%が
	PRINTFORML 開いたライブで{M}枚の音楽チケットが売れた。
ENDIF
;カットフラグを戻す
CFLAG:99 = Z:302
;!;CFLAG:99 = LOCAL:100

;ここでL+Mを再計算する必要性はあるのだろうか
;無いと思ったので書き換えてやるー
;さらに安定処理
IF P > 0
	P = (P + RAND:P) / 2
	M *= 5 * P
ELSE
	M *= 5
ENDIF
MONEY += M
PRINTFORML 売上金 +${M}
PRINTL 
WAIT
RETURN 1

;------------------------------------------
;歌うのが上手そうな方
;------------------------------------------
@SINGER_BONUS
;歌唱素質で補正をかける
SIF ABL:歌唱素質 == 3
	TIMES N , 1.50
SIF ABL:歌唱素質 == 2
	TIMES N , 1.25
SIF ABL:歌唱素質 == 1
	TIMES N , 1.10
SIF ABL:歌唱素質 == -1
	TIMES N , 0.90
SIF ABL:歌唱素質 == -2
	TIMES N , 0.80
RETURN 0

;------------------------------------------
;キャラ固有の持ち歌
;ToDo:そのうち曲の無いキャラのも追加したい　
;------------------------------------------
@SP_SONG_CONCERT
IF NO:TARGET == 1 
	;ショウタ
	TSTR:1 = 『だんだん早くなる』
	N -= 20
	N += RAND:80
ELSEIF NO:TARGET == 2 
	;アイ
	TSTR:1 = 『アイアイ』
	N -= 20
	N += RAND:71
ELSEIF NO:TARGET == 3 && CFLAG:0 == 0
	;マキ
	TSTR:1 = 『吹 っ 切 れ た』
	N -= 20
	N += RAND:71
ELSEIF NO:TARGET == 3 && CFLAG:0
	;マキ様
	TSTR:1 = 『バスト占いのうた』
	;誰か作れ
	N += RAND:100
ELSEIF NO:TARGET == 4 && CFLAG:0 == 0
	;ゆかり
	P = RAND:4
	IF P == 0
		TSTR:1 = 『チュルリラ・チュルリラ・ダッダッダ！』
		N += 20
		N += RAND:80
	ELSEIF P == 1
		TSTR:1 = 『幸せになれる隠しコマンドがあるらしい』
		N += 10
		N += RAND:80
	ELSEIF P == 2
		TSTR:1 = 『チェチェ・チェック・ワンツー！』
		N -= 10
		N += RAND:80
	ELSE
		TSTR:1 = 『自殺予防ウサギ』
		N -= 20
		N += RAND:71
	ENDIF
	
ELSEIF NO:TARGET == 5
	;ゆかり様
	TSTR:1 = 『私の新曲まだですか？』
	N += RAND:100
ELSEIF NO:TARGET == 6 
	;茜
	P = RAND:2
	IF P == 0
		TSTR:1 = 『カレンデュラ』
		N += 10
		N += RAND:80
	ELSE
		TSTR:1 = 『何でも言うことを聞いてくれるアカネチャン』
		N -= 20
		N += RAND:71
	ENDIF
ELSEIF NO:TARGET == 7
	;葵
	P = RAND:2
	IF P == 0
		TSTR:1 = 『カレンデュラ』
		N += 10
		N += RAND:80
	ELSE
		TSTR:1 = 『チョコミントアイスエイヤッ↑(/＞_＜)/』
		N -= 20
		N += RAND:71
	ENDIF
ELSEIF NO:TARGET == 50
	;ささら
	TSTR:1 = 『星空』
	;どのバージョンなんでしょうね
	;元ネタを知らなければ名曲ってそれ一
	N -= 20
	N += RAND:80
ELSEIF NO:TARGET == 52
	;タカハシ
	TSTR:1 = 『俺の名はタカハシ』
	N -= 20
	N += RAND:71
ELSEIF NO:TARGET == 53
	;ＯＮＥ
	TSTR:1 = 『CITRUS』
	N -= 20
	N += RAND:71
ELSEIF NO:TARGET == 54
	;ＩＡ
	P = RAND:4
	IF P == 0
		TSTR:1 = 『六兆年と一夜物語』
		N += 20
		N += RAND:70
	ELSEIF P == 1
		TSTR:1 = 『チルドレンレコード』
		N += 10
		N += RAND:70
	ELSEIF P == 2
		TSTR:1 = 『アスノヨゾラ哨戒班』
		N -= 10
		N += RAND:80
	ELSE
		TSTR:1 = 『延命治療』
		N -= 20
		N += RAND:71
	ENDIF
ELSEIF NO:TARGET == 80
	;ギャラ子
	TSTR:1 = 『galaxias!』
	N -= 20
	N += RAND:71
ELSEIF NO:TARGET == 81
	;ウナ
	TSTR:1 = 『はやくそれになりたい！』
	N -= 10
	N += RAND:71
ELSE
	;一応例外設定
	;とりあえずこれ歌わせとけ感がすごい
	TSTR:1 = 『だんだん早くなる』
	N -= 20
	N += RAND:80
ENDIF
RETURN 0

;テンプレ用
;ELSEIF NO:TARGET == 
;キャラ
;	TSTR:1 = 『(歌名)』
;	N -= 
;	N += RAND:
;テンプレ用2
;ELSEIF NO:TARGET == 
;キャラ
;	P = RAND:2
;	IF P == 0
;		TSTR:1 = 『(歌名)』
;		N -= 
;		N += RAND:
;	ELSE
;		TSTR:1 = 『(歌名)』
;		N -= 
;		N += RAND:
;	ENDIF

