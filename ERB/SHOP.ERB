@EVENTSHOP
DRAWLINE
DRAWLINE
;オートセーブ判定
TFLAG:99 = 1

@SHOW_SHOP
;フラグ移動の互換処理
CALL SAVEDATA_CONVERT
;グローバル変数読み込み
CALL LOAD_GLOBAL_CONFIGURE
;上限設定
CALL ALL_LIMIT
;全キャラ指定中の待機中行動をできるか判定し、できなくなっている場合は何もさせないに変更
CALL ALL_ACT_ABLE


;オートセーブ判定を解除
TFLAG:99 = 0

;所持金の上限設定
SIF MONEY >= 999999999999999999
	MONEY = 999999999999999999

DRAWLINE
;残り日数取得
CALL SYSTEM_GET_LIMITDAY
;年月日、曜日、天気等取得
CALL YOUBI
PRINTFORML {DAY:3}年 %STR:2%月 {DAY:1}日 (%STR:1%) 天気:%STR:3% {DAY+1}日目(%STR:4%)  Stage{FLAG:24}攻略中  %STR:5%
IF TARGET >= 0
	PRINTFORM %NAME:TARGET%と一緒です
ELSE
	PRINTFORM パートナーなし
ENDIF
SIF ASSI >= 0
	PRINTFORM （助手：%NAME:ASSI%）

PRINTFORM (残り${MONEY}) 
IF TARGET >= 0
	SIF ((FLAG:23 & 1p26) == 0) && FLAG:2 != 1
		PRINTFORM (好感度：{CFLAG:2})
	PRINTL 
	PRINT           耐力
	A = BASE:耐力
	SIF BASE:耐力 < 0
		A = 0
	CALL PRINT_BAR_4, A, MAXBASE:耐力, 32
	PRINTFORML ({BASE:耐力}/{MAXBASE:耐力})
	PRINT           体力
	A = BASE:体力
	SIF BASE:体力 < 0
		A = 0
	CALL PRINT_BAR_0, A, MAXBASE:体力, 32
	PRINTFORML ({BASE:体力,4}/{MAXBASE:体力,4})
	PRINT           気力
	A = BASE:気力
	SIF BASE:気力 < 0
		A = 0
	CALL PRINT_BAR, A, MAXBASE:気力, 32, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("緑"), RESULT:1
	PRINTFORM ({BASE:気力,4}/{MAXBASE:気力,4})
ELSE
	PRINTL 
ENDIF

PRINTL 

;PRINT_ITEM
;アイテム表示方法変更
;FLAG:23 & 8192がONだと非消費型のアイテム非表示
;FLAG:23 & 4096がONだとJ用アイテム非表示
PRINT 所持アイテム:
LOCAL = 0
REPEAT 99
	SIF ITEM:COUNT > 0 || ITEM:(COUNT+500)
		LOCAL = 1
REND
IF LOCAL == 0
	PRINTL なし
ELSE
	REPEAT 60
		STRLENS ITEMNAME:COUNT
		IF RESULT < 1
			ITEM:COUNT = 0
			CONTINUE
		ENDIF
		SIF (FLAG:23 & 8192) && (COUNT <= 24 || COUNT >= 40)
			CONTINUE
		LOCAL = COUNT
		IF ITEM:LOCAL > 0 && (COUNT <= 24 || COUNT >= 40)
			PRINTFORM %ITEMNAME:LOCAL% 
		ELSEIF ITEM:LOCAL > 0
			PRINTFORM %ITEMNAME:LOCAL%×{ITEM:LOCAL} 
		ENDIF
	REND
	REPEAT 100
		STRLENS ITEMNAME:(COUNT+500)
		IF RESULT < 1
			ITEM:(COUNT+500) = 0
			CONTINUE
		ENDIF
		SIF (FLAG:23 & 4096)
			CONTINUE
		SIF ITEM:(COUNT+500) > 0
			PRINTFORM %ITEMNAME:(COUNT+500)%×{ITEM:(COUNT+500)} 
	REND
	;調合の作成アイテム関連
	REPEAT 4
		STRLENS ITEMNAME:(COUNT+850)
		IF RESULT < 1
			ITEM:(COUNT+850) = 0
			CONTINUE
		ENDIF
		SIF (FLAG:23 & 4096)
			CONTINUE
		IF ITEM:(COUNT+850) > 0
			;壊れたドールがある時ライオット～は持てない
			IF ITEM:853 > 0
				ITEM:852 = 0
			;ライオットドールがある時、壊れた～は持てない
			ELSEIF ITEM:852 > 0
				ITEM:853 = 0
			ENDIF
			;ペンデュラム、魔法瓶
			IF ITEM:850 > 0 || ITEM:851 > 0
				PRINTFORM %ITEMNAME:(COUNT+850)%({ITEM:(COUNT+850)})
			;ライオットドール 
			ELSEIF ITEM:852 > 0
				;永夜サーキットの処理
				SIF ITEM:852 > 50
					ITEM:852 -= 50
				PRINTFORM %ITEMNAME:852%LV
				SIF ITEM:852 < 10
					PRINTFORM  
				PRINTFORM {ITEM:852} 
			;壊れたドール
			ELSEIF ITEM:853 > 0
				PRINTFORM %ITEMNAME:853% 
			ENDIF
		ENDIF
	REND
ENDIF
PRINTL 
LOCAL = 0

;別れているキャラ数の確認
;現助手＋元助手のキャラ数の確認
S = 0
T = 0
REPEAT CHARANUM
	IF TARGET >= 0
		R = NO:TARGET
		SIF (CFLAG:COUNT:1 == 2 || RELATION:COUNT:R >= 120)
			S += 1
	ELSE
		SIF CFLAG:COUNT:1 == 2
			S += 1
	ENDIF
	SIF CFLAG:COUNT:21
		T += 1
REND

;TIMEに合った調教キャラが居るか
E = 0
REPEAT CHARANUM
	;主人公とパートナーと別れているキャラは排除
	SIF COUNT == 0
		CONTINUE
	SIF COUNT == TARGET
		CONTINUE
	SIF CFLAG:COUNT:17 || CFLAG:COUNT:21
		CONTINUE
	E += 1
REND

;アクセサリイベント関係
;F:1は小物入れを見るの条件等で使用
F:1 = 0
REPEAT CHARANUM
	;主人公用アクセサリを持っているか否か
	SIF FLAG:(6000 + NO:COUNT)
		F:1 += 1
REND

CALL SALEITEM_CHECK

;エッチなアイテム非表示
IF FLAG:23 & 16
	REPEAT 60
		TFLAG:COUNT = ITEMSALES:COUNT
		ITEMSALES:COUNT = 0
	REND
ENDIF

;0-99に自動的に売り物が表示される。
;今回は何も無い
PRINT_SHOPITEM

;エッチなアイテム非表示(販売フラグを戻す。見えて無くても購入可能になってしまうけど、処理が楽なのでとりあえず暫定)
IF FLAG:23 & 16
	REPEAT 60
		ITEMSALES:COUNT = TFLAG:COUNT
	REND
ENDIF

;SHOPコマンドを並べて表示
;COUNT == 16は元展示室枠。Jのベースに存在したがJでは封印されている
;ボス選択は非表示にして、知ってる人かつ使いたい人だけ使うようにする
;PRINTLC [115] - ボス選択    
;YMっぽく線とか入れてみたり
DRAWLINE
;改行用変数
LOCAL:0 = 0
REPEAT 30
	IF COUNT == 0 && TARGET >= 0 && MONEY > 0
		IF FLAG:23 & 1048576
			IF TALENT:相愛
				PRINTLC [100] - 妻とイチャイチャ  
			ELSEIF TALENT:恋人
				PRINTLC [100] - 恋人と過ごす      
			ELSE
				PRINTLC [100] - パートナーと過ごす
			ENDIF
		ELSE
			PRINTLC [100] - パートナーと過ごす
		ENDIF
	ELSEIF COUNT == 1
		PRINTLC [101] - Stage攻略         
	ELSEIF COUNT == 2 && TARGET >= 0
		PRINTLC [102] - お金を稼ぐ        
	ELSEIF COUNT == 3 && TARGET >= 0 && MONEY > 0
		PRINTLC [103] - 戦闘訓練          
	ELSEIF COUNT == 4 && (CHARANUM - T) > 0
		PRINTLC [104] - 能力表示          
	ELSEIF COUNT == 5 && (CHARANUM - T) > 1
		PRINTLC [105] - 待機中動作指示    
	ELSEIF COUNT == 6 && MONEY > 0
		PRINTLC [106] - 休憩              
	ELSEIF COUNT == 7 && (CHARANUM - T) > 2
		PRINTLC [107] - 休憩所に行く      
	ELSEIF COUNT == 8
		PRINTLC [110] - パートナー交代    
	ELSEIF COUNT == 9 && S >= 1
		PRINTLC [111] - 助手交代          
	ELSEIF COUNT == 10 && (CHARANUM - T) > 2
		PRINTLC [112] - 別れる            
	ELSEIF COUNT == 11 && FLAG:28
		PRINTLC [114] - 強くてニューゲーム
	ELSEIF COUNT == 12
		CONTINUE
	ELSEIF COUNT == 13 && TALENT:MASTER:後戻りできる程度の能力 && FLAG:24 > 1
		PRINTLC [116] - 前のStageに戻る   
	ELSEIF COUNT == 14 && (CHARANUM - T) > 1
		PRINTLC [117] - 能力値を上げる    
	ELSEIF COUNT == 15 && (CHARANUM - T) > 2
		PRINTLC [118] - キャラソート      
	ELSEIF COUNT == 16 && (CHARANUM - T) > 2
		PRINTLC [119] - キャラ検索        
	ELSEIF COUNT == 17 && TARGET > 0 && ITEM:45 > 0 && TFLAG:79 == 0 && ABL:露出癖 >= 4 && (TALENT:恋慕 || ABL:親密 >= 5)
		PRINTLC [130] - 野外ライブを開く  
	ELSEIF COUNT == 18 && TALENT:MASTER:結界操作
		CONTINUE
	ELSEIF COUNT == 19 && F:1 > 0 && FLAG:62 & 4
		PRINTLC [160] - 小物入れを見る    
	ELSEIF COUNT == 20 && TALENT:MASTER:結界操作
		PRINTLC [161] - 怪しい衣装店      
	ELSEIF COUNT == 21 && FLAG:62 & 1 && FLAG:97
		PRINTLC [170] - 育児室に行く      
;	ELSEIF COUNT == 22 && SUMARRAY(ITEM, 900, 1000) > 0
;		PRINTLC [180] - 調合素材表示
	ELSEIF COUNT == 23
		PRINTLC [190] - トロフィー
	ELSEIF COUNT == 24
		PRINTLC [191] - 特殊能力取得
	ELSE
		CONTINUE
	ENDIF
	IF FLAG:23 & 32768
		LOCAL:0 += 1
		;Emueraの「PRINTCを並べる数」の指定数だけ並んだら改行
		IF LOCAL:0 == PRINTCPERLINE()
			PRINTL 
			LOCAL:0 = 0
		ENDIF
	ELSE
		;並べて表示しない場合1行で改行
		PRINTL 
	ENDIF
REND
IF FLAG:23 & 32768
	;なんとなくこの三つは並べたかった
	SIF LOCAL:0 != 0
		PRINTL 
	PRINTLC [113] - コンフィグ        
	PRINTLC [200] - セーブ            
	PRINTLC [300] - ロード            
	PRINTL 
ELSE
	PRINTL [113] - コンフィグ        
	PRINTL [200] - セーブ            
	PRINTL [300] - ロード            
ENDIF
;主人に[Debug]がある=debugモードである場合
SIF TALENT:MASTER:Debug
	PRINTLC [998] - デバッグメニュー

@USERSHOP

;イチャイチャ開始
IF RESULT == 100 && TARGET >= 0 && MONEY > 0
	CALL ACCESSORY_EQUIP
	BEGIN TRAIN
	RETURN 1
;攻略開始
ELSEIF RESULT == 101
	TFLAG:600 = 34
	CALL KOJO_JUN
	CALL STAGE_CAPTURE
	BEGIN TURNEND
	RETURN 1
;お金稼ぎ
ELSEIF RESULT == 102 && TARGET >= 0
	CALL EARN_MONEY
	SIF RESULT
		BEGIN TURNEND
	RETURN 1
;戦闘訓練
ELSEIF RESULT == 103 && TARGET >= 0 && MONEY > 0
	IF ABL:TARGET:戦闘技能 >= 9999
		PRINTFORMW これ以上レベルは上がりません
	ELSE
		T = TARGET
		CALL GET_BATTLE_PRICE
		IF MONEY < P
			PRINTFORMW 次のレベルになるには${P}が必要です。お金が足りません。
		ELSE
			LOCAL = ABL:TARGET:戦闘技能
			PRINTFORML 次のレベルになるには${P}が必要です。レベルを上げますか？（現在のLv：{(ABL:TARGET:戦闘技能)}）
			PRINTL [0] はい
			PRINTL [1] いいえ
			IF P == 10
				CALL BATTLE_PRICE_I
				PRINTFORML [2] $10(最低価格)で上がる分だけレベルを上げる（Lv：{(ABL:TARGET:戦闘技能)+K}までLvUP　使用金額:${M}）
			ENDIF
			T = TARGET
			CALL GET_BATTLE_PRICE
			CALL BATTLE_PRICE_G
			PRINTFORML [3] お金が無くなるまでレベルを上げる（Lv：{(ABL:TARGET:戦闘技能)+K}までLvUP　使用金額:${M}）
			PRINTFORML [4] 選択してレベルを上げる
			
			T = TARGET
			CALL GET_BATTLE_PRICE
			
			$INPUT_LOOP_BP
			INPUT
	
			IF RESULT == 2 && P > 10
				PRINTFORML 正しい値を入力してください
				GOTO INPUT_LOOP_BP
			ENDIF
	
			IF RESULT == 0
				MONEY -= P
				TFLAG:600 = 20
				CALL KOJO_JUN
				ABL:TARGET:戦闘技能 += 1
				PRINTFORMW ＜%NAME:TARGET%の戦闘技能レベルが1つ上がった　今のLv：{ABL:TARGET:戦闘技能}＞
			ELSEIF RESULT == 1
				RETURN 1
			ELSEIF RESULT == 2
				CALL BATTLE_PRICE_I
				ABL:T:戦闘技能 += K
				MONEY -= M
				TFLAG:600 = 20
				CALL KOJO_JUN
				PRINTFORML ＜%NAME:TARGET%の戦闘技能レベルが{K}上がった　今のLv：{ABL:TARGET:戦闘技能}＞
				PRINTFORMW ＜使用金額:${M}＞
			ELSEIF RESULT == 3
				CALL BATTLE_PRICE_G
				ABL:T:戦闘技能 += K
				MONEY -= M
				TFLAG:600 = 20
				CALL KOJO_JUN
				PRINTFORML ＜%NAME:TARGET%の戦闘技能レベルが{K}上がった　今のLv：{ABL:TARGET:戦闘技能}＞
				PRINTFORMW ＜使用金額:${M}＞
			ELSEIF RESULT == 4
				PRINTFORML 何レベル上げますか(1-{K}、-1でレベルを上げずに戻る)
				$INPUT_LOOP_BP2
				INPUT
				IF RESULT == -1
					PRINTFORMW レベルを上げずに戻ります
					RETURN 1
				ELSEIF RESULT > K || RESULT < 1
					PRINTFORML 正しい値を入力してください
					GOTO INPUT_LOOP_BP2
				ELSE
					CALL BATTLE_PRICE_4, RESULT
					ABL:T:戦闘技能 += K
					MONEY -= M
					TFLAG:600 = 20
					CALL KOJO_JUN
					PRINTFORML ＜%NAME:TARGET%の戦闘技能レベルが{K}上がった　今のLv：{ABL:TARGET:戦闘技能}＞
					PRINTFORMW ＜使用金額:${M}＞
				ENDIF
			ELSE
				PRINTFORML 正しい値を入力してください
				GOTO INPUT_LOOP_BP
			ENDIF
			RETURN 1
		ENDIF
	ENDIF
	RETURN 1
;能力表示
ELSEIF RESULT == 104 && (CHARANUM - T) > 0
	;全員表示フラグオフ
	U = 0
	CALL SHOW_CHARADATA
;待機中動作指示
ELSEIF RESULT == 105 && (CHARANUM - T) > 1
	;全員表示フラグオフ
	U = 0
	CALL CHANGE_ACTION
;休憩
ELSEIF RESULT == 106 && MONEY > 0
	IF TARGET >= 0
		TFLAG:600 = 21
		CALL KOJO_JUN

		IF CFLAG:20 > 0
			CFLAG:20 -= 10
			SIF CFLAG:20 < 0
				CFLAG:20 = 0
		ENDIF
	ENDIF
	;休憩フラグを立てる
	FLAG:0 = 1
	CALL DAILY_LIFE
	BEGIN TURNEND
	RETURN 1
;避難所
ELSEIF RESULT == 107 && (CHARANUM - T) > 2
	CALL AZUKARI
;パートナー交代
ELSEIF RESULT == 110
	;全員表示フラグオフ
	U = 0
	CALL CHANGE_TARGET
;助手交代
ELSEIF RESULT == 111 && S >= 1
	;全員表示フラグオフ
	U = 0
	CALL SELECT_ASSI
;別れる
ELSEIF RESULT == 112 && (CHARANUM - T) > 2
	;全員表示フラグオフ
	U = 0
	CALL CHARA_SALE
;コンフィグ
ELSEIF RESULT == 113
	CALL START_CONFIGURE
;強くてニューゲーム
ELSEIF RESULT == 114 && FLAG:28
	CALL NEWGAME_SELECT
	RETURN 1
;ボス選択
ELSEIF RESULT == 115
	CALL BOSS_CHOICE
;戻る
ELSEIF RESULT == 116 && TALENT:MASTER:後戻りできる程度の能力 && FLAG:24 > 1
	PRINTFORML Stage{FLAG:24 - 1}に戻ってもよろしいですか？
	PRINTL [0] はい
	PRINTL [1] いいえ

	$INPUT_LOOP_STAGE
	INPUT

	IF RESULT != 0 && RESULT != 1
		PRINTFORML 正しい値を入力してください
		GOTO INPUT_LOOP_STAGE
	ENDIF

	IF RESULT == 0
		PRINTFORMW それではStage{FLAG:24 - 1}に戻ります
		FLAG:24 -= 1
		FLAG:25 = 0
		FLAG:27 = 0
	ENDIF
	RETURN 1

;能力アップ
ELSEIF RESULT == 117 && (CHARANUM - T) > 1
	;全員表示フラグオフ
	U = 0
	CALL ABL_UP_SELECT
;キャラソート
ELSEIF RESULT == 118 && (CHARANUM - T) > 2
	CALL CHARA_SORT
;キャラ検索
ELSEIF RESULT == 119 && (CHARANUM - T) > 2
	CALL CHARA_SEARCH

ELSEIF RESULT == 130
	IF TARGET > 0
		IF ITEM:45 > 0 && TFLAG:79 == 0 && ABL:露出癖 >= 4 && (TALENT:恋慕 || ABL:親密 >= 5)
			CALL CONCERT
			RETURN 1
		ENDIF
	ELSE
		RETURN 0
	ENDIF
;ELSEIF RESULT == 150 && FLAG:10 > 0
;	PRINTW 展示室に向かった……
;	PRINTL 
;	PRINTL 
;	CALL COLLECTION_ROOM
ELSEIF RESULT == 160 && F:1 > 0
	CALL ACCESSORY_SHOW
ELSEIF RESULT == 161 && TALENT:MASTER:結界操作
	CALL CLOTHES_SHOP
ELSEIF RESULT == 170 && FLAG:62 & 1 && FLAG:97
	CALL CHILD_CARE_ROOM
;調合素材を確認する
;ELSEIF RESULT == 180 && SUMARRAY(ITEM, 900, 1000) > 0
;	CALL SOZAI_SHOP
;トロフィー(実績一覧)
ELSEIF RESULT == 190
	CALL TROPHY
;特殊能力取得
ELSEIF RESULT == 191
	CALL TROPHY_FEAT_GET
ELSEIF RESULT == 200
	SAVEGAME
ELSEIF RESULT == 300
	LOADGAME
ELSEIF RESULT == 998 && TALENT:MASTER:Debug
	PRINTL デバッグメニューを呼び出します
	CALL DEBUG_MENU_SHOP
ENDIF

RETURN 0

;---------------------------------------------------------
;パートナー交代
;---------------------------------------------------------
@CHANGE_TARGET
LOCAL = 0
$START
X = 0
REPEAT CHARANUM
	SIF CFLAG:COUNT:16 == 1
		X = 1
REND

IF TARGET >= 0
	PRINT 現在
	PRINTFORML %NAME:TARGET%をパートナーとしています
ENDIF
PRINTL 今回は誰をパートナーにしますか？
;変数Vは出産育児関連で使用
V = 1
CALL LIFE_LIST(0, LOCAL)
V = 0
IF CHARANUM <= 100
	PRINTL [100] 戻る
ELSE
	PRINTL [1000][-1] 戻る
ENDIF
IF X == 1 && U == 0
	PRINTL [200] 休憩中のキャラも表示する
ELSEIF X == 1 && U == 1
	PRINTL [200] 休憩中のキャラを非表示にする
ENDIF
IF FLAG:23 & 16384
	PRINTL [300] キャラリスト簡易表示 OFF
ELSE
	PRINTL [300] キャラリスト簡易表示 ON
ENDIF
PRINTL [400] パートナーなしにする
PRINTL [500] 個別表示項目切替

$INPUT_LOOP
INPUT
IF RESULT == -1 || (CHARANUM <= 100 && RESULT == 100) || RESULT == 1000
	RETURN 0
ELSEIF RESULT == 200 && X == 1
	IF U == 0
		U = 1
		GOTO START
	ELSE
		U = 0
		GOTO START
	ENDIF
ELSEIF RESULT == 300
	IF FLAG:23 & 16384
		FLAG:23 -= 16384
		;共通コンフィグ自動保存
		CALL STORE_GLOBAL_CONFIGURE
		GOTO START
	ELSE
		FLAG:23 |= 16384
		;共通コンフィグ自動保存
		CALL STORE_GLOBAL_CONFIGURE
		GOTO START
	ENDIF
ELSEIF RESULT == 400
	PRINTFORMW パートナーなしにしました
	TARGET = -1
	RESTART
ELSEIF RESULT == 500
	LOCAL = (LOCAL == 3) ? 1 # LOCAL + 1
	GOTO START
ELSEIF RESULT < 1 || RESULT >= CHARANUM
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
;非表示中は預かり中のキャラは選べない
ELSEIF CFLAG:RESULT:16 == 1 && U == 0
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
;霧に飲まれかけているキャラは選べない
ELSEIF CFLAG:RESULT:17 == 1
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
;別れ中のキャラは選べない
ELSEIF CFLAG:RESULT:21 == 1
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
;臨月、育児中のキャラも選べない
ELSEIF FLAG:62 & 1 && (MARK:RESULT:90 == 1)
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
;ELSEIF RESULT == ASSI
	;GOTO INPUT_LOOP
ENDIF

IF TARGET != RESULT
	;一応誰に代わるのかはTで得られるようにしておく(LOCALでも記録)
	T = RESULT
	LOCAL = RESULT
	TFLAG:600 = 22
	CALL KOJO_JUN
	;RESULTを戻しておく
	RESULT = T
	RESULT = LOCAL
ENDIF

IF RESULT != 0
	;誰から代わるかはTで得られるようにしておく
	T = TARGET
	TARGET = RESULT
ENDIF
;相性だけで助手になっていた場合、未設定に戻しておく
IF ASSI >= 0
	SIF CFLAG:ASSI:1 < 2
		ASSI = -1
ENDIF
SIF TARGET == ASSI
	ASSI = -1

;モンスター退治はさせない
SIF CFLAG:TARGET:13 == 6
	CFLAG:TARGET:13 = 0

;休憩（非表示）にはさせない
SIF CFLAG:TARGET:16 == 1
	CFLAG:TARGET:16 = 0

PRINTFORMW %CALLNAME:TARGET%をパートナーにしました

IF T != TARGET
	;誰から代わってきたかはT
	TFLAG:600 = 60
	CALL KOJO_JUN
	RESULT = TARGET
ENDIF

@SELECT_ASSI
X = 0
REPEAT CHARANUM
	SIF CFLAG:COUNT:16 == 1
		X = 1
REND

PRINT 現在
IF ASSI >= 0
	PRINTFORML [{ASSI}]%NAME:ASSI%が助手
ELSE
	PRINTFORML 助手はいません
ENDIF
PRINTL 誰を助手にしますか？(現在の助手は☆)

PRINTFORML   [ 0]助手なし
REPEAT CHARANUM
	TFLAG:COUNT = 0
	;0（主人公）とASSI以外は排除
	SIF COUNT == 0
		CONTINUE
	;預かり中のキャラは非表示だと選べない
	SIF CFLAG:COUNT:16 == 1 && U == 0
		CONTINUE
	;別れ中のキャラは選べない
	SIF CFLAG:COUNT:21
		CONTINUE
	;臨月、育児中のキャラも選べない
	SIF FLAG:62 & 1 && (MARK:COUNT:90 == 1)
		CONTINUE
	;霧に飲まれかけているキャラは選べない
	SIF CFLAG:COUNT:17 == 1
		CONTINUE
	IF TARGET >= 0
		R = NO:TARGET
		SIF (CFLAG:COUNT:1 < 2 && RELATION:COUNT:R < 120)
			CONTINUE
	ELSE
		SIF CFLAG:COUNT:1 < 2
			CONTINUE
	ENDIF
	IF COUNT == ASSI
		PRINT ☆
	ELSE
		PRINT 　
	ENDIF
	PRINTFORM [{COUNT,2}]%NAME:COUNT,16,LEFT% 
	;CALL ARRANGE_CHARANAME
	IF COUNT == ASSI
		PRINT 　　　　　
	ELSEIF ISASSI:COUNT == 1
		PRINT [元助手]　
	ELSE
		PRINT [未経験]　
	ENDIF
	IF ABL:COUNT:技巧 < 10
		PRINTFORM %ABLNAME:2%Lv{ABL:COUNT:技巧}  
	ELSE
		PRINTFORM %ABLNAME:2%Lv{ABL:COUNT:技巧} 
	ENDIF
	IF ABL:COUNT:レズっ気 < 10
		PRINTFORM %ABLNAME:9%Lv{ABL:COUNT:レズっ気}  
	ELSE
		PRINTFORM %ABLNAME:9%Lv{ABL:COUNT:レズっ気} 
	ENDIF
	IF ABL:COUNT:レズ中毒 < 10
		PRINTFORM %ABLNAME:12%Lv{ABL:COUNT:レズ中毒}  
	ELSE
		PRINTFORM %ABLNAME:12%Lv{ABL:COUNT:レズ中毒} 
	ENDIF
	SIF TALENT:COUNT:両刀
		PRINT 両刀
	PRINTFORML 
REND
IF CHARANUM <= 100
	PRINTL [100] 戻る
ELSE
	PRINTL [1000][-1] 戻る
ENDIF
IF X == 1 && U == 0
	PRINTFORML [200] 休憩中のキャラも表示する
ELSEIF X == 1 && U == 1
	PRINTFORML [200] 休憩中のキャラを非表示にする
ENDIF


$INPUT_LOOP
INPUT
IF RESULT == -1 || (CHARANUM <= 100 && RESULT == 100) || RESULT == 1000
	RETURN 0
ELSEIF RESULT == 200 && X == 1
	IF U == 0
		U = 1
		RESTART
	ELSE
		U = 0
		RESTART
	ENDIF
ELSEIF RESULT == 0
	ASSI = -1
	RETURN 0
ELSEIF RESULT < 0 || RESULT >= CHARANUM
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
ELSEIF CFLAG:RESULT:16 == 1 && U == 0
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
;霧に飲まれかけている、別れているキャラは選べない
ELSEIF CFLAG:RESULT:17 == 1 || CFLAG:RESULT:21
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
ELSEIF CFLAG:RESULT:1 < 2
	IF TARGET >= 0
		R = NO:TARGET
		IF RELATION:RESULT:R < 120
			PRINTFORML 正しい値を入力してください
			GOTO INPUT_LOOP
		ENDIF
	ELSE
		PRINTFORML 正しい値を入力してください
		GOTO INPUT_LOOP
	ENDIF
ENDIF

ASSI = RESULT

IF TARGET != RESULT
	;変更後の助手番号はASSIで見れる
	TFLAG:600 = 23
	CALL KOJO_JUN
ENDIF

;モンスター退治はさせない
SIF CFLAG:ASSI:13 == 6
	CFLAG:ASSI:13 = 0

;休憩（非表示）にはさせない
SIF CFLAG:ASSI:16 == 1
	CFLAG:ASSI:16 = 0

CALL ASSI_CHANGE
ISASSI:ASSI = 1
SIF ASSI == TARGET
	TARGET = -1

PRINTFORMW %CALLNAME:ASSI%を助手にしました

@LIFE_LIST(ARG, ARG:1)
;FLAG:23 & 16384 ONでキャラリスト簡易表示
W = 0
DRAWLINE
PRINT 耐力
SIF BASE:MASTER:耐力 < 0
	BASE:MASTER:耐力 = 0
CALL PRINT_BAR_4, BASE:MASTER:耐力, MAXBASE:MASTER:耐力, 32
PRINTFORM ({BASE:MASTER:耐力}/{MAXBASE:MASTER:耐力})
PRINTFORML  [0]%NAME:MASTER%
DRAWLINE
IF FLAG:23 & 16384
	IF TARGET > 0
		PRINTFORM パートナー：
		SETCOLOR 0xFFFFF
		PRINTFORM %CALLNAME:TARGET%　
		RESETCOLOR
	ENDIF
	IF ASSI > 0
		PRINTFORM 助手：
		SETCOLOR 0x50B0FF
		PRINTFORM %CALLNAME:ASSI%　
		RESETCOLOR
	ENDIF
	PRINTL 
	REPEAT CHARANUM
		;主人公は排除
		SIF COUNT == 0
			CONTINUE
		;預かり中のキャラは非表示なら排除
		SIF CFLAG:COUNT:16 == 1 && U == 0
			CONTINUE
		;別れ中のキャラは排除
		SIF CFLAG:COUNT:21
			CONTINUE
		;臨月、育児中のキャラは排除
		SIF FLAG:62 & 1 && (MARK:COUNT:90 == 1) && V
			CONTINUE
		;3列で表示
		PRINTFORM [{COUNT,3}]
		;パートナー（とついでに助手）が分かりやすいように
		IF COUNT == TARGET
			SETCOLOR 0xFFFFF
		ELSEIF COUNT == ASSI
			SETCOLOR 0x50B0FF
		ELSEIF FLAG:5 == 10 && CFLAG:COUNT:17
			SETCOLOR 105, 105, 105
		ENDIF
		PRINTFORM %CALLNAME:COUNT,10,LEFT%
		RESETCOLOR
		IF ARG
			CALL ISABLUP_ALL
		ELSE
			PRINT    
		ENDIF
		VARSET LOCALS, ""
		IF TALENT:COUNT:相愛
			LOCALS = 相愛
		ELSEIF TALENT:COUNT:親愛
			LOCALS = 親愛
		ELSEIF TALENT:COUNT:恋人
			LOCALS = 恋人
		;恋慕についてはインビシブルモードかどうかで表示するかどうかを変える
		ELSEIF TALENT:COUNT:恋慕 && (FLAG:2 != 1 || (CFLAG:COUNT:62 & 262144))
			LOCALS = 恋慕
		ENDIF
		SIF TALENT:COUNT:淫乱
			LOCALS += " 淫"
		PRINTFORM %LOCALS,10,LEFT%
		W += 1
		IF W == 3
			W = 0
			PRINTL 
		ENDIF
	REND
	PRINTL 
	SETCOLOR 0xFFFFF
	PRINT 　　　　　　パートナー　
	SETCOLOR 0x50B0FF
	PRINT 助手　
	IF FLAG:5 == 10
		SETCOLOR 105, 105, 105
		PRINT 霧に飲まれかけているキャラ　
	ENDIF
	RESETCOLOR
	PRINTL を表しています
ELSE
	REPEAT CHARANUM
		;主人公は排除
		SIF COUNT == 0
			CONTINUE
		;預かり中のキャラは非表示なら排除
		SIF CFLAG:COUNT:16 == 1 && U == 0
			CONTINUE
		;別れ中のキャラは排除
		SIF CFLAG:COUNT:21
			CONTINUE
		;臨月、育児中のキャラは排除
		SIF FLAG:62 & 1 && (MARK:COUNT:90 == 1) && V
			CONTINUE
		PRINT 体力
		SIF BASE:COUNT:体力 < 0
			BASE:COUNT:体力 = 0
		CALL PRINT_BAR_0, BASE:COUNT:体力, MAXBASE:COUNT:体力, 16
		PRINTFORM ({BASE:COUNT:体力,4}/{MAXBASE:COUNT:体力,4})
		IF COUNT == TARGET
			PRINT  ☆
		ELSEIF COUNT == ASSI
			PRINT  ★
		ELSE
			PRINT  　
		ENDIF
		PRINTFORM [{COUNT,3}]%NAME:COUNT,16,LEFT%　
		SIF ARG
			CALL ISABLUP_ALL
		IF COUNT == ASSI
			PRINT 　　　　
		;ELSEIF ISASSI:COUNT == 1
			;PRINT [●元助手]
		ELSEIF CFLAG:COUNT:1 == 2
			PRINT [助手可]
		ENDIF
		IF TALENT:COUNT:相愛
			PRINT [相愛]
		ELSEIF TALENT:COUNT:親愛
			PRINT [親愛]
		ELSEIF TALENT:COUNT:恋人
			PRINT [恋人]
		;恋慕についてはインビシブルモードかどうかで表示するかどうかを変える
		ELSEIF TALENT:COUNT:恋慕 && (FLAG:2 != 1 || (CFLAG:COUNT:62 & 262144))
			PRINT [恋慕]
		ENDIF
		SIF TALENT:COUNT:淫乱
			PRINT [淫乱]

		PRINTL 

		T = COUNT

		;ARG:1 により表示するものを変えてみるテスト(ARG:1==0 で従来どおり）
		SELECTCASE ARG:1
			;刻印系表示
			CASE 2
				PRINTFORML     苦痛依存：{MARK:T:苦痛依存, 2, LEFT}  快楽依存：{MARK:T:快楽依存, 2, LEFT}  既成事実：{MARK:T:既成事実, 2, LEFT}  反発感情：{MARK:T:反発感情, 2, LEFT}  
				IF FLAG:5 == 10 && CFLAG:T:17
					PRINTFORML   ×
				ELSEIF FLAG:5 == 10
					PRINTFORML 
				ENDIF
			;恋愛系能力表示
			CASE 1
				PRINTFORM     親密：{ABL:T:親密, 4, LEFT}  欲望：{ABL:T:欲望, 4, LEFT}  技巧：{ABL:T:技巧, 4, LEFT}  奉仕精神：{ABL:T:奉仕精神, 4, LEFT}  
				SIF ((FLAG:23 & 1p26) == 0) && FLAG:2 != 1
					PRINTFORM 好感度：{CFLAG:T:2}
				PRINTFORML 
				IF FLAG:5 == 10 && CFLAG:T:17
					PRINTFORML   ×
				ELSEIF FLAG:5 == 10
					PRINTFORML 
				ENDIF
			;戦闘系能力表示
			CASEELSE
				CALL PRINT_BATTLE_POWER
				SIF FLAG:5 == 10
					PRINTFORM     仲間になってから{CFLAG:T:18}日経過  今までに{CFLAG:T:19}回霧に飲まれかけました
				IF FLAG:5 == 10 && CFLAG:T:17
					PRINTFORML   ×
				ELSEIF FLAG:5 == 10
					PRINTFORML 
				ENDIF
		ENDSELECT
	REND
	IF FLAG:5 == 10
		PRINTL 　　　　　　☆：パートナー　★：助手　×：霧に飲まれかけ　を表しています
	ELSE
		PRINTL 　　　　　　☆：パートナー　★：助手　を表しています
	ENDIF
ENDIF

;戦闘系能力表示
@PRINT_BATTLE_POWER
CALL GET_BATTLE_POWER
A = BASE:T:耐力
SIF BASE:T:耐力 < 0
	A = 0
CALL VALUE_FORM, A, "耐力"
LOCALS:0 = %RESULTS%
CALL VALUE_FORM, MAXBASE:T:耐力, "耐力"
LOCALS:1 = %RESULTS%
CALL VALUE_FORM, P
LOCALS:2 = %RESULTS%
CALL VALUE_FORM, BASE:T:魔力
LOCALS:3 = %RESULTS%
CALL VALUE_FORM, CFLAG:T:2
LOCALS:4 = %RESULTS%
PRINTFORM     耐力：%LOCALS:0,6,RIGHT%/%LOCALS:1,6,RIGHT%  戦闘能力：%LOCALS:2,12,LEFT% 魔力：%LOCALS:3,12,LEFT% 
SIF ((FLAG:23 & 1p26) == 0) && FLAG:2 != 1
	PRINTFORM 好感度：%LOCALS:4,12,LEFT%
PRINTFORML 

;数値表示変更
@VALUE_FORM(ARG, ARGS)
VARSET LOCAL, 0
VARSET LOCALS, ""

IF ARGS == "耐力"
	IF ARG <= 999999
		RESULTS = {ARG}
		RETURN
	ENDIF
ELSEIF ARGS == "珠"
	IF ARG <= 9999999
		RESULTS = {ARG}
		RETURN
	ENDIF
ELSEIF ARGS == "経験"
	IF ARG <= 999999
		RESULTS = {ARG}
		RETURN
	ENDIF
ELSE
	IF ARG <= 99999999
		RESULTS = {ARG}
		RETURN
	ENDIF
ENDIF

LOCAL:0 = 10000000000000000
LOCAL:1 = 1000000000000
LOCAL:2 = 100000000
LOCAL:3 = 10000
LOCAL:4 = 1
LOCALS:0 = 京
LOCALS:1 = 兆
LOCALS:2 = 億
LOCALS:3 = 万

FOR LOCAL:98, 0, 4
	IF ARG >= LOCAL:(LOCAL:98)
		LOCAL:99 = LOCAL:98
		BREAK
	ENDIF
NEXT

LOCAL:10 = ARG / LOCAL:(LOCAL:99)
LOCAL:11 = (ARG % LOCAL:(LOCAL:99)) / LOCAL:((LOCAL:99)+1)

IF ARGS == "耐力" || ARGS == "珠" || ARGS == "経験"
	RESULTS = {LOCAL:10}%LOCALS:(LOCAL:99)%
ELSE
	RESULTS = {LOCAL:10}%LOCALS:(LOCAL:99)%{LOCAL:11}%LOCALS:((LOCAL:99)+1)%
ENDIF

;避難所（キャラ預かり所）
@AZUKARI
X = 0
REPEAT CHARANUM
	SIF CFLAG:COUNT:16 == 1
		X = 1
REND

PRINTL 一覧などで表示させたくないキャラを、休憩させておく事ができます。
PRINTL 維持費などは通常通りかかります。あくまで表示しないだけなので注意してください。
CALL KYUUKEIJO

@KYUUKEIJO
DRAWLINE
PRINTL 休憩所です。休ませるor連れて行くキャラを選択してください。
PRINTL （※：休憩中を表しています）

X = 0
PRINTL  
;LOCALに表示可能なキャラ数を記録し、2人以上なら全員へ指示する選択肢表示
LOCAL = 0
REPEAT CHARANUM
	SIF COUNT == MASTER
		CONTINUE
	SIF COUNT == ASSI
		CONTINUE
	SIF COUNT == TARGET
		CONTINUE
	SIF CFLAG:COUNT:21
		CONTINUE
	PRINTFORM [{COUNT,3}] 
	IF CFLAG:COUNT:16 == 1
		PRINT ※ 
	ELSE
		PRINT 　 
	ENDIF
	PRINTFORM %NAME:COUNT,16,LEFT%
	;CALL ARRANGE_CHARANAME
	X += 1
	IF X == 3
		X = 0
		PRINTL 
	ENDIF
	LOCAL += 1
REND
PRINTL  
IF CHARANUM <= 100
	PRINTL [100] 戻る
ELSE
	PRINTL [1000][-1] 戻る
ENDIF
IF LOCAL >= 2
	PRINTL [200] パートナー・助手以外を全員休ませる
	PRINTL [300] 全員連れて行く
ENDIF

$INPUTLOOPAZUKARI
INPUT
IF RESULT == -1 || (CHARANUM <= 100 && RESULT == 100) || RESULT == 1000
	RETURN 1
ELSEIF (RESULT == 200 || RESULT == 300) && LOCAL >= 2
	REPEAT CHARANUM
		SIF COUNT == MASTER
			CONTINUE
		SIF COUNT == ASSI
			CONTINUE
		SIF COUNT == TARGET
			CONTINUE
		SIF CFLAG:COUNT:21
			CONTINUE
		IF RESULT == 200
			;非表示にする
			CFLAG:COUNT:16 = 1
		ELSEIF RESULT == 300
			;連れて行く
			CFLAG:COUNT:16 = 0
		ENDIF
	REND
	IF RESULT == 200
		PRINTFORMW 全員休ませました
	ELSEIF RESULT == 300
		PRINTFORMW 全員連れて行きます
	ENDIF
	RESTART
ELSEIF RESULT == ASSI || RESULT == MASTER || RESULT == TARGET || CFLAG:RESULT:21 == 1
	GOTO INPUTLOOPAZUKARI
ELSEIF RESULT < CHARANUM && RESULT != 0
	IF CFLAG:RESULT:16 == 1
		CFLAG:RESULT:16 = 0
		PRINTFORMW %NAME:RESULT%を連れて行きます
		RESTART
	ELSEIF CFLAG:RESULT:16 == 0
		CFLAG:RESULT:16 = 1
		PRINTFORMW %NAME:RESULT%を休ませました
		RESTART
	ENDIF
ELSEIF RESULT < 0 || RESULT >= CHARANUM
	PRINTFORML 正しい値を入力してください
	GOTO INPUTLOOPAZUKARI
ELSE
	GOTO INPUTLOOPAZUKARI
ENDIF

RETURN 1

;ステータス表示
@SHOW_CHARADATA
LOCAL = 0
$START
X = 0
REPEAT CHARANUM
	SIF CFLAG:COUNT:16 == 1
		X = 1
REND
V = 0
CALL LIFE_LIST(0, LOCAL)
IF CHARANUM <= 100
	PRINTL [100] 戻る
ELSE
	PRINTL [1000][-1] 戻る
ENDIF
IF X == 1 && U == 0
	PRINTFORML [200] 休憩中のキャラも表示する
ELSEIF X == 1 && U == 1
	PRINTFORML [200] 休憩中のキャラを非表示にする
ENDIF
IF FLAG:23 & 16384
	PRINTFORML [300] キャラリスト簡易表示 OFF
ELSE
	PRINTFORML [300] キャラリスト簡易表示 ON
ENDIF
PRINTFORML [500] 個別表示項目切替

$INPUT_LOOP
INPUT
IF RESULT == -1 || (CHARANUM <= 100 && RESULT == 100) || RESULT == 1000
	RETURN 0
ELSEIF RESULT == 200 && X == 1
	IF U == 0
		U = 1
		GOTO START
	ELSE
		U = 0
		GOTO START
	ENDIF
ELSEIF RESULT == 300
	IF FLAG:23 & 16384
		FLAG:23 -= 16384
	ELSE
		FLAG:23 |= 16384
	ENDIF
	;共通コンフィグ自動保存
	CALL STORE_GLOBAL_CONFIGURE
	GOTO START
ELSEIF RESULT == 500
	LOCAL = (LOCAL == 3) ? 1 # LOCAL + 1
	GOTO START
ELSEIF RESULT < 0 || RESULT >= CHARANUM
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
;非表示中は預かり中のキャラは選べない
ELSEIF CFLAG:RESULT:16 == 1 && U == 0
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
;別れ中のキャラは選べない
ELSEIF CFLAG:RESULT:21 == 1
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
ENDIF

;現在のTARGET一時記憶
O = TARGET

TARGET = RESULT
;新表示方式用にWにも入れておく
W = TARGET
L = NO:TARGET
CALL GET_STRAINNAME
;新型表示
DRAWLINE
PRINTFORML ■ %NAME:TARGET%のステータス
SIF FLAG:23 & 524288
	PRINTFORML   種族:%STR:203%
PRINTFORML   体力:({BASE:体力,4}/{MAXBASE:体力,4})  気力:({BASE:気力,4}/{MAXBASE:気力,4})
SIF TARGET != MASTER
	CALL SHOW_BATTLE_INFO
DRAWLINE
PRINTL □ 珠
CALL NEW_PRINT_JUEL
CALL SHOW_INFO
TARGET = O

GOTO START

@SHOW_BATTLE_INFO
;戦闘用の情報を表示
T = TARGET
CALL GET_BATTLE_POWER
PRINTFORM   戦闘能力：{P}  魔力：{BASE:魔力}
SIF ((FLAG:23 & 1p26) == 0) && FLAG:2 != 1
	PRINTFORM   好感度：{CFLAG:2}
PRINTFORML 
PRINT   耐力
A = BASE:耐力
SIF BASE:耐力 < 0
	A = 0
CALL PRINT_BAR_4, A, MAXBASE:耐力, 32
PRINTFORML ({BASE:耐力}/{MAXBASE:耐力})

RETURN 1

;奴隷売却の処理
@CHARA_SALE
X = 0
REPEAT CHARANUM
	SIF CFLAG:COUNT:16 == 1
		X = 1
REND

DRAWLINE
PRINTV DAY+1
PRINT 日目
IF TIME == 0
	PRINTL  昼
ELSE
	PRINTL  夜
ENDIF
SIF TARGET >= 0
	PRINTFORM パートナー %NAME:TARGET%
SIF ASSI >= 0
	PRINTFORM （助手：%NAME:ASSI%）

PRINTFORML (残り${MONEY})
DRAWLINE
DRAWLINE

PRINTL 誰と別れますか？
PRINTL 
;別れられるキャラを記録する変数初期化
REPEAT CHARANUM
	LOCAL:COUNT = 0
REND
REPEAT CHARANUM
	TFLAG:COUNT = 0
	IF COUNT == 0
		CONTINUE
	;預かり中のキャラは非表示だと選べない
	ELSEIF CFLAG:COUNT:16 == 1 && U == 0
		CONTINUE
	;別れ中のキャラは選べない
	ELSEIF CFLAG:COUNT:21
		CONTINUE
	;妊娠中、育児中のキャラは選べない(主人の子供の場合不可)
	ELSEIF (TALENT:COUNT:妊娠 || TALENT:COUNT:育児中) && CFLAG:COUNT:111 == NO:MASTER
		CONTINUE
	ELSEIF TALENT:COUNT:パートナー == 0 && TALENT:COUNT:相愛 == 0 && CFLAG:COUNT:7 == 0 && TALENT:COUNT:烙印 == 0
		PRINTFORM [{COUNT}] %NAME:COUNT% 
		IF ASSI == COUNT
			PRINT (助手)
		ELSEIF ISASSI:COUNT == 1
			PRINT (元助手)
		ELSEIF CFLAG:COUNT:1 == 2
			PRINT (助手可)
		ENDIF
		CALL ARRANGE_CHARANAME
		T = COUNT
		Z = COUNT
		CALL GET_BATTLE_POWER
		COUNT = Z
		A = BASE:T:耐力
		SIF BASE:T:耐力 < 0
			A = 0
		PRINTFORM   戦闘能力：{P}  魔力：{BASE:T:魔力}  
		SIF ((FLAG:23 & 1p26) == 0) && FLAG:2 != 1
			PRINTFORM 好感度：{CFLAG:T:2}
		PRINTFORML 
		
		;別れられるキャラを記録
		LOCAL:COUNT = 1
	ENDIF
REND
DRAWLINE
IF CHARANUM <= 100
	PRINTL [100] 戻る
ELSE
	PRINTL [1000][-1] 戻る
ENDIF
IF X == 1 && U == 0
	PRINTFORML [200] 休憩中のキャラも表示する
ELSEIF X == 1 && U == 1
	PRINTFORML [200] 休憩中のキャラを非表示にする
ENDIF
PRINTFORML [300] 別れられるキャラ全員と別れる

$INPUT_LOOP_CHARA_SALE
INPUT

IF RESULT == -1 || (CHARANUM <= 100 && RESULT == 100) || RESULT == 1000
	RETURN 0
ELSEIF RESULT == 200 && X == 1
	IF U == 0
		U = 1
		RESTART
	ELSE
		U = 0
		RESTART
	ENDIF
ELSEIF RESULT == 300
	PRINTFORML 別れられるキャラ全員と別れます。よろしいですか？
	PRINTFORML ※現在のパートナー、初期パートナー、助手、[相愛]キャラとは別れられません。
	PRINTFORML 
	PRINTFORML [0] はい（再会時に能力を引き継ぎます）
	PRINTFORML [1] はい（再会時に能力を引き継ぎません）
	PRINTFORML [2] いいえ
	
	$INPUT_LOOP_CHARA_SALE_ALL
	INPUT
	IF RESULT < 0 || RESULT > 2
		PRINTFORML 正しい値を入力してください
		GOTO INPUT_LOOP_2
	ELSEIF RESULT == 2
		RESTART
	ENDIF
	
	;対象の情報を取得
	LOCAL:0 = -1
	SIF TARGET > -1
		LOCAL:0 = NO:TARGET
	
	;助手の情報を取得
	LOCAL:2 = -1
	SIF ASSI > -1
		LOCAL:2 = NO:ASSI
	
	;口上表示等は省略
	LOCAL:1 = 0
	REPEAT CHARANUM
		;初期パートナーは除く
		SIF TALENT:(COUNT - LOCAL:1):150
			CONTINUE
		;相愛キャラは除く
		SIF TALENT:(COUNT - LOCAL:1):89
			CONTINUE
		;主人公は除く
		SIF COUNT == MASTER
			CONTINUE
		;現在のパートナーは除く
		SIF COUNT == TARGET
			CONTINUE
		;助手は除く
		SIF COUNT == ASSI
			CONTINUE
		
		IF RESULT == 0
			;一時的に別れる（CFLAG:21立てるだけ）
			CFLAG:COUNT:21 = 1
		ELSE
			;完全に別れる（キャラデータ削除）
			DELCHARA (COUNT - LOCAL:1)
			LOCAL:1 += 1
		ENDIF
	REND
	PRINTFORMW 別れられるキャラ全員と別れました
	
	;対象、助手を戻す
	TARGET = -1
	ASSI = -1
	REPEAT CHARANUM
		SIF NO:COUNT == LOCAL:0
			TARGET = COUNT
		SIF NO:COUNT == LOCAL:2
			ASSI = COUNT
	REND
	
ELSEIF LOCAL:RESULT != 1
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP_CHARA_SALE
ELSE
	;変数保管
	LOCAL:0 = RESULT
	LOCAL:1 = 0

	PRINTFORML %CALLNAME:(LOCAL:0)%と別れます。よろしいですか？
	PRINTFORML [0] はい（再会時に能力を引き継ぎます）
	PRINTFORML [1] はい（再会時に能力を引き継ぎません）
	PRINTFORML [2] いいえ

	$INPUT_LOOP_2
	INPUT
	IF RESULT < 0 || RESULT > 2
		PRINTFORML 正しい値を入力してください
		GOTO INPUT_LOOP_2
	ELSEIF RESULT == 0
		LOCAL:1 = 0
	ELSEIF RESULT == 1
		LOCAL:1 = 1
	ELSEIF RESULT == 2
		RESTART
	ENDIF

	;変数を戻す
	RESULT = LOCAL:0

	;対象の情報を取得
	LOCAL:0 = -1
	SIF TARGET > -1
		LOCAL:0 = NO:TARGET

	;助手の情報を取得
	LOCAL:2 = -1
	SIF ASSI >= 0
		LOCAL:2 = NO:ASSI
	
	;売却処理
	TARGET = RESULT
	TFLAG:600 = 24
	CALL KOJO_JUN
	
	;別れる拒否パッチより、別れる口上内でCFLAG:7に1を代入していた場合別れるを拒否
	IF CFLAG:7 == 1
		CFLAG:7 = 0
		RESTART
	ENDIF
	
	PRINTFORMW %CALLNAME:TARGET%と別れました
	
	IF LOCAL:1 == 0
		;一時的に別れる（CFLAG:21立てるだけ）
		CFLAG:21 = 1
	ELSE
		;完全に別れる（キャラデータ削除）
		DELCHARA TARGET
	ENDIF

	;対象、助手を戻す
	TARGET = -1
	ASSI = -1
	REPEAT CHARANUM
		SIF NO:COUNT == LOCAL:0
			TARGET = COUNT
		SIF NO:COUNT == LOCAL:2
			ASSI = COUNT
	REND

	RESTART
ENDIF



;能力アップ対象選択
@ABL_UP_SELECT
LOCAL = 0
$INPUT_LOOP2
X = 0
REPEAT CHARANUM
	SIF CFLAG:COUNT:16 == 1
		X = 1
REND
PRINTL 誰の能力を上げますか？
V = 0
CALL LIFE_LIST(1, LOCAL)
IF CHARANUM <= 100
	PRINTL [100] 戻る
ELSE
	PRINTL [1000][-1] 戻る
ENDIF
IF X == 1 && U == 0
	PRINTFORML [200] 休憩中のキャラも表示する
ELSEIF X == 1 && U == 1
	PRINTFORML [200] 休憩中のキャラを非表示にする
ENDIF
IF FLAG:23 & 16384
	PRINTFORML [300] キャラリスト簡易表示 OFF
ELSE
	PRINTFORML [300] キャラリスト簡易表示 ON
ENDIF
PRINTFORML [500] 個別表示項目切替

$INPUT_LOOP
INPUT
IF RESULT == -1 || (CHARANUM <= 100 && RESULT == 100) || RESULT == 1000
	RETURN 0
ELSEIF RESULT == 0
	CALL ABL_UP_MASTER
	GOTO INPUT_LOOP2
ELSEIF RESULT == 200 && X == 1
	IF U == 0
		U = 1
	ELSE
		U = 0
	ENDIF
	GOTO INPUT_LOOP2
ELSEIF RESULT == 300
	IF FLAG:23 & 16384
		FLAG:23 -= 16384
	ELSE
		FLAG:23 |= 16384
	ENDIF
	;共通コンフィグ自動保存
	CALL STORE_GLOBAL_CONFIGURE
	GOTO INPUT_LOOP2
ELSEIF RESULT == 500
	LOCAL = (LOCAL == 3) ? 1 # LOCAL + 1
	GOTO INPUT_LOOP2
ELSEIF RESULT < 1 || RESULT >= CHARANUM
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
;非表示中は預かり中のキャラは選べない
ELSEIF CFLAG:RESULT:16 == 1 && U == 0
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
;霧に飲まれかけているキャラは選べない
ELSEIF CFLAG:RESULT:17 == 1
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
;別れ中のキャラは選べない
ELSEIF CFLAG:RESULT:21 == 1
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
;ELSEIF RESULT == ASSI
	;GOTO INPUT_LOOP
ENDIF

;現在のターゲットを記憶
Y = TARGET
TARGET = RESULT
CALL ABL_MAN
;復元
TARGET = Y
GOTO INPUT_LOOP2

;-------------------------------------------------
;主人公の能力アップ対象選択
;-------------------------------------------------
@ABL_UP_MASTER
PRINTFORML 所持金を消費して%CALLNAME:MASTER%の能力アップができます(残り${MONEY})
DRAWLINE
CALL SHOW_ABLUP_SELECT_MASTER
INPUT

LOCAL = X:RESULT
IF RESULT == 100
	RETURN 1
ELSEIF RESULT > 100
	RESTART
ELSEIF LOCAL == 13 && TALENT:オトコ == 0
	RESTART
ELSEIF (LOCAL == 4 || LOCAL == 9 || LOCAL == 12 || LOCAL == 13) && TALENT:オトコ
	RESTART
ELSEIF LOCAL == 88
	CALL ABLUP_MASTER_88
ELSE
	CALL ABLUP_MASTER_MONEY(LOCAL)
ENDIF
RESTART

;-------------------------------------------------
;主人公の能力を上げる
;-------------------------------------------------
@ABLUP_MASTER_MONEY(ARG)
A = 5000
IF ABL:MASTER:ARG >= 6
	B = (ABL:MASTER:ARG - 2) * (ABL:MASTER:ARG - 5) / 2
ELSE
	B = 1
ENDIF
C = A * B

DRAWLINE
IF MONEY < C
	PRINTFORML 次のLvになるためには【%ABLNAME:ARG%Lv】が{B}個必要です(合計費用 ${C})
	PRINTFORMW お金が足りません
	ITEMSALES:BOUGHT = 1
	RETURN 0
ELSE
	PRINTFORML ＜今の%ABLNAME:ARG%Lv：{ABL:MASTER:ARG}＞
	PRINTFORML 次のLvになるためには【%ABLNAME:ARG%Lv】が{B}個必要です(合計費用 ${C})
	PRINTFORML 購入しますか？
	PRINTL [0] はい
	PRINTL [1] いいえ
	CALL ABLUP_MASTER_MONEY_CHECK(ARG)
	PRINTFORML [2] お金が無くなるまでレベルを上げる（Lv：{(ABL:MASTER:ARG)+K}までLvUP　使用金額:${M}）
ENDIF

$INPUT_LOOP
INPUT

IF RESULT == 0
	MONEY -= C
	ABL:MASTER:ARG += 1
	PRINTFORMW ＜%NAME:MASTER%の%ABLNAME:ARG%Lvが1つ上がった　今の%ABLNAME:ARG%Lv：{ABL:MASTER:ARG}＞
	SIF ABL:MASTER:ARG < 9999
		ITEMSALES:BOUGHT = 1
ELSEIF RESULT == 1
	ITEMSALES:BOUGHT = 1
	RETURN 0
ELSEIF RESULT == 2
	MONEY -= M
	ABL:MASTER:ARG += K
	PRINTFORMW ＜%NAME:MASTER%の%ABLNAME:ARG%Lvが{K}上がった　今の%ABLNAME:ARG%Lv：{ABL:MASTER:ARG}＞
	SIF ABL:MASTER:ARG < 9999
		ITEMSALES:BOUGHT = 1
ELSE
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
ENDIF
RETURN 1

;-------------------------------------------------
;主人公の能力を上げるのに必要な金額計算
;-------------------------------------------------
@ABLUP_MASTER_MONEY_CHECK(ARG)
VARSET LOCAL, 0
VARSET K, 0
VARSET M, 0
LOCAL:1 = ABL:MASTER:ARG
LOCAL:2 = 9999 - ABL:MASTER:ARG

FOR LOCAL, 0, LOCAL:2
	IF ABL:MASTER:ARG >= 6
		B = (ABL:MASTER:ARG - 2) * (ABL:MASTER:ARG - 5) / 2
	ELSE
		B = 1
	ENDIF
	LOCAL:3 = 5000 * B
	SIF M + LOCAL:3 > MONEY
		BREAK
	M += LOCAL:3
	K += 1
	ABL:MASTER:ARG += 1
NEXT

ABL:MASTER:ARG = LOCAL:1

;-------------------------------------------------
;主人公の能力を上げる（戦闘技能）
;-------------------------------------------------
@ABLUP_MASTER_88
IF ABL:MASTER:戦闘技能 >= 9999
	PRINTFORMW これ以上レベルは上がりません
ELSE
	T = MASTER
	CALL GET_BATTLE_PRICE
	IF MONEY < P
		PRINTFORMW 次のレベルになるには${P}が必要です。お金が足りません。
	ELSE
		LOCAL = ABL:MASTER:戦闘技能
		PRINTFORML 次のレベルになるには${P}が必要です。レベルを上げますか？（現在のLv：{(ABL:MASTER:戦闘技能)}）
		PRINTL [0] はい
		PRINTL [1] いいえ
		IF P == 10
			CALL BATTLE_PRICE_I
			PRINTFORML [2] $10(最低価格)で上がる分だけレベルを上げる（Lv：{(ABL:MASTER:戦闘技能)+K}までLvUP　使用金額:${M}）
		ENDIF
		T = MASTER
		CALL GET_BATTLE_PRICE
		CALL BATTLE_PRICE_G
		PRINTFORML [3] お金が無くなるまでレベルを上げる（Lv：{(ABL:MASTER:戦闘技能)+K}までLvUP　使用金額:${M}）
		PRINTFORML [4] 選択してレベルを上げる
		
		T = MASTER
		CALL GET_BATTLE_PRICE
		
		$INPUT_LOOP_BP
		INPUT

		IF RESULT == 2 && P > 10
			PRINTFORML 正しい値を入力してください
			GOTO INPUT_LOOP_BP
		ENDIF

		IF RESULT == 0
			MONEY -= P
			ABL:MASTER:戦闘技能 += 1
			PRINTFORMW ＜%NAME:MASTER%の戦闘技能レベルが1つ上がった　今のLv：{ABL:MASTER:戦闘技能}＞
		ELSEIF RESULT == 1
			RETURN 1
		ELSEIF RESULT == 2
			CALL BATTLE_PRICE_I
			ABL:T:戦闘技能 += K
			MONEY -= M
			PRINTFORML ＜%NAME:MASTER%の戦闘技能レベルが{K}上がった　今のLv：{ABL:MASTER:戦闘技能}＞
			PRINTFORMW ＜使用金額:${M}＞
		ELSEIF RESULT == 3
			CALL BATTLE_PRICE_G
			ABL:T:戦闘技能 += K
			MONEY -= M
			PRINTFORML ＜%NAME:MASTER%の戦闘技能レベルが{K}上がった　今のLv：{ABL:MASTER:戦闘技能}＞
			PRINTFORMW ＜使用金額:${M}＞
		ELSEIF RESULT == 4
			PRINTFORML 何レベル上げますか(1-{K}、-1でレベルを上げずに戻る)
			$INPUT_LOOP_BP2
			INPUT
			IF RESULT == -1
				PRINTFORMW レベルを上げずに戻ります
				RETURN 1
			ELSEIF RESULT > K || RESULT < 1
				PRINTFORML 正しい値を入力してください
				GOTO INPUT_LOOP_BP2
			ELSE
				CALL BATTLE_PRICE_4, RESULT
				ABL:T:戦闘技能 += K
				MONEY -= M
				PRINTFORML ＜%NAME:MASTER%の戦闘技能レベルが{K}上がった　今のLv：{ABL:MASTER:戦闘技能}＞
				PRINTFORMW ＜使用金額:${M}＞
			ENDIF
		ELSE
			PRINTFORML 正しい値を入力してください
			GOTO INPUT_LOOP_BP
		ENDIF
		RETURN 1
	ENDIF
ENDIF
RETURN 1

;-------------------------------------------------
;主人公の能力を上げるステータス表示
;-------------------------------------------------
@SHOW_ABLUP_SELECT_MASTER
;改行用にLOCAL:1を使用
LOCAL:1 = 0
;ABLの並び設定
CALL ABL_SORT
REPEAT (A:0 - 2)
	;CALL ABL_SORTで取得したX:COUNT（並べ直したABL）をLOCALに代入
	LOCAL = X:COUNT
	
	;オトコ、女でそれぞれ不要なABLを表示しない
	SIF LOCAL == 13 && TALENT:MASTER:オトコ == 0
		CONTINUE
	SIF (LOCAL == 4 || LOCAL == 9 || LOCAL == 12 || LOCAL == 13) && TALENT:MASTER:オトコ
		CONTINUE
	
	;以下行数合わせ処理を含んだもの
	PRINTFORM [{COUNT,2}]%ABLNAME:LOCAL%

	;ABLNAMEに合わせて-を埋めていく
	STRLENS ABLNAME:LOCAL
	IF RESULT:0 == 2
		PRINT -------
	ELSEIF RESULT:0 == 4
		PRINT -----
	ELSEIF RESULT:0 == 6
		PRINT ---
	ELSE
		PRINT -
	ENDIF
	
	LOCALS = {ABL:MASTER:LOCAL}LV   
	PRINTFORM %LOCALS,9,LEFT%

	LOCAL:1 += 1
	;Emueraの「PRINTCを並べる数」の指定数だけ並んだら改行
	IF LOCAL:1 > PRINTCPERLINE()-1
		PRINTL 
		LOCAL:1 = 0
	ENDIF
REND
SIF LOCAL:1
	PRINTL  

PRINTL [100] 能力値アップの終了

@ISABLUP_ALL
VARSET LOCAL, 0
LOCAL:100 = COUNT
LOCAL:101 = TARGET
TARGET = COUNT

CALL ABL_SORT
REPEAT (A:0 - 3)
	;CALL ABL_SORTで取得したX:COUNT（並べ直したABL）をLOCALに代入
	LOCAL = X:COUNT
	;オトコ、女でそれぞれ不要なABLをスルー
	SIF LOCAL == 13 && TALENT:オトコ == 0
		CONTINUE
	SIF (LOCAL == 4 || LOCAL == 9 || LOCAL == 12) && TALENT:オトコ
		CONTINUE
	;ISABLUPのRESULTが1なら半角スペース1個と*、以外なら半角スペース2個
	CALLFORM ISABLUP{LOCAL}
	IF RESULT == 1
		PRINT  * 
		LOCAL:102 = 1
		BREAK
	ENDIF
REND
SIF LOCAL:102 == 0
	PRINT    
COUNT = LOCAL:100
TARGET = LOCAL:101



;@SALEITEM_CHECK,@EVENTBUY,@BUY_PLURAL,@USE_ITEMはSHOP_ITEM.ERBに移動

@EARN_MONEY
;選択肢を表示して、お金を稼ぐ
I = 0
J = 0
K = 0
IF TARGET >= 0
	SIF BASE:MASTER:耐力 > 300 && BASE:TARGET:体力 > 200 && (ABL:親密 >= 5 || CFLAG:2 >= 500 || TALENT:恋人)
		I = 1
	SIF BASE:MASTER:耐力 > 400 && BASE:TARGET:体力 > 500 && ABL:料理技能 >= 5 && (ABL:親密 >= 10 || CFLAG:2 >= 1000 || TALENT:恋人)
		J = 1
	SIF ABL:奉仕精神 >= 5 && ABL:料理技能 >= 5 && (ABL:親密 >= 10 || CFLAG:2 >= 1000 || TALENT:恋人)
		K = 1
ENDIF

;選択メニュー名を素質で変更用処理
IF FLAG:23 & 1048576 && TARGET >= 0 && TALENT:相愛
	TSTR:2 = 愛妻弁当販売で稼ぐ
	TSTR:3 = 愛妻弁当を頂く(※お金は稼げません！)
ELSEIF FLAG:23 & 1048576 && TARGET >= 0 && TALENT:恋人
	TSTR:2 = 愛情弁当販売で稼ぐ
	TSTR:3 = 愛情弁当を頂く(※お金は稼げません！)
ELSE
	TSTR:2 = 弁当販売で稼ぐ
	TSTR:3 = 弁当を作って食べる(※お金は稼げません！)
ENDIF

PRINTFORML どうしますか？
PRINTFORML [0] %CALLNAME:MASTER%がお金を稼ぐ
SIF I == 1
	PRINTFORML [1] ２人でお金を稼ぐ
SIF J == 1
	PRINTFORML [2] %TSTR:2%
SIF K == 1
	PRINTFORML [3] %TSTR:3%
PRINTFORML [4] %CALLNAME:MASTER%が宝探し
SIF I == 1
	PRINTFORML [5] ２人で宝探し
PRINTFORML [100] やっぱりやめる

$INPUT_LOOP_MO
INPUT
IF RESULT == 0
	A = RAND:100
	TFLAG:17 = 0
	IF A <= 10
		TFLAG:17 = 1
	ELSEIF A <= 40
		TFLAG:17 = 2
	ELSEIF A <= 70
		TFLAG:17 = 3
	ELSEIF A <= 96
		TFLAG:17 = 4
	ELSE
		TFLAG:17 = 5
	ENDIF
	IF RAND:20 <= ABL:MASTER:料理技能
		TFLAG:17 = 6
	ENDIF
	TFLAG:600 = 16
	CALL KOJO_JUN
	BASE:MASTER:耐力 -= 500

	;弁当作り判定
	IF TFLAG:17 == 6
		PRINTFORML %CALLNAME:MASTER%は料理を作って……
		PRINTFORML [0] 売ることにした
		PRINTFORML [1] 皆で食べることにした

		$INPUT_MAKE
		INPUT

		IF RESULT < 0 || RESULT > 1
			GOTO INPUT_MAKE
		ELSEIF RESULT == 0
			CALL LUNCH_SELF_SALE
		ELSEIF RESULT == 1
			CALL LUNCH_SELF_EAT
		ENDIF
		RETURN 1
	ENDIF

	P = (ABL:MASTER:技巧 + 10) * (RAND:11 + 15)
	PRINTFORM %CALLNAME:MASTER%は
	IF TFLAG:17 == 1
		P *= 50
		PRINT 内職をして
	ELSEIF TFLAG:17 == 2
		P *= 85
		PRINT 街でアルバイトをして
	ELSEIF TFLAG:17 == 3
		P *= 100
		SIF TALENT:MASTER:野菜を育てる程度の能力
			P *= 10
		PRINT 畑仕事を手伝って
	ELSEIF TFLAG:17 == 4
		P *= 120
		PRINT モンスター退治をして
	ELSE
		P *= 250
		PRINT ギャンブルで成功して
	ENDIF

	;獲得資金増加ボーナス
	IF ABL:MASTER:獲得資金増加 == 1
		TIMES P , 1.50
	ELSEIF ABL:MASTER:獲得資金増加 > 1
		P *= ABL:MASTER:獲得資金増加
	ENDIF
	
	PRINTFORMW ${P}の収入を得た
	MONEY += P
	;素材を得る(COMF309.ERB)
	;CALL EARN_SOZAI_1
	RETURN 1
ELSEIF RESULT == 1 && I == 1
	A = RAND:100
	TFLAG:17 = 0
	IF A <= 10
		TFLAG:17 = 1
	ELSEIF A <= 40
		TFLAG:17 = 2
	ELSEIF A <= 70
		TFLAG:17 = 3
	ELSEIF A <= 97
		TFLAG:17 = 4
	ELSE
		TFLAG:17 = 5
	ENDIF
	TFLAG:600 = 17
	CALL KOJO_JUN
	BASE:MASTER:耐力 -= 300
	BASE:TARGET:体力 -= 200
	;変数Bはアクセサリイベント関連で使用
	B = 0
	SIF CFLAG:MASTER:24 == NO:TARGET
		B = 1
	P = (ABL:MASTER:技巧 * 2 + ABL:TARGET:技巧 + ABL:TARGET:奉仕精神 * 2 + B + 15) * (RAND:5 + 8)
	PRINTFORM %CALLNAME:MASTER%と%CALLNAME:TARGET%は２人で
	IF TFLAG:17 == 1
		P *= 60
		PRINT 雑貨屋でアルバイトをして
	ELSEIF TFLAG:17 == 2
		P *= 90
		PRINT 内職をして
	ELSEIF TFLAG:17 == 3
		P *= 110
		PRINT 街でアルバイトをして
	ELSEIF TFLAG:17 == 4
		P *= 140
		PRINT 畑仕事を手伝って
		SIF TALENT:MASTER:野菜を育てる程度の能力
			P *= 10
	ELSE
		P *= 400
		PRINT 埋蔵金を掘り当てて
	ENDIF

	;獲得資金増加ボーナス
	IF ABL:MASTER:獲得資金増加 == 1
		TIMES P , 1.50
	ELSEIF ABL:MASTER:獲得資金増加 > 1
		P *= ABL:MASTER:獲得資金増加
	ENDIF
	
	PRINTFORML ${P}の収入を得た
	MONEY += P
	;素材を得る(COMF309.ERB)
	;CALL EARN_SOZAI_2
	A = RAND:41 + 30 + B
	CFLAG:2 += A
	PRINTL  
	PRINTFORMW 好感度が {A} 増えた
ELSEIF RESULT == 2 && J == 1
	CALL LUNCH_SALE
ELSEIF RESULT == 3 && K == 1
	CALL LUNCH_EAT
ELSEIF RESULT == 4
	CALL TREASURE_MASTER
ELSEIF RESULT == 5 && I == 1
	CALL TREASURE_TWO
ELSEIF RESULT == 100
	RETURN 0
ELSE
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP_MO
ENDIF

RETURN 1

;@CHANGE_ACTION、@SHOP_ACT_LIST、@ACTION_LISTはSHOP_COM_ACT.ERBに移動

@ARRANGE_CHARANAME
;STRLENS
;文字列の長さを測定し、RESULT:0に代入
;長さはSHIFT-JISでのバイト数です。つまり全角文字を2文字と数えます。 
STRLENS NAME:COUNT
IF RESULT:0 == 2
	PRINT  　　　　　　　
ELSEIF RESULT:0 == 4
	PRINT  　　　　　　
ELSEIF RESULT:0 == 6
	PRINT  　　　　　
ELSEIF RESULT:0 == 8
	PRINT  　　　　
ELSEIF RESULT:0 == 10
	PRINT  　　　
ELSEIF RESULT:0 == 12
	PRINT  　　
ELSEIF RESULT:0 == 14
	PRINT  　
ELSE
	PRINT  
ENDIF

;-------------------------------------------------
;強くてニューゲーム
;-------------------------------------------------
@NEWGAME_SELECT
PRINTFORML 1日目、Stage1に戻って{FLAG:26 + 1}周目を開始します。よろしいですか？
PRINTL [0] はい
PRINTL [1] いいえ

$INPUT_LOOP_NEW
INPUT

IF RESULT == 1
	RETURN 1
ELSEIF RESULT != 0 && RESULT != 1
	PRINTL 正しい値を入力してください
	GOTO INPUT_LOOP_NEW
ENDIF

TRYCALL ADD_YUKARISAMA
TRYCALL ADD_MAKISAMA

PRINTL それでは1日目、Stage1に戻ります。
;日付初期化
DAY = 0
TIME = 0
;Stage1に戻る
FLAG:24 = 1
;ボスをランダムに
FLAG:25 = 0
;周回数加算
FLAG:26 += 1
;Stage進度リセット
FLAG:27 = 0

VARSET LOCALS, ""
LOCALS:0 = EASY
LOCALS:1 = NORMAL
LOCALS:2 = HARD
LOCALS:3 = LUNATIC
LOCALS:4 = S-LUNATIC
LOCALS:11 = I-EASY
LOCALS:12 = I-NORMAL
LOCALS:13 = I-HARD
LOCALS:14 = I-LUNATIC
LOCALS:15 = I-S-LUNATIC

;モードを変更する処理を追加
IF FLAG:5 >= 0 && FLAG:5 <= 4
	PRINTFORMW 難易度を変更できますがどうしますか？　現在は%LOCALS:(FLAG:3)%モードです
	PRINTL ※現在と同じ難易度を選択すると変更されません
	
	PRINTL [ 0]EASY   （Stage数*15日期限）
	PRINTL [ 1]NORMAL （Stage数*10日期限）
	PRINTL [ 2]HARD   （Stage数* 8日期限）
;	PRINTL [ 3]LUNATIC（Stage数* 5日期限）
	PRINTL 
;	PRINTL [20]INVISIBLE　※ステータスが見えない特殊モードです
	PRINTL [100] 変更しない
	
	$INPUT_LOOP_MODE
	INPUT
	
	IF RESULT == 100 || RESULT == FLAG:3
		PRINTW 変更しませんでした
	ELSEIF RESULT >= 0 && RESULT <= 3
		;攻略ルート
		FLAG:3 = RESULT
		;難易度
		FLAG:5 = RESULT
		;インビジブルモードOFF
		FLAG:2 = 0
		PRINTFORMW %LOCALS:(FLAG:3)%モードに変更しました
	ELSEIF RESULT == 20
		PRINTL ★★INVISIBLEモードの難易度を選択してください★★
		PRINTL [11]I-EASY   （Stage数*15日期限）
		PRINTL [12]I-NORMAL （Stage数*10日期限）
		PRINTL [13]I-HARD   （Stage数* 8日期限）
		PRINTL [14]I-LUNATIC（Stage数* 5日期限）
		PRINTL 
		PRINTL [100] 変更しない
		
		$INPUT_LOOP_MODE_2
		INPUT
		IF RESULT == 100 || RESULT == FLAG:3
			PRINTW 変更しませんでした
		ELSEIF RESULT >= 11 && RESULT <= 15
			;攻略ルート
			FLAG:3 = RESULT
			;I-LUNATICとI-S-LUNAは主人公組（I-LUNATICは神霊廟組にしたかったけれど未実装なので……）
			IF RESULT == 14 || RESULT == 15
				FLAG:3 = RESULT - 11
			ENDIF
			;難易度
			FLAG:5 = RESULT - 11
			;インビジブルモードON
			FLAG:2 = 1
			PRINTFORMW %LOCALS:(FLAG:3)%モードに変更しました
		ELSE
			PRINTL 正しい値を入力してください
			GOTO INPUT_LOOP_MODE_2
		ENDIF
	ELSE
		PRINTL 正しい値を入力してください
		GOTO INPUT_LOOP_MODE
	ENDIF
ENDIF
;攻略ルート選択(デフォルトでは選択不可能。選択可能にしたい場合はSYSTEM_DEBUG.ERBの該当関数参照。)
CALL EDIT_GAME_ROOT
;エンゲージリングを渡している場合
REPEAT CHARANUM
	IF TALENT:COUNT:相愛
		PRINTFORML 現在、%CALLNAME:COUNT%が婚約者となっていますが
		PRINTFORML 婚約を破棄しますか？
		PRINTL [0] はい
		PRINTL [1] いいえ
		$INPUT_LOOP_ENGAGEMENT
		INPUT
		SIF RESULT != 0 && RESULT != 1
			GOTO INPUT_LOOP_ENGAGEMENT
		IF RESULT == 0
			PRINTFORMW %CALLNAME:COUNT%との婚約を破棄しました
			;相愛の喪失
			TALENT:COUNT:相愛 = 0
			;別れる不可フラグを落とす
			CFLAG:COUNT:7 = 0
			;結婚記念日の初期化
			TIME:9 = 0
			TIME:10 = 0
			;指輪は渡していない事にする
			FLAG:29 = 0
		ENDIF
	ENDIF
REND

PRINTL 
PRINTL オープニングストーリーを見ますか？
PRINTL [0] はい
PRINTL [1] いいえ
$INPUT_LOOP_OP
INPUT
IF RESULT < 0 || RESULT > 1
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP_OP
ELSEIF RESULT == 0
	TFLAG:600 = 28
	CALL KOJO_JUN
ENDIF
RETURN 1

;-------------------------------------------------
;ボス選択
;-------------------------------------------------
@BOSS_CHOICE
$INPUT_LOOP_BOSS
CSVCALLNAME 1, 0
TSTR:1 = %RESULTS%
CSVCALLNAME 2, 0
TSTR:2 = %RESULTS%
PRINTFORML 戦いたいボスのキャラ番号を入力してください。(例：%TSTR:1%→1 %TSTR:2%→2)
PRINTFORML 基本的にチートなので、変な入力をされても動作は保証しません。
PRINTFORML (キャラが存在しない番号はやめてね)
PRINTL  
PRINTFORML それでは入力をどうぞ。
IF FLAG:25
	T = FLAG:25
	CALL GET_CHARA_NAME
	PRINTFORML 現在出現予定のボス：%STR:N%
ELSE
	PRINTFORML 現在出現予定のボス：なし
ENDIF

FOR LOCAL, 1, 200
	T = LOCAL
	EXISTCSV T,0
	IF RESULT == 1
		CALL GET_CHARA_NAME
		RESULT = 0
		;口上があるキャラは強調表示
		TRYCALLFORM KOJO_EXIST, LOCAL
		SIF RESULT == 1
			SETCOLOR 0xFFFFF
		PRINTFORMLC [{T,3}] %STR:N%
		RESETCOLOR
	ENDIF
NEXT
PRINTL 
PRINTFORML [ -2] 現在出現予定のボス番号をキャンセル
PRINTL [1000][-1] 戻る

INPUT
IF RESULT == 0
	PRINTFORMW あなたとは戦えません。残念でした
	GOTO INPUT_LOOP_BOSS
ELSEIF RESULT == -2
	PRINTFORMW 現在出現予定のボス番号をキャンセルしました。次のボスはランダムで選ばれます
	FLAG:25 = 0
ELSEIF RESULT == -1 || RESULT == 1000
	PRINTFORMW キャンセルしました
	RETURN 1
ELSE
	;CSVが存在しない番号（存在すればRESULT = 1）は選択できない
	T = RESULT
	EXISTCSV T,0
	IF RESULT == 0
		PRINTFORML 止めてくださいってば。チートのバグ取りとかしなくて良いですって
		GOTO INPUT_LOOP_BOSS
	ENDIF
	CSVCALLNAME T, 0
	PRINTFORMW %RESULTS%をボスに設定しました。既に仲間にいる場合は、幻影との勝負になります
	FLAG:25 = T
ENDIF
RETURN 1

;-------------------------------------------------
;上限設定
;-------------------------------------------------
@ALL_LIMIT
FOR LOCAL, 0, CHARANUM
	FOR LOCAL:1, 0, 101
		ABL:LOCAL:(LOCAL:1) = LIMIT(ABL:LOCAL:(LOCAL:1), 0, 9999)
		EXP:LOCAL:(LOCAL:1) = LIMIT(EXP:LOCAL:(LOCAL:1), 0, 999999999999999999)
		JUEL:LOCAL:(LOCAL:1) = LIMIT(JUEL:LOCAL:(LOCAL:1), 0, 999999999999999999)
	NEXT
	SIF CFLAG:LOCAL:2 >= 999999999999999999
		CFLAG:LOCAL:2 = 999999999999999999
	IF MAXBASE:LOCAL:耐力 >= 999999999999999999
		MAXBASE:LOCAL:耐力 = 999999999999999999
		BASE:LOCAL:耐力 = MAXBASE:LOCAL:耐力
	ENDIF
	IF MAXBASE:LOCAL:魔力 >= 999999999999999999
		MAXBASE:LOCAL:魔力 = 999999999999999999
		BASE:LOCAL:魔力 = MAXBASE:LOCAL:魔力
	ENDIF
NEXT

