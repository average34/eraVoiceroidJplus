;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/
;_/
;_/欲望upの処理
;_/
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/

;-------------------------------------------------
;欲望up本体
;-------------------------------------------------
@ABLUP1MAIN
DRAWLINE
IF ABL:欲望 >= 9999
	PRINTW 既にMAXです。
	RETURN 0
ENDIF

;能力アップ可能かを取得
;その際に能力アップに必要なコストを計算
CALL ISABLUP1

;ＬＶ１２から１３に上げるときは異常経験必要（素質：[快感に素直][淫乱化]なら無視できる）
IF C
	PRINTS EXPNAME:50
	PRINTL 5以上、かつ
ENDIF

PRINTFORM [0] - %PALAMNAME:5%の珠×{A}……
IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT 珠不足 
	SIF I & 2
		PRINT 経験不足 
ENDIF
PRINTL 

IF B > 0
	PRINTFORM [1] - %PALAMNAME:17%の珠×{B}……
	IF J == 0
		PRINT ＯＫ
	ELSE
		SIF J & 1
			PRINT 珠不足 
		SIF J & 2
			PRINT 経験不足 
	ENDIF
	PRINTL 
ENDIF

PRINTFORM [20] - %PALAMNAME:5%の珠で限界まであげる……
IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT 珠不足 
	SIF I & 2
		PRINT 経験不足 
ENDIF
PRINTL 

IF B > 0
	PRINTFORM [21] - %PALAMNAME:17%の珠で限界まであげる……
	IF J == 0
		PRINT ＯＫ
	ELSE
		SIF J & 1
			PRINT 珠不足 
		SIF J & 2
			PRINT 経験不足 
	ENDIF
	PRINTL 
ENDIF

PRINTL [100] - やめる


INPUT
IF (RESULT < 0 || RESULT > 1) && RESULT != 100 && (RESULT < 20 || RESULT > 21)
	RESTART
ELSEIF I != 0 && RESULT == 0
	PRINTL 条件を満たしていません。
	RESTART
ELSEIF B <= 0 && RESULT == 1
	RESTART
ELSEIF J != 0 && RESULT == 1
	PRINTL 条件を満たしていません。
	RESTART
ELSEIF I != 0 && RESULT == 20
	PRINTL 条件を満たしていません。
	RESTART
ELSEIF B <= 0 && RESULT == 21
	RESTART
ELSEIF J != 0 && RESULT == 21
	PRINTL 条件を満たしていません。
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

SIF RESULT < 20
	ABL:欲望 += 1

IF RESULT == 0
	JUEL:5 -= A
ELSEIF RESULT == 1
	JUEL:17 -= B
ELSEIF RESULT == 20
	CALL AUT_YOKUBOU_0
ELSEIF RESULT == 21
	CALL AUT_YOKUBOU_1
ENDIF

PRINTS ABLNAME:1
PRINT のレベルが
PRINTV ABL:欲望
PRINTW になりました。

CALL YOKUBO_UP_CHECK




;-------------------------------------------------
;欲望が上げられるかチェック
;-------------------------------------------------
@ISABLUP1
IF ABL:欲望 >= 9999
	CALL ISABLUP_LIMIT
	RETURN 0
ENDIF
;能力アップに必要なコストを計算
CALL COSTABLUP1

;条件別にＯＫかダメかを記録する
I = 0
J = 0
;能力アップ可能なら1にする
Z = 0

;ＬＶ１２から１３に上げるときは異常経験必要（素質：[快感に素直][淫乱化]なら無視できる）
IF C && EXP:異常経験 < 5
	;異常経験がない
	I |= 2
	J |= 2
ENDIF

;欲情の珠で上げる
SIF JUEL:5 < A
	I |= 1

IF I == 0
	Z = 1
ENDIF

IF B > 0
	;優越の珠で上げる
	SIF JUEL:17 < B
		J |= 1

	IF J == 0
		Z = 1
	ENDIF
ENDIF

IF Z == 0
	RETURN 0
ELSE
	RETURN 1
ENDIF



;-------------------------------------------------
;親密を上げるのに必要なコストを計算
;-------------------------------------------------
@COSTABLUP1
IF ABL:欲望 <= 1
	A = 5
	B = 3
	A = A * (ABL:欲望 + 1)
	B = B * (ABL:欲望 + 1)
ELSEIF ABL:欲望 <= 3
	A = 50
	B = 30
	A = A * (ABL:欲望 - 1)
	B = B * (ABL:欲望 - 1)
ELSEIF ABL:欲望 <= 5
	A = 1000
	B = 700
	A = A * (ABL:欲望 - 3)
	B = B * (ABL:欲望 - 3)
ELSEIF ABL:欲望 <= 8
	A = 8000
	B = 0
	;一線越えない
	IF TALENT:27
		TIMES A , 1.10
	ENDIF
	A = A * (ABL:欲望 - 5)
ELSEIF ABL:欲望 <= 11
	A = 24000
	B = 0
	;一線越えない
	IF TALENT:27
		TIMES A , 1.30
	ENDIF
	A = A + 8000 * (ABL:欲望 - 9)
ELSE
	A = 24000
	B = 0

	A = A * (ABL:欲望 - 9)
	;一線越えない
	IF TALENT:27
		TIMES A , 1.30
	ENDIF
ENDIF

;淫乱化
IF TALENT:76
	TIMES A , 0.80
	TIMES B , 0.80
ENDIF

;ＬＶ１２から１３に上げるときは異常経験必要（素質：[快感に素直][淫乱化]なら無視できる）
IF ABL:欲望 == 12 && TALENT:70 == 0 && TALENT:76 == 0
	C = 1
ELSE
	C = 0
ENDIF

