;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/
;_/
;_/レズ中毒upの処理
;_/
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/

;-------------------------------------------------
;レズ中毒up本体
;-------------------------------------------------
@ABLUP12MAIN
SIF TALENT:オトコ
	RETURN 0

DRAWLINE
IF ABL:レズ中毒 >= 9999
	PRINTW 既にMAXです。
	RETURN 0
ENDIF

;能力アップ可能かを取得
;その際に能力アップに必要なコストを計算
CALL ISABLUP12

;ＬＶ２以降に上げるときは異常経験必要（素質：[両刀][中毒しやすい]なら無視できる）
IF F
	PRINTFORML %EXPNAME:50%{F}以上、かつ
ENDIF

;奉仕精神が必要
PRINTFORML %ABLNAME:6%{ABL:レズ中毒 + 1}LV以上、かつ

PRINTFORM [0] - %PALAMNAME:0%の珠×{B}、%PALAMNAME:5%の珠×{A}、%PALAMNAME:6%の珠×{A}、%EXPNAME:40%{C}以上……

IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT 珠不足 
	SIF I & 2
		PRINT 経験不足 
	SIF I & 4
		PRINT 能力不足 
ENDIF
PRINTL 


PRINTFORM [20] - %PALAMNAME:0%、%PALAMNAME:5%、%PALAMNAME:6%の珠で限界まであげる……

IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT 珠不足 
	SIF I & 2
		PRINT 経験不足 
	SIF I & 4
		PRINT 能力不足 
ENDIF
PRINTL 

PRINTL [100] - やめる


INPUT
IF (RESULT < 0 || RESULT > 0) && RESULT != 100 && RESULT != 20
	RESTART
ELSEIF I != 0 && RESULT == 0
	PRINTL 条件を満たしていません。
	RESTART
ELSEIF I != 0 && RESULT == 20
	PRINTL 条件を満たしていません。
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

SIF RESULT < 20
	ABL:レズ中毒 += 1

IF RESULT == 0
	JUEL:快Ｃ -= B
	JUEL:欲情 -= A
	JUEL:屈服 -= A
ELSEIF RESULT == 20
	CALL AUT_REZUTYUUDOKU_0
ENDIF

PRINTS ABLNAME:12
PRINT のレベルが
PRINTV ABL:レズ中毒
PRINTW になりました。



;-------------------------------------------------
;レズ中毒が上げられるかチェック
;-------------------------------------------------
@ISABLUP12
IF ABL:レズ中毒 >= 9999 || TALENT:オトコ
	CALL ISABLUP_LIMIT
	RETURN 0
ENDIF

;能力アップに必要なコストを計算
CALL COSTABLUP12

;条件別にＯＫかダメかを記録する
I = 0
;能力アップ可能なら1にする
Z = 0

IF EXP:異常経験 < F
	;異常経験が不足
	I |= 2
ENDIF

;奉仕精神が必要
IF ABL:奉仕精神 < ABL:レズ中毒 + 1
	I |= 4
	J |= 4
ENDIF

;快Ｃの珠で上げる
SIF JUEL:快Ｃ < B
	I |= 1
;欲情の珠で上げる
SIF JUEL:欲情 < A
	I |= 1
;充実の珠で上げる
SIF JUEL:屈服 < A
	I |= 1
;レズ経験が必要
SIF EXP:レズ経験 < C
	I |= 2

IF I == 0
	Z = 1
ENDIF

IF Z == 0
	RETURN 0
ELSE
	RETURN 1
ENDIF



;-------------------------------------------------
;レズ中毒を上げるのに必要なコストを計算
;-------------------------------------------------
@COSTABLUP12
IF ABL:レズ中毒 == 0
	A = 3000
	B = 10000
	C = 1000
ELSEIF ABL:レズ中毒 == 1
	A = 8000
	B = 25000
	C = 1500
ELSEIF ABL:レズ中毒 == 2
	A = 15000
	B = 50000
	C = 2500
ELSEIF ABL:レズ中毒 == 3
	A = 30000
	B = 100000
	C = 5000
	;一線越えない
	IF TALENT:一線越えない
		TIMES A , 1.10
		TIMES B , 1.10
		TIMES C , 1.10
	ENDIF
ELSEIF ABL:レズ中毒 == 4
	A = 55000
	B = 300000
	C = 8000
	;一線越えない
	IF TALENT:一線越えない
		TIMES A , 1.30
		TIMES B , 1.30
		TIMES C , 1.30
	ENDIF
ELSE
	A = 55000
	B = 300000
	C = 8000
	
	LOCAL:1 = ABL:レズっ気
	SIF LOCAL:1 <= 0
		LOCAL:1 = 1
	;ABL:レズっ気はレズっ気
	LOCAL = ( LOCAL:1-ABL:レズ中毒 )*100 / LOCAL:1
	
	A = A * (ABL:レズ中毒 - 3)
	B = B * (ABL:レズ中毒 - 3)*(100 - LOCAL)/100
	C = C * (ABL:レズ中毒 - 2) / 2
	;一線越えない
	IF TALENT:一線越えない
		TIMES A , 1.30
		TIMES B , 1.30
		TIMES C , 1.30
	ENDIF
ENDIF

;両刀
IF TALENT:両刀
	TIMES A , 0.25
	TIMES B , 0.25
	TIMES C , 0.25
ENDIF

;中毒しやすい
IF TALENT:中毒しやすい
	TIMES A , 0.50
	TIMES B , 0.50
	TIMES C , 0.50
ENDIF

;倒錯的
IF TALENT:倒錯的
	TIMES A , 0.75
	TIMES B , 0.75
	TIMES C , 0.75
ENDIF

;淫乱化
IF TALENT:淫乱
	TIMES A , 0.50
	TIMES B , 0.50
	TIMES C , 0.50
ENDIF

;淫核
SIF TALENT:淫核
	TIMES B , 0.50

;ＬＶ２以降に上げるときは異常経験必要（素質：[両刀][中毒しやすい]なら無視できる）
IF ABL:レズ中毒 >= 2 && TALENT:両刀 == 0 && TALENT:中毒しやすい == 0
	F = ABL:レズ中毒 * 5
ELSE
	F = 0
ENDIF
