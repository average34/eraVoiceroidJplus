;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/
;_/
;_/歌唱技能upの処理
;_/
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/

;-------------------------------------------------
;歌唱技能up本体
;-------------------------------------------------
@ABLUP92MAIN
DRAWLINE
IF ABL:92 >= 9999
	PRINTW 既にMAXです。
	RETURN 0
ENDIF

;能力アップ可能かを取得
;その際に能力アップに必要なコストを計算
CALL ISABLUP92

PRINTFORM [0] - %PALAMNAME:7%の珠×{A}
;IF ABL:92 >= 2
	PRINTFORM 、%EXPNAME:93%{B}
;ENDIF
PRINT 以上……
IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT 珠不足 
	SIF I & 2
		PRINT 経験不足 
ENDIF
PRINTL 

PRINTFORM [20] - %PALAMNAME:7%の珠
;IF ABL:92 >= 2
	PRINTFORM 
;ENDIF
PRINT で限界まであげる……
IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT 珠不足 
	SIF I & 2
		PRINT 経験不足 
ENDIF
PRINTL 


PRINTL [100] - やめる


INPUT
IF (RESULT < 0 || RESULT > 0) && RESULT != 100 && RESULT != 20
	RESTART
ELSEIF I != 0 && RESULT == 0
	PRINTL 条件を満たしていません。
	RESTART
ELSEIF I != 0 && RESULT == 20
	PRINTL 条件を満たしていません。
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

SIF RESULT < 20
	ABL:92 += 1

IF RESULT == 0
	JUEL:7 -= A
ELSEIF RESULT == 20
	CALL AUT_KASHOU_0
ENDIF

PRINTS ABLNAME:92
PRINT のレベルが
PRINTV ABL:92
PRINTW になりました。



;-------------------------------------------------
;歌唱技能が上げられるかチェック
;-------------------------------------------------
@ISABLUP92
IF ABL:92 >= 9999
	CALL ISABLUP_LIMIT
	RETURN 0
ENDIF

;能力アップに必要なコストを計算
CALL COSTABLUP92

;条件別にＯＫかダメかを記録する
I = 0

;能力アップ可能なら1にする
Z = 0

;習得の珠で上げる
SIF JUEL:7 < A
	I |= 1
SIF EXP:93 < B
	I |= 2
IF I == 0
	Z = 1
ENDIF

IF Z == 0
	RETURN 0
ELSE
	RETURN 1
ENDIF



;-------------------------------------------------
;歌唱技能を上げるのに必要なコストを計算
;-------------------------------------------------
@COSTABLUP92
IF ABL:92 == 0
	A = 50
	B = 10
ELSEIF ABL:92 == 1
	A = 100
	B = 25
	SIF EXP:93 >= EXPLV:3
		A = 35
ELSEIF ABL:92 == 2
	A = 250
	B = 40
	SIF EXP:93 >= EXPLV:4
		A = 200
ELSEIF ABL:92 == 3
	A = 2000
	B = 80
	SIF EXP:93 >= EXPLV:5
		A = 1700
ELSEIF ABL:92 == 4
	A = 30000
	B = 100
	SIF EXP:93 >= EXPLV:5
		A = 15000
ELSE
	A = 20000
	B = 120
	
	A = A * (ABL:92 - 3)
	B = B * (ABL:92 - 2) / 2
ENDIF

IF ABL:7 == 1
	TIMES A , 0.90
	TIMES B , 1.00
ELSEIF ABL:7 == 2
	TIMES A , 0.80
	TIMES B , 0.90
ELSEIF ABL:7 == 3
	TIMES A , 0.75
	TIMES B , 0.80
ELSEIF ABL:7 == 4
	TIMES A , 0.70
	TIMES B , 0.70
ELSEIF ABL:7 >= 5
	TIMES A , 0.60
	TIMES B , 0.66
ENDIF

;臆病
IF TALENT:10
	TIMES A , 1.10
	TIMES B , 1.20
ENDIF

;目立ちたがり
IF TALENT:28
	TIMES A , 0.90
	TIMES B , 0.80
ENDIF

;習得早い
IF TALENT:50
	TIMES A , 0.80
	TIMES B , 0.75
ENDIF

;習得遅い
IF TALENT:51
	TIMES A , 1.20
	TIMES B , 1.10
ENDIF


;歌上手
IF ABL:52 >= 3
	TIMES A , 0.90
	TIMES B , 0.90
ENDIF

;歌苦手
IF ABL:52 <= -2
	TIMES A , 1.20
	TIMES B , 1.20
ENDIF

;必要経験を上限9999に変更
SIF B >= 9999
	B = 9999
;倍の経験を持っているなら必要数軽減
IF EXP:93 >= B * 2
	A = A / 2
ENDIF
