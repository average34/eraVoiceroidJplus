;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/
;_/
;_/奉仕精神upの処理
;_/
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/

;-------------------------------------------------
;奉仕精神up本体
;-------------------------------------------------
@ABLUP6MAIN
DRAWLINE
IF ABL:奉仕精神 >= 9999
	PRINTW 既にMAXです。
	RETURN 0
ENDIF

;能力アップ可能かを取得
;その際に能力アップに必要なコストを計算
CALL ISABLUP6

;従順が奉仕精神＋１レベルでないといけない
PRINTS ABLNAME:0
PRINTV ABL:奉仕精神+1
PRINTL LV以上、かつ

;ＬＶ９から１０、１２から１３に上げるときは異常経験必要（素質：[恋慕]なら無視できる）
IF G == 1
	PRINTS EXPNAME:50
	PRINTL あり、かつ
ELSEIF G == 2
	PRINTS EXPNAME:50
	PRINTL 2以上、かつ
ENDIF

PRINTFORM [0] - %PALAMNAME:6%の珠×{A}
IF E > 0
	PRINTFORM 、%EXPNAME:2%{E}以上、%EXPNAME:20%{E}以上
ENDIF
PRINT ……

IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT 珠不足 
	SIF I & 2
		PRINT 経験不足 
	SIF I & 4
		PRINT 能力不足 
ENDIF
PRINTL 

IF B > 0
	PRINTFORM [1] - %PALAMNAME:4%の珠×{B}
	IF D > 0
		PRINTFORM 、%EXPNAME:21%または%EXPNAME:94%{D}以上
	ENDIF
	PRINT ……
	
	IF J == 0
		PRINT ＯＫ
	ELSE
		SIF J & 1
			PRINT 珠不足 
		SIF J & 2
			PRINT 経験不足 
		SIF J & 4
			PRINT 能力不足 
	ENDIF
	PRINTL 
ENDIF

IF C > 0
	PRINTFORM [2] - %PALAMNAME:7%の珠×{C}、%EXPNAME:2%{1}以上……

	IF K == 0
		PRINT ＯＫ
	ELSE
		SIF K & 1
			PRINT 珠不足 
		SIF K & 2
			PRINT 経験不足 
		SIF K & 4
			PRINT 能力不足 
	ENDIF
	PRINTL 
ENDIF

IF F > 0
	PRINTFORM [3] - %PALAMNAME:15%の珠×{F}……

	IF L == 0
		PRINT ＯＫ
	ELSE
		SIF L & 1
			PRINT 珠不足 
		SIF L & 2
			PRINT 経験不足 
		SIF L & 4
			PRINT 能力不足 
	ENDIF
	PRINTL 
ENDIF

PRINTFORM [20] - %PALAMNAME:6%の珠で限界まであげる
IF E > 0
	PRINTFORM 
ENDIF
PRINT ……

IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT 珠不足 
	SIF I & 2
		PRINT 経験不足 
	SIF I & 4
		PRINT 能力不足 
ENDIF
PRINTL 

IF B > 0
	PRINTFORM [21] - %PALAMNAME:4%の珠で限界まであげる
	IF D > 0
		PRINTFORM 
	ENDIF
	PRINT ……
	
	IF J == 0
		PRINT ＯＫ
	ELSE
		SIF J & 1
			PRINT 珠不足 
		SIF J & 2
			PRINT 経験不足 
		SIF J & 4
			PRINT 能力不足 
	ENDIF
	PRINTL 
ENDIF

IF C > 0
	PRINTFORM [22] - %PALAMNAME:7%の珠で限界まであげる……

	IF K == 0
		PRINT ＯＫ
	ELSE
		SIF K & 1
			PRINT 珠不足 
		SIF K & 2
			PRINT 経験不足 
		SIF K & 4
			PRINT 能力不足 
	ENDIF
	PRINTL 
ENDIF

IF F > 0
	PRINTFORM [23] - %PALAMNAME:15%の珠で限界まであげる……

	IF L == 0
		PRINT ＯＫ
	ELSE
		SIF L & 1
			PRINT 珠不足 
		SIF L & 2
			PRINT 経験不足 
		SIF L & 4
			PRINT 能力不足 
	ENDIF
	PRINTL 
ENDIF


PRINTL [100] - やめる

$INPUT_LOOP
INPUT
IF (RESULT < 0 || RESULT > 3) && RESULT != 100 && (RESULT < 20 || RESULT > 23)
	RESTART
ELSEIF I != 0 && RESULT == 0
	PRINTL 条件を満たしていません。
	RESTART
ELSEIF B <= 0 && RESULT == 1
	RESTART
ELSEIF J != 0 && RESULT == 1
	PRINTL 条件を満たしていません。
	RESTART
ELSEIF C <= 0 && RESULT == 2
	RESTART
ELSEIF K != 0 && RESULT == 2
	PRINTL 条件を満たしていません。
	RESTART
ELSEIF F <= 0 && RESULT == 3
	RESTART
ELSEIF L != 0 && RESULT == 3
	PRINTL 条件を満たしていません。
	RESTART
ELSEIF I != 0 && RESULT == 20
	PRINTL 条件を満たしていません。
	RESTART
ELSEIF B <= 0 && RESULT == 21
	RESTART
ELSEIF J != 0 && RESULT == 21
	PRINTL 条件を満たしていません。
	RESTART
ELSEIF C <= 0 && RESULT == 22
	RESTART
ELSEIF K != 0 && RESULT == 22
	PRINTL 条件を満たしていません。
	RESTART
ELSEIF F <= 0 && RESULT == 23
	RESTART
ELSEIF L != 0 && RESULT == 23
	PRINTL 条件を満たしていません。
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

SIF RESULT < 20
	ABL:奉仕精神 += 1

IF RESULT == 0
	JUEL:屈服 -= A
ELSEIF RESULT == 1
	JUEL:恭順 -= B
ELSEIF RESULT == 2
	JUEL:習得 -= C
ELSEIF RESULT == 3
	JUEL:好意 -= F
ELSEIF RESULT == 20
	CALL AUT_HOUSHI_0
ELSEIF RESULT == 21
	CALL AUT_HOUSHI_1
ELSEIF RESULT == 22
	CALL AUT_HOUSHI_2
ELSEIF RESULT == 23
	CALL AUT_HOUSHI_3
ENDIF

PRINTS ABLNAME:6
PRINT のレベルが
PRINTV ABL:奉仕精神
PRINTW になりました。



;-------------------------------------------------
;奉仕精神が上げられるかチェック
;-------------------------------------------------
@ISABLUP6
IF ABL:奉仕精神 >= 9999
	CALL ISABLUP_LIMIT
	RETURN 0
ENDIF
;能力アップに必要なコストを計算
CALL COSTABLUP6

;条件別にＯＫかダメかを記録する
I = 0
J = 0
K = 0
L = 0
;能力アップ可能なら1にする
Z = 0

;従順が奉仕精神＋１レベルでないといけない
IF ABL:親密 < ABL:奉仕精神+1
	;従順が不足
	I |= 4
	J |= 4
	K |= 4
	L |= 4
ENDIF

;ＬＶ９から１０、１２から１３に上げるときは異常経験必要（素質：[妄信][恋慕]なら無視できる）
IF G == 1 && EXP:異常経験 == 0
	;異常経験が不足
	I |= 2
	J |= 2
	K |= 2
	L |= 2
ELSEIF G == 2 && EXP:異常経験 < 2
	;異常経験が不足
	I |= 2
	J |= 2
	K |= 2
	L |= 2
ENDIF

;屈服の珠で上げる
SIF JUEL:屈服 < A
	I |= 1
;絶頂経験が必要な場合
SIF EXP:絶頂経験 < E
	I |= 2
;精液経験が必要な場合
SIF EXP:精液経験 < E
	I |= 2

IF I == 0
	Z = 1
ENDIF

IF B > 0
	;恭順の珠で上げる
	SIF JUEL:恭順 < B
		J |= 1
	;この場合奉仕快楽経験またはデート経験が必要なことあり
	SIF EXP:奉仕快楽経験 < D && EXP:デート経験 < D
		J |= 2

	IF J == 0
		Z = 1
	ENDIF
ENDIF

IF C > 0
	;習得の珠で上げる
	SIF JUEL:習得 < C
		K |= 1
	;この場合絶頂経験が１以上必要
	SIF EXP:絶頂経験 < 1
		K |= 2

	IF K == 0
		Z = 1
	ENDIF
ENDIF

IF F > 0
	;好意の珠で上げる
	SIF JUEL:好意 < F
		L |= 1

	IF L == 0
		Z = 1
	ENDIF
ENDIF

IF Z == 0
	RETURN 0
ELSE
	RETURN 1
ENDIF



;-------------------------------------------------
;奉仕精神を上げるのに必要なコストを計算
;-------------------------------------------------
@COSTABLUP6
IF ABL:奉仕精神 <= 1
	A = 100
	B = 20
	C = 100
	D = 1
	E = 1
	F = 50

	A = A * (ABL:奉仕精神 + 1)
	B = B * (ABL:奉仕精神 + 1)
	C = C * (ABL:奉仕精神 + 1)
	D = D * (ABL:奉仕精神 + 1)
	E = E * (ABL:奉仕精神 + 1)
	F = F * (ABL:奉仕精神 + 1)

ELSEIF ABL:奉仕精神 <= 3
	A = 1200
	B = 100
	C = 0
	D = 3
	E = 3
	F = 500

	A = A * (ABL:奉仕精神 - 1)
	B = B * (ABL:奉仕精神 - 1)
	D = D * (ABL:奉仕精神 - 1)
	E = E * (ABL:奉仕精神 - 1)
	F = F * (ABL:奉仕精神 - 1)
ELSEIF ABL:奉仕精神 <= 5
	A = 5000
	B = 600
	C = 0
	D = 20
	E = 6
	F = 5000

	A = A * (ABL:奉仕精神 - 3)
	B = B * (ABL:奉仕精神 - 3)
	D = D * (ABL:奉仕精神 - 3)
	E = E * (ABL:奉仕精神 - 3)
	F = F * (ABL:奉仕精神 - 3)
ELSEIF ABL:奉仕精神 <= 8
	A = 10000
	B = 2000
	C = 0
	D = 50
	E = 20
	F = 0
	;一線越えない
	IF TALENT:一線越えない
		TIMES A , 1.10
		TIMES B , 1.10
		TIMES D , 1.10
	ENDIF

	A = A * (ABL:奉仕精神 - 5)
	B = B * (ABL:奉仕精神 - 5)
	D = D * (ABL:奉仕精神 - 5)
	E = E * (ABL:奉仕精神 - 5)
ELSEIF ABL:奉仕精神 <= 11
	A = 30000
	B = 8000
	C = 0
	D = 200
	E = 80
	F = 0
	;一線越えない
	IF TALENT:一線越えない
		TIMES A , 1.30
		TIMES B , 1.30
		TIMES D , 1.30
	ENDIF

	A = A + 10000 * (ABL:奉仕精神 - 9)
	B = B + 2000 * (ABL:奉仕精神 - 9)
	D = D + 60 * (ABL:奉仕精神 - 9)
	E = E + 20 * (ABL:奉仕精神 - 9)
ELSE
	A = 30000
	B = 8000
	C = 0
	D = 200
	E = 80
	F = 0
	
	A = A * (ABL:奉仕精神 - 9)
	B = B * (ABL:奉仕精神 - 9)
	D = D * (ABL:奉仕精神 - 8) / 2
	E = E * (ABL:奉仕精神 - 8) / 2
	;一線越えない
	IF TALENT:一線越えない
		TIMES A , 1.30
		TIMES B , 1.30
		TIMES D , 1.30
	ENDIF
ENDIF

;反抗的
IF TALENT:反抗的
	TIMES A , 1.20
	TIMES B , 1.40
	TIMES D , 1.20
	TIMES F , 1.20
ENDIF

;気丈
IF TALENT:気丈
	TIMES A , 1.20
	TIMES D , 1.20
	TIMES F , 1.20
ENDIF

;素直
IF TALENT:素直
	TIMES A , 0.80
	TIMES B , 0.80
	TIMES D , 0.70
	TIMES F , 0.80
ENDIF

;プライド高い
IF TALENT:プライド高い
	TIMES A , 1.40
	TIMES B , 1.30
	TIMES D , 1.20
	TIMES F , 1.40
ENDIF

;生意気
IF TALENT:生意気
	TIMES A , 1.10
	TIMES B , 1.10
	TIMES F , 1.10
ENDIF

;プライド低い
IF TALENT:プライド低い
	TIMES A , 0.90
	TIMES B , 0.80
	TIMES D , 0.80
	TIMES F , 0.90
ENDIF

;自制心
IF TALENT:自制心
	TIMES A , 1.20
	TIMES B , 1.20
	TIMES D , 1.20
	TIMES E , 1.20
	TIMES F , 1.20
ENDIF

;無関心
IF TALENT:無関心
	TIMES A , 1.10
	TIMES B , 1.10
	TIMES D , 1.10
	TIMES F , 1.10
ENDIF

;感情乏しい
IF TALENT:感情乏しい
	TIMES A , 1.20
	TIMES B , 1.20
	TIMES D , 1.40
	TIMES F , 1.20
ENDIF

;保守的
IF TALENT:保守的
	TIMES A , 1.30
	TIMES B , 1.30
	TIMES C , 1.30
	TIMES D , 1.20
	TIMES E , 1.20
	TIMES F , 1.30
ENDIF

;楽観的
IF TALENT:楽観的
	TIMES A , 0.90
	TIMES B , 0.80
	TIMES D , 0.70
	TIMES E , 0.75
	TIMES F , 0.90
ENDIF

;悲観的
IF TALENT:悲観的
	TIMES A , 0.80
	TIMES B , 0.80
	TIMES D , 0.80
	TIMES E , 0.80
	TIMES F , 0.80
ENDIF

;目立ちたがり
IF TALENT:目立ちたがり
	TIMES A , 0.80
	TIMES B , 0.80
	TIMES C , 0.80
	TIMES D , 0.80
	TIMES F , 0.80
ENDIF

;習得早い
IF TALENT:習得早い
	TIMES C , 0.80
ENDIF

;習得遅い
IF TALENT:習得遅い
	TIMES C , 1.60
ENDIF

;舌使い
IF TALENT:舌使い
	TIMES C , 0.80
ENDIF

;献身的
IF TALENT:献身的
	TIMES A , 0.75
	TIMES B , 0.75
	TIMES D , 0.60
	TIMES F , 0.75
ENDIF

;快感に素直
IF TALENT:快感に素直
	TIMES E , 0.70
ENDIF

;倒錯的
IF TALENT:倒錯的
	TIMES A , 0.75
	TIMES B , 0.75
	TIMES C , 0.75
	TIMES D , 0.75
	TIMES F , 0.75
ENDIF

;サド
IF TALENT:サド
	TIMES A , 1.30
	TIMES B , 1.30
	TIMES D , 1.30
	TIMES F , 1.30
ENDIF

;小悪魔
IF TALENT:小悪魔
	TIMES A , 1.20
	TIMES B , 1.20
	TIMES C , 1.20
	TIMES D , 1.20
	TIMES F , 1.20
ENDIF

;ＬＶ９から１０、１２から１３に上げるときは異常経験必要（素質：[恋慕]なら無視できる）
IF ABL:奉仕精神 == 9 && !TALENT:恋慕
	G = 1
ELSEIF ABL:奉仕精神 == 12 && !TALENT:恋慕
	G = 2
ELSE
	G = 0
ENDIF
