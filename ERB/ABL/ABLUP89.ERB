;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/
;_/
;_/金銭感覚upの処理
;_/
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/

;-------------------------------------------------
;金銭感覚up本体
;-------------------------------------------------
@ABLUP89MAIN
DRAWLINE
IF ABL:89 >= 9999
	PRINTW 既にMAXです。
	RETURN 0
ENDIF

;能力アップ可能かを取得
;その際に能力アップに必要なコストを計算
CALL ISABLUP89

PRINTFORM [0] - %PALAMNAME:16%の珠×{A}以上……
IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT 珠不足 
ENDIF
PRINTL 

PRINTFORM [1] - %PALAMNAME:7%の珠×{C}以上……
IF J == 0
	PRINT ＯＫ
ELSE
	SIF J & 1
		PRINT 珠不足 
ENDIF
PRINTL 

PRINTFORM [20] - %PALAMNAME:16%の珠で限界まであげる……
IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT 珠不足 
ENDIF
PRINTL 

PRINTFORM [21] - %PALAMNAME:7%の珠で限界まであげる……
IF J == 0
	PRINT ＯＫ
ELSE
	SIF J & 1
		PRINT 珠不足 
ENDIF
PRINTL 


PRINTL [100] - やめる


INPUT
IF (RESULT < 0 || RESULT > 1) && RESULT != 100 && (RESULT < 20 || RESULT > 21)
	RESTART
ELSEIF I != 0 && RESULT == 0
	PRINTL 条件を満たしていません。
	RESTART
ELSEIF J != 0 && RESULT == 1
	PRINTL 条件を満たしていません。
	RESTART
ELSEIF I != 0 && RESULT == 20
	PRINTL 条件を満たしていません。
	RESTART
ELSEIF J != 0 && RESULT == 21
	PRINTL 条件を満たしていません。
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

SIF RESULT < 20
	ABL:89 += 1

IF RESULT == 0
	JUEL:16 -= A
ELSEIF RESULT == 1
	JUEL:7 -= C
ELSEIF RESULT == 20
	CALL AUT_KINSEN_0
ELSEIF RESULT == 21
	CALL AUT_KINSEN_1
ENDIF

PRINTS ABLNAME:89
PRINT のレベルが
PRINTV ABL:89
PRINTW になりました。



;-------------------------------------------------
;金銭感覚が上げられるかチェック
;-------------------------------------------------
@ISABLUP89
;上限越してるならリターン
IF ABL:89 >= 9999
	CALL ISABLUP_LIMIT
	RETURN 0
ENDIF

;能力アップに必要なコストを計算
CALL COSTABLUP89

;条件別にＯＫかダメかを記録する
I = 0
J = 0

;能力アップ可能なら1にする
Z = 0

;生活の珠で上げる
SIF JUEL:16 < A
	I |= 1

;習得の珠で上げる
SIF JUEL:7 < C
	J |= 1

IF I == 0
	Z = 1
ENDIF

IF J == 0
	Z = 1
ENDIF

IF Z == 0
	RETURN 0
ELSE
	RETURN 1
ENDIF



;-------------------------------------------------
;金銭感覚を上げるのに必要なコストを計算
;-------------------------------------------------
@COSTABLUP89
IF ABL:89 == 0
	A = 1
	B = 0
	C = 75
ELSEIF ABL:89 == 1
	A = 50
	B = 0
	C = 500
ELSEIF ABL:89 == 2
	A = 600
	C = 3000
ELSEIF ABL:89 == 3
	A = 5000
	C = 9000
ELSEIF ABL:89 == 4
	A = 20000
	C = 24000
ELSE
	A = 20000
	C = 24000
	
	A = A * (ABL:89 - 3)
	C = C * (ABL:89 - 3)
ENDIF

;自制心
IF TALENT:20
	TIMES A , 0.80
	TIMES C , 0.80
ENDIF

;恋慕
IF TALENT:85
	TIMES A , 0.80
	TIMES C , 0.80
ENDIF
