;Ver2.03
@GET_VERSION_YUSYS(ARG)
#FUNCTION
RETURNF 203
;----------------------------------------------------------------
;イベント関数関係
;----------------------------------------------------------------
@EVENTTRAIN
#PRI
CALL PREV_RECORD_EVENTTRAIN
CALL NEXT_PLAY_EVENTTRAIN

;CALL KOJO_CUT_RETURNはCFLAGを利用しているのでコメントアウト
@EVENTCOM
#LATER
;SELECTCOM属性取得
CALL SELECTCOM_TYPE
;主導権判定を潰していた場合、復帰させる
IF CFLAG:15 <= -10000
	CFLAG:15 += 10000
	;毎回指定するに戻す
	FLAG:23 -= (FLAG:23 & 8)
ENDIF
SIF CFLAG:15 <= -1000
	CFLAG:15 += 1000
;CALL KOJO_CUT_RETURN
;CFLAG:70が2の時のみ呼び出す
SIF CFLAG:70 == 2
	CALL NEXT_PLAY_EVENTCOM
@EVENTCOMEND
#PRI
CALL PREV_RECORD_EVENTCOMEND
;CFLAG:70が2の時のみ呼び出す
SIF CFLAG:70 == 2
	CALL NEXT_PLAY_EVENTCOMEND

@EVENTEND
CALL PREV_RECORD_EVENTEND

@NEXT_PLAY_EVENTTRAIN
;NEXTCOM関係を一通りからに
NEXTCOM:1 = 0
NEXTCOM:998 = 0
NEXTCOM:997 = -1

;----------------------------------------------------------------
;次回コマンド強制決定関数関係
;----------------------------------------------------------------
@NEXT_PLAY_EVENTCOM
;消去待ちの場合は消去
IF NEXTCOM:998 < 0
	NEXTCOM = -1
	NEXTCOM:1 = 0
	NEXTCOM:998 = 0
ELSEIF NEXTCOM:998 > 0
	;NEXTCOM属性取得
	CALL NEXTCOM_TYPE
	;主導権絡みの指定数値を正常化及び主導権の決定
	IF NEXTCOM:1 <= -3000 && NEXTCOM:1 > -4000 
		NEXTCOM:1 += 3000
		TFLAG:45 = 0
	ELSEIF NEXTCOM:1 >= 3000 && NEXTCOM:1 < 4000
		NEXTCOM:1 -= 3000
		TFLAG:45 = 1
	ENDIF
	;実行待ちの場合は実行判定
	CALL NEXT_COM_ABLE
	;RETURN 1で帰って来たら進行
	IF RESULT == 1
		;強制コマンド指定
		IF NEXTCOM:998 == 1
			;本来選ぼうとしたコマンド番号を保存
			SELECTCOM:998 = SELECTCOM
			NEXTCOM:998 = -1
			;ここでNEXTCOMがもしも空だった場合は通常進行へ
			IF NEXTCOM:1 >= 0
				;連続実行及び派生回避のためにPREVCOMを破壊
				PREVCOM = -1
				;口上呼び出し
				CALL KOJO_NEXT_PLAY
				;SELECTCOMの書き換え
				SELECTCOM = NEXTCOM:1
				;SELECTCOM属性の上書き
				CALL SELECTCOM_TYPE
			ENDIF
		;コマンド要請
		ELSEIF NEXTCOM:998 == 2
			;コマンド一致もしくは属性一致
			IF (NEXTCOM:1 == SELECTCOM) || (NEXTCOM:997 == SELECTCOM:997 && NEXTCOM:997 >= 1)
				;連続実行及び派生回避のためにPREVCOMを破壊
				PREVCOM = -1
				NEXTCOM:998 = -2
				CALL KOJO_NEXT_PLAY
			;不一致の場合、ここでNEXTCOM:1を空に
			ELSE
				NEXTCOM:1 = -1
				NEXTCOM:998 = -1
			ENDIF
		;連続コマンド指定
		ELSEIF NEXTCOM:998 == 3 && NEXTCOM >= 0
			NEXTCOM:998 = -3
			;ここでNEXTCOM:1がもしも空だった場合は口上呼び出しなどを行わない
			IF NEXTCOM:1 >= 0
				;連続実行及び派生回避のためにPREVCOMを破壊
				PREVCOM = -1
				;口上呼び出し
				CALL KOJO_NEXT_PLAY
			ENDIF
			;NEXTCOMを使った場合に破棄されないものを破棄
			LOSEBASE:体力 = 0
			LOSEBASE:気力 = 0
			REPEAT 18
				UP:COUNT = 0
				DOWN:COUNT = 0
			REND
			UP:否定 = 0
			DOWN:否定 = 0
		ENDIF
	;RETURN 0で帰って来たら、NEXTCOM:1とNEXTCOM:998とNEXTCOM:997を破壊
	ELSE
		NEXTCOM:1 = -1
		NEXTCOM:998 = -1
		NEXTCOM:997 = -1
	ENDIF
ENDIF



@NEXT_PLAY_EVENTCOMEND
;本来コマンドを消去
SELECTCOM:998 = -1
;消去待ち状態ならここでも一応消去
IF NEXTCOM:998 < 0
	NEXTCOM:998 = 0
	NEXTCOM = -1
	NEXTCOM:1 = 0
	NEXTCOM:997 = -1
ENDIF
;主導権関係の調整
IF NEXTCOM:998 > 0 && NEXTCOM:1 == 998 || ((NEXTCOM:1 <= -3000 && NEXTCOM:1 > -4000) || (NEXTCOM:1 >= 3000 && NEXTCOM:1 < 4000) || NEXTCOM:997 == 5 || NEXTCOM:997 == 6)
	;主導権判定発生妨害
	CFLAG:15 -= 1000
	;毎回指定するの場合、指定できないように
	IF (FLAG:23 & 8) == 0
		FLAG:23 |= 8
		CFLAG:15 -= 10000
	ENDIF
ENDIF
;NEXTCOM属性の取得・及び振り分け
CALL NEXTCOM_TYPE
;強制連続実行状態ならNEXTCOMを書き換える
IF NEXTCOM:998 == 3
	;実行判定
	CALL NEXT_COM_ABLE
	;RETURN 0で帰って来たら、NEXTCOMに数値をいれて進行
	IF RESULT == 1
		NEXTCOM = NEXTCOM:1
	;RETURN 0で帰って来たら、NEXTCOM:1とNEXTCOM:998を破壊
	ELSE
		NEXTCOM:1 = 0
		NEXTCOM:998 = 0
	ENDIF
ENDIF

@NEXTCOM_TYPE
;NEXTCOM:1の属性決定　派生系は派生前の属性をとるんで注意。たとえばシックスナインはクンニ指定でだすと愛撫属性、フェラ指定でだすと奉仕属性になる
;属性指定の場合は振り分け作業を開始(ただし、NEXTCOM:998が2の場合はそのままスルー)
IF NEXTCOM:1 == -2
	;要請の場合は振り分けない
	IF NEXTCOM:998 == 1 || NEXTCOM:998 == 3
		IF NEXTCOM:997 == 1
			IF RAND:5 == 0
				NEXTCOM:1 = 1
			ELSEIF RAND:4 == 0
				NEXTCOM:1 = 5
			ELSEIF RAND:3 == 0
				NEXTCOM:1 = 8
			ELSE
				NEXTCOM:1 = 0
			ENDIF
		ELSEIF NEXTCOM:997 == 2
			IF RAND:6 == 0
				NEXTCOM:1 = 20
			ELSEIF RAND:5 == 0
				NEXTCOM:1 = 21
			ELSEIF RAND:4 == 0
				NEXTCOM:1 = 24
			ELSEIF RAND:3 == 0
				NEXTCOM:1 = 25
			ELSE
				NEXTCOM:1 = 34
			ENDIF
		;封印コマンドがでても困るので実質意味のない指定に、封印コマンドも振り分けたい場合は口上内で適当に処置してください
		ELSEIF NEXTCOM:997 == 3
			NEXTCOM:1 = 22
		ELSEIF NEXTCOM:997 == 4
			IF RAND:5 == 0
				NEXTCOM:1 = 30
			ELSEIF RAND:4 == 0
				NEXTCOM:1 = 31
			ELSEIF RAND:3 == 0
				NEXTCOM:1 = 32
			ELSEIF RAND:2 == 0
				NEXTCOM:1 = 33
			ELSE
				NEXTCOM:1 = 35
			ENDIF
		ELSEIF NEXTCOM:997 == 5
			IF RAND:5 == 0
				NEXTCOM:1 = 3040
			ELSEIF RAND:4 == 0
				NEXTCOM:1 = 3041
			ELSEIF RAND:3 == 0
				NEXTCOM:1 = 3042
			ELSEIF RAND:2 == 0
				NEXTCOM:1 = 3037
			ELSE
				NEXTCOM:1 = 3075
			ENDIF
		;装備系は微妙なので除外
		ELSEIF NEXTCOM:997 == 6
			IF RAND:5 == 0
				NEXTCOM:1 = -3040
			ELSEIF RAND:4 == 0
				NEXTCOM:1 = -3041
			ELSEIF RAND:3 == 0
				NEXTCOM:1 = -3042
			ELSEIF RAND:2 == 0
				NEXTCOM:1 = -3075
			ELSE
				NEXTCOM:1 = -3045
			ENDIF
		ELSEIF NEXTCOM:997 == 7
			;ウフフ中なら通常キス確定
			IF TFLAG:44 == 1
				NEXTCOM:1 = 6
			ELSEIF RAND:3 == 0
				NEXTCOM:1 = 340
			ELSEIF RAND:2 == 0
				NEXTCOM:1 = 341
			ELSE
				NEXTCOM:1 = 342
			ENDIF
		ELSEIF NEXTCOM:997 == 8
			IF RAND:3 == 0
				NEXTCOM:1 = 362
			ELSEIF RAND:2 == 0
				NEXTCOM:1 = 361
			ELSE
				NEXTCOM:1 = 360
			ENDIF
		ENDIF
	ENDIF
;愛撫系 愛撫・クンニ・胸愛撫・指挿入　アナル愛撫はちょっと特殊なんで除く方向で（ローターは削除）
ELSEIF NEXTCOM:1 == 0 || NEXTCOM:1 == 1 || NEXTCOM:1 == 5 || NEXTCOM:1 == 8
	NEXTCOM:997 = 1
;セックス系 騎乗位がちょっと微妙だけどとりあえずこっちに区分
ELSEIF NEXTCOM:1 == 20 || NEXTCOM:1 == 21 || NEXTCOM:1 == 24 || NEXTCOM:1 == 25 || NEXTCOM:1 == 34
	NEXTCOM:997 = 2
;Aセックス系 アナルセックスを除き、デフォで封印設定。封印されててもNEXTCOM:1指定だと普通にでてくるから注意
ELSEIF NEXTCOM:1 == 22 || NEXTCOM:1 == 26 || NEXTCOM:1 == 27 || NEXTCOM:1 == 28 || NEXTCOM:1 == 29  || NEXTCOM:1 == 36
	NEXTCOM:997 = 3
;奉仕系 足扱きはのぞくことに
ELSEIF NEXTCOM:1 == 30 || NEXTCOM:1 == 31 || NEXTCOM:1 == 32 || NEXTCOM:1 == 33 || NEXTCOM:1 == 35
	NEXTCOM:997 = 4
;S系 奴隷がS、つまり主人が鞭打たれる側,性質的に足扱きはこっちに
ELSEIF (NEXTCOM:1 == 3040 || NEXTCOM:1 == 3041 || NEXTCOM:1 == 3042 || NEXTCOM:1 == 3037 || NEXTCOM:1 == 3075)
	NEXTCOM:997 = 5
;M系 奴隷がM、つまり奴隷を鞭打つ場合、一応装備もいれたけど微妙な気分
ELSEIF NEXTCOM:1 == -3040 || NEXTCOM:1 == -3041 || NEXTCOM:1 == -3042 || NEXTCOM:1 == -3075 || NEXTCOM:1 == -3043 && TEQUIP:43 == 0 || (NEXTCOM:1 == -3044 && TEQUIP:44 == 0) || (NEXTCOM:1 == -3045 && TEQUIP:45 == 0)
	NEXTCOM:997 = 6
;キス系 一応ウフフ版のキスも
ELSEIF NEXTCOM:1 == 6 || NEXTCOM:1 == 340 || NEXTCOM:1 == 341 || NEXTCOM:1 == 342
	NEXTCOM:997 = 7
;着衣愛撫系 着衣セックスで悩むけど、まぁとりあえずおいておこう
ELSEIF NEXTCOM:1 == 360 || NEXTCOM:1 == 361 || NEXTCOM:1 == 362
	NEXTCOM:997 = 8
ELSE
	NEXTCOM:997 = -1
ENDIF

;-------------------------------------------------
;次回コマンドの実行判定処理関数
;-------------------------------------------------
@NEXT_COM_ORDER
X = SELECTCOM
SELECTCOM = NEXTCOM:1
CALL MASK_COM_ORDER
SELECTCOM = X
RETURN 0
@PREV_RECORD_EVENTTRAIN
;初期状態の調整
IF PREVCOM:996 == 0
	REPEAT 998
		PREVCOM:COUNT = -1
	REND
PREVCOM:680 = 0
PREVCOM:700 = 0
PREVCOM:720 = 0
PREVCOM:740 = 0
PREVCOM:760 = 0
PREVCOM:780 = 0
ENDIF
;調教開始からのコマンド実行回数
PREVCOM:998 = 0
;サーチ条件の初期化
I = 981
REPEAT 13
	PREVCOM:I = -2
	I += 1
REND
PREVCOM:617 = -2
PREVCOM:618 = -2
PREVCOM:619 = -2
PREVCOM:637 = -2
PREVCOM:638 = -2
PREVCOM:639 = -2
PREVCOM:679 = -2
;SELECTCOM属性を初期化
SELECTCOM:997 = -1
;PREVCOM属性を初期か
PREVCOM:997 = -1
;存在判定
SIF CFLAG:70 == 0
	CFLAG:70 = 1
@PREV_RECORD_EVENTEND
;PREVCOMを上から50づつひいていく。
;ただし、150~101の間は100づつひいていく
I = 600
REPEAT 500
	IF I <= 150
		PREVCOM:I = PREVCOM:(I-100)
	ELSE
		PREVCOM:I = PREVCOM:(I-50)
	ENDIF
	I -= 1
REND
;今回の調教履歴に-1を突っ込む
REPEAT 101
	PREVCOM:COUNT = -1
REND

I = 906
I:1 = 16
REPEAT 306
	IF I == 901
		PREVCOM:901 = EX:射精
	ELSEIF I == 881
		PREVCOM:881 = EX:噴乳
	ELSEIF I == 861
		PREVCOM:861 = EX:Ｂ絶頂
	ELSEIF I == 841
		PREVCOM:841 = EX:Ａ絶頂
	ELSEIF I == 821
		PREVCOM:821 = EX:Ｖ絶頂
	ELSEIF I == 801
		PREVCOM:801 = EX:Ｃ絶頂
	ELSEIF I == 781
		PREVCOM:781 = PREVCOM:780
	ELSEIF I == 761
		PREVCOM:761 = PREVCOM:760
	ELSEIF I == 741
		PREVCOM:741 = PREVCOM:740
	ELSEIF I == 721
		PREVCOM:721 = PREVCOM:720
	ELSEIF I == 701
		PREVCOM:701 = PREVCOM:700
	ELSEIF I == 681
		PREVCOM:681 = PREVCOM:680
	ELSEIF I == 661
		PREVCOM:661 = TFLAG:44
	ELSEIF I == 641
		;夜は日付がマイナスになる
		IF TIME
			PREVCOM:641 = -(DAY + 1)
		ELSE
			PREVCOM:641 = (DAY + 1)
		ENDIF
	ELSEIF I == 621
		IF ASSI > 0
			PREVCOM:621 = NO:ASSI
		ELSE
			PREVCOM:621 = -2
		ENDIF
	ELSEIF I == 601
		PREVCOM:601 = NO:TARGET
	ELSEIF I:1 == 0
		PREVCOM:I = 0
	ELSEIF I:1 == 17 || I:1 == 18 || I:1 == 19
		PREVCOM:I = -2
	ELSE
		PREVCOM:I = PREVCOM:(I - 1)
	ENDIF
	I -= 1
	I:1 = (I % 20)
REND
;BEGIN TURNENDするとここにこないけどまぁ誤差ってことで
;パートナーと過ごした回数をカウント
CFLAG:71 += 1

;パートナーとウフフした回数をカウント
SIF TFLAG:44
	CFLAG:73 += 1
IF ASSI >= 0
	;助手とすごした回数をカウント
	CFLAG:ASSI:72 += 1
	;助手とウフフした回数をカウント
	CFLAG:ASSI:74 += 1
ENDIF
;とりあえず使った関数を0に
I = 0
I:1 = 0

@PREV_RECORD_EVENTCOMEND
;射精回数カウント
;アナルセックス
IF SELECTCOM == 22 || SELECTCOM == 26 || SELECTCOM == 27 || SELECTCOM == 28 || SELECTCOM == 29 || SELECTCOM == 36
	IF ASSIPLAY
		PREVCOM:760 += TFLAG:2
	ELSE
		PREVCOM:700 += TFLAG:2
	ENDIF
;セックス
ELSEIF SELECTCOM == 20 || SELECTCOM == 21 || SELECTCOM == 24 || SELECTCOM == 25 || SELECTCOM == 34 || SELECTCOM == 88
	IF ASSIPLAY
		PREVCOM:740 += TFLAG:2
	ELSE
		PREVCOM:680 += TFLAG:2
	ENDIF
;3P
ELSEIF SELECTCOM == 64
	IF TFLAG:40 == 1
		PREVCOM:680 += TFLAG:2
	ELSEIF TFLAG:40 == 2
		PREVCOM:700 += TFLAG:2
	ELSEIF TFLAG:40 == 3
		PREVCOM:720 += TFLAG:2
	ENDIF
	IF TFLAG:41 == 1
		PREVCOM:740 += TFLAG:6
	ELSEIF TFLAG:41 == 2
		PREVCOM:760 += TFLAG:6
	ELSEIF TFLAG:41 == 3
		PREVCOM:780 += TFLAG:6
	ENDIF
;ダブルフェラ・ダブル素股
ELSEIF SELECTCOM == 68 || SELECTCOM == 62
	PREVCOM:720 += (TFLAG:0 + TFLAG:9)
;Wフェラ
ELSEIF SELECTCOM == 66
	PREVCOM:720 += TFLAG:0
	PREVCOM:780 += TFLAG:6
;そのほか射精(奉仕射精)
ELSE
	IF ASSIPLAY
		PREVCOM:780 += (TFLAG:0 + TFLAG:1 + TFLAG:4 + TFLAG:6 + TFLAG:9)
	ELSE
		PREVCOM:720 += (TFLAG:0 + TFLAG:1 + TFLAG:4 + TFLAG:9)
	ENDIF
ENDIF
;初回の履歴は100でカンスト
IF PREVCOM:998 <= 100
	PREVCOM:998 += 1
ENDIF
;100回を越えた場合は履歴更新作業中止
SIF PREVCOM:998 == 101
	RETURN 0


I = PREVCOM:998
;PREVCOM:n にn回前の調教内容を保存
;上から一つ前の内容で再設定していく。
REPEAT I
	PREVCOM:I = PREVCOM:(I-1)
	I -= 1
REND

;PREVCOM:1はSELECTCOMをいれる。ただしデートの場合は終了時にもとってしまうと問題がでるので例外処理(デート終了時の場合は323が格納)
IF SELECTCOM == 323 && TEQUIP:2
	PREVCOM:1 = 1000
ELSE
	PREVCOM:1 = SELECTCOM
ENDIF
;デートの場合は1000台特殊
IF PREVCOM:1 == 1000
	PREVCOM:1 += TEQUIP:2
;プレゼントの場合は2000台特殊
ELSEIF PREVCOM:1 == 302
	PREVCOM:1 = 2000
	PREVCOM:1 += TFLAG:17
;SM系で主導権が向こうにある場合は+3000
ELSEIF (PREVCOM:1 == 40 || PREVCOM:1 == 41 || PREVCOM:1 == 42 || PREVCOM:1 == 75) && TFLAG:45 == 1
	PREVCOM:1 += 3000
ENDIF
;助手調教の場合は+10000
IF ASSIPLAY
	PREVCOM:1 += 10000
ENDIF
;PREVCOM属性の取得
PREVCOM:997 = SELECTCOM:997

;SELECTCOM属性を消去
SELECTCOM:997 = -1
@SELECTCOM_TYPE
;SELECTCOMの属性決定　派生系は派生前の属性をとるんで注意。たとえばシックスナインはクンニ指定でだすと愛撫属性、フェラ指定でだすと奉仕属性になる
;愛撫系 愛撫・クンニ・胸愛撫・指挿入　アナル愛撫はちょっと特殊なんで除く方向で（ローター削除注意）
IF SELECTCOM == 0 || SELECTCOM == 1 || SELECTCOM == 5 || SELECTCOM == 8
	SELECTCOM:997 = 1
;セックス系 騎乗位がちょっと微妙だけどとりあえずこっちに区分
ELSEIF SELECTCOM == 20 || SELECTCOM == 21 || SELECTCOM == 24 || SELECTCOM == 25 || SELECTCOM == 34
	SELECTCOM:997 = 2
;Aセックス系 アナルセックスを除き、デフォで封印設定。封印されててもSELECTCOM指定だと普通にでてくるから注意
ELSEIF SELECTCOM == 22 || SELECTCOM == 26 || SELECTCOM == 27 || SELECTCOM == 28 || SELECTCOM == 29  || SELECTCOM == 36
	SELECTCOM:997 = 3
;奉仕系 足扱きはのぞくことに
ELSEIF SELECTCOM == 30 || SELECTCOM == 31 || SELECTCOM == 32 || SELECTCOM == 33 || SELECTCOM == 35
	SELECTCOM:997 = 4
;S系 奴隷がS、つまり主人が鞭打たれる側,性質的に足扱きはこっちに
ELSEIF (SELECTCOM == 40 || SELECTCOM == 41 || SELECTCOM == 42 || SELECTCOM == 37 || SELECTCOM == 75) && TFLAG:45 == 1
	SELECTCOM:997 = 5
;M系 奴隷がM、つまり奴隷を鞭打つ場合、一応装備もいれたけど微妙な気分
ELSEIF SELECTCOM == 40 || SELECTCOM == 41 || SELECTCOM == 42 || SELECTCOM == 75 || SELECTCOM == 43 && TEQUIP:43 == 0 || (SELECTCOM == 44 && TEQUIP:44 == 0) || (SELECTCOM == 45 && TEQUIP:45 == 0)
	SELECTCOM:997 = 6
;キス系 一応ウフフ版のキスも
ELSEIF SELECTCOM == 6 || SELECTCOM == 340 || SELECTCOM == 341 || SELECTCOM == 342
	SELECTCOM:997 = 7
;着衣愛撫系 着衣セックスで悩むけど、まぁとりあえずおいておこう
ELSEIF SELECTCOM == 360 || SELECTCOM == 361 || SELECTCOM == 362
	SELECTCOM:997 = 8
ENDIF
;--------------------------------------------------
;@KOJO_NEXT_PLAY関係
;コマンド実行時に出力されます
;--------------------------------------------------
@KOJO_NEXT_PLAY
SIF FLAG:7 <= 0
	RETURN 0

;口上の存在判定
SIF FLAG:(NO:TARGET + 100) == 0
	RETURN 0

;キャラ別
CALLFORM KOJO_NEXT_PLAY_{NO:TARGET}

;-------------------------------------------------
;NEXTCOMABLE
;コマンド実行条件
;基本的にはCOMABLEとおなじ
;-------------------------------------------------
@NEXT_COM_ABLE
CALL NEXT_COM_ORDER
SIF Z == 0
	RETURN 0
;CFLAG:652が立ってる場合、判定スルー,またCFLAG:652が1の時は自動的に0にする（永続的に全コマンドで判定無視したい場合はCFLAG:652に2あたりを代入しとくといいです）
IF CFLAG:652
	SIF CFLAG:652 == 1
		CFLAG:652 = 0
	RETURN 1
ENDIF

;強制終了フラグによる終了を回避するためにフラグを変動させる
TFLAG:101 = 1
CALLFORM COM_ABLE{NEXTCOM:1}
TFLAG:101 = 0
;RESULTが0ならコマンド実行不可、1ならコマンド実行可能
RETURN RESULT

;-------------------------------------------------
;異常経験コマンド判別関数 @ABNOMALCOMABLE
;iARGにいれた番号のコマンドが現在の状況で異常経験を与えうるかどうかを見る関数
;異常経験を与える条件を満たしてる場合、1が帰ってくる
;-------------------------------------------------
@ABNOMALCOMABLE(ARG)
#FUNCTION
;自慰
IF ARG == 3
	SIF TEQUIP:撮影中
		RETURNF 1
ENDIF
;バイブ(処女)
IF ARG == 11
	SIF (EXP:Ｖ経験 < EXPLV:1)
		RETURNF 1
ENDIF
;アナルバイブ
SIF ARG == 13
	RETURNF 1
;正常位・後背位・対面座位・背面座位
IF ARG == 20 || ARG == 21 || ARG == 24 || ARG == 25
	SIF (EXP:Ｖ経験 < EXPLV:1) && (ASSIPLAY && (TALENT:PLAYER:オトコ == 0))
		RETURNF 1
ENDIF
;逆レイプ
IF ARG == 23
	SIF (TALENT:PLAYER:処女 == 1)
		RETURNF 1
ENDIF
;正常位アナル/後背位アナル/対面座位アナル/背面座位アナル
;正式実装する際に、アナルセックスを継承するなら異常経験が付く
;SIF ARG >= 26 &&  ARG <= 29
;	RETURNF 1
;騎乗位アナル
SIF ARG == 36
	RETURNF 1
;鞭/針/縄/ボールギャグ
SIF ARG == 41 || ARG == 42 || ARG == 44 || ARG == 45
	RETURNF 1
;空気浣腸
;異常経験フィルタ(まとめのみの条件。Jオリジナルでは封印コマンド＆異常経験つかないのでこの条件なし)
SIF ARG == 47
	RETURNF 1

;拡張バルーン
;異常経験フィルタ(もっと条件細かくなるけど、封印コマンドだし良いよね？)
SIF ARG == 48
	RETURNF 1

;媚薬
SIF ARG == 51
	RETURNF 1

;ビデオ撮影
SIF ARG == 53
	RETURNF 1

;野外プレイ
SIF ARG == 54
	RETURNF 1

;助手を犯す
IF ARG == 56
	SIF ASSI >= 0 && (TALENT:ASSI:処女 == 1)
		RETURNF 1
ENDIF

;3P
SIF ARG == 64
	RETURNF 1

;助手を犯させる
SIF ARG == 65
	RETURNF 1

;公開ライブ
SIF ARG == 74
	RETURNF 1
;虫召喚
SIF ARG == 79
	RETURNF 1
;両穴フィスト
SIF ARG == 83
	RETURNF 1
;触手召喚
SIF ARG == 100
	RETURNF 1
;膣酒呑み
SIF ARG == 128
	RETURNF 1
;吸血
SIF ARG == 150
	RETURNF 1
;吸血させる
SIF ARG == 151 && (EXP:吸血経験 == 0)
	RETURNF 1
;焼芋責め
SIF ARG == 154 && (EXP:Ｖ経験 < EXPLV:1)
	RETURNF 1
;蜘蛛の脚　封印コマンドすぎるから中身のチェックはしない
SIF ARG == 159 && (EXP:Ｖ経験 < EXPLV:1) && (PREVCOM == 159 && TEQUIP:11 == 0 && TEQUIP:13 == 0 && TEQUIP:14 == 0 && TEQUIP:15 == 0 && TEQUIP:16 == 0 && TEQUIP:19 == 0 && TEQUIP:46 == 0 && TEQUIP:47 == 0 && TEQUIP:48 == 0 && TEQUIP:49 == 0 && TEQUIP:61 == 0 && TEQUIP:70 == 0 && TEQUIP:71 == 0 && TEQUIP:79 == 0)
	RETURNF 1
;角挿入
SIF ARG == 167 && (EXP:Ｖ経験 < EXPLV:1) && (TALENT:PLAYER:オトコ == 0)
	RETURNF 1
;正常位させる・後背位させる・騎上位する・セックスさせる
IF ARG == 193 || ARG == 194 || ARG == 195 || ARG == 381
	SIF (TALENT:PLAYER:処女 == 1)
		RETURNF 1
ENDIF
;挿入Ｇスポ責め/挿入子宮口責め実行判定;これ、先に処女奪うから無理なんじゃ
IF ARG == 255 || ARG == 256
	SIF (EXP:Ｖ経験 < EXPLV:1) && (ASSIPLAY && (TALENT:PLAYER:オトコ == 0))
		RETURNF 1
ENDIF
;縁側でくつろぐ実行判定
SIF ARG == 325 && ASSI >= 0 && NO:ASSI == 1 && CFLAG:ASSI:0
	RETURNF 1
;オムツ
SIF ARG == 371
	RETURNF 1
;着衣フェラチオ
SIF ARG == 373 && TEQUIP:デート中 > 0
	RETURNF 1
;着衣フェラする
SIF ARG == 380  && (TEQUIP:デート中 > 0)
	RETURNF 1
;衣装破り
SIF ARG == 392
	RETURNF 1
RETURNF 0


;以下は単なるテスト関数
@TEST_PREV_HISTORY
;10回前まで振り返る
I = 1
REPEAT 10
	IF PREVCOM:I >= 0
		PRINTFORM {I}回前 {PREVCOM:I}を実行 
	ENDIF
	I += 1
REND
PRINTL 

@TEST_COMMAND_HISTORY
;今までの調教内容を初めから振り返る
I = 998

REPEAT 998
	IF PREVCOM:(999 - I) >= 0
		PRINTFORML {COUNT+1}回目 {PREVCOM:(999 - I)}を実行 
	ENDIF
	I -= 1
REND
PRINTL 

@TEST_COMMAND_RECORD
;今までの調教内容を初めから振り返る
I = 1
REPEAT 998
	Y = ((I - 51) / 50)
	X = (I % 20)
	Z = (I % 50)
	PRINTFORM PREVCOM{I}　　
	IF I <= 100
		PRINTFORML 今回の調教　{COUNT+1}回前 {PREVCOM:I}を実行 
	ELSEIF I <= 600
		PRINTFORML {Y}回前の調教　{Z}回前 {PREVCOM:I}を実行
	ELSEIF I == 991
		PRINTFORML サーチ範囲指定　{PREVCOM:I}
	ELSEIF I == 992
		PRINTFORML 打ち切り属性　{PREVCOM:I}
	ELSEIF I == 993
		PRINTFORML 出力属性　{PREVCOM:I}
	ELSEIF I == 997
		PRINTFORML 初期履歴作成用　{PREVCOM:I}
	ELSEIF I == 998
		PRINTFORML 調教回数記録　{PREVCOM:I}
	ELSEIF (X == 17 || X == 18 || X == 19) && I <= 680
		IF I < 620
			PRINTFORML 対象のサーチ用　{PREVCOM:I}
		ELSEIF I < 640
			PRINTFORML 助手のサーチ用　{PREVCOM:I}
		ELSEIF I < 680 && I > 660
			PRINTFORML ウフフのサーチ用　{PREVCOM:I}
		ELSE
			PRINTFORML 未使用　{PREVCOM:I}
		ENDIF
	ELSEIF (X == 0) && (I >= 699) && (I <= 800)
		IF I <= 699
			PRINTFORML {X}回前の膣射精回数(主人)は{PREVCOM:I}回
		ELSEIF I <= 719
			PRINTFORML {X}回前のA射精回数(主人)は{PREVCOM:I}回
		ELSEIF I <= 739
			PRINTFORML {X}回前の奉仕射精回数(主人)は{PREVCOM:I}回
		ELSEIF I <= 759
			PRINTFORML {X}回前の膣射精回数(助手)は{PREVCOM:I}回
		ELSEIF I <= 779
			PRINTFORML {X}回前のA射精回数(助手)は{PREVCOM:I}回
		ELSEIF I <= 799
			PRINTFORML {X}回前の奉仕射精回数(助手)は{PREVCOM:I}回
		ELSE
			PRINTFORML 未使用　{PREVCOM:I}
		ENDIF
	ELSEIF (X >= 1) && (X <= 16)
		IF I <= 619
			PRINTFORML {X}回前の対象は{PREVCOM:I}
		ELSEIF I <= 639
			PRINTFORML {X}回前の助手は{PREVCOM:I}
		ELSEIF I <= 659
			PRINTFORML {X}回前の日付は{PREVCOM:I}
		ELSEIF I <= 679
			PRINTFORM {X}回前のウフフは
			IF PREVCOM:1 == 1
				PRINTFORML あった
			ELSE
				PRINTFORML なかった
			ENDIF
		ELSEIF I <= 699
			PRINTFORML {X}回前の膣射精回数(主人)は{PREVCOM:I}回
		ELSEIF I <= 719
			PRINTFORML {X}回前のA射精回数(主人)は{PREVCOM:I}回
		ELSEIF I <= 739
			PRINTFORML {X}回前の奉仕射精回数(主人)は{PREVCOM:I}回
		ELSEIF I <= 759
			PRINTFORML {X}回前の膣射精回数(助手)は{PREVCOM:I}回
		ELSEIF I <= 779
			PRINTFORML {X}回前のA射精回数(助手)は{PREVCOM:I}回
		ELSEIF I <= 799
			PRINTFORML {X}回前の奉仕射精回数(助手)は{PREVCOM:I}回
		ELSEIF I <= 819
			PRINTFORML {X}回前のC絶頂は{PREVCOM:I}回
		ELSEIF I <= 839
			PRINTFORML {X}回前のV絶頂は{PREVCOM:I}回
		ELSEIF I <= 859
			PRINTFORML {X}回前のA絶頂は{PREVCOM:I}回
		ELSEIF I <= 879
			PRINTFORML {X}回前のB絶頂は{PREVCOM:I}回
		ELSEIF I <= 899
			PRINTFORML {X}回前の噴乳は{PREVCOM:I}回
		ELSEIF I <= 919
			PRINTFORML {X}回前の射精は{PREVCOM:I}回
		ELSEIF I <= 980
			PRINTFORML 未使用　{PREVCOM:I}
		ELSEIF I <= 990
			PRINTFORML 調教番号サーチ用　{PREVCOM:I}
		ELSE
			PRINTFORML 未使用　{PREVCOM:I}
		ENDIF
	ELSE
		PRINTFORML 未使用　{PREVCOM:I}
	ENDIF
	I += 1
REND
PRINTL 
