;｢CALL EVENT_NEXTDAY_T｣｢CALL EVENTCHECK_M｣を追加
;朝イベント回避用の仕掛けを追加

@EVENTFIRST

TARGET = -1
ASSI = -1

REPEAT 99
	ITEMSALES:COUNT = 1
REND

;キャラ選択で表示するためのフラグ設定(キャラが増えた時のために200までREPEAT)
;EXISTCSVでCSVの有無を判定し、ない番号はフラグを立てない。
FOR LOCAL, 1, 200
	EXISTCSV LOCAL,0
	IF RESULT == 1
		FLAG:(999+LOCAL) = 1
	ELSE
		CONTINUE
	ENDIF
NEXT

PBAND = 4

FLAG:6 = 1
TIME:6 = 2

;モード選択
PRINTL ★★モードを選択してください★★
PRINTL [ 0]EASY   （Stage数*15日期限、所持金$50000）
PRINTL [ 1]NORMAL （Stage数*10日期限、所持金$20000）
PRINTL [ 2]HARD   （Stage数* 8日期限、所持金$10000）
;PRINTL [ 3]LUNATIC（Stage数* 5日期限、所持金$ 5000）
PRINTL 
PRINTL [ 9]EXTRA  （期限なし、所持金$100000）       
;PRINTL [10]PHANTASM(期限なし、所持金$ 10000）※誰にもオススメできない難易度です
;PRINTL [20]INVISIBLE　　　　　　　　　　　　 ※ステータスが見えない特殊モードです
;PRINTL [100]タイトルに戻る
$INPUT_LOOP
INPUT

IF RESULT == 100
	BEGIN TITLE
ELSEIF RESULT == 998
	PRINTW “DEBUG”モードを選択しました
	TALENT:MASTER:千里眼 = 1
	TALENT:MASTER:Debug = 1
	RESULT = 1
;ELSEIF RESULT == 20
;	PRINTL ★★INVISIBLEモードの難易度を選択してください★★
;	PRINTL [11]I-EASY   （Stage数*15日期限、所持金$50000）ラスボス：風神録組
;	PRINTL [12]I-NORMAL （Stage数*10日期限、所持金$20000）ラスボス：地霊殿組
;	PRINTL [13]I-HARD   （Stage数* 8日期限、所持金$10000）ラスボス：命蓮寺組
;	PRINTL [14]I-LUNATIC（Stage数* 5日期限、所持金$ 5000）ラスボス：主人公組
;	;PRINTL [100]タイトルに戻る
;	$INPUT_LOOP_I
;	INPUT
;	IF RESULT == 100
;		BEGIN TITLE
;	ELSEIF RESULT >= 11 && RESULT <= 15
;		;攻略ルート
;		FLAG:3 = RESULT
;		;I-LUNATICとI-S-LUNAは主人公組（I-LUNATICは神霊廟組にしたかったけれど未実装なので……）
;		IF RESULT == 14 || RESULT == 15
;			FLAG:3 = RESULT - 11
;		ENDIF
;		;難易度
;		FLAG:5 = RESULT - 11
;		;インビジブルモードON
;		FLAG:2 = 1
;	ELSE
;		PRINTFORML 正しい値を入力してください
;		GOTO INPUT_LOOP_I
;	ENDIF
;ELSEIF (RESULT >= 0 && RESULT <= 4) || RESULT == 9 || RESULT == 10
ELSEIF (RESULT >= 0 && RESULT <= 3) || RESULT == 9
	;攻略ルート（デフォルトでは難易度と同じ）
	FLAG:3 = RESULT
	;難易度
	FLAG:5 = RESULT
	;インビジブルモードは念のためOFF
	FLAG:2 = 0
ELSE
	PRINTFORML 正しい値を入力してください
	GOTO INPUT_LOOP
ENDIF
;攻略ルート選択(デフォルトでは選択不可能。選択可能にしたい場合はSYSTEM_DEBUG.ERBの該当関数参照。)
SIF TALENT:MASTER:Debug == 0
	CALL EDIT_GAME_ROOT

;日付を1日に設定
DAY:1 = 1
;この直後の行を削除すると、全モードでキャラ選択可能
SIF FLAG:5 == 9 || FLAG:5 == 10
	CALL START_CHARA_SELECT

;ラスボスで困った事が起きないようにフラグを折っておく
IF FLAG:3 == 0 
	IF NO:MASTER == 10
		D = 11 + 999
		FLAG:D = 0
	ELSEIF NO:MASTER == 11
		D = 10 + 999
		FLAG:D = 0
	ENDIF
ELSEIF FLAG:3 == 1
	IF NO:MASTER == 29
		D = 30 + 999
		FLAG:D = 0
	ELSEIF NO:MASTER == 30
		D = 29 + 999
		FLAG:D = 0
	ENDIF
ELSEIF FLAG:3 == 2
	IF NO:MASTER == 20
		D = 22 + 999
		FLAG:D = 0
	ELSEIF NO:MASTER == 22
		D = 20 + 999
		FLAG:D = 0
	ENDIF
ELSEIF FLAG:3 == 3
	IF NO:MASTER == 1
		D = 2 + 999
		FLAG:D = 0
	ELSEIF  NO:MASTER == 2
		D = 1 + 999
		FLAG:D = 0
	ENDIF
ELSEIF FLAG:3 == 4
	IF NO:MASTER == 1
		D = 2 + 999
		FLAG:D = 0
	ELSEIF  NO:MASTER == 2
		D = 1 + 999
		FLAG:D = 0
	ENDIF
ELSEIF FLAG:3 == 11
	IF NO:MASTER == 43
		D = 44 + 999
		FLAG:D = 0
	ELSEIF NO:MASTER == 44
		D = 43 + 999
		FLAG:D = 0
	ENDIF
ELSEIF FLAG:3 == 12
	IF NO:MASTER == 81
		D = 84 + 999
		FLAG:D = 0
	ELSEIF NO:MASTER == 84
		D = 81 + 999
		FLAG:D = 0
	ENDIF
ELSEIF FLAG:3 == 13
	IF NO:MASTER == 96
		D = 52 + 999
		FLAG:D = 0
	ELSEIF NO:MASTER == 52
		D = 96 + 999
		FLAG:D = 0
	ENDIF
ENDIF

IF NO:MASTER == 0
	PRINTFORML %CALLNAME:MASTER%の素質を設定できます
	PRINTFORML どの方式で設定しますか？
	PRINTL 
	PRINTL [0]質問形式
	PRINTL [1]一覧から選択形式
	PRINTL [2]デフォルトのまま開始（オトコ・童貞・キス未経験）
	$INPUT_LOOP_MTS
	INPUT
	IF RESULT == 0
		;質問形式
		CALL MASTER_TALENT_SET_Q
		CALL BIRTHDAY
		CALL TALENT_SET_J
		CALL MASTER_TALENT_SET_400
		CALL STORE_GLOBAL_TALENT_SET
	ELSEIF RESULT == 1
		;選択形式
		CALL MASTER_TALENT_SET_DEFAULT
		CALL MASTER_TALENT_SET
	ELSEIF RESULT == 2
		CALL MASTER_TALENT_SET_DEFAULT
	ELSE
		PRINTFORML 正しい値を入力してください
		GOTO INPUT_LOOP_MTS
	ENDIF
ENDIF

SIF TALENT:MASTER:オトコ == 0
	ABL:MASTER:レズっ気 = 3

;主人公のキャラID設定
CFLAG:MASTER:81 = 1
FLAG:998 = CHARANUM + 1

;キャラ加入時の陰毛設定
CALL GET_BASE8, MASTER

CALL START_PARTNER_SELECT

;パートナーのキャラID設定
CFLAG:TARGET:81 = FLAG:998
FLAG:998 += 1

SIF FLAG:5 == 0
	MONEY = 50000
SIF FLAG:5 == 1
	MONEY = 20000
SIF FLAG:5 == 2
	MONEY = 10000
SIF FLAG:5 == 3 || FLAG:5 == 4
	MONEY = 5000
SIF FLAG:5 == 9
	MONEY = 100000
SIF FLAG:5 == 10
	MONEY = 10000

;J用アイテムをいくつか所持させる
ITEM:500 = 1
ITEM:503 = 1
ITEM:506 = 1
ITEM:510 = 1
ITEM:513 = 1

;現在のStageは1から
FLAG:24 = 1

;初期のオプション設定
;主導権は面倒だと思うので最初からON
;ボス戦はコマンドバトルして欲しいので最初からON
;FLAG:23 |= 8
;FLAG:23 |= 64
CALL CONFIGURE_ALL_DEFAULT

;最初は１周目
FLAG:26 = 1


;ボス選択初期化
FLAG:25 = 0

;初期の耐力と魔力を計算
REPEAT CHARANUM
	IF COUNT == MASTER
		;主人公限定で、パートナーを絶頂させた回数で最大耐力増加
		MAXBASE:COUNT:耐力 = MAXBASE:COUNT:体力 * (100 + CFLAG:COUNT:2) / 100
		;主人公限定で魔力＝気力の約3%を基準値として、射精経験で増加
		MAXBASE:COUNT:魔力 = MAXBASE:COUNT:気力 / 30 * (50 + EXP:COUNT:射精経験) / 50
	ELSE
		;耐力＝体力を基準値として、絶頂経験で増加
		MAXBASE:COUNT:耐力 = MAXBASE:COUNT:体力 * (100 + EXP:COUNT:絶頂経験) / 100
		;魔力＝気力の10%を基準値として、精液経験で増加
		MAXBASE:COUNT:魔力 = MAXBASE:COUNT:気力 / 10 * (50 + EXP:COUNT:精液経験) / 50
	ENDIF
	BASE:COUNT:耐力 = MAXBASE:COUNT:耐力
	BASE:COUNT:魔力 = MAXBASE:COUNT:魔力
REND

;種族設定
TRYCALL GET_STRAINVALUE

$OPENING_SELECT
IF FLAG:5 == 10
	PRINTFORML PHANTASMモードへようこそ。このモードには特にクリア目標が設定されていません
	PRINTFORML Stage6のクリア、ゆかり様を倒す、どのStageまで行けるかに挑戦する……
	PRINTFORMW 好きな目標を自分で設定し、それに挑戦すると良いでしょう
	PRINTL 
	PRINTFORML 最初に、主人公に特殊能力を３つまで持たせることができます
	PRINTFORMW 恋人が増えると持てる特殊能力も増えていくので、頑張ってください
	CALL GET_FEAT
	CALL GET_FEAT
	CALL GET_FEAT
	PRINTL 
	PRINTFORMW それではPHANTASMモードを開始します
ELSE
	PRINTL オープニングストーリーを見ますか？
	PRINTL [0] はい
	PRINTL [1] いいえ

	$INPUT_LOOP_5
	INPUT

	IF RESULT < 0 || RESULT > 1
		PRINTFORML 正しい値を入力してください
		GOTO INPUT_LOOP_5
	ELSEIF RESULT == 0
		TFLAG:600 = 28
		CALL KOJO_JUN
	ENDIF
ENDIF

;エクストラ構文の呼び出し
TFLAG:600 = 0
CALL KOJO_JUN

BEGIN SHOP


@EVENTCOM
#PRI
;TFLAG:0-29,100,540毎ターンリセット
REPEAT 30
	TFLAG:COUNT = 0
REND
TFLAG:100 = 0
TFLAG:540 = 0

;主導権設定
IF (FLAG:23 & 8) == 0
	;主導権を毎回設定
	PRINTL 今回はどちらが主導権を握りますか？
	PRINTL [0] - 成り行きに任せる
	SIF ABL:親密 >= 3 || CFLAG:2 >= 300 || TALENT:素直 || TALENT:大人しい || TALENT:プライド低い || TALENT:恋人
		PRINTL [1] - 自分で握る
	SIF ABL:親密 >= 3 || CFLAG:2 >= 300 || TALENT:サド || TALENT:恋人
		PRINTFORML [2] - %CALLNAME:TARGET%に握らせる

	INPUT
	IF (RESULT < 0 || RESULT > 2)
		RESTART
	ELSEIF RESULT == 0
		CFLAG:15 = 0
	ELSEIF RESULT == 1 && (ABL:親密 >= 3 || CFLAG:2 >= 300 || TALENT:素直 || TALENT:大人しい || TALENT:プライド低い || TALENT:恋人)
		CFLAG:15 = 1
		TFLAG:45 = 0
	ELSEIF RESULT == 1
		RESTART
	ELSEIF RESULT == 2 && (ABL:親密 >= 3 || CFLAG:2 >= 300 || TALENT:サド || TALENT:恋人)
		CFLAG:15 = 2
		TFLAG:45 = 1
	ELSEIF RESULT == 2
		RESTART
	ENDIF
ENDIF
;成り行きに任せる場合の判定
IF CFLAG:15 == 0
	T = TARGET
	CALL GET_INITIATIVE_RATE
	A = RAND:101 - 50
	IF A < R
		;主人公側の主導権ポイントが低い場合、相手が握る
		TFLAG:45 = 1
	ELSE
		;それ以外なら主人公が握る
		TFLAG:45 = 0
	ENDIF
;自分で握る場合の判定
ELSEIF CFLAG:15 == 1
	TFLAG:45 = 0

	T = TARGET
	CALL GET_INITIATIVE_RATE
	;相手の主導権ポイントが極端に高いと、たまに持っていかれる
	IF R >= 50
		A = R / 10
		;霊夢様以外は確率に上限アリ
		SIF A > 15 && TALENT:eraVoiceroidJも支配する程度の能力 == 0
			A = 15
		IF A > RAND:100
			;相手が握る
			PRINTFORMW %CALLNAME:PLAYER%が主導権を握っていたはずが、気がつくと%CALLNAME:TARGET%に主導権を奪われていた……
			TFLAG:45 = 1
		ENDIF
	ENDIF

;相手に握らせる場合の判定
ELSEIF CFLAG:15 == 2
	TFLAG:45 = 1

	T = TARGET
	CALL GET_INITIATIVE_RATE
	;相手の主導権ポイントが極端に低いと、たまに持ってきてしまう
	IF R <= -50
		R = R * -1
		A = R / 10
		SIF A > 15
			A = 15
		IF A > RAND:100
			;主人公が握る
			PRINTFORMW %CALLNAME:TARGET%に主導権を握らせていたはずが、気がつくと%CALLNAME:PLAYER%が主導権を握ってしまっていた……
			TFLAG:45 = 0
		ENDIF
	ENDIF
ENDIF


@EVENTCOMEND
#PRI
IF BASE:体力 <= 0 && TALENT:蓬莱人
	PRINTW ＜リザレクション＞
	EXP:異常経験 += 3
	PRINTL 異常経験＋３
	BASE:体力 = 1
	IF MARK:4 <= 1
		MARK:反発感情 = 2
		MARK:4 = 2
		TFLAG:21 = 2
		PRINTL 反発感情LV2を取得
		;感情乏しいの場合は反発感情による従順ダウンはなし
		IF ABL:親密 > 0 && ABL:親密 <= 3 && TALENT:感情乏しい == 0
			ABL:親密 -= 2
			SIF ABL:親密 < 0
				ABL:親密 = 0
			PRINTFORML そして親密がLV{ABL:親密}に下がった
		ELSEIF ABL:親密 <= 6 && TALENT:感情乏しい == 0
			ABL:親密 -= 2
			SIF ABL:親密 < 0
				ABL:親密 = 0
			PRINTFORML そして親密がLV{ABL:親密}に下がった
		ELSE
			WAIT
		ENDIF
	ENDIF
	PRINTFORMW （体力が限界に来ています。%CALLNAME:MASTER%は%CALLNAME:TARGET%にゆっくり休むよう伝えてから、一旦別れました）
	BEGIN AFTERTRAIN
ELSEIF BASE:体力 <= 0
	BASE:体力 = 1
	IF MARK:4 <= 2
		MARK:反発感情 = 3
		MARK:4 = 3
		TFLAG:21 = 3
		PRINTL 反発感情LV3を取得
		;感情乏しいの場合は反発感情による従順ダウンはなし
		IF ABL:親密 > 0 && ABL:親密 <= 6 && TALENT:感情乏しい == 0
			ABL:親密 -= 4
			SIF ABL:親密 < 0
				ABL:親密 = 0
			PRINTFORML そして親密がLV{ABL:親密}に下がった
		ELSEIF ABL:親密 > 0 && ABL:親密 <= 9 && TALENT:感情乏しい == 0
			ABL:親密 -= 2
			SIF ABL:親密 < 0
				ABL:親密 = 0
			PRINTFORML そして親密がLV{ABL:親密}に下がった
		ENDIF
	ENDIF

	PRINTFORMW （%CALLNAME:TARGET%は疲れ果てて気絶してしまった。%CALLNAME:MASTER%は%CALLNAME:TARGET%を寝床まで運び、一旦別れました）
	IF ABL:親密 > 0 && TALENT:恋人 == 0
		ABL:親密 -= 1
		PRINTFORML %CALLNAME:TARGET%の親密が１下がった
	ENDIF
	BEGIN AFTERTRAIN
ELSEIF BASE:体力 < 500
	PRINTFORMW （体力が限界に来ています。%CALLNAME:MASTER%は%CALLNAME:TARGET%にゆっくり休むよう伝えてから、一旦別れました）
	BEGIN AFTERTRAIN
ELSEIF TFLAG:44 == 0 && BASE:気力 <= 300 && CFLAG:27
	PRINTFORMW （%CALLNAME:TARGET%は何か用事があるようです。%CALLNAME:MASTER%は%CALLNAME:TARGET%と一旦別れました）
	BEGIN AFTERTRAIN
ELSEIF TFLAG:44 == 0 && BASE:気力 <= 0
	;襲われ判定
	;IF ASSIPLAY == 0 && (ABL:欲望 >= 7 || PALAM:3 >= 3000 || PALAM:5 >= 3000 || CFLAG:2 >= 10000)
	;	CALL PUSH_OVER
	;	SIF RESULT
	;		RETURN 1
	;ENDIF

	IF TIME == 0
		PRINTFORMW （そろそろ夜になります。%CALLNAME:MASTER%は%CALLNAME:TARGET%と一旦別れました）
	ELSE
		PRINTFORMW （そろそろ眠る時間です。%CALLNAME:MASTER%は%CALLNAME:TARGET%と一旦別れました）
	ENDIF
	BEGIN AFTERTRAIN
ENDIF


@EVENTTURNEND
#PRI
VARSET LOCAL, 0
E = 0
F = 0
G = 0
H = 0
I = 0
REPEAT CHARANUM
	Y:COUNT = -1
REND

;Phモード限定の霧飲まれチェック
IF FLAG:5 == 10 && TARGET >= 0
	SIF CFLAG:17
		CALL SET_TMP_PARTNER
ENDIF

;Phモード限定の淫乱ボーナス
IF FLAG:5 == 10 || TALENT:MASTER:淫乱ボーナス || TALENT:MASTER:淫乱ボーナス強化
	I = 0
	REPEAT CHARANUM
		SIF TALENT:COUNT:淫乱
			I += 1
	REND
	SIF TALENT:MASTER:淫乱ボーナス強化
		TIMES I , 3.00
ENDIF

;主人公の体力が残ってるかチェック
SIF BASE:MASTER:耐力 > 0
	H = 1

;待機行動中に入手した経験や宝珠や好感度を表示する場合PRINTL 
;それが無くても宝探し詳細報告アリならPRINTL 
IF FLAG:23 & 1p25
	PRINTL 
ELSE
	IF FLAG:23 & 1024
		REPEAT CHARANUM
			IF CFLAG:COUNT:13 == 12
				PRINTL 
				BREAK
			ENDIF
		REND
	ENDIF
ENDIF

REPEAT CHARANUM
	;別れているキャラは無視
	IF CFLAG:COUNT:21
		;……しつつリング資金稼ぎ
		CALL TURNEND_EARN_MONEY_TM
		CONTINUE
	ENDIF
	
	;耐力と魔力の成長を計算
	IF COUNT == MASTER
		;主人公限定で、パートナーを絶頂させた回数で最大耐力増加
		IF CFLAG:COUNT:2 <= 100
			MAXBASE:COUNT:耐力 = MAXBASE:COUNT:体力 * (100 + CFLAG:COUNT:2) / 100
		ELSEIF CFLAG:COUNT:2 <= 700
			MAXBASE:COUNT:耐力 = MAXBASE:COUNT:体力 * (300 + CFLAG:COUNT:2) / 200
		ELSEIF CFLAG:COUNT:2 <= 100000
			MAXBASE:COUNT:耐力 = MAXBASE:COUNT:体力 * (1300 + CFLAG:COUNT:2) / 400
		ELSE
			;オーバーフロー対策
			IF 999999999999999999 / MAXBASE:COUNT:体力 <= (1300 + CFLAG:COUNT:2) / 400
				MAXBASE:COUNT:耐力 = 999999999999999999
			ELSE
				;オーバーフロー対策に先に100で割っておく
				MAXBASE:COUNT:耐力 = MAXBASE:COUNT:体力 / 100 * (1300 + CFLAG:COUNT:2) / 4
			ENDIF
		ENDIF
	ELSE
		;耐力＝体力を基準値として、絶頂経験で増加
		IF EXP:COUNT:絶頂経験 <= 100
			MAXBASE:COUNT:耐力 = MAXBASE:COUNT:体力 * (100 + EXP:COUNT:絶頂経験) / 100
		ELSEIF EXP:COUNT:絶頂経験 <= 700
			MAXBASE:COUNT:耐力 = MAXBASE:COUNT:体力 * (300 + EXP:COUNT:絶頂経験) / 200
		ELSEIF EXP:COUNT:絶頂経験 <= 100000
			MAXBASE:COUNT:耐力 = MAXBASE:COUNT:体力 * (1300 + EXP:COUNT:絶頂経験) / 400
		ELSE
			;オーバーフロー対策
			IF 999999999999999999 / MAXBASE:COUNT:体力 <= (1300 + EXP:COUNT:絶頂経験) / 400
				MAXBASE:COUNT:耐力 = 999999999999999999
			ELSE
				;オーバーフロー対策に先に100で割っておく
				MAXBASE:COUNT:耐力 = MAXBASE:COUNT:体力 / 100 * (1300 + EXP:COUNT:絶頂経験) / 4
			ENDIF
		ENDIF
	ENDIF

	IF COUNT == MASTER
		;主人公限定で魔力＝気力の約3%を基準値として、射精経験で増加
		IF EXP:COUNT:射精経験 <= 100
			MAXBASE:COUNT:魔力 = MAXBASE:COUNT:気力 / 30 * (50 + EXP:COUNT:射精経験) / 50
		ELSEIF EXP:COUNT:射精経験 <= 400
			MAXBASE:COUNT:魔力 = MAXBASE:COUNT:気力 / 30 * (EXP:COUNT:射精経験 - 25) / 25
		ELSEIF EXP:COUNT:射精経験 <= 1000000
			MAXBASE:COUNT:魔力 = MAXBASE:COUNT:気力 / 30 * (EXP:COUNT:射精経験 - 250) / 10
		ELSE
			;オーバーフロー対策
			IF 999999999999999999 / MAXBASE:COUNT:気力 <= (EXP:COUNT:射精経験 - 250) / 300
				MAXBASE:COUNT:魔力 = 999999999999999999
			ELSE
				;オーバーフロー対策に先に100で割っておく
				MAXBASE:COUNT:魔力 = MAXBASE:COUNT:気力 / 300 * (EXP:COUNT:射精経験 - 250)
			ENDIF
		ENDIF
	ELSE
		;魔力＝気力の10%を基準値として、精液経験で増加
		IF EXP:COUNT:精液経験 <= 100
			MAXBASE:COUNT:魔力 = MAXBASE:COUNT:気力 / 10 * (50 + EXP:COUNT:精液経験) / 50
		ELSEIF EXP:COUNT:精液経験 <= 400
			MAXBASE:COUNT:魔力 = MAXBASE:COUNT:気力 / 10 * (EXP:COUNT:精液経験 - 25) / 25
		ELSEIF EXP:COUNT:精液経験 <= 1000000
			MAXBASE:COUNT:魔力 = MAXBASE:COUNT:気力 / 10 * (EXP:COUNT:精液経験 - 250) / 10
		ELSE
			;オーバーフロー対策
			IF 999999999999999999 / MAXBASE:COUNT:気力 <= (EXP:COUNT:精液経験 - 250) / 100
				MAXBASE:COUNT:魔力 = 999999999999999999
			ELSE
				;オーバーフロー対策に先に100で割っておく
				MAXBASE:COUNT:魔力 = MAXBASE:COUNT:気力 / 100 * (EXP:COUNT:精液経験 - 250)
			ENDIF
		ENDIF
	ENDIF

	;Phモード限定の淫乱ボーナス
	IF FLAG:5 == 10 || TALENT:MASTER:淫乱ボーナス || TALENT:MASTER:淫乱ボーナス強化
		IF 999999999999999999 / (5 + I) * 5 <= MAXBASE:COUNT:耐力
			MAXBASE:COUNT:耐力 = 999999999999999999
		ELSE
			MAXBASE:COUNT:耐力 = MAXBASE:COUNT:耐力 * (5 + I) / 5
		ENDIF
		IF 999999999999999999 / (5 + I) * 5 <= MAXBASE:COUNT:魔力
			MAXBASE:COUNT:魔力 = 999999999999999999
		ELSE
			MAXBASE:COUNT:魔力 = MAXBASE:COUNT:魔力 * (5 + I) / 5
		ENDIF
	ENDIF

	C = 0
	D = 0
;体力の回復（午後の調教後は夜の休みが入るので回復大きい）
	IF TIME == 0
		A = MAXBASE:COUNT:体力 / 20
		B = MAXBASE:COUNT:耐力 / 20
	ELSE
		A = MAXBASE:COUNT:体力 / 4
		B = MAXBASE:COUNT:耐力 / 4
	ENDIF

	;回復早い、回復遅い、吸血鬼、蓬莱人
	IF TALENT:COUNT:蓬莱人
		A *= 3
		B *= 3
	ELSEIF TALENT:COUNT:回復早い || (TALENT:COUNT:吸血鬼 && TALENT:COUNT:眷属 == 0)
		A *= 2
		B *= 2
	ELSEIF TALENT:COUNT:吸血鬼 && TALENT:COUNT:眷属
		TIMES A , 1.5
		TIMES B , 1.5
	ELSEIF TALENT:COUNT:回復遅い
		A /= 2
		B /= 2
	ENDIF
	
	;日光浴、月光浴、吸血鬼
	IF TIME == 0
		IF TALENT:COUNT:日光浴 && TIME:5 == 1
			A += 400
			B += 400
		ELSEIF TALENT:COUNT:日光浴 && (TIME:5 == 0 || TIME:5 == 7)
			A += 200
			B += 200
		ENDIF
	ELSE
		IF (TALENT:COUNT:吸血鬼 || TALENT:COUNT:月光浴) && TIME:5 == 1
			A += 400
			B += 400
			IF TALENT:COUNT:吸血鬼 && TALENT:COUNT:眷属
				A -= 200
				B -= 200
			ENDIF
		ELSEIF (TALENT:COUNT:吸血鬼 || TALENT:COUNT:月光浴) && TIME:5 == 0
			A += 200
			B += 200
			IF TALENT:COUNT:吸血鬼 && TALENT:COUNT:眷属
				A -= 100
				B -= 100
			ENDIF
		ENDIF
	ENDIF

	;治療
	IF ASSI >= 0
		IF (TALENT:ASSI:治療 || TALENT:MASTER:治療)
			A += 100
			B += 100
		ENDIF
	ELSE
		IF TALENT:MASTER:治療
			A += 100
			B += 100
		ENDIF
	ENDIF

	;休憩フラグ（「何もしない」を選択、もしくは調教対象でない）
	IF FLAG:0 || TARGET != COUNT
		A += 100
		B += 100
	ENDIF

	;純愛版休憩フラグ
	IF FLAG:0 && COUNT == MASTER
		A += 300
		TIMES B , 1.30
	ENDIF
	IF FLAG:0 && COUNT == TARGET
		A += 100
		TIMES B , 1.20
		SIF TALENT:恋人
			CFLAG:2 += 1
	ENDIF
	IF FLAG:0 && COUNT == ASSI
		A += 100
		TIMES B , 1.20
		SIF TALENT:ASSI:恋人
			CFLAG:ASSI:2 += 1
	ENDIF

	;維持費算出
	IF ABL:COUNT:浪費癖 == 0
		;浪費癖未設定＝Lv50相当
		C = 2000
		;戦闘技能レベルによって増加
		C += ABL:COUNT:戦闘技能 * 100
	ELSE
		IF ABL:COUNT:浪費癖 < 30
			;安い
			C = ABL:COUNT:浪費癖 * 5
			;戦闘技能レベルによって増加
			C += ABL:COUNT:戦闘技能 * 40
		ELSEIF ABL:COUNT:浪費癖 < 50
			;普通
			C = ABL:COUNT:浪費癖 * 20
			;戦闘技能レベルによって増加
			C += ABL:COUNT:戦闘技能 * 75
		ELSEIF ABL:COUNT:浪費癖 < 75
			;高い
			C = ABL:COUNT:浪費癖 * 40
			;戦闘技能レベルによって増加
			C += ABL:COUNT:戦闘技能 * 100
		ELSEIF ABL:COUNT:浪費癖 < 90
			;非常に高い
			C = ABL:COUNT:浪費癖 * 60
			;戦闘技能レベルによって増加
			C += ABL:COUNT:戦闘技能 * 150
		ELSEIF ABL:COUNT:浪費癖 < 100
			;詐欺のように高い
			C = ABL:COUNT:浪費癖 * 100
			;戦闘技能レベルによって増加
			C += ABL:COUNT:戦闘技能 * 200
		ELSE
			;破滅
			C = ABL:COUNT:浪費癖 * 400
			;戦闘技能レベルによって増加
			C += ABL:COUNT:戦闘技能 * 10000
		ENDIF
	ENDIF


	;現在ステージによって少し増減
	IF FLAG:24 == 1
		;Stage1は半額
		TIMES C , 0.50
	ELSEIF FLAG:24 == 2
		;Stage2は20%OFF
		TIMES C , 0.80
	ELSEIF FLAG:24 == 3
		;Stage3は10%OFF
		TIMES C , 0.90
	ELSEIF FLAG:24 <= 6
		;Stage6までは通常通り
	ELSE
		;クリア後は1.2倍
		TIMES C , 1.20
	ENDIF

	;主人公の維持費は100
	SIF COUNT == MASTER
		C = 100
	;夜は寝てる時間も多いので維持費が半分
	SIF TIME == 1
		C /= 2

	;雨の日は経費ダウン
	SIF TIME:5 == 3 || TIME:5 == 7
		TIMES C , 0.90
	SIF TIME:5 == 4
		TIMES C , 0.60

	;霧に飲まれかけているキャラは維持費無し
	SIF CFLAG:COUNT:17
		C = 0

	;待機中指示コマンド実行はSHOP_COM_ACT.ERBに移動
	CALL TURNEND_COM

	;回復処理
	;料理技能で回復量増加
	IF A < 1000000
		A = A * (100 + ABL:COUNT:料理技能 * 3) / 100
	ELSE
		A = A / 100 * (100 + ABL:COUNT:料理技能 * 3)
	ENDIF
	IF B < 1000000
		B = B * (100 + ABL:COUNT:料理技能 * 3) / 100
	ELSE
		B = B / 100 * (100 + ABL:COUNT:料理技能 * 3)
	ENDIF
	BASE:COUNT:体力 += A
	SIF BASE:COUNT:体力 > MAXBASE:COUNT:体力
		BASE:COUNT:体力 = MAXBASE:COUNT:体力
	BASE:COUNT:耐力 += B
	SIF BASE:COUNT:耐力 > MAXBASE:COUNT:耐力
		BASE:COUNT:耐力 = MAXBASE:COUNT:耐力

	;気力の回復
	BASE:COUNT:気力 = MAXBASE:COUNT:気力
	BASE:COUNT:魔力 = MAXBASE:COUNT:魔力

	;金銭感覚による維持費軽減
	IF ABL:COUNT:金銭感覚 == 0
		;何もしない
	ELSEIF ABL:COUNT:金銭感覚 <= 5
		C = C * (100 - ABL:COUNT:金銭感覚 * 7) / 100
	ELSEIF ABL:COUNT:金銭感覚 <= 10
		C = C * (65 - (ABL:COUNT:金銭感覚 - 5) * 3) / 100
	ELSEIF ABL:COUNT:金銭感覚 <= 100
		C = C * (50 - (ABL:COUNT:金銭感覚 - 10) / 2) / 100
	ELSEIF ABL:COUNT:金銭感覚 > 100
		C = C / 100
	ENDIF
	

	LOCAL:0 += C
	LOCAL:1 += D

	;霧に飲まれているとランダムで色々増える
	IF CFLAG:COUNT:17
		A = RAND:100
		;1%で処女喪失
		IF TALENT:COUNT:処女 && RAND:100 == 0
			TALENT:COUNT:処女 = 0
			EXP:COUNT:Ｖ経験 += 1
			CFLAG:COUNT:2 -= 100
		ENDIF

		IF CFLAG:COUNT:2 > 0
			IF CFLAG:COUNT:2 > 30000
				CFLAG:COUNT:2 -= 750
			ELSE
				CFLAG:COUNT:2 -= CFLAG:COUNT:2 / 40
			ENDIF
		ENDIF
		CFLAG:COUNT:2 -= RAND:21 + 20

		;50％で自慰
		IF A < 50
			EXP:COUNT:自慰経験 += RAND:5 + 3
			EXP:COUNT:絶頂経験 += RAND:3 + 1
			JUEL:COUNT:0 += RAND:998 + 1000
			JUEL:COUNT:4 += RAND:200 + 200
			JUEL:COUNT:5 += RAND:500 + 500
		;30％でレズ
		ELSEIF A < 80
			EXP:COUNT:レズ経験 += RAND:50 + 30
			EXP:COUNT:絶頂経験 += RAND:10 + 3
		
			JUEL:COUNT:6 += RAND:500 + 500
			JUEL:COUNT:7 += RAND:500 + 500
			IF TALENT:COUNT:ふたなり
				EXP:COUNT:射精経験 += RAND:10 + 3
				JUEL:COUNT:8 += RAND:200 + 200
			ENDIF

		;20％で精液
		ELSEIF TALENT:COUNT:処女
			EXP:COUNT:絶頂経験 += RAND:3 + 1
			EXP:COUNT:精液経験 += RAND:20 + 10
			JUEL:COUNT:5 += RAND:500 + 500
			JUEL:COUNT:6 += RAND:500 + 500
			JUEL:COUNT:10 += RAND:998 + 1000
		ELSE
			EXP:COUNT:Ｖ経験 += RAND:5 + 3
			EXP:COUNT:絶頂経験 += RAND:3 + 1
			EXP:COUNT:精液経験 += RAND:10 + 5
			JUEL:COUNT:1 += RAND:500 + 500
			JUEL:COUNT:5 += RAND:500 + 500
			JUEL:COUNT:6 += RAND:500 + 500
		ENDIF
	ENDIF
REND

;待機行動中に入手した経験や宝珠や好感度を表示する場合確認用に最後に1回だけWAIT
;それが無くても宝探し詳細報告アリなら、確認用に最後に1回だけWAIT
IF FLAG:23 & 1p25
	WAIT
ELSE
	IF FLAG:23 & 1024
		REPEAT CHARANUM
			IF CFLAG:COUNT:13 == 12
				WAIT
				BREAK
			ENDIF
		REND
	ENDIF
ENDIF

;待機中行動口上呼び出し
CALL KOJO_TAIKI_CHECK

;パートナーの金銭感覚による全体維持費の軽減
IF TARGET >= 0
	IF ABL:TARGET:金銭感覚 == 0
		;何もしない
	ELSEIF ABL:TARGET:金銭感覚 <= 5
		LOCAL:0 = LOCAL:0 * (100 - ABL:TARGET:金銭感覚 * 5) / 100
	ELSEIF ABL:TARGET:金銭感覚 <= 10
		LOCAL:0 = LOCAL:0 * (75 - (ABL:TARGET:金銭感覚 - 5) * 2) / 100
	ELSEIF ABL:TARGET:金銭感覚 <= 100
		LOCAL:0 = LOCAL:0 * (65 - (ABL:TARGET:金銭感覚 - 10) / 2) / 100
	ELSEIF ABL:TARGET:金銭感覚 > 100
		LOCAL:0 = LOCAL:0 / 100
	ENDIF
ENDIF

;生活費軽減
SIF ABL:MASTER:生活費軽減 >= 1
	LOCAL:0 = LOCAL:0 * (100 - ABL:MASTER:生活費軽減) / 100

;獲得資金増加ボーナス
IF ABL:MASTER:獲得資金増加 == 1
	TIMES LOCAL:1 , 1.50
ELSEIF ABL:MASTER:獲得資金増加 > 1
	LOCAL:1 *= ABL:MASTER:獲得資金増加
ENDIF

;アクセサリ関係
IF (FLAG:62 & 4) && TARGET > -1
	;パートナーが装備中のアクセサリで補正
	;アクセサリから浪費癖を算出
	IF CFLAG:TARGET:23 > 0
		CSVABL CFLAG:TARGET:23, 97, 0
		;例えば浪費癖が30のキャラのアクセサリを付けていた場合0.80倍になる
		LOCAL:0 = LOCAL:0 * (50 + RESULT) / 100
	ENDIF
	IF CFLAG:TARGET:24 > 0
		CSVABL CFLAG:TARGET:24, 97, 0
		;例えば浪費癖が30のキャラのアクセサリを付けていた場合0.80倍になる
		LOCAL:0 = LOCAL:0 * (50 + RESULT) / 100
	ENDIF
ENDIF

PRINTL  

MONEY -= LOCAL:0
MONEY += LOCAL:1
SIF LOCAL:1 > 0
	PRINTFORML モンスター退治をして${LOCAL:1}入手しました
SIF LOCAL:0 > 0
	PRINTFORMW 生活費として${LOCAL:0}消費しました

;キャラ独自イベント
TRYCALL MAKISAMA_EVENT
;恋慕等取得
CALL EVENTCHECK_M
;吸血鬼化チェック
CALL EVENTCHECK_V
;体力回復計算に使う休憩フラグを初期化
FLAG:0 = 0

;午後なら次の日、午前なら午後にする
IF TIME == 1
	;日付変更時のイベント呼び出し
	;キャラ独自イベント
	TRYCALL EVENT_NEXTDAY_T
	;日付変更時のイベント
	CALL EVENT_NEXTDAY
	;月経進行
	CALL EVENT_DANGERDAY
	DAY = DAY+1
	DAY:7 = DAY:7+1
	PRINTW 一日が終わった・・・

	TFLAG:79 = 0
	TIME = 0
ELSE
	TIME = 1
ENDIF


BEGIN SHOP

;-------------------------------------------------
;待機中行動口上呼び出し
;-------------------------------------------------
@KOJO_TAIKI_CHECK
;LOCAL FOR-NEXTで使用
;LOCAL:1 ZにMASTERを記録したフラグ
;LOCAL:2 TARGET保存（ローカル変数でも念のため保存）
;LOCAL:3 待機中行動口上カウント用

VARSET LOCAL, 0
Z = TARGET
LOCAL:2 = TARGET
;パートナーがいない時は口上のエラー防止にZにMASTERを記録
IF TARGET <= 0
	Z = MASTER
	LOCAL:2 = MASTER
	LOCAL:1 = 1
ENDIF

VARSET B, 0
VARSET Y, 0
;非表示
SKIPDISP 1
FOR LOCAL, 0, CHARANUM
	TFLAG:120 = 0
	TARGET = LOCAL
	TRYCCALLFORM KOJO_TAIKI_K{NO:LOCAL}
		;口上がある＆別れていない場合
		IF TFLAG:120 && CFLAG:LOCAL:21 == 0
			Y:(LOCAL:3) = LOCAL
			LOCAL:3 += 1
		ENDIF
	CATCH
		;口上がない場合
	ENDCATCH
NEXT
;非表示解除
SKIPDISP 0

;待機中行動口上が一つ以上ある場合
IF LOCAL:3
	B = Y:(RAND:(LOCAL:3))
	TARGET = B
	TFLAG:17 = CFLAG:B:13
	PRINTL 
	PRINTFORML ＜%CALLNAME:B%待機中＞
	CALL KOJO_TAIKI
ENDIF
TARGET = LOCAL:2
IF LOCAL:1
	TARGET = -1
ENDIF

;-------------------------------------------------
;セーブデータ互換処理
;-------------------------------------------------
@SAVEDATA_CONVERT
;以下のセーブデータ互換処理は既に必要無さそうなので削除しました
;異常経験保存フラグ互換
;主人公の体力と耐力互換
;星蓮船キャラ番号調整

REPEAT CHARANUM
	;本来休憩所預かりフラグが立たないキャラにフラグが立っていたら消去
	SIF COUNT == MASTER || COUNT == ASSI || COUNT == TARGET
		CFLAG:COUNT:16 = 0
	;本来別れフラグが立たないキャラにフラグが立っていたら消去
	SIF COUNT == MASTER
		CFLAG:COUNT:21 = 0
REND

;FLAG:63をセーブデータ互換処理に使用（元は調合の互換処理に使用していたフラグ）
;ニューゲーム時用
IF FLAG:63 == 0
	FLAG:63 = 20
ENDIF

;v1.01.1でFLAG:63 === 1使用
;v1.01.4でFLAG:63 === 2使用
IF FLAG:63 <= 2
	FLAG:63 = 3
ENDIF

;v1.04.9でFLAG:63 == 3使用
IF FLAG:63 == 3
	;一部の口上と干渉する可能性のあるCFLAG(キャラID)を移動
	REPEAT CHARANUM
		CFLAG:COUNT:81 = CFLAG:COUNT:182
		CFLAG:COUNT:182 = 0
	REND
	FLAG:63 = 4
ENDIF

;v1.05.7でFLAG:63 == 4使用
IF FLAG:63 == 4
	REPEAT CHARANUM
		;響子のCSV変更に伴う調整
		IF NO:COUNT == 101
			IF MAXBASE:COUNT:体力 <= 2000
				MAXBASE:COUNT:体力 = 2000
				BASE:COUNT:体力 = MAXBASE:COUNT:体力
			ENDIF
			IF MAXBASE:COUNT:気力 <= 1200
				MAXBASE:COUNT:気力 = 1200
				BASE:COUNT:気力 = MAXBASE:COUNT:気力
			ENDIF
		;芳香のCSV変更に伴う調整
		ELSEIF NO:COUNT == 102
			MAXBASE:COUNT:体力 = 2500
			BASE:COUNT:体力 = MAXBASE:COUNT:体力
			MAXBASE:COUNT:気力 = 1000
			BASE:COUNT:気力 = MAXBASE:COUNT:気力
			TALENT:COUNT:妄信 = 0
		ENDIF
	REND
	FLAG:63 = 5
ENDIF

;v1.05.9でFLAG:63 == 5使用
IF FLAG:63 == 5
	;コンフィグ整理
	SIF FLAG:23 & 128
		FLAG:23 -= 128
	SIF FLAG:23 & 256
		FLAG:23 -= 256
	FLAG:63 = 6
ENDIF

;v1.06.0でFLAG:63 == 6使用
IF FLAG:63 == 6
	;コンフィグ整理
	SIF FLAG:62 & 16
		FLAG:62 -= 16
	SIF FLAG:62 & 128
		FLAG:62 -= 128
	SIF FLAG:62 & 1024
		FLAG:62 -= 1024
	SIF FLAG:62 & 2097152
		FLAG:62 -= 2097152
	FLAG:63 = 7
ENDIF

;v1.06.3でFLAG:63 == 7使用
IF FLAG:63 == 7
	;J用アイテムをいくつか所持させる
	ITEM:500 = 1
	ITEM:503 = 1
	ITEM:506 = 1
	ITEM:510 = 1
	ITEM:513 = 1
	
	;妊娠期間判定用のDAY:7設定
	DAY:7 = DAY
	FLAG:63 = 8
ENDIF

;v1.06.8(スペルいじるパッチ)でFLAG:63 == 8使用
IF FLAG:63 == 8
	;スペカを設定する
	REPEAT CHARANUM
		IF CFLAG:COUNT:0 == 0 && CSTR:COUNT:22 == ""
			CSVCSTR NO:COUNT, 21, 0
			CSTR:COUNT:21 = %RESULTS%
			CSVCSTR NO:COUNT, 22, 0
			CSTR:COUNT:22 = %RESULTS%
			CSVCSTR NO:COUNT, 23, 0
			CSTR:COUNT:23 = %RESULTS%
			CSVCSTR NO:COUNT, 24, 0
			CSTR:COUNT:24 = %RESULTS%
		ELSEIF CFLAG:COUNT:0 && CSTR:COUNT:22 == ""
			CSVCSTR NO:COUNT, 21, 1
			CSTR:COUNT:21 = %RESULTS%
			CSVCSTR NO:COUNT, 22, 1
			CSTR:COUNT:22 = %RESULTS%
			CSVCSTR NO:COUNT, 23, 1
			CSTR:COUNT:23 = %RESULTS%
			CSVCSTR NO:COUNT, 24, 1
			CSTR:COUNT:24 = %RESULTS%
		ENDIF
	REND
	FLAG:63 = 9
ENDIF

;v1.06.9でFLAG:63 == 9使用
IF FLAG:63 == 9
	;攻略ルートと難易度を同じに設定
	FLAG:3 = FLAG:5
	FLAG:63 = 10
ENDIF

;v1.07.2でFLAG:63 == 10使用
IF FLAG:63 == 10
	;コンフィグ整理
	;グローバル変数読み込み
	CALL LOAD_GLOBAL_CONFIGURE
	;異常経験が付くコマンドを表示する
	IF FLAG:23 & 1p11
		FLAG:23 -= 1p11
		SIF (FLAG:61 & 1) == 0
			FLAG:61 |= 1
	ENDIF

	;パートナーが[親愛]を持っている場合、異常経験が付くコマンドを表示する
	;FLAG:61 & 2は新規コンフィグなので互換なし

	;挿入コマンドの派生追加
	IF FLAG:62 & 1p5
		FLAG:62 -= 1p5
		SIF (FLAG:61 & 1p2) == 0
			FLAG:61 |= 1p2
	ENDIF

	;以下の2つは今までのまとめだとONがデフォルトだったのでONにしておく
	;ウフフモード時のコマンド拡張
	SIF (FLAG:61 & 1p3) == 0
		FLAG:61 |= 1p3
	;通常モード（ウフフモード突入前）のコマンド拡張
	SIF (FLAG:61 & 1p4) == 0
		FLAG:61 |= 1p4

	;Ａ系コマンド拡張
	IF FLAG:62 & 1p6
		FLAG:62 -= 1p6
		SIF (FLAG:61 & 1p5) == 0
			FLAG:61 |= 1p5
	ENDIF
	;着衣挿入コマンド拡張
	IF FLAG:62 & 1p14
		FLAG:62 -= 1p14
		SIF (FLAG:61 & 1p6) == 0
			FLAG:61 |= 1p6
	ENDIF
	;仲間にしたキャラのコマンドフィルタを共通設定にする
	IF FLAG:62 & 1p20
		FLAG:62 -= 1p20
		SIF (FLAG:61 & 1p10) == 0
			FLAG:61 |= 1p10
	ENDIF
	;共通コンフィグ自動保存
	CALL STORE_GLOBAL_CONFIGURE
	FLAG:63 = 11
ENDIF

;v1.07.5でFLAG:63 == 11使用
IF FLAG:63 == 11
	;コンフィグ整理 ステータスインビジブル廃止
	SIF FLAG:62 & 1p18
		FLAG:62 -= 1p18
	FLAG:63 = 12
ENDIF

;v1.08.0でFLAG:63 == 12使用
IF FLAG:63 == 12
	;未設定のPALAMLVを設定
	PALAMLV:10 = 1000000
	PALAMLV:11 = 5000000
	PALAMLV:12 = 30000000
	PALAMLV:13 = 100000000
	
	;素質関連互換
	CALL TALENT_INTERCHANGE

	;主人公のキャラIDがなければキャラIDを設定する
	IF CFLAG:MASTER:81 == 0
		CFLAG:MASTER:81 = 1
		FLAG:998 = 2
	ENDIF

	;キャラIDがないキャラにはキャラIDを設定する
	FOR LOCAL, 0, CHARANUM
		IF CFLAG:LOCAL:81 == 0
			;キャラID設定
			CFLAG:LOCAL:81 = FLAG:998
			FLAG:998 += 1
		ENDIF
	NEXT
	FLAG:63 = 13
ENDIF
;v1.08.2でFLAG:63 == 13使用
IF FLAG:63 == 13
	;素質関連互換
	CALL TALENT_INTERCHANGE
	FLAG:63 = 14
ENDIF

;v1.10.2でFLAG:63 == 14使用
IF FLAG:63 == 14
	;回復素質記録互換（妊娠時に付加される回復遅い等を元々持っているか記録するフラグ）
	REPEAT CHARANUM
		;妊娠、もしくは臨月or育児中のキャラでCFLAG:98が1か2
		IF (TALENT:COUNT:妊娠 || TALENT:COUNT:育児中 || MARK:COUNT:90) && (CFLAG:COUNT:98 == 1 || CFLAG:COUNT:98 == 2)
			CFLAG:COUNT:96 = CFLAG:COUNT:98
			CFLAG:COUNT:98 = 0
		ENDIF
	REND
	FLAG:63 = 15
ENDIF

;J+ v1.01.9でFLAG:63 == 15使用(番号使用済扱い)
SIF FLAG:63 == 15
	FLAG:63 = 16

;J+ v1.01.9でFLAG:63 == 16使用
IF FLAG:63 == 16
	;陰毛設定互換
	FOR LOCAL, 0, CHARANUM
		CALL GET_BASE8, LOCAL
	NEXT
	
	;未設定のPALAMLVを設定
	PALAMLV:14 = 300000000
	PALAMLV:15 = 500000000
	PALAMLV:16 = 1000000000
	PALAMLV:17 = 3000000000
	PALAMLV:18 = 5000000000
	PALAMLV:19 = 10000000000
	PALAMLV:20 = 30000000000
	PALAMLV:21 = 50000000000
	PALAMLV:22 = 100000000000
	PALAMLV:23 = 300000000000
	PALAMLV:24 = 500000000000
	PALAMLV:25 = 1000000000000
	PALAMLV:26 = 3000000000000
	PALAMLV:27 = 5000000000000
	PALAMLV:28 = 10000000000000
	PALAMLV:29 = 30000000000000
	PALAMLV:30 = 50000000000000
	PALAMLV:31 = 100000000000000
	PALAMLV:32 = 500000000000000
	PALAMLV:33 = 1000000000000000
	PALAMLV:34 = 5000000000000000
	PALAMLV:35 = 9999999999999999
	PALAMLV:36 = 10000000000000000

	FLAG:63 = 17
ENDIF

;J+ v1.02.1でFLAG:63 == 17使用
IF FLAG:63 == 17
	;未設定のPALAMLVを設定
	PALAMLV:10 = 1000000
	PALAMLV:11 = 5000000
	PALAMLV:12 = 30000000
	PALAMLV:13 = 100000000
	PALAMLV:14 = 300000000
	PALAMLV:15 = 500000000
	PALAMLV:16 = 1000000000
	PALAMLV:17 = 3000000000
	PALAMLV:18 = 5000000000
	PALAMLV:19 = 10000000000
	PALAMLV:20 = 30000000000
	PALAMLV:21 = 50000000000
	PALAMLV:22 = 100000000000
	PALAMLV:23 = 300000000000
	PALAMLV:24 = 500000000000
	PALAMLV:25 = 1000000000000
	PALAMLV:26 = 3000000000000
	PALAMLV:27 = 5000000000000
	PALAMLV:28 = 10000000000000
	PALAMLV:29 = 30000000000000
	PALAMLV:30 = 50000000000000
	PALAMLV:31 = 100000000000000
	PALAMLV:32 = 500000000000000
	PALAMLV:33 = 1000000000000000
	PALAMLV:34 = 5000000000000000
	PALAMLV:35 = 9999999999999999
	PALAMLV:36 = 10000000000000000
	
	FLAG:63 = 18
ENDIF

;J+ v1.02.7でFLAG:63 == 18使用
IF FLAG:63 == 18
	FOR LOCAL, 0, CHARANUM
		SIF NO:LOCAL != 102
			TALENT:LOCAL:Ｍ鈍感 = 0
	NEXT
	FLAG:63 = 19
ENDIF

;J+ v1.04.6でFLAG:63 == 19使用
IF FLAG:63 == 19
	FOR LOCAL, 0, CHARANUM
		;あなたの97,浪費癖 98,戦闘素質を設定なし（0）から50に変更
		SIF NO:LOCAL == 0 && ABL:LOCAL:浪費癖 == 0
			ABL:LOCAL:浪費癖 = 50
		SIF NO:LOCAL == 0 && ABL:LOCAL:戦闘素質 == 0
			ABL:LOCAL:戦闘素質 = 50
	NEXT
	FLAG:63 = 20
ENDIF



;GLOBAL関係　互換用にGLOBAL:199使用
;実績リセット
LOADGLOBAL

IF GLOBAL:199 <= 1
	GLOBAL:200 = 0
	GLOBAL:199 = 2
ENDIF

IF GLOBAL:199 == 2
	VARSET LOCAL, 0
	FOR LOCAL, 0, 50
		GETBIT GLOBAL:201, LOCAL
		SIF RESULT
			SETBIT LOCAL:1, (LOCAL+1)
	NEXT
	
	GLOBAL:201 = LOCAL:1
	
	FOR LOCAL, 202, 900
		GLOBAL:LOCAL = 0
	NEXT
	GLOBAL:199 = 3
	
ENDIF


;このSAVEGLOBALが毎日呼び出されるので、マクロで呼ぶと時々エラー出る
SAVEGLOBAL

;ゲーム開始時の年が未設定なら設定
SIF DAY:4 == 0
	DAY:4 = 2012

;-------------------------------------------------
;素質関連互換
;-------------------------------------------------
@TALENT_INTERCHANGE
REPEAT CHARANUM

REND

@EVENTSHOP
#PRI

IF FLAG:60
	LOADGAME
	IF FLAG:60
		PRINTW データをロードしなかったので終了します
		QUIT
	ENDIF
	RETURN 0
ENDIF

@EVENTSHOP
#LATER

IF TIME == 0 && DAY
	CALL EVENT_NEWDAY
	DRAWLINE
	IF TFLAG:121 == 0 && TARGET > -1
		CALL EVENT_RING
		DRAWLINE
	ELSE
		TFLAG:121 = 1
	ENDIF
	TFLAG:600 = 38
	CALL KOJO_JUN
ENDIF

