;---------------------------------------------------------------------
;キャラ入れ替え・ソート
;ここで使用する変数---------------------------------------------------
; CFLAG:79: キャラ検索する際に使用
; CFLAG:80: 1 = 存在 2 = 調教対象 3 = 助手
; TFLAG:0  : オートソート時に使用
; TFLAG:1  : オートソート時に使用
; TFLAG:2  : オートソート時に使用
;---------------------------------------------------------------------
@CHARA_SORT
;変数初期化
REPEAT CHARANUM
	CFLAG:COUNT:79 = 0
	CFLAG:COUNT:80 = 0
REND
REPEAT 22
	LOCAL:COUNT = 0
	LOCAL:(100+COUNT) = 0
REND
TFLAG:1 = 0

;仲間チェック
REPEAT CHARANUM
	;主人公は除外
	SIF COUNT == 0
		CONTINUE
	;別れ中も除外
	SIF CFLAG:COUNT:21
		CONTINUE
	;調教対象と助手を保存する
	IF COUNT == TARGET
		CFLAG:COUNT:80 = 2
	ELSEIF COUNT == ASSI
		CFLAG:COUNT:80 = 3
	ELSE
		CFLAG:COUNT:80 = 1
	ENDIF
	;20限定表記の基礎処理
	Q:(LOCAL:0) = COUNT
	LOCAL:4 = LOCAL:0
	LOCAL:0 += 1
REND
IF LOCAL:0 < 1
	PRINTW 並び替えのできる仲間がいません
	RETURN 0
ENDIF
DRAWLINE
CALL SORT_LIST
DRAWLINE
PRINTL 順番を並べ変えられます
REPEAT 22
	SIF FLAG:5 != 10 && (COUNT == 20 || COUNT == 21)
		CONTINUE

	PRINTFORM [{1001+COUNT}] 
	IF COUNT == 0
		PRINTFORMLC キャラ番号が小さい順
	ELSEIF COUNT == 1
		PRINTFORMLC キャラ番号が大きい順
	ELSEIF COUNT == 2
		PRINTFORMLC 好感度が低い順
	ELSEIF COUNT == 3
		PRINTFORMLC 好感度が高い順
	ELSEIF COUNT == 4
		PRINTFORMLC 戦闘能力が低い順
	ELSEIF COUNT == 5
		PRINTFORMLC 戦闘能力が高い順
	ELSEIF COUNT == 6
		PRINTFORMLC %BASENAME:0%などが少ない順
	ELSEIF COUNT == 7
		PRINTFORMLC %BASENAME:0%などが少ない順
	ELSEIF COUNT == 8
		PRINTFORMLC 最大%BASENAME:0%などが少ない順
	ELSEIF COUNT == 9
		PRINTFORMLC 最大%BASENAME:0%などが多い順
	ELSEIF COUNT == 10
		PRINTFORMLC 弱気な順
	ELSEIF COUNT == 11
		PRINTFORMLC 強気な順
	ELSEIF COUNT == 12
		PRINTFORMLC 不所持素質順
	ELSEIF COUNT == 13
		PRINTFORMLC 所持素質順
	ELSEIF COUNT == 14
		PRINTFORMLC 能力が低い順
	ELSEIF COUNT == 15
		PRINTFORMLC 能力が高い順
	ELSEIF COUNT == 16
		PRINTFORMLC 経験が少ない順
	ELSEIF COUNT == 17
		PRINTFORMLC 経験が多い順
	ELSEIF COUNT == 18
		PRINTFORMLC 刻印LVが低い順
	ELSEIF COUNT == 19
		PRINTFORMLC 刻印LVが高い順
	ELSEIF COUNT == 20
		PRINTFORMLC 霧に飲まれる猶予日数が短い順
	ELSEIF COUNT == 21
		PRINTFORMLC 霧に飲まれる猶予日数が長い順
	ENDIF
	LOCAL:(100+COUNT) = 1
	;2列表示
	IF COUNT%2 == 1
		PRINTL 
	ENDIF
REND
DRAWLINE
PRINTFORML [1030] キャラいれかえ
PRINTFORML [1031] パートナーを前に並べる
DRAWLINE
PRINTL [1000]戻る
INPUT
IF RESULT == 1000
	REPEAT CHARANUM
		CFLAG:COUNT:79 = 0
		CFLAG:COUNT:80 = 0
	REND
	RETURN 0
ELSEIF (RESULT > 1000) && LOCAL:(100 + RESULT - 1001)
	TFLAG:0 = RESULT - 1001
	CALL AUTO_SORT
ELSEIF RESULT == 1030
	;キャラいれかえ
	CALL SWAP_SORT
ELSEIF RESULT == 1031
	SORTCHARA TALENT:150, BACK
	PRINTFORMW パートナーを前に並べました
ENDIF
RESTART

;---------------------------------------------------------------------
;キャラいれかえ処理
;---------------------------------------------------------------------
@SWAP_SORT
;変数初期化
REPEAT CHARANUM
	CFLAG:COUNT:79 = 0
	CFLAG:COUNT:80 = 0
REND
REPEAT 22
	LOCAL:COUNT = 0
	LOCAL:(100+COUNT) = 0
REND
TFLAG:1 = 0

;仲間チェック
REPEAT CHARANUM
	;主人公は除外
	SIF COUNT == 0
		CONTINUE
	;別れ中も除外
	SIF CFLAG:COUNT:21
		CONTINUE
	;調教対象と助手を保存する
	IF COUNT == TARGET
		CFLAG:COUNT:80 = 2
	ELSEIF COUNT == ASSI
		CFLAG:COUNT:80 = 3
	ELSE
		CFLAG:COUNT:80 = 1
	ENDIF
	;20限定表記の基礎処理
	Q:(LOCAL:0) = COUNT
	LOCAL:4 = LOCAL:0
	LOCAL:0 += 1
REND
;一人目選択
$CHARA_SORT_FIRST
PRINTFORMW 誰を入れ替えますか？
DRAWLINE
CALL SORT_LIST
DRAWLINE
PRINTL [1000]戻る
INPUT
IF RESULT == 1000
	REPEAT CHARANUM
		CFLAG:COUNT:79 = 0
		CFLAG:COUNT:80 = 0
	REND
	RETURN 0
ELSEIF RESULT < 0 || RESULT >= CHARANUM || CFLAG:RESULT:80 == 0
	PRINTL 無効な値です
	RESTART
ENDIF
;キャラを選択したらLOCAL:1とTFLAG:1にキャラを記録
LOCAL:1 = RESULT
TFLAG:1 = RESULT

DRAWLINE
DRAWLINE
;二人目選択
$CHARA_SORT_SECOND
PRINTFORMW 誰を入れ替えますか？(現在 {LOCAL:1}.%CALLNAME:(LOCAL:1)% を選択中)
DRAWLINE
CALL SORT_LIST
DRAWLINE
IF FLAG:5 == 10
	PRINTL 　　　　　　☆：パートナー　★：助手　×：霧に飲まれかけ　を表しています
ELSE
	PRINTL 　　　　　　☆：パートナー　★：助手　を表しています
ENDIF
DRAWLINE
PRINTL [1000]戻る
INPUT

IF RESULT == 1000
	LOCAL:1 = 0
	GOTO CHARA_SORT_FIRST
ELSEIF RESULT < 0 || RESULT >= CHARANUM || RESULT == LOCAL:1 || CFLAG:RESULT:80 == 0
	PRINTL 無効な値です
	GOTO CHARA_SORT_SECOND
ENDIF
LOCAL:2 = RESULT

PRINTFORMW %CALLNAME:(LOCAL:1)%と%CALLNAME:(LOCAL:2)%を入れ替えました
SWAPCHARA LOCAL:1, LOCAL:2
;調教対象と助手を保存していたものにする
REPEAT CHARANUM
	SIF CFLAG:COUNT:80 == 2
		TARGET = COUNT
	SIF CFLAG:COUNT:80 == 3
		ASSI = COUNT
REND
LOCAL:1 = 0
LOCAL:2 = 0
RESTART

;---------------------------------------------------------------------
;キャラオートソート処理
;---------------------------------------------------------------------
@AUTO_SORT
;TFLAG:0 = RESULT - 1001
IF (TFLAG:0 >= 6 && TFLAG:0 <= 9) || (TFLAG:0 >= 12 && TFLAG:0 <= 19)
	CALL CHARA_SORT_INPUT
	;戻るで戻ってきたらRETURN
	SIF RESULT == 1000
		RETURN 0
ELSE
	CALL CHARA_SORT_GET
ENDIF

IF TFLAG:0 == 0
	SORTCHARA NO
	PRINTFORMW キャラ番号が小さい順に並べました
ELSEIF TFLAG:0 == 1
	SORTCHARA NO, BACK
	PRINTFORMW キャラ番号が大きい順に並べました
ELSEIF TFLAG:0 == 2
	SORTCHARA CFLAG:2
	PRINTFORMW 好感度が低い順に並べました
ELSEIF TFLAG:0 == 3
	SORTCHARA CFLAG:2, BACK
	PRINTFORMW 好感度が高い順に並べました
ELSEIF TFLAG:0 == 4
	SORTCHARA CFLAG:79
	PRINTFORMW 戦闘能力が低い順に並べました
ELSEIF TFLAG:0 == 5
	SORTCHARA CFLAG:79, BACK
	PRINTFORMW 戦闘能力が高い順に並べました
ELSEIF TFLAG:0 == 6
	SORTCHARA BASE:(TFLAG:2)
	PRINTFORMW %BASENAME:(TFLAG:2)%が少ない順に並べました
ELSEIF TFLAG:0 == 7
	SORTCHARA BASE:(TFLAG:2), BACK
	PRINTFORMW %BASENAME:(TFLAG:2)%が多い順に並べました
ELSEIF TFLAG:0 == 8
	SORTCHARA MAXBASE:(TFLAG:2)
	PRINTFORMW 最大%BASENAME:(TFLAG:2)%が少ない順に並べました
ELSEIF TFLAG:0 == 9
	SORTCHARA MAXBASE:(TFLAG:2), BACK
	PRINTFORMW 最大%BASENAME:(TFLAG:2)%が多い順に並べました
ELSEIF TFLAG:0 == 10
	SORTCHARA CFLAG:79
	PRINTFORMW 弱気な順に並べました
ELSEIF TFLAG:0 == 11
	SORTCHARA CFLAG:79, BACK
	PRINTFORMW 強気な順に並べました
ELSEIF TFLAG:0 == 12
	SORTCHARA TALENT:(TFLAG:2)
	PRINTFORMW %TALENTNAME:(TFLAG:2)%を持っているキャラを後ろに並べました
ELSEIF TFLAG:0 == 13
	SORTCHARA TALENT:(TFLAG:2), BACK
	PRINTFORMW %TALENTNAME:(TFLAG:2)%を持っているキャラを前に並べました
ELSEIF TFLAG:0 == 14
	SORTCHARA ABL:(TFLAG:2)
	PRINTFORMW %ABLNAME:(TFLAG:2)%が低い順に並べました
ELSEIF TFLAG:0 == 15
	SORTCHARA ABL:(TFLAG:2), BACK
	PRINTFORMW %ABLNAME:(TFLAG:2)%が高い順に並べました
ELSEIF TFLAG:0 == 16
	SORTCHARA EXP:(TFLAG:2)
	PRINTFORMW %EXPNAME:(TFLAG:2)%が少ない順に並べました
ELSEIF TFLAG:0 == 17
	SORTCHARA EXP:(TFLAG:2), BACK
	PRINTFORMW %EXPNAME:(TFLAG:2)%が多い順に並べました
ELSEIF TFLAG:0 == 18
	SORTCHARA MARK:(TFLAG:2)
	PRINTFORMW %MARKNAME:(TFLAG:2)%LVが低い順に並べました
ELSEIF TFLAG:0 == 19
	SORTCHARA MARK:(TFLAG:2), BACK
	PRINTFORMW %MARKNAME:(TFLAG:2)%LVが高い順に並べました
ELSEIF TFLAG:0 == 20
	SORTCHARA CFLAG:79
	PRINTFORMW 霧に飲まれるまでの日数が短い順に並べました
ELSEIF TFLAG:0 == 21
	SORTCHARA CFLAG:79, BACK
	PRINTFORMW 霧に飲まれるまでの日数が長い順に並べました
ENDIF

;別れ中フラグで昇順ソート
SORTCHARA CFLAG:21

;---------------------------------------------------------------------
;キャラオートソート対象選択
;---------------------------------------------------------------------
@CHARA_SORT_INPUT
LOCAL:1 = 0
$PRINT_LIST
LOCAL:2 = 0
DRAWLINE
PRINTFORML 基準を選択してください。

IF TFLAG:0 == 12 || TFLAG:0 == 13
	;素質
	REPEAT 720
		B:COUNT = 0
		;この間素質がないので少し処理の負担軽減
		SIF COUNT > 230 && COUNT < 500
			CONTINUE
		STRLENS TALENTNAME:COUNT
		SIF RESULT < 1
			CONTINUE
		TSTR:0 = %TALENTNAME:COUNT%
		SIF LOCAL:2 / 30 >= LOCAL:1 && LOCAL:2 / 30 < 1 + LOCAL:1
			PRINTFORMLC [{COUNT, 3}] %TSTR:0, 12, LEFT%
		LOCAL:2 += 1
		B:COUNT = 1
	REND
ELSE
	REPEAT 100
		B:COUNT = 0
		IF TFLAG:0 == 6 || TFLAG:0 == 7
			;BASE
			SIF COUNT >= 6 || COUNT == 2 || COUNT == 3
				CONTINUE
			STRLENS BASENAME:COUNT
			SIF RESULT < 1
				CONTINUE
			TSTR:0 = %BASENAME:COUNT%
		ELSEIF TFLAG:0 == 8 || TFLAG:0 == 9
			;MAXBASE
			SIF COUNT >= 6 || COUNT == 2 || COUNT == 3
				CONTINUE
			STRLENS BASENAME:COUNT
			SIF RESULT < 1
				CONTINUE
			TSTR:0 = 最大%BASENAME:COUNT%
		ELSEIF TFLAG:0 == 14 || TFLAG:0 == 15
			;能力
			STRLENS ABLNAME:COUNT
			SIF RESULT < 1
				CONTINUE
			TSTR:0 = %ABLNAME:COUNT%
		ELSEIF TFLAG:0 == 16 || TFLAG:0 == 17
			;経験
			STRLENS EXPNAME:COUNT
			SIF RESULT < 1
				CONTINUE
			TSTR:0 = %EXPNAME:COUNT%
		ELSEIF TFLAG:0 == 18 || TFLAG:0 == 19
			;刻印LV
			STRLENS MARKNAME:COUNT
			SIF RESULT < 1
				CONTINUE
			TSTR:0 = %MARKNAME:COUNT%
		ENDIF
		SIF LOCAL:2 / 30 >= LOCAL:1 && LOCAL:2 / 30 < 1 + LOCAL:1
			PRINTFORMLC [{COUNT, 2}] %TSTR:0, 12, LEFT%
		LOCAL:2 += 1
		B:COUNT = 1
	REND
ENDIF
PRINTL 
DRAWLINE
PRINTFORMLC \@(LOCAL:1 <= 0) ? %" " * 16% # [1001]前のページ\@
PRINTLC [1000]戻る
PRINTFORMLC \@(LOCAL:1 >= LOCAL:2 / 30) ? %" " * 16% # [1009]次のページ\@

$INPUT_LOOP_0
INPUT
IF RESULT == 1000
	RETURN 1000
ELSEIF RESULT == 1001
	IF LOCAL:1 > 0
		LOCAL:1 -= 1
		GOTO PRINT_LIST
	ELSE
		GOTO INPUT_LOOP_0
	ENDIF
ELSEIF RESULT == 1009
	IF LOCAL:1 < LOCAL:2 / 30
		LOCAL:1 += 1
		GOTO PRINT_LIST
	ELSE
		GOTO INPUT_LOOP_0
	ENDIF
ELSEIF RESULT < 0 || RESULT > 720
	GOTO INPUT_LOOP_0
ELSEIF B:RESULT == 0
	GOTO INPUT_LOOP_0
ELSE
	TFLAG:2 = RESULT
ENDIF

;---------------------------------------------------------------------
;キャラオートソート数値計算
;---------------------------------------------------------------------
@CHARA_SORT_GET
IF TFLAG:0 == 4 || TFLAG:0 == 5
	;戦闘力
	REPEAT CHARANUM
		T = COUNT
		CALL GET_BATTLE_POWER
		CFLAG:COUNT:79 = P
	REND
ELSEIF TFLAG:0 == 10 || TFLAG:0 == 11
	;主導権
	REPEAT CHARANUM
		T = COUNT
		CALL GET_SUB_INITIATIVE_RATE
		CFLAG:COUNT:79 = R
	REND
ELSEIF TFLAG:0 == 20 || TFLAG:0 == 21
	;霧に飲まれるまでの日数
	REPEAT CHARANUM
		IF CFLAG:COUNT:17
			CFLAG:COUNT:79 = -1
		ELSE
			CFLAG:COUNT:79 = CFLAG:COUNT:18 - (10 + ABL:MASTER:霧無効化能力強化 * 2) * (TALENT:COUNT:150 * 5 + 10) / 10 - CFLAG:COUNT:20 / 10
			SIF CFLAG:COUNT:79 < 0
				CFLAG:COUNT:79 = 0
		ENDIF
	REND
ENDIF

;---------------------------------------------------------------------
;キャラソート用リスト
;---------------------------------------------------------------------
@SORT_LIST
W = 0
REPEAT CHARANUM
	;主人公は排除
	SIF COUNT == 0
		CONTINUE
	;預かり中のキャラは非表示なら排除
	SIF CFLAG:COUNT:16 == 1 && U == 0
		CONTINUE
	;別れ中のキャラは排除
	SIF CFLAG:COUNT:21
		CONTINUE

	;2列で表示
	PRINTFORM [{COUNT,2}]
	;パートナー（とついでに助手）が分かりやすいように
	IF COUNT == TARGET
		PRINT ☆ 
	ELSEIF COUNT == ASSI
		PRINT ★ 
	ELSEIF FLAG:5 == 10 && CFLAG:COUNT:17
		PRINT × 
	ELSE
		PRINT 　 
	ENDIF
	PRINTFORM %NAME:COUNT,16,LEFT%
	IF COUNT == TFLAG:1
		PRINT [ 選択中 ] 
	ELSEIF TALENT:COUNT:89
		PRINT [※相愛※] 
	ELSEIF TALENT:COUNT:88
		PRINT [※親愛※] 
	ELSEIF TALENT:COUNT:153
		PRINT [※恋人※] 
	ELSE
		PRINT 　　　　　 
	ENDIF
	W += 1
	IF W == 2
		W = 0
		PRINTL 
	ENDIF
REND
SIF W != 0
	PRINTL 



;=============================================================================
;キャラの能力・素質・経験・刻印検索
;=============================================================================
@CHARA_SEARCH
;変数初期化
REPEAT CHARANUM
	CFLAG:COUNT:79 = 0
	CFLAG:COUNT:80 = 0
REND
REPEAT 22
	LOCAL:COUNT = 0
	LOCAL:(100+COUNT) = 0
REND

PRINTL 何を検索しますか？
REPEAT 22
	SIF FLAG:5 != 10 && (COUNT == 20 || COUNT == 21)
		CONTINUE

	PRINTFORM [{COUNT,2}] 
	IF COUNT == 0
		PRINTFORMLC キャラ番号が小さいキャラ
	ELSEIF COUNT == 1
		PRINTFORMLC キャラ番号が大きいキャラ
	ELSEIF COUNT == 2
		PRINTFORMLC 好感度が低いキャラ
	ELSEIF COUNT == 3
		PRINTFORMLC 好感度が高いキャラ
	ELSEIF COUNT == 4
		PRINTFORMLC 戦闘能力が低いキャラ
	ELSEIF COUNT == 5
		PRINTFORMLC 戦闘能力が高いキャラ
	ELSEIF COUNT == 6
		PRINTFORMLC %BASENAME:0%などが少ないキャラ
	ELSEIF COUNT == 7
		PRINTFORMLC %BASENAME:0%などが少ないキャラ
	ELSEIF COUNT == 8
		PRINTFORMLC 最大%BASENAME:0%などが少ないキャラ
	ELSEIF COUNT == 9
		PRINTFORMLC 最大%BASENAME:0%などが多いキャラ
	ELSEIF COUNT == 10
		PRINTFORMLC 弱気なキャラ
	ELSEIF COUNT == 11
		PRINTFORMLC 強気なキャラ
	ELSEIF COUNT == 12
		PRINTFORMLC 素質を持っていないキャラ
	ELSEIF COUNT == 13
		PRINTFORMLC 素質を持っているキャラ 
	ELSEIF COUNT == 14
		PRINTFORMLC 能力が低いキャラ
	ELSEIF COUNT == 15
		PRINTFORMLC 能力が高いキャラ
	ELSEIF COUNT == 16
		PRINTFORMLC 経験が少ないキャラ
	ELSEIF COUNT == 17
		PRINTFORMLC 経験が多いキャラ
	ELSEIF COUNT == 18
		PRINTFORMLC 刻印LVが低いキャラ
	ELSEIF COUNT == 19
		PRINTFORMLC 刻印LVが高いキャラ
	ELSEIF COUNT == 20
		PRINTFORMLC 霧に飲まれる猶予日数が短いキャラ
	ELSEIF COUNT == 21
		PRINTFORMLC 霧に飲まれる猶予日数が長いキャラ
	ENDIF
	LOCAL:(COUNT) = 1
	;2列表示
	IF COUNT%2 == 1
		PRINTL 
	ENDIF
REND
DRAWLINE
PRINTL [1000]戻る
$INPUT_CHARA_SEARCH
INPUT
IF RESULT == 1000
	REPEAT CHARANUM
		CFLAG:COUNT:79 = 0
		CFLAG:COUNT:80 = 0
	REND
	RETURN 0
ELSEIF LOCAL:RESULT
	TFLAG:0 = RESULT
	CALL CHARA_SEARCH_START
	RESTART
ELSE
	PRINTL 無効な値です
	GOTO INPUT_CHARA_SEARCH
ENDIF

;---------------------------------------------------------------------
;キャラ検索処理
;---------------------------------------------------------------------
@CHARA_SEARCH_START
IF (TFLAG:0 >= 6 && TFLAG:0 <= 9) || (TFLAG:0 >= 12 && TFLAG:0 <= 19)
	CALL CHARA_SORT_INPUT
	;戻るで戻ってきたらRETURN
	SIF RESULT == 1000
		RETURN 0
ELSE
	CALL CHARA_SORT_GET
ENDIF

IF TFLAG:0 == 0
	PRINTFORML キャラ番号が何番より小さいキャラをサーチしますか？
	INPUT
	REPEAT CHARANUM
		CFLAG:COUNT:79 = NO:COUNT < RESULT
	REND
	PRINTFORMW キャラ番号が{RESULT}番より小さいキャラを表示します
ELSEIF TFLAG:0 == 1
	PRINTFORML キャラ番号が何番より大きいキャラをサーチしますか？
	INPUT
	REPEAT CHARANUM
		CFLAG:COUNT:79 = NO:COUNT > RESULT
	REND
	PRINTFORMW キャラ番号が{RESULT}番より大きいキャラを表示します
ELSEIF TFLAG:0 == 2
	PRINTFORML 好感度がいくつより小さいキャラをサーチしますか？
	INPUT
	REPEAT CHARANUM
		CFLAG:COUNT:79 = CFLAG:COUNT:2 < RESULT
	REND
	PRINTFORMW 好感度が{RESULT}より小さいキャラを表示します
ELSEIF TFLAG:0 == 3
	PRINTFORML 好感度がいくつより大きいキャラをサーチしますか？
	INPUT
	REPEAT CHARANUM
		CFLAG:COUNT:79 = CFLAG:COUNT:2 > RESULT
	REND
	PRINTFORMW 好感度が{RESULT}より大きいキャラを表示します
ELSEIF TFLAG:0 == 4
	PRINTFORML 戦闘能力がいくつより小さいキャラをサーチしますか？
	INPUT
	REPEAT CHARANUM
		CFLAG:COUNT:79 = CFLAG:COUNT:79 < RESULT
	REND
	PRINTFORMW 戦闘能力が{RESULT}より小さいキャラを表示します
ELSEIF TFLAG:0 == 5
	PRINTFORML 戦闘能力がいくつより大きいキャラをサーチしますか？
	INPUT
	REPEAT CHARANUM
		CFLAG:COUNT:79 = CFLAG:COUNT:79 > RESULT
	REND
	PRINTFORMW 戦闘能力が{RESULT}より大きいキャラを表示します
ELSEIF TFLAG:0 == 6
	PRINTFORML %BASENAME:(TFLAG:2)%がいくつより少ないキャラをサーチしますか？
	INPUT
	REPEAT CHARANUM
		CFLAG:COUNT:79 = BASE:COUNT:(TFLAG:2) < RESULT
	REND
	PRINTFORMW %BASENAME:(TFLAG:2)%が{RESULT}より小さいキャラを表示します
ELSEIF TFLAG:0 == 7
	PRINTFORML %BASENAME:(TFLAG:2)%がいくつより多いキャラをサーチしますか？
	INPUT
	REPEAT CHARANUM
		CFLAG:COUNT:79 = BASE:COUNT:(TFLAG:2) > RESULT
	REND
	PRINTFORMW %BASENAME:(TFLAG:2)%が{RESULT}より多いキャラを表示します
ELSEIF TFLAG:0 == 8
	PRINTFORML 最大%BASENAME:(TFLAG:2)%がいくつより少ないキャラをサーチしますか？
	INPUT
	REPEAT CHARANUM
		CFLAG:COUNT:79 = MAXBASE:COUNT:(TFLAG:2) < RESULT
	REND
	PRINTFORMW %BASENAME:(TFLAG:2)%が{RESULT}より小さいキャラを表示します
ELSEIF TFLAG:0 == 9
	PRINTFORML 最大%BASENAME:(TFLAG:2)%がいくつより多いキャラをサーチしますか？
	INPUT
	REPEAT CHARANUM
		CFLAG:COUNT:79 = MAXBASE:COUNT:(TFLAG:2) > RESULT
	REND
	PRINTFORMW 最大%BASENAME:(TFLAG:2)%が{RESULT}より多いキャラを表示します
ELSEIF TFLAG:0 == 10
	PRINTFORML どの程度より弱気なキャラをサーチしますか？
	PRINTFORML [0] “超強気”より弱気
	PRINTFORML [1] “強気”より弱気
	PRINTFORML [2] “普通”より弱気
	PRINTFORML [3] “弱気”より弱気
	$INPUT_LOOP_INITIATIVE_1
	INPUT
	IF RESULT == 0
		RESULT = 46
	ELSEIF RESULT == 1
		RESULT = 16
	ELSEIF RESULT == 2
		RESULT = -14
	ELSEIF RESULT == 3
		RESULT = -44
	ELSE
		REUSELASTLINE 正しい値を入力してください
		GOTO INPUT_LOOP_INITIATIVE_1
	ENDIF
	REPEAT CHARANUM
		CFLAG:COUNT:79 = CFLAG:COUNT:79 < RESULT
	REND
	PRINTFORM 性格が
	IF RESULT == 46
		PRINTFORM “超強気”
	ELSEIF RESULT == 16
		PRINTFORM “強気”
	ELSEIF RESULT == -14
		PRINTFORM “普通”
	ELSEIF RESULT == -44
		PRINTFORM “弱気”
	ENDIF
	PRINTFORMW より弱気なキャラを表示します
ELSEIF TFLAG:0 == 11
	PRINTFORML どの程度より強気なキャラをサーチしますか？
	PRINTFORML [0] “強気”より強気
	PRINTFORML [1] “普通”より強気
	PRINTFORML [2] “弱気”より強気
	PRINTFORML [3] “超弱気”より強気
	$INPUT_LOOP_INITIATIVE_2
	INPUT
	IF RESULT == 0
		RESULT = 45
	ELSEIF RESULT == 1
		RESULT = 15
	ELSEIF RESULT == 2
		RESULT = -15
	ELSEIF RESULT == 3
		RESULT = -45
	ELSE
		REUSELASTLINE 正しい値を入力してください
		GOTO INPUT_LOOP_INITIATIVE_2
	ENDIF
	REPEAT CHARANUM
		CFLAG:COUNT:79 = CFLAG:COUNT:79 > RESULT
	REND
	PRINTFORM 性格が
	IF RESULT == 45
		PRINTFORM “強気”
	ELSEIF RESULT == 15
		PRINTFORM “普通”
	ELSEIF RESULT == -15
		PRINTFORM “弱気”
	ELSEIF RESULT == -45
		PRINTFORM “超弱気”
	ENDIF
	PRINTFORMW より強気なキャラを表示します
ELSEIF TFLAG:0 == 12
	PRINTFORMW %TALENTNAME:(TFLAG:2)%を持っていないキャラを表示します
	REPEAT CHARANUM
		CFLAG:COUNT:79 = !TALENT:COUNT:(TFLAG:2)
	REND
ELSEIF TFLAG:0 == 13
	PRINTFORMW %TALENTNAME:(TFLAG:2)%を持っているキャラを表示します
	REPEAT CHARANUM
		CFLAG:COUNT:79 = TALENT:COUNT:(TFLAG:2)
	REND
ELSEIF TFLAG:0 == 14
	LOCAL:2 = RESULT
	PRINTFORML %ABLNAME:(TFLAG:2)%がいくつより低いキャラをサーチしますか？
	INPUT
	REPEAT CHARANUM
		CFLAG:COUNT:79 = ABL:COUNT:(TFLAG:2) < RESULT
	REND
	PRINTFORMW %ABLNAME:(TFLAG:2)%が{RESULT}より低いキャラを表示します
ELSEIF TFLAG:0 == 15
	LOCAL:2 = RESULT
	PRINTFORML %ABLNAME:(TFLAG:2)%がいくつより高いキャラをサーチしますか？
	INPUT
	REPEAT CHARANUM
		CFLAG:COUNT:79 = ABL:COUNT:(TFLAG:2) > RESULT
	REND
	PRINTFORMW %ABLNAME:(TFLAG:2)%が{RESULT}より高いキャラを表示します
ELSEIF TFLAG:0 == 16
	LOCAL:2 = RESULT
	PRINTFORML %EXPNAME:(TFLAG:2)%がいくつより少ないキャラをサーチしますか？
	INPUT
	REPEAT CHARANUM
		CFLAG:COUNT:79 = EXP:COUNT:(TFLAG:2) < RESULT
	REND
	PRINTFORMW %EXPNAME:(TFLAG:2)%が{RESULT}より少ないキャラを表示します
ELSEIF TFLAG:0 == 17
	LOCAL:2 = RESULT
	PRINTFORML %EXPNAME:(TFLAG:2)%がいくつより多いキャラをサーチしますか？
	INPUT
	REPEAT CHARANUM
		CFLAG:COUNT:79 = EXP:COUNT:(TFLAG:2) > RESULT
	REND
	PRINTFORMW %EXPNAME:(TFLAG:2)%が{RESULT}より多いキャラを表示します
ELSEIF TFLAG:0 == 18
	LOCAL:2 = RESULT
	PRINTFORML %MARKNAME:(TFLAG:2)%がいくつより低いキャラをサーチしますか？
	INPUT
	REPEAT CHARANUM
		CFLAG:COUNT:79 = MARK:COUNT:(TFLAG:2) < RESULT
	REND
	PRINTFORMW %MARKNAME:(TFLAG:2)%が{RESULT}より低いキャラを表示します
ELSEIF TFLAG:0 == 19
	LOCAL:2 = RESULT
	PRINTFORML %MARKNAME:(TFLAG:2)%がいくつより高いキャラをサーチしますか？
	INPUT
	REPEAT CHARANUM
		CFLAG:COUNT:79 = MARK:COUNT:(TFLAG:2) > RESULT
	REND
	PRINTFORMW %MARKNAME:(TFLAG:2)%が{RESULT}より高いキャラを表示します
ELSEIF TFLAG:0 == 20
	PRINTFORMW 霧に飲まれるまでの日数がいくつより少ないキャラをサーチしますか？
	INPUT
	REPEAT CHARANUM
		CFLAG:COUNT:79 = CFLAG:COUNT:79 < RESULT
	REND
	PRINTFORMW 霧に飲まれるまでの日数が{RESULT}より少ないキャラを表示します
ELSEIF TFLAG:0 == 21
	PRINTFORMW 霧に飲まれるまでの日数がいくつより多いキャラをサーチしますか？
	INPUT
	REPEAT CHARANUM
		CFLAG:COUNT:79 = CFLAG:COUNT:79 > RESULT
	REND
	PRINTFORMW 霧に飲まれるまでの日数が{RESULT}より多いキャラを表示します
ENDIF

;該当するキャラを表示し、番号を選択したらそのキャラの能力画面へ
CALL SEARCH_LIST

;---------------------------------------------------------------------
;キャラ検索用リスト
;---------------------------------------------------------------------
@SEARCH_LIST
DRAWLINE
REPEAT CHARANUM
	LOCAL:COUNT = 0
REND
LOCAL:2 = 0
W = 0

REPEAT CHARANUM
	;主人公は排除
	SIF COUNT == 0
		CONTINUE
	;@CHARA_SEARCH_STARTで該当したキャラのみ表示
	SIF CFLAG:COUNT:79 == 0
		CONTINUE
	;別れ中のキャラは排除
	SIF CFLAG:COUNT:21
		CONTINUE

	;2列で表示
	PRINTFORM [{COUNT,2}]
	;パートナー（とついでに助手）が分かりやすいように
	IF COUNT == TARGET
		PRINT ☆ 
	ELSEIF COUNT == ASSI
		PRINT ★ 
	ELSEIF FLAG:5 == 10 && CFLAG:COUNT:17
		PRINT × 
	ELSE
		PRINT 　 
	ENDIF
	PRINTFORM %NAME:COUNT,16,LEFT%
	IF TALENT:COUNT:89
		PRINT [※相愛※] 
	ELSEIF TALENT:COUNT:88
		PRINT [※親愛※] 
	ELSEIF TALENT:COUNT:153
		PRINT [※恋人※] 
	ELSE
		PRINT 　　　　　 
	ENDIF
	;INPUTの判定に使用
	LOCAL:COUNT = 1
	;LOCAL:2は該当キャラがいるかの判定に使用
	LOCAL:2 += 1
	W += 1
	IF W == 2
		W = 0
		PRINTL 
	ENDIF
REND
PRINTL 
IF LOCAL:2 == 0
	PRINTW 該当するキャラはいません
	PRINTL 
	RETURN 0
ELSE
	IF FLAG:5 == 10
		PRINTL 　　　　　　☆：パートナー　★：助手　×：霧に飲まれかけ　を表しています
	ELSE
		PRINTL 　　　　　　☆：パートナー　★：助手　を表しています
	ENDIF
ENDIF

DRAWLINE
PRINTL [1000]戻る
$INPUT_SEARCH_LIST
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF LOCAL:RESULT
	;TARGET保存し、選択番号を一時的にTARGETに変更。表示後戻す
	LOCAL:3 = TARGET
	TARGET = RESULT
	
	PRINTFORML %NAME:TARGET%
	SIF TARGET != MASTER
		CALL SHOW_BATTLE_INFO
	;新型表示
	DRAWLINE
	PRINTFORM ■ %NAME:TARGET%のステータス
	SIF ((FLAG:23 & 1p26) == 0) && FLAG:2 != 1
		PRINTFORM  (好感度:{CFLAG:2})
	PRINTFORML 
	PRINTL ･･･････････････････････････････････････････････････････････････････････････････
	PRINTL □ 珠
	CALL NEW_PRINT_JUEL
	CALL SHOW_INFO
	
	TARGET = LOCAL:3
ELSE
	REUSELASTLINE 正しい値を入力してください
	GOTO INPUT_SEARCH_LIST
ENDIF

RESTART

