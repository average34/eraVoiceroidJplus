;----------------------------------------------------------------
;REVMODEの骨格
;----------------------------------------------------------------
@REVMODE
#DIM DYNAMIC PRICONFIG, 1

;コンフィグを保存
PRICONFIG = FLAG:62
;パートナーのターンを削除
CLEARBIT FLAG:62, 4
;カウンターモードON
SETBIT FLAG:62, 12

;PREVCOMを初期化
PREVCOM = -1
;SELECTCOMを初期化
SELECTCOM = -1

;TRAINの骨格
CALL REVMODE_TRAIN_MAIN

;コンフィグを元に戻す
FLAG:62 = PRICONFIG
;PLAYERの体力が0以下
SIF BASE:MASTER:0 <= 0
	CALL COUNTER_DOWN
;調教終了
BEGIN AFTERTRAIN

;----------------------------------------------------------------
;REVMODEに固有のTRAIN
;----------------------------------------------------------------
@REVMODE_TRAIN_MAIN
;STATUSを表示
CALL SHOW_STATUS
;PREVCOMを表示
CALL REVMODE_SHOW_PREVCOM
;パートナーの行動
CALL REVMODE_PASSIVEPHASE(TARGET)
;REVMODEに固有のUSERCOMを表示
CALL REVMODE_SHOW_USERCOM
;プレイヤーの行動
CALL REVMODE_ACTIVEPHASE

;----------------------------------------------------------------
;PREVCOMを表示
;----------------------------------------------------------------
@REVMODE_SHOW_PREVCOM
IF PREVCOM > -1
	DRAWLINE
	PRINTFORML < 前回実行コマンド : %STR:0% >
ENDIF

;----------------------------------------------------------------
;REVMODEに固有のSHOW_USERCOM
;----------------------------------------------------------------
@REVMODE_SHOW_USERCOM
#DIM LCOUNT, 1
VARSET C
FOR LCOUNT, 0, 9
	;REVMODEに固有のCOMABLE
	SIF !REVMODE_ACTIVE_COMABLE(SELECTCOM, LCOUNT)
		CONTINUE
	;コマンド名を表示
	PRINTFORML %GET_TRAINNAME_ACTIVECOM(LCOUNT)%[{LCOUNT, 3}]
NEXT
PRINTL 
PRINTC 履歴表示[800]
CALL NEWLINE
PRINTC 汚れ表示[801]
CALL NEWLINE
PRINTC 能力表示[802]
CALL NEWLINE
PRINTC 能力表示(マスター)[902]
PRINTL 

;----------------------------------------------------------------
;REVMODEに固有のTRAINNAMEを取得
;----------------------------------------------------------------
@GET_TRAINNAME_ACTIVECOM(ARG)
#FUNCTIONS
SELECTCASE ARG
;キス
CASE 0
	RETURNF "キス"
;クンニ
CASE 1
	RETURNF "クンニ"
;フェラする
CASE 2
	RETURNF "フェラする"
;胸愛撫
CASE 3
	RETURNF "胸愛撫"
;消極的にやる
CASE 4
	RETURNF "消極的にやる"
;積極的にやる
CASE 5
	RETURNF "積極的にやる"
;快感に耐える
CASE 6
	RETURNF "快感に耐える"
;快感を受け入れる
CASE 7
	RETURNF "快感を受け入れる"
;おねだり
CASE 8
	RETURNF "おねだり"
ENDSELECT

;----------------------------------------------------------------
;REVMODEに固有のUSERCOM
;----------------------------------------------------------------
@REVMODE_USERCOM(ARG)
SELECTCASE ARG
;履歴表示
CASE 800
	CALL SHOW_EQUIP
;汚れ表示
CASE 801
	CALL STAIN_INFO
;能力表示
CASE 802
	;新型表示
	DRAWLINE
	PRINTFORM ■ %NAME:TARGET%(パートナー)のステータス 
	SIF ((FLAG:23 & 1p26) == 0) && FLAG:2 != 1
		PRINTFORM (好感度:{CFLAG:2})
	PRINTL 
	CALL SHOW_INFO
;能力表示(マスター)
CASE 902
	;新型表示
	W = PLAYER
	DRAWLINE
	PRINTFORML ■ %NAME:PLAYER%(マスター)のステータス
	DRAWLINE
	PRINTL □ 経験
	CALL NEW_PRINT_EXP
	DRAWLINE
	PRINTL □ 能力
	CALL NEW_PRINT_ABL
	DRAWLINE
	PRINTL □ 素質
	CALL NEW_PRINT_TALENT
	WAIT
ENDSELECT

;----------------------------------------------------------------
;PASSIVEPHASE(TARGET)
;----------------------------------------------------------------
@REVMODE_PASSIVEPHASE(ARG)
#DIM LCOUNT, 1
#DIM LINE, 1
VARSET LOCAL

;恥じらい成分
LOCAL:200 = TALENT:ARG:目立ちたがり + (TALENT:ARG:解放 - TALENT:ARG:抑圧) - TALENT:ARG:自制心 - (TALENT:ARG:貞操観念 - TALENT:ARG:貞操無頓着) - 2 * (TALENT:ARG:恥じらい - TALENT:ARG:恥薄い)
;誘惑成分
LOCAL:201 = TALENT:ARG:魅力 + TALENT:ARG:魅惑 + TALENT:ARG:謎の魅力 + (TALENT:ARG:巨乳 - TALENT:ARG:貧乳) + TALENT:ARG:小柄体型 - (TALENT:ARG:貞操観念 - TALENT:ARG:貞操無頓着) + TALENT:ARG:小悪魔 - TALENT:ARG:幼稚
;積極性
LOCAL:202 = (TALENT:ARG:気丈 - TALENT:ARG:臆病) + (TALENT:ARG:楽観的 - TALENT:ARG:悲観的) - TALENT:ARG:無関心 - TALENT:ARG:感情乏しい + (TALENT:ARG:好奇心 - TALENT:ARG:保守的) - TALENT:ARG:一線越えない + TALENT:ARG:献身的
;タチ成分
LOCAL:203 = (TALENT:ARG:生意気 - TALENT:ARG:大人しい) + (TALENT:ARG:プライド高い - TALENT:ARG:プライド低い) + TALENT:ARG:献身的 + 2 * (TALENT:ARG:サド - TALENT:ARG:マゾ)

;奉仕
LOCAL:300 += 2 * (LOCAL:202 + LOCAL:203)
;強要
LOCAL:301 += 2 * (LOCAL:201 - LOCAL:202)
;性交
LOCAL:302 += 2 * (LOCAL:202 + LOCAL:203)
;誘導
LOCAL:303 += 2 * (LOCAL:201 - LOCAL:200)


;キス
LOCAL:0 = 10 + 2 * (TALENT:ARG:Ｍ敏感 - TALENT:ARG:Ｍ鈍感) + 2 * TALENT:ARG:蕩唇 + 2 * TALENT:ARG:キス魔 + ABL:ARG:技巧
;手淫
LOCAL:1 = 10 + ABL:ARG:技巧
;フェラチオ
LOCAL:2 = 10 + 2 * (TALENT:ARG:汚臭鈍感 - TALENT:ARG:汚臭敏感) + 2 * TALENT:ARG:舌使い + ABL:ARG:技巧
;クンニする
LOCAL:3 = 10 + 2 * (TALENT:ARG:汚臭鈍感 - TALENT:ARG:汚臭敏感) + 2 * TALENT:ARG:舌使い + ABL:ARG:技巧
;パイズリ
LOCAL:4 = 10 + 2 * (TALENT:ARG:巨乳 - TALENT:ARG:貧乳) + 2 * TALENT:ARG:淫乳 + ABL:ARG:技巧
;素股
LOCAL:5 = 10 + 2 * (TALENT:ARG:濡れやすい - TALENT:ARG:濡れにくい) + ABL:ARG:技巧
;貝あわせ
LOCAL:6 = 10 + 2 * (TALENT:ARG:濡れやすい - TALENT:ARG:濡れにくい) + ABL:ARG:技巧
;足扱き
LOCAL:7 = 10 + 2 * TALENT:ARG:サド + ABL:ARG:サドっ気 + ABL:ARG:技巧



;確率調整
FOR LCOUNT, 0, 8
	LOCAL:LCOUNT = LOCAL:LCOUNT * (LOCAL:300 + 1) * (90 + RAND:21) / 100
NEXT


;正常位させる
LOCAL:8 = 10 + 2 * (TALENT:ARG:Ｖ敏感 - TALENT:ARG:Ｖ敏感) + 2 * TALENT:ARG:淫壷 + ABL:ARG:Ｖ感覚 + ABL:ARG:技巧
;後背位させる
LOCAL:9 = 10 + 2 * (TALENT:ARG:Ｖ敏感 - TALENT:ARG:Ｖ敏感) + 2 * TALENT:ARG:淫壷 + ABL:ARG:Ｖ感覚 + ABL:ARG:技巧
;対面座位させる
LOCAL:10 = 10 + 2 * (TALENT:ARG:Ｖ敏感 - TALENT:ARG:Ｖ敏感) + 2 * TALENT:ARG:淫壷 + ABL:ARG:Ｖ感覚 + ABL:ARG:技巧
;背面座位させる
LOCAL:11 = 10 + 2 * (TALENT:ARG:Ｖ敏感 - TALENT:ARG:Ｖ敏感) + 2 * TALENT:ARG:淫壷 + ABL:ARG:Ｖ感覚 + ABL:ARG:技巧
;騎乗位する
LOCAL:12 = 10 + 2 * (TALENT:ARG:Ｖ敏感 - TALENT:ARG:Ｖ敏感) + 2 * TALENT:ARG:淫壷 + ABL:ARG:Ｖ感覚 + ABL:ARG:技巧

;確率調整
FOR LCOUNT, 8, 13
	LOCAL:LCOUNT = LOCAL:LCOUNT * (LOCAL:302 + 1) * (90 + RAND:21) / 100
NEXT

;顔面騎乗クンニ
LOCAL:13 = 10 + 2 * (TALENT:ARG:Ｃ敏感 - TALENT:ARG:Ｃ敏感) + 2 * TALENT:ARG:淫核 + ABL:ARG:Ｃ感覚
;指挿入れ強制
LOCAL:14 = 10 + 2 * (TALENT:ARG:Ｖ敏感 - TALENT:ARG:Ｖ敏感) + 2 * TALENT:ARG:淫壷 + ABL:ARG:Ｖ感覚
;顔面騎乗アナル
LOCAL:15 = 10 + 2 * (TALENT:ARG:Ａ敏感 - TALENT:ARG:Ａ敏感) + 2 * TALENT:ARG:尻穴狂い + ABL:ARG:Ａ感覚
;胸を揉ませる
LOCAL:16 = 10 + 2 * (TALENT:ARG:Ｂ敏感 - TALENT:ARG:Ｂ敏感) + 2 * TALENT:ARG:淫乳 + ABL:ARG:Ｂ感覚
;フェラさせる
LOCAL:17 = 10 + 2 * (TALENT:ARG:Ｃ敏感 - TALENT:ARG:Ｃ敏感) + ABL:ARG:Ｃ感覚

;確率調整
FOR LCOUNT, 13, 18
	LOCAL:LCOUNT = LOCAL:LCOUNT * (LOCAL:301 + 1) * (90 + RAND:21) / 100
NEXT

;正常位する
LOCAL:18 = 10 + 2 * (TALENT:ARG:Ｃ敏感 - TALENT:ARG:Ｃ敏感) + ABL:ARG:Ｃ感覚 + ABL:ARG:技巧
;後背位する
LOCAL:19 = 10 + 2 * (TALENT:ARG:Ｃ敏感 - TALENT:ARG:Ｃ敏感) + ABL:ARG:Ｃ感覚 + ABL:ARG:技巧
;騎乗位させる
LOCAL:20 = 10 + 2 * (TALENT:ARG:Ｃ敏感 - TALENT:ARG:Ｃ敏感) + ABL:ARG:Ｃ感覚 + ABL:ARG:技巧

;確率調整
FOR LCOUNT, 18, 21
	LOCAL:LCOUNT = LOCAL:LCOUNT * (LOCAL:302 + 1) * (90 + RAND:21) / 100
NEXT

;自慰見せつけ
LOCAL:21 = 10 + 2 * TALENT:ARG:自慰しやすい

;確率調整
LOCAL:21 = LOCAL:21 * (LOCAL:303 + 1) * (90 + RAND:21) / 100

;自慰強制
LOCAL:22 = 10 + 2 * TALENT:ARG:サド + ABL:ARG:サドっ気

;確率調整
LOCAL:22 = LOCAL:22 * (LOCAL:301 + 1) * (90 + RAND:21) / 100

;ランダマイズ
FOR LCOUNT, 0, 23
	LOCAL:LCOUNT = RAND:(MAX(LOCAL:LCOUNT, 1))
NEXT



;特殊な処理
;プレイヤーに男性器がある
IF PENIS(PLAYER)
	SELECTCASE SELECTCOM
	;正常位させる OR 後背位させる OR 対面座位させる OR 背面座位させる OR 騎乗位する OR キス正常位 OR キス後背位 OR キス対面座位 OR キス背面座位 OR キス騎乗位 OR 挿入Ｇスポ責め OR 挿入子宮口責め
	CASE 20, 21, 24, 25, 34, 120 TO 124, 255, 256
		;続行
		IF !TFLAG:302 || (RAND:100 >= (50 + GETPALAMLV(PALAM:5, 10)))
			FOR LCOUNT, 0, 23
				LOCAL:LCOUNT *= INRANGE(LCOUNT, 8, 12)
			NEXT
		;終了
		ELSE
			FOR LCOUNT, 0, 23
				LOCAL:LCOUNT *= !INRANGE(LCOUNT, 8, 12)
			NEXT
		ENDIF
	ENDSELECT
ENDIF
;パートナーに男性器がある
IF PENIS(TARGET)
	SELECTCASE SELECTCOM
	;正常位する OR 後背位する OR 騎乗位させる
	CASE 193 TO 195
		;続行
		IF !TFLAG:10 || (RAND:100 >= (50 + GETPALAMLV(PALAM:5, 10)))
			FOR LCOUNT, 0, 23
				LOCAL:LCOUNT *= INRANGE(LCOUNT, 18, 20)
			NEXT
		;終了
		ELSE
			FOR LCOUNT, 0, 23
				LOCAL:LCOUNT *= !INRANGE(LCOUNT, 18, 20)
			NEXT
		ENDIF
	ENDSELECT
ENDIF

;実行判定
FOR LCOUNT, 0, 23
	LOCAL:LCOUNT *= REVMODE_PASSIVE_COMABLE(LCOUNT)
	IF LOCAL:LCOUNT
		SKIPDISP 1
		CALL PRE_COMORDER(REVMODE_CONVERT_PASSIVECOM(PREVCOM, LCOUNT))
		SKIPDISP 0
		SIF RESULT
			LOCAL:LCOUNT = 0
	ENDIF
NEXT

;選択
LOCAL:100 = 0
FOR LCOUNT, 0, 23
	IF LOCAL:LCOUNT > LOCAL:100
		LOCAL:100 = LOCAL:LCOUNT
		LOCAL:101 = LCOUNT
	ENDIF
NEXT

TFLAG:36 = LOCAL:101

;実行
IF LOCAL:100
	SELECTCOM = REVMODE_CONVERT_PASSIVECOM(PREVCOM, LOCAL:101)
	DRAWLINE
	LINE = LINECOUNT
	;PASSIVEPHASEの地の文
	IF FLAG:6 && !(CFLAG:99 & 2)
		;個別の地の文
		TRYCCALLFORM TRAIN_MESSAGE_REVMODE_PASSIVEPHASE_K{NO:TARGET}(PREVCOM, LOCAL:101)
		;共通の地の文
		CATCH
			CALL TRAIN_MESSAGE_REVMODE_PASSIVEPHASE(PREVCOM, LOCAL:101)
		ENDCATCH
	ENDIF
	;PASSIVEPHASEの口上
	SIF FLAG:7
		TRYCALLFORM KOJO_MESSAGE_REVMODE_PASSIVEPHASE_K{NO:TARGET}(PREVCOM, LOCAL:101)
	IF LINECOUNT == LINE
		CLEARLINE 1
	ELSE
		DRAWLINE
	ENDIF
ENDIF

;----------------------------------------------------------------
;PASSIVE_COMABLE
;----------------------------------------------------------------
@REVMODE_PASSIVE_COMABLE(ARG)
#FUNCTION
SELECTCASE ARG
;キス
CASE 0
	;キス経験あり
	RETURNF (EXP:96 > 0)
;手淫
CASE 1
	;プレイヤーに男性器がある
	RETURNF PENIS(PLAYER)
;フェラチオ
CASE 2
	;プレイヤーに男性器がある AND フェラ経験あり
	RETURNF (PENIS(PLAYER) && EXP:22 > 0)
;クンニする
CASE 3
	;プレイヤーに男性器がない
	RETURNF !PENIS(PLAYER)
;パイズリ
CASE 4
	;プレイヤーに男性器がある AND フェラ経験あり
	RETURNF (PENIS(PLAYER) && EXP:22 > 0)
;素股
CASE 5
	;プレイヤーに男性器がある AND 潤滑2000以上
	RETURNF (PENIS(PLAYER) && PALAM:3 >= 2000)
;貝あわせ
CASE 6
	;プレイヤーに男性器がない AND 潤滑2000以上
	RETURNF (!PENIS(PLAYER) && PALAM:3 >= 2000)
;足扱き
CASE 7
	;プレイヤーに男性器がある
	RETURNF PENIS(PLAYER)
;正常位させる
CASE 8
	;パートナーが処女でない AND プレイヤーに男性器がある AND パートナーがオトコでない AND 潤滑10000以上
	RETURNF (!TALENT:TARGET:0 && PENIS(PLAYER) && !TALENT:TARGET:122 && PALAM:3 >= 10000)
;後背位させる
CASE 9
	;パートナーが処女でない AND プレイヤーに男性器がある AND パートナーがオトコでない AND 潤滑10000以上
	RETURNF (!TALENT:TARGET:0 && PENIS(PLAYER) && !TALENT:TARGET:122 && PALAM:3 >= 10000)
;対面座位させる
CASE 10
	;パートナーが処女でない AND プレイヤーに男性器がある AND パートナーがオトコでない AND 潤滑10000以上
	RETURNF (!TALENT:TARGET:0 && PENIS(PLAYER) && !TALENT:TARGET:122 && PALAM:3 >= 10000)
;背面座位させる
CASE 11
	;パートナーが処女でない AND プレイヤーに男性器がある AND パートナーがオトコでない AND 潤滑10000以上
	RETURNF (!TALENT:TARGET:0 && PENIS(PLAYER) && !TALENT:TARGET:122 && PALAM:3 >= 10000)
;騎乗位する
CASE 12
	;パートナーが処女でない AND プレイヤーに男性器がある AND パートナーがオトコでない AND 潤滑10000以上
	RETURNF (!TALENT:TARGET:0 && PENIS(PLAYER) && !TALENT:TARGET:122 && PALAM:3 >= 10000)
;顔面騎乗クンニ
CASE 13
	;パートナーに男性器がない
	RETURNF !PENIS(TARGET)
;指挿入れ強制
CASE 14
	;パートナーが処女でない AND パートナーがオトコでない
	RETURNF (!TALENT:TARGET:0 && !TALENT:TARGET:122)
;顔面騎乗アナル
CASE 15
	;アナル経験100以上 AND 汚れ無視あり
	RETURNF (EXP:1 >= 100 && TALENT:PLAYER:64)
;胸を揉ませる
CASE 16
	;パートナーがオトコでない
	RETURNF !TALENT:TARGET:122
;フェラさせる
CASE 17
	;パートナーに男性器がある
	RETURNF PENIS(TARGET)
;正常位する
CASE 18
	;パートナーが童貞でない AND パートナーに男性器がある AND プレイヤーが処女でない AND プレイヤーがオトコでない
	RETURNF (!TALENT:TARGET:154 && PENIS(TARGET) && !TALENT:PLAYER:0 && !TALENT:PLAYER:122)
;後背位する
CASE 19
	;パートナーが童貞でない AND パートナーに男性器がある AND プレイヤーが処女でない AND プレイヤーがオトコでない
	RETURNF (!TALENT:TARGET:154 && PENIS(TARGET) && !TALENT:PLAYER:0 && !TALENT:PLAYER:122)
;騎乗位させる
CASE 20
	;パートナーが童貞でない AND パートナーに男性器がある AND プレイヤーが処女でない AND プレイヤーがオトコでない
	RETURNF (!TALENT:TARGET:154 && PENIS(TARGET) && !TALENT:PLAYER:0 && !TALENT:PLAYER:122)
;自慰見せつけ
CASE 21
	;強制自慰経験あり
	RETURNF (EXP:11 > 0)
;自慰強制
CASE 22
	;無条件で実行可能
	RETURNF 1
ENDSELECT

;----------------------------------------------------------------
;PASSIVECOMのコマンド番号変換
;----------------------------------------------------------------
@REVMODE_CONVERT_PASSIVECOM(ARG:0, ARG:1)
#FUNCTION
SELECTCASE ARG:1
;キス
CASE 0
	SELECTCASE ARG:0
	;それ以外
	CASEELSE
		RETURNF 6
	ENDSELECT
;手淫
CASE 1
	SELECTCASE ARG:0
	;それ以外
	CASEELSE
		RETURNF 30
	ENDSELECT
;フェラチオ
CASE 2
	SELECTCASE ARG:0
	;顔面騎乗クンニ OR フェラさせる OR シックスナイン
	CASE 1, 4, 69
		RETURNF 69
	;それ以外
	CASEELSE
		RETURNF 31
	ENDSELECT
;クンニする
CASE 3
	SELECTCASE ARG:0
	;顔面騎乗クンニ OR フェラさせる OR シックスナイン
	CASE 1, 4, 69
		RETURNF 69
	;それ以外
	CASEELSE
		RETURNF 61
	ENDSELECT
;パイズリ
CASE 4
	SELECTCASE ARG:0
	;それ以外
	CASEELSE
		RETURNF 32
	ENDSELECT
;素股
CASE 5
	SELECTCASE ARG:0
	;それ以外
	CASEELSE
		RETURNF 33
	ENDSELECT
;貝あわせ
CASE 6
	SELECTCASE ARG:0
	;それ以外
	CASEELSE
		RETURNF 63
	ENDSELECT
;足扱き
CASE 7
	SELECTCASE ARG:0
	;それ以外
	CASEELSE
		RETURNF 37
	ENDSELECT
;正常位させる
CASE 8
	SELECTCASE ARG:0
	;それ以外
	CASEELSE
		RETURNF 20
	ENDSELECT
;後背位させる
CASE 9
	SELECTCASE ARG:0
	;それ以外
	CASEELSE
		RETURNF 21
	ENDSELECT
;対面座位させる
CASE 10
	SELECTCASE ARG:0
	;それ以外
	CASEELSE
		RETURNF 24
	ENDSELECT
;背面座位させる
CASE 11
	SELECTCASE ARG:0
	;それ以外
	CASEELSE
		RETURNF 25
	ENDSELECT
;騎乗位する
CASE 12
	SELECTCASE ARG:0
	;それ以外
	CASEELSE
		RETURNF 34
	ENDSELECT
;顔面騎乗クンニ
CASE 13
	SELECTCASE ARG:0
	;フェラチオ OR クンニする OR シックスナイン
	CASE 31, 61, 69
		RETURNF 69
	;それ以外
	CASEELSE
		RETURNF 1
	ENDSELECT
;指挿入れ強制
CASE 14
	RETURNF 8
;顔面騎乗アナル
CASE 15
	SELECTCASE ARG:0
	;それ以外
	CASEELSE
		RETURNF 9
	ENDSELECT
;胸を揉ませる
CASE 16
	RETURNF 5
;フェラさせる
CASE 17
	SELECTCASE ARG:0
	;フェラチオ OR クンニする OR シックスナイン
	CASE 31, 61, 69
		RETURNF 69
	;それ以外
	CASEELSE
		RETURNF 4
	ENDSELECT
;正常位する
CASE 18
	RETURNF 193
;後背位する
CASE 19
	RETURNF 194
;騎乗位させる
CASE 20
	RETURNF 195
;自慰見せつけ
CASE 21
	RETURNF 3
;自慰強制
CASE 22
	RETURNF 205
;それ以外
CASEELSE
	RETURNF -1
ENDSELECT

;----------------------------------------------------------------
;PASSIVEPHASEの地の文(PREVCOM, PASSIVECOM)
;----------------------------------------------------------------
@TRAIN_MESSAGE_REVMODE_PASSIVEPHASE(ARG:0, ARG:1)
SELECTCASE ARG:1
;キス
CASE 0
	SELECTCASE ARG:0
	;それ以外
	CASEELSE
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の口を自分の口で塞いだ…
	ENDSELECT
;手淫
CASE 1
	SELECTCASE ARG:0
	;手淫
	CASE 30
		PRINTFORM %CALLNAME:TARGET%は
		;プレイヤーがオトコ
		IF !RAND:2 && TALENT:PLAYER:122
			PRINTFORM %CALLNAME:PLAYER%の陰嚢を優しく揉みながら
		;それ以外
		ELSE
			PRINTFORM 期待に満ちた表情で%CALLNAME:PLAYER%の目を見つめながら
		ENDIF
		PRINTFORML 丁寧に男性器をしごきあげた…
	;それ以外
	CASEELSE
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の男性器を手で愛撫し始めた…
	ENDSELECT
;フェラチオ
CASE 2
	SELECTCASE ARG:0
	;フェラチオ OR シックスナイン
	CASE 31, 69
		PRINTFORM %CALLNAME:TARGET%は
		;シックスナイン
		SIF ARG:0 == 69
			PRINTFORM %CALLNAME:PLAYER%の顔面に跨ったまま
		PRINTFORML すぼめた唇で亀頭をしごきあげている…
	;それ以外
	CASEELSE
		PRINTFORM %CALLNAME:TARGET%は
		;顔面騎乗クンニ
		SIF ARG:0 == 1
			PRINTFORM 濡れた女性器を%CALLNAME:PLAYER%の顔面にグチュグチュとこすりつけながら
		PRINTFORML %CALLNAME:PLAYER%の股間に顔を埋めて男性器を口で愛撫し始めた…
	ENDSELECT
;クンニする
CASE 3
	SELECTCASE ARG:0
	;クンニする OR シックスナイン
	CASE 61, 69
		PRINTFORM %CALLNAME:TARGET%は
		;シックスナイン
		SIF ARG:0 == 69
			PRINTFORM %CALLNAME:PLAYER%の顔面に跨ったまま
		PRINTFORML すぼめた唇で陰核に何度もキスをしている…
	;それ以外
	CASEELSE
		PRINTFORM %CALLNAME:TARGET%は
		;顔面騎乗クンニ
		SIF ARG:0 == 1
			PRINTFORM 濡れた女性器を%CALLNAME:PLAYER%の顔面にグチュグチュとこすりつけながら
		PRINTFORML %CALLNAME:PLAYER%の股間に顔を埋めて秘唇を舐め始めた…
	ENDSELECT
;パイズリ
CASE 4
	SELECTCASE ARG:0
	;パイズリ
	CASE 32
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%のペニスを胸で挟んだまま
		PRINTFORML 時折舌で先走りを舐めとっては激しく上半身を揺さぶっている…
	;それ以外
	CASEELSE
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の男性器を胸で挟んで擦り始めた…
	ENDSELECT
;素股
CASE 5
	SELECTCASE ARG:0
	;素股
	CASE 33
		PRINTFORML %CALLNAME:TARGET%は快感にあえぐ%CALLNAME:PLAYER%の上で性器が触れ合う感触を楽しんでいる…
	;それ以外
	CASEELSE
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の男性器を太ももで挟んで擦り始めた…
	ENDSELECT
;貝あわせ
CASE 6
	SELECTCASE ARG:0
	;貝あわせ
	CASE 63
		PRINTFORML %CALLNAME:TARGET%は快感にあえぐ%CALLNAME:PLAYER%の上で性器が触れ合う感触を楽しんでいる…
	;それ以外
	CASEELSE
		PRINTFORML %CALLNAME:TARGET%は二人の女性器をぴったり重ねて擦りつけ始めた…
	ENDSELECT
;足扱き
CASE 7
	SELECTCASE ARG:0
	;足扱き
	CASE 37
		PRINTFORML %CALLNAME:TARGET%は愉快そうに%CALLNAME:PLAYER%を見下ろしながら、
		PRINTFORML ペニスをぐりぐりと踏みつけている…
	;それ以外
	CASEELSE
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の男性器を踏みつけ擦り始めた…
	ENDSELECT
;正常位させる
CASE 8
	SELECTCASE ARG:0
	;正常位 OR キス正常位
	CASE 20, 120
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の腰を引き寄せペニスを膣奥まで引きずりこんだ…
	;性交
	CASE 21, 24, 25, 34, 121, 122, 123, 124
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%に挿入させたまま倒れこみ体位を正常位に変えた…
	;それ以外
	CASEELSE
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%のモノに軽く手を添え、股を開いてその先端を女陰へと誘導した…
	ENDSELECT
;後背位させる
CASE 9
	SELECTCASE ARG:0
	;後背位 OR キス後背位
	CASE 21, 121
		PRINTFORML %CALLNAME:TARGET%は腰をくねらせ%CALLNAME:PLAYER%に更なる快感を与えた…
	;性交
	CASE 20, 24, 25, 34, 120, 121, 123, 124
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%に挿入させたまま四つん這いになった…
	;それ以外
	CASEELSE
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の前で四つん這いになり、後ろ手で陰唇を開いて誘惑した…
	ENDSELECT
;対面座位させる
CASE 10
	SELECTCASE ARG:0
	;対面座位 OR キス対面座位
	CASE 24, 122
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%をきつく抱きしめ行動の自由を奪い
		PRINTFORML ねっとりとした腰使いで膣内のペニスをなぶっている…
	;性交
	CASE 20, 21, 25, 34, 120, 121, 123, 124
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%をきつく抱きしめ行動の自由を奪うと
		PRINTFORML ねっとりとした腰使いで膣内のペニスをなぶり始めた…
	;それ以外
	CASEELSE
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%のモノに軽く手を添え、先端を膣口に導いてからゆっくりと腰を沈めていった…
	ENDSELECT
;背面座位させる
CASE 11
	SELECTCASE ARG:0
	;背面座位 OR キス背面座位
	CASE 25, 123
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の首に手を回し
		PRINTFORML ねっとりとした腰使いで膣内のペニスをなぶり始めた…
	;性交
	CASE 20, 21, 24, 34, 120, 121, 122, 124
		PRINTFORML %CALLNAME:TARGET%は挿入させたまま%CALLNAME:PLAYER%にもたれかかり
		PRINTFORML ねっとりとした腰使いで膣内のペニスをなぶり始めた…
	;それ以外
	CASEELSE
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%のモノに軽く手を添え、そのまま腰をかけるようにゆっくりと膣に収めようとした…
	ENDSELECT
;騎乗位する
CASE 12
	SELECTCASE ARG:0
	;騎乗位 OR キス騎乗位
	CASE 34, 124
		PRINTFORML %CALLNAME:TARGET%は膣内をきつく締めたまま
		PRINTFORML ねっとりとした腰使いで%CALLNAME:PLAYER%のペニスをなぶっている…
	;性交
	CASE 20, 21, 24, 25, 120, 121, 122, 123
		PRINTFORML %CALLNAME:TARGET%は挿入したまま%CALLNAME:PLAYER%を押し倒すと、
		PRINTFORML ねっとりとした腰使いで膣内のペニスをなぶり始めた…
	;それ以外
	CASEELSE
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%のモノを強引にヴァギナの奥まで咥えこもうとした…
	ENDSELECT
;顔面騎乗クンニ
CASE 13
	SELECTCASE ARG:0
	;顔面騎乗クンニ
	CASE 1
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%に愛撫を続けるように促した…
	;顔面騎乗アナル
	CASE 9
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の顔面に自分の女性器を押し付けた…
	;それ以外
	CASEELSE
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の顔面にまたがった…
		PRINTFORML %CALLNAME:PLAYER%の目の前に無防備な陰唇がさらけ出されている……
	ENDSELECT
;指挿入れ強制
CASE 14
	SELECTCASE ARG:0
	;指挿入れ強制
	CASE 8
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%に愛撫を続けるように促した…
	;それ以外
	CASEELSE
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の手を自分の女性器に導いた…
	ENDSELECT
;顔面騎乗アナル
CASE 15
	SELECTCASE ARG:0
	;顔面騎乗アナル
	CASE 9
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%に愛撫を続けるように促した…
	;顔面騎乗クンニ
	CASE 1
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の顔面に自分の菊門を押し付けた…
	;それ以外
	CASEELSE
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の顔面にまたがった…
		PRINTFORML %CALLNAME:PLAYER%の目の前にひくつく菊門がさらけ出されている……
	ENDSELECT
;胸を揉ませる
CASE 16
	SELECTCASE ARG:0
	;胸を揉ませる
	CASE 3
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%に愛撫を続けるように促した…
	;それ以外
	CASEELSE
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の手を自分の胸に導いた…
	ENDSELECT
;フェラさせる
CASE 17
	SELECTCASE ARG:0
	;フェラさせる
	CASE 4
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%に口淫を続けるように促した…
	;それ以外
	CASEELSE
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の眼前に自分の男性器を突き出した…
	ENDSELECT
;正常位する
CASE 18
	SELECTCASE ARG:0
	;正常位する
	CASE 193
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の膣内からペニスを抜かずに、再び腰を動かし始めた…
	;後背位する OR 騎乗位させる
	CASE 194, 195
		PRINTFORML %CALLNAME:TARGET%は挿入したままで体位を正常位に変えた…
	;正常位させる OR 後背位させる OR 対面座位させる OR 背面座位させる OR 騎乗位する OR キス正常位 OR キス後背位 OR キス対面座位 OR キス背面座位 OR キス騎乗位
	CASE 20, 21, 24, 25, 34, 120 TO 124
		PRINTFORML %CALLNAME:TARGET%は自分に挿入されたペニスを抜いて、逆に%CALLNAME:PLAYER%に正常位で挿入した…
	;それ以外
	CASEELSE
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の秘裂を貫こうとした…
	ENDSELECT
;後背位する
CASE 19
	SELECTCASE ARG:0
	;後背位する
	CASE 194
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の膣内からペニスを抜かずに、再び腰を動かし始めた…
	;正常位する OR 騎乗位させる
	CASE 193, 195
		PRINTFORML %CALLNAME:TARGET%は挿入したままで体位を後背位に変えた…
	;正常位させる OR 後背位させる OR 対面座位させる OR 背面座位させる OR 騎乗位する OR キス正常位 OR キス後背位 OR キス対面座位 OR キス背面座位 OR キス騎乗位
	CASE 20, 21, 24, 25, 34, 120 TO 124
		PRINTFORML %CALLNAME:TARGET%は自分に挿入されたペニスを抜いて、逆に%CALLNAME:PLAYER%に後背位で挿入した…
	;それ以外
	CASEELSE
		PRINTFORML %CALLNAME:TARGET%は後ろから%CALLNAME:PLAYER%の秘裂の最奥まで突き入れようとした…
	ENDSELECT
;騎乗位させる
CASE 20
	SELECTCASE ARG:0
	;騎乗位させる
	CASE 195
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の膣内からペニスを抜かずに、再び腰を動かし始めた…
	;正常位する OR 後背位する
	CASE 193, 194
		PRINTFORML %CALLNAME:TARGET%は挿入したままで体位を騎乗位に変えた…
	;正常位させる OR 後背位させる OR 対面座位させる OR 背面座位させる OR 騎乗位する OR キス正常位 OR キス後背位 OR キス対面座位 OR キス背面座位 OR キス騎乗位
	CASE 20, 21, 24, 25, 34, 120 TO 124
		PRINTFORML %CALLNAME:TARGET%は自分に挿入されたペニスを抜いて、逆に%CALLNAME:PLAYER%に騎乗位で挿入した…
	;それ以外
	CASEELSE
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%を上に跨らせ、ゆっくりと腰を下ろすよう命令した…
	ENDSELECT
;自慰見せつけ
CASE 21
	SELECTCASE ARG:0
	;自慰見せつけ
	CASE 3
		PRINTFORML %CALLNAME:TARGET%は自慰を続けている…
	;それ以外
	CASEELSE
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%に見せつけるように目の前で自慰を始めた…
	ENDSELECT
;自慰強制
CASE 22
	SELECTCASE ARG:0
	;自慰強制
	CASE 205
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%に自慰を続けるように促した…
	;それ以外
	CASEELSE
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%に自分でするところを見せろと命令した…
	ENDSELECT
ENDSELECT
WAIT

;----------------------------------------------------------------
;ACTIVEPHASE
;----------------------------------------------------------------
@REVMODE_ACTIVEPHASE
#DIM LINE, 1
#DIM DYNAMIC REACTION, 1
LINE = LINECOUNT
DRAWLINE
INPUT
SELECTCASE RESULT
;COM
CASE 0 TO 9
	REACTION = RESULT
	;REVMODEに固有のCOMABLE
	SIF !REVMODE_ACTIVE_COMABLE(SELECTCOM, REACTION)
		RESTART
	;COMORDER
	SKIPDISP 1
	CALL PRE_COMORDER(REVMODE_CONVERT_ACTIVECOM(SELECTCOM, REACTION))
	SKIPDISP 0
	SIF !RESULT
		SELECTCOM = REVMODE_CONVERT_ACTIVECOM(SELECTCOM, REACTION)
	;COM
	CALL REVMODE_ACTIVECOM(SELECTCOM, REACTION)
	
	DRAWLINE
	LINE = LINECOUNT
	;ACTIVEPHASEの地の文
	IF FLAG:6 && !(CFLAG:99 & 2)
		;個別の地の文
		TRYCCALLFORM TRAIN_MESSAGE_REVMODE_ACTIVEPHASE_K{NO:TARGET}(SELECTCOM, REACTION)
		;共通の地の文
		CATCH
			CALL TRAIN_MESSAGE_REVMODE_ACTIVEPHASE(SELECTCOM, REACTION)
		ENDCATCH
	ENDIF
	;ACTIVEPHASEの口上
	SIF FLAG:7
		TRYCALLFORM KOJO_MESSAGE_REVMODE_ACTIVEPHASE_K{NO:TARGET}(SELECTCOM, REACTION)
	SIF LINECOUNT != LINE
		DRAWLINE
	
	;REVMODEに固有のSOURCE_CHECK
	CALL REVMODE_SOURCE_CHECK
	
	;TARGETの体力が500以上 AND TARGETの気力が0より大きい AND PLAYERの体力が0より大きい
	SIF BASE:TARGET:0 >= 500 && BASE:TARGET:1 > 0 && BASE:PLAYER:0 > 0
		CALL REVMODE_TRAIN_MAIN
;USERCOM
CASEELSE
	CALL REVMODE_USERCOM(RESULT)
	CLEARLINE LINECOUNT - LINE
	RESTART
ENDSELECT

;----------------------------------------------------------------
;ACTIVE_COMABLE
;----------------------------------------------------------------
@REVMODE_ACTIVE_COMABLE(ARG:0, ARG:1)
#FUNCTION
SELECTCASE ARG:1
;キス
CASE 0
	;正常位させる、後背位させる、対面座位させる、背面座位させる、騎乗位する、キス正常位、キス後背位、キス対面座位、キス騎乗位だと実行可能
	RETURNF GROUPMATCH(ARG:0, 20, 21, 24, 25, 34, 120, 121, 122, 123, 124)
;クンニ
CASE 1
	;パートナーに男性器がなく、フェラチオ、クンニする、シックスナインだと実行可能
	RETURNF (!PENIS(TARGET) && GROUPMATCH(ARG:0, 31, 61, 69))
;フェラする
CASE 2
	;パートナーに男性器があり、フェラチオ、クンニする、シックスナインだと実行可能
	RETURNF (PENIS(TARGET) && GROUPMATCH(ARG:0, 31, 61, 69))
;胸愛撫
CASE 3
	;正常位させる、後背位させる、対面座位させる、背面座位させる、騎乗位する、正常位する、後背位する、騎乗位させるだと実行可能
	RETURNF GROUPMATCH(ARG:0, 20, 21, 24, 25, 34, 193, 194, 195)
;消極的にやる
CASE 4
	;手淫、フェラチオ、パイズリ、クンニする、シックスナイン、素股、貝あわせ、足扱き、自慰見せつけだと実行不可
	RETURNF !GROUPMATCH(ARG:0, 30, 31, 61, 69, 32, 33, 63, 37, 3)
;積極的にやる
CASE 5
	;手淫、フェラチオ、パイズリ、クンニする、シックスナイン、素股、貝あわせ、足扱き、自慰見せつけだと実行不可
	RETURNF !GROUPMATCH(ARG:0, 30, 31, 61, 69, 32, 33, 63, 37, 3)
;快感に耐える
CASE 6
	;キス、顔面騎乗クンニ、指挿入れ強制、顔面騎乗アナル、胸を揉ませる、フェラさせる、自慰強制だと実行不可
	RETURNF !GROUPMATCH(ARG:0, 6, 1, 8, 9, 5, 4, 205)
;快感を受け入れる
CASE 7
	;キス、顔面騎乗クンニ、指挿入れ強制、顔面騎乗アナル、胸を揉ませる、フェラさせる、自慰強制だと実行不可
	RETURNF !GROUPMATCH(ARG:0, 6, 1, 8, 9, 5, 4, 205)
;おねだり
CASE 8
	;手淫、フェラチオ、パイズリ、クンニする、シックスナイン、素股、貝あわせ、足扱き、自慰見せつけだと実行可能
	RETURNF GROUPMATCH(ARG:0, 30, 31, 61, 69, 32, 33, 63, 37, 3)
ENDSELECT

;----------------------------------------------------------------
;ACTIVECOMのコマンド番号変換
;----------------------------------------------------------------
@REVMODE_CONVERT_ACTIVECOM(ARG:0, ARG:1)
#FUNCTION
SELECTCASE ARG:1
;キス
CASE 0
	SELECTCASE ARG:0
	;正常位させる
	CASE 20
		;キス正常位
		RETURNF 120
	;後背位させる
	CASE 21
		;キス後背位
		RETURNF 121
	;対面座位させる
	CASE 24
		;キス対面座位
		RETURNF 122
	;背面座位させる
	CASE 25
		;キス背面座位
		RETURNF 123
	;騎乗位する
	CASE 34
		;キス騎乗位
		RETURNF 124
	;それ以外
	CASEELSE
		;そのまま
		RETURNF ARG:0
	ENDSELECT
;クンニ
CASE 1
	SELECTCASE ARG:0
	;顔面騎乗クンニ、フェラさせる、フェラチオ、クンニ強制
	CASE 1, 4, 31, 61
		;シックスナイン
		RETURNF 69
	;それ以外
	CASEELSE
		;そのまま
		RETURNF ARG:0
	ENDSELECT
;フェラする
CASE 2
	SELECTCASE ARG:0
	;顔面騎乗クンニ、フェラさせる、フェラチオ
	CASE 1, 4, 31
		;シックスナイン
		RETURNF 69
	;それ以外
	CASEELSE
		;そのまま
		RETURNF ARG:0
	ENDSELECT
;胸愛撫
CASE 3
	;そのまま
	RETURNF ARG:0
;消極的にやる
CASE 4
	;そのまま
	RETURNF ARG:0
;積極的にやる
CASE 5
	;そのまま
	RETURNF ARG:0
;快感に耐える
CASE 6
	;そのまま
	RETURNF ARG:0
;快感を受け入れる
CASE 7
	;そのまま
	RETURNF ARG:0
;おねだり
CASE 8
	;そのまま
	RETURNF ARG:0
;それ以外
CASEELSE
	;そのまま
	RETURNF ARG:0
ENDSELECT

;----------------------------------------------------------------
;ACTIVECOM
;----------------------------------------------------------------
@REVMODE_ACTIVECOM(ARG:0, ARG:1)
#DIM LCOUNT, 1

;TFLAG:0-29,100,540毎ターンリセット
REPEAT 30
	TFLAG:COUNT = 0
REND
TFLAG:100 = 0
TFLAG:540 = 0

;PLAYERに男性器がある
IF PENIS(PLAYER)
	SELECTCASE ARG:1
	;快感に耐える（1/4）
	CASE 6
		BASE:PLAYER:2 -= CALC_EJAC(ARG:0, PLAYER) / 4 * 3
	;快感を受け入れる（3/4）
	CASE 7
		BASE:PLAYER:2 -= CALC_EJAC(ARG:0, PLAYER) / 4
	;それ以外（1/2）
	CASEELSE
		BASE:PLAYER:2 -= CALC_EJAC(ARG:0, PLAYER) / 2
	ENDSELECT
ENDIF

SKIPDISP 1
TRYCALLFORM COM{ARG:0}
SKIPDISP 0

;胸愛撫選択時に快Bソース無い場合(背面座位の快Bソースを参考）
IF SOURCE:17 == 0 && ARG:1 == 3
	SOURCE:17 += (ABL:PLAYER:2 * 100) * 2 + (ABL:14 * 30)
ENDIF

SELECTCASE ARG:1
;消極的にやる（1/4）
CASE 4
	FOR LCOUNT, 0, 25, 1
		TIMES SOURCE:LCOUNT, 0.25
	NEXT
;積極的にやる（3/4）
CASE 5
	FOR LCOUNT, 0, 25, 1
		TIMES SOURCE:LCOUNT, 0.75
	NEXT
;それ以外（2/1）
CASEELSE
	FOR LCOUNT, 0, 25, 1
		TIMES SOURCE:LCOUNT, 0.5
	NEXT
ENDSELECT

;----------------------------------------------------------------
;ACTIVEPHASEの地の文(SELECTCOM, REACTION)
;----------------------------------------------------------------
@TRAIN_MESSAGE_REVMODE_ACTIVEPHASE(ARG:0, ARG:1)
SELECTCASE ARG:0
;キス
CASE 6
	SELECTCASE ARG:1
	;消極的にやる
	CASE 4
		PRINTFORML %CALLNAME:TARGET%は強引に%CALLNAME:PLAYER%の唇を奪うと
		PRINTFORML %CALLNAME:PLAYER%の口内を舌で蹂躙した…
	;積極的にやる
	CASE 5
		PRINTFORML %CALLNAME:PLAYER%は自らも%CALLNAME:TARGET%の口内に舌を侵入させ互いに舌を絡め合った…
	ENDSELECT
;手淫
CASE 30
	SELECTCASE ARG:1
	;快感に耐える
	CASE 6
		PRINTFORML %CALLNAME:TARGET%は目を閉じて耐えようとする%CALLNAME:PLAYER%の下半身を押さえつけ、
		PRINTFORML 繰り返し弄ぶように陰茎のくびれを擦り上げた…
	;快感を受け入れる
	CASE 7
		;射精する
		IF TFLAG:302
			PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の顔を見つめながら、
			PRINTFORML 激しく男性器をしごきあげ射精へと導いた…
		;射精しない
		ELSE
			PRINTFORML %CALLNAME:TARGET%は腰を浮かせて快感をせがむ%CALLNAME:PLAYER%を上目遣いに見ながら
			PRINTFORML びくびくと痙攣する陰茎を手で弄んでいる…
		ENDIF
	;おねだり
	CASE 8
		PRINTFORML %CALLNAME:TARGET%は情けなく懇願する%CALLNAME:PLAYER%のペニスに先走りを塗りつけ、
		PRINTFORML 亀頭にキスをしながら追い詰めるように愛撫した…
	ENDSELECT
;フェラチオ
CASE 31
	SELECTCASE ARG:1
	;快感に耐える
	CASE 6
		PRINTFORML %CALLNAME:TARGET%は息を止めて顔を背ける%CALLNAME:PLAYER%の下半身を抱え込み、
		PRINTFORML 雁首を唇で包み、声が漏れるまで執拗に裏筋を舐め上げた…
	;快感を受け入れる
	CASE 7
		;射精する
		IF TFLAG:302
			PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の腰を掴んでペニスを根元まで飲み込むと
			PRINTFORML 一滴でも多く射精させようと一心不乱に吸いあげた…
		;射精しない
		ELSE
			PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の睾丸を優しくもみほぐしながら、
			PRINTFORML 亀頭をチロチロと舌で刺激し間欠的に噴き上がる先走りを舐めとっている…
		ENDIF
	;おねだり
	CASE 8
		PRINTFORML %CALLNAME:TARGET%は情けなく懇願する%CALLNAME:PLAYER%の亀頭をぱっくり咥え、
		PRINTFORML 磨くように全体を舐め回し、ゆっくり頭を前後に動かした…
	ENDSELECT
;クンニする
CASE 61
	SELECTCASE ARG:1
	;快感に耐える
	CASE 6
		PRINTFORML %CALLNAME:TARGET%は息を止めて顔を背ける%CALLNAME:PLAYER%の下半身を抱え込み、
		PRINTFORML 陰核を唇で包み、声が漏れるまで執拗に裏筋を舐め上げた…
	;快感を受け入れる
	CASE 7
		IF !RAND:2
			PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の脚の付け根を大きく左右に開かせると、
			PRINTFORML 同時に陰核に吸い付いて舌で潰すように舐め始めた…
		ELSE
			PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の秘唇を指で押し開きながら、
			PRINTFORML 陰核をチロチロと舌で刺激し間欠的に噴き上がる愛液を舐めとっている…
		ENDIF
	;おねだり
	CASE 8
		PRINTFORML %CALLNAME:TARGET%は情けなく懇願する%CALLNAME:PLAYER%の陰核にしゃぶりつき、
		PRINTFORML 磨くように全体を舐め回し、何度も先端にキスをした…
	ENDSELECT
;シックスナイン
CASE 69
	SELECTCASE ARG:1
	;クンニ OR フェラする
	CASE 1, 2
		PRINTFORML %CALLNAME:PLAYER%は目の前の\@ PENIS(TARGET) ? 陰茎 # 陰唇 \@にむしゃぶりつき、
		PRINTFORML %CALLNAME:TARGET%と互いの性器を舐めあった…
	;快感に耐える
	CASE 6
		PRINTFORML %CALLNAME:TARGET%は息を止めて顔を背ける%CALLNAME:PLAYER%の下半身を抱え込み、
		PRINTFORML \@ PENIS(PLAYER) ? 雁首 # 陰核 \@を唇で包み、声が漏れるまで執拗に\@ PENIS(PLAYER) ? 裏筋を # \@舐め上げた…
	;快感を受け入れる
	CASE 7
		;射精する
		IF TFLAG:302
			PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の顔面に性器をこすりつけながら
			PRINTFORML 一滴でも多く射精させようと一心不乱にペニスに吸いついた…
		;射精しない
		ELSE
			PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の顔面に性器をこすりつけながら、
			;プレイヤーに男性器がある
			IF PENIS(PLAYER)
				PRINTFORML 亀頭をチロチロと舌で刺激し間欠的に噴き上がる先走りを舐めとっている…
			;プレイヤーに男性器がない
			ELSE
				IF !RAND:2
					PRINTFORML 陰核をチロチロと舌で刺激し間欠的に噴き上がる愛液を舐めとっている…
				ELSE
					PRINTFORML 同時に陰核に吸い付いて舌で潰すように舐め始めた…
				ENDIF
			ENDIF
		ENDIF
	;おねだり
	CASE 8
		PRINTFORML %CALLNAME:TARGET%は情けなく懇願する%CALLNAME:PLAYER%の\@ PENIS(PLAYER) ? 亀頭をぱっくり咥え # 陰核にしゃぶりつき \@、
		PRINTFORML 磨くように全体を舐め回し、\@ PENIS(PLAYER) ? ゆっくり頭を前後に動か # 何度も先端にキスを \@した…
	ENDSELECT
;パイズリ
CASE 32
	SELECTCASE ARG:1
	;快感に耐える
	CASE 6
		PRINTFORML %CALLNAME:PLAYER%は快感を堪えようと身体を強張らせるが、
		PRINTFORML %CALLNAME:TARGET%は構わず乳房でペニスを挟み、慈しむように上下に動かした…
	;快感を受け入れる
	CASE 7
		;射精する
		IF TFLAG:302
			;巨乳
			IF TALENT:巨乳
				PRINTFORML %CALLNAME:TARGET%は豊満な胸からかろうじて顔をのぞかせた%CALLNAME:PLAYER%の亀頭に
				PRINTFORML 口づけし、射精の瞬間を待ち構えた…
			;貧乳
			ELSEIF TALENT:貧乳
				PRINTFORML %CALLNAME:TARGET%は両手で胸を寄せささやかな谷間を作ると
				PRINTFORML %CALLNAME:PLAYER%のペニスを挟んで射精を促した…
			;それ以外
			ELSE
				PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%のペニスを柔らかい胸で優しく包みこむと
				PRINTFORML %CALLNAME:PLAYER%に自分から突き込むように促した…
			ENDIF
		;射精しない
		ELSE
			;巨乳
			IF TALENT:巨乳
				PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%のペニスを手にとると
				PRINTFORML 豊満な乳房で押し包み、やわやわと揉み焦らし始めた…
			;貧乳
			ELSEIF TALENT:貧乳
				PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%のペニスを手にとると
				PRINTFORML 控えめな胸の先で亀頭をくすぐり、焦らしている…
			;それ以外
			ELSE
				PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%が自ら胸の谷間に突き込むのを制し、
				PRINTFORML 亀頭を舌でくすぐっている…
			ENDIF
		ENDIF
	;おねだり
	CASE 8
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%にされるままペニスを乳房に挟み込まれ、
		PRINTFORML 亀頭が甘く蕩けるような責めを丹念に施された…
	ENDSELECT
;素股
CASE 33
	SELECTCASE ARG:1
	;快感に耐える
	CASE 6
		PRINTFORML 快感にもだえる%CALLNAME:PLAYER%が顔を背けると
		PRINTFORML %CALLNAME:TARGET%はわざと音を立てながら腰を揺すった…
	;快感を受け入れる
	CASE 7
		;射精する
		IF TFLAG:302
			PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の亀頭を膣口に密着させると、
			PRINTFORML グチュグチュと腰を前後させ射精へと導いた…
		;射精しない
		ELSE
			PRINTFORML %CALLNAME:TARGET%は陰唇で%CALLNAME:PLAYER%のペニスを挟んだまま腰をゆっくりと大きく前後させ
			PRINTFORML %CALLNAME:PLAYER%が喘ぐ姿を微笑みながら見下ろしている…
		ENDIF
	;おねだり
	CASE 8
		PRINTFORML %CALLNAME:PLAYER%は性器が深く擦れるように腰を前後させ、
		PRINTFORML %CALLNAME:TARGET%の与える快楽を自らも求めた…
	ENDSELECT
;貝あわせ
CASE 63
	SELECTCASE ARG:1
	;快感に耐える
	CASE 6
		PRINTFORML 快感にもだえる%CALLNAME:PLAYER%が顔を背けると
		PRINTFORML %CALLNAME:TARGET%はわざと音を立てながら腰を揺すった…
	;快感を受け入れる
	CASE 7
		;絶頂する
		IF 0
			PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の陰核を膣口で捉えると、
			PRINTFORML グチュグチュと腰を前後させ絶頂へと導いた…
		;絶頂しない
		ELSE
			PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%を組み敷く姿勢で腰を大きく前後させ
			PRINTFORML %CALLNAME:PLAYER%が喘ぐ姿を微笑みながら見下ろしている…
		ENDIF
	;おねだり
	CASE 8
		PRINTFORML %CALLNAME:PLAYER%は性器が深く擦れるように腰を前後させ、
		PRINTFORML %CALLNAME:TARGET%の与える快楽を自らも求めた…
	ENDSELECT
;足扱き
CASE 37
	SELECTCASE ARG:1
	;快感に耐える
	CASE 6
		PRINTFORML %CALLNAME:TARGET%は抵抗しない%CALLNAME:PLAYER%に卑猥な言葉を投げかけながら、
		PRINTFORML 愛撫を続けた…
	;快感を受け入れる
	CASE 7
		PRINTFORML %CALLNAME:TARGET%は快感に喘ぐ%CALLNAME:PLAYER%に卑猥な言葉を投げかけながら、
		PRINTFORML 愛撫を続けた…
	;おねだり
	CASE 8
		PRINTFORML %CALLNAME:PLAYER%は屈辱的な責めを受け、許してと懇願した…
		PRINTFORML %CALLNAME:TARGET%は笑いながら、足での愛撫をより強くしていく…
	ENDSELECT
;正常位させる
CASE 20
	SELECTCASE ARG:1
	;胸愛撫
	CASE 3
		PRINTFORML %CALLNAME:PLAYER%は繋がったまま%CALLNAME:TARGET%の胸を正面から揉みしだいた…
	;消極的にやる
	CASE 4
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%に覆い被さってペニスを挿入したが、
		PRINTFORML 満足に動けず、%CALLNAME:TARGET%が腰をしゃくり上げるのに翻弄されるのみだった…
	;積極的にやる
	CASE 5
		;射精する
		IF TFLAG:302
			PRINTFORML %CALLNAME:PLAYER%は壊れたように激しく腰を振り、
			PRINTFORML 射精の瞬間まで%CALLNAME:TARGET%を満足させようとした…
		;射精しない
		ELSE
			PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の膣内の敏感な所に当たるよう、
			PRINTFORML 挿入に角度をつけ、小刻みにくいくいと刺激した…
		ENDIF
	;快感に耐える
	CASE 6
		PRINTFORML %CALLNAME:PLAYER%は自分が快楽に溺れてしまわないよう、腰の動きを小さくし
		PRINTFORML %CALLNAME:TARGET%の感じる場所を繰り返し擦り上げた…
	;快感を受け入れる
	CASE 7
		;射精する
		IF TFLAG:302
			PRINTFORML %CALLNAME:PLAYER%はがっちりと抱え込まれたまま腰を振り
			PRINTFORML 射精の寸前にペニスを%CALLNAME:TARGET%の子宮口に届くほど深く突き入れた…
		;射精しない
		ELSE
			PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%に覆い被さり、体重をかけて挿入すると
			PRINTFORML その感覚の虜になり、快感を求めて腰を打ち付けた…
		ENDIF
	ENDSELECT
;後背位させる
CASE 21
	SELECTCASE ARG:1
	;胸愛撫
	CASE 3
		PRINTFORML %CALLNAME:PLAYER%は繋がったまま%CALLNAME:TARGET%の胸を背後から揉みしだいた…
	;消極的にやる
	CASE 4
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%に背後からペニスを挿入したが、
		PRINTFORML 満足に動けず、%CALLNAME:TARGET%が艶めかしく腰を揺する動きに弄ばれた…
	;積極的にやる
	CASE 5
		;射精する
		IF TFLAG:302
			PRINTFORML %CALLNAME:PLAYER%は壊れたように激しく腰を振り、
			PRINTFORML 射精の瞬間まで%CALLNAME:TARGET%を満足させようとした…
		;射精しない
		ELSE
			PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の尻を掴んで引き寄せるようにし、
			PRINTFORML 膣内の敏感なくぼみを先端で抉るようにして突き入れた…
		ENDIF
	;快感に耐える
	CASE 6
		PRINTFORML 自分が感じ過ぎないよう、%CALLNAME:PLAYER%は腰を引かずに押し付けるようにして
		PRINTFORML %CALLNAME:TARGET%が悦ぶ場所をぐりぐりと刺激した…
	;快感を受け入れる
	CASE 7
		;射精する
		IF TFLAG:302
			PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の腰をつかんで激しく腰を振り、
			PRINTFORML 射精の寸前にペニスを%CALLNAME:TARGET%の子宮口に届くほど深く突き入れた…
		;射精しない
		ELSE
			PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の背に覆い被さり、後ろから挿入すると
			PRINTFORML 腰を激しく振り、情欲と快楽に溺れていった…
		ENDIF
	ENDSELECT
;対面座位させる
CASE 24
	SELECTCASE ARG:1
	;胸愛撫
	CASE 3
		PRINTFORML %CALLNAME:PLAYER%は繋がったまま%CALLNAME:TARGET%の胸を正面から揉みしだいた…
	;消極的にやる
	CASE 4
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%と抱き合うようにしてペニスを挿入したが、
		PRINTFORML 満足に動けず、%CALLNAME:TARGET%に唇を奪われ、腰を揺すって弄ばれている…
	;積極的にやる
	CASE 5
		;射精する
		IF TFLAG:302
			PRINTFORML %CALLNAME:PLAYER%は壊れたように激しく腰を振り、
			PRINTFORML 射精の瞬間まで%CALLNAME:TARGET%を満足させようとした…
		;射精しない
		ELSE
			PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の唇を奪い、背中を撫でながら、
			PRINTFORML 腰を抱いて体重を受け止め、気持ち良くさせようとした…
		ENDIF
	;快感に耐える
	CASE 6
		PRINTFORML %CALLNAME:PLAYER%は自分より%CALLNAME:TARGET%が快感を得られるよう、
		PRINTFORML キスをしながら抱き合って腰を回す動きで奉仕した…
	;快感を受け入れる
	CASE 7
		;射精する
		IF TFLAG:302
			PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の腰をつかんで激しく腰を振り、
			PRINTFORML 射精の寸前にペニスを%CALLNAME:TARGET%の子宮口に届くほど深く突き入れた…
		;射精しない
		ELSE
			PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の腰と背を抱き、上半身全体でしがみつき、
			PRINTFORML 唇を貪られながら%CALLNAME:TARGET%の身体ごと上下に揺さぶり快感を求めた…
		ENDIF
	ENDSELECT
;背面座位させる
CASE 25
	SELECTCASE ARG:1
	;胸愛撫
	CASE 3
		PRINTFORML %CALLNAME:PLAYER%は繋がったまま%CALLNAME:TARGET%の胸を背後から揉みしだいた…
	;消極的にやる
	CASE 4
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の背中を抱いてペニスを挿入したが、
		PRINTFORML 満足に動けず、%CALLNAME:TARGET%は前屈みになって腰を使って責め立てている…
	;積極的にやる
	CASE 5
		;射精する
		IF TFLAG:302
			PRINTFORML %CALLNAME:PLAYER%は壊れたように激しく腰を振り、
			PRINTFORML 射精の瞬間まで%CALLNAME:TARGET%を満足させようとした…
		;射精しない
		ELSE
			PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の乳首と\@ PENIS(TARGET) ? 亀頭 # 陰核 \@を弄りつつ、
			PRINTFORML 腰を突き上げる反動で膣内を擦り、気持ち良くさせようとした…
		ENDIF
	;快感に耐える
	CASE 6
		PRINTFORML %CALLNAME:PLAYER%は腰を大きく動かさずに%CALLNAME:TARGET%の胸と股間を愛撫し、
		PRINTFORML 自分が溺れてしまわずに%CALLNAME:TARGET%が快感を得られるようにした…
	;快感を受け入れる
	CASE 7
		;射精する
		IF TFLAG:302
			PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の腰をつかんで激しく腰を振り、
			PRINTFORML 射精の寸前にペニスを%CALLNAME:TARGET%の子宮口に届くほど深く突き入れた…
		;射精しない
		ELSE
			PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の腰と胸を抱き上げ、前後に揺するようにして
			PRINTFORML 浅く挿入されたペニスを摩擦して快感を得ることに没頭した…
		ENDIF
	ENDSELECT
;騎乗位する
CASE 34
	SELECTCASE ARG:1
	;胸愛撫
	CASE 3
		PRINTFORML %CALLNAME:PLAYER%は繋がったまま%CALLNAME:TARGET%の胸を正面から揉みしだいた…
	;消極的にやる
	CASE 4
		PRINTFORML 押し倒されたまま抵抗できない%CALLNAME:PLAYER%に跨り、
		PRINTFORML %CALLNAME:TARGET%は全身をばねのように使って激しく犯した…
	;積極的にやる
	CASE 5
		;射精する
		IF TFLAG:302
			PRINTFORML %CALLNAME:PLAYER%は組み敷かれながらも腰を突き上げ、、
			PRINTFORML 射精の瞬間まで%CALLNAME:TARGET%を満足させようとした…
		;射精しない
		ELSE
			PRINTFORML %CALLNAME:TARGET%が腰を押し付けて前後にくいくい動かしだしたのを見て取ると、
			PRINTFORML %CALLNAME:PLAYER%は腰を持ち上げ、気持ち良い部分に深く当たるように手伝った…
		ENDIF
	;快感に耐える
	CASE 6
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%に犯される快感を必死に我慢し、
		PRINTFORML %CALLNAME:TARGET%が気持ち良くなれるまで奉仕しようとした…
	;快感を受け入れる
	CASE 7
		;射精する
		IF TFLAG:302
			PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の体が跳ね上がるほど激しく腰を突き上げ
			PRINTFORML 射精の寸前にペニスを%CALLNAME:TARGET%の子宮口に届くほど深く突き入れた…
		;射精しない
		ELSE
			PRINTFORML %CALLNAME:TARGET%は組み敷いた%CALLNAME:PLAYER%に快感を与えるため上下に激しく腰を使い、
			PRINTFORML %CALLNAME:PLAYER%は上擦った声で快感を訴えることしかできない…
		ENDIF
	ENDSELECT
;キス正常位 OR キス対面座位 OR キス騎乗位
CASE 120, 122, 124
	SELECTCASE ARG:1
	;キス
	CASE 0
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%と繋がったまま身体をぎゅっと抱き寄せ、何度もキスをした…
	ENDSELECT
;キス後背位 OR キス背面座位
CASE 121, 123
	SELECTCASE ARG:1
	;キス
	CASE 0
		PRINTFORML %CALLNAME:PLAYER%は後ろから%CALLNAME:TARGET%と体を密着させ、振り返った%CALLNAME:TARGET%とキスした…
	ENDSELECT
;挿入Ｇスポ責め OR 挿入子宮口責め
CASE 255, 256
	CALL TRAIN_MESSAGE_REVMODE_ACTIVEPHASE(PREVCOM, ARG:1)
;顔面騎乗クンニ
CASE 1
	SELECTCASE ARG:1
	;消極的にやる
	CASE 4
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の口を性器でふさぎ、奉仕を強要している…
	;積極的にやる
	CASE 5
		PRINTFORML %CALLNAME:PLAYER%は反応を１つずつ確かめるようにして%CALLNAME:TARGET%の女性器に舌を這わせた…
	ENDSELECT
;指挿入れ強制
CASE 8
	SELECTCASE ARG:1
	;消極的にやる
	CASE 4
		PRINTFORML %CALLNAME:TARGET%は渋る%CALLNAME:PLAYER%の手をとって、自分の秘所にこすりつけ始めた…
	;積極的にやる
	CASE 5
		PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%にキスをしながら全身を愛撫した…
	ENDSELECT
;胸を揉ませる
CASE 5
	SELECTCASE ARG:1
	;消極的にやる
	CASE 4
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%と手を重ね自分の胸を揉みしだいている…
	;積極的にやる
	CASE 5
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%の胸を思う存分揉みしだいた…
	ENDSELECT
;顔面騎乗アナル
CASE 9
	SELECTCASE ARG:1
	;消極的にやる
	CASE 4
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の口にアヌスを押し付け、奉仕を強要している…
	;積極的にやる
	CASE 5
		PRINTFORML %CALLNAME:PLAYER%は目の前でひくつく%CALLNAME:TARGET%のアヌスを丁寧に舐めた…
	ENDSELECT
;フェラさせる
CASE 4
	SELECTCASE ARG:1
	;消極的にやる
	CASE 4
		PRINTFORML %CALLNAME:PLAYER%は鼻先に突きつけられたペニスに、顔をしかめながらも奉仕した…
	;積極的にやる
	CASE 5
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%のペニスを根元までくわえ込み熱心に奉仕した…
	ENDSELECT
;正常位する
CASE 193
	SELECTCASE ARG:1
	;消極的にやる
	CASE 4
		PRINTFORML %CALLNAME:PLAYER%は懸命に快感を堪え、真っ赤に染まった顔を背けたが
		PRINTFORML 腰は妖しくうねり、%CALLNAME:TARGET%を離そうとしない…
	;積極的にやる
	CASE 5
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%に負けじと脚を絡めて腰を浮かせ、
		PRINTFORML %CALLNAME:TARGET%が感じるように括約筋を締め、突き込みを迎え入れた…
	;快感に耐える
	CASE 6
		PRINTFORML %CALLNAME:PLAYER%は懸命に快楽を堪えながら、%CALLNAME:TARGET%の腰使いに合わせて
		PRINTFORML 気持ち良くさせようと締め付け、腰を浮かせた…
	;快感を受け入れる
	CASE 7
		PRINTFORML %CALLNAME:TARGET%が腰をくねらせるたびに%CALLNAME:PLAYER%は深い快楽を与えられ、
		PRINTFORML 性交の悦楽に頭までずぶずぶと沈んでいく…
	ENDSELECT
;後背位する
CASE 194
	SELECTCASE ARG:1
	;消極的にやる
	CASE 4
		PRINTFORML %CALLNAME:PLAYER%は懸命に快感を堪え、震えながら顔を伏せたが
		PRINTFORML 腰は高く掲げられ、%CALLNAME:TARGET%に捧げられたままだった…
	;積極的にやる
	CASE 5
		PRINTFORML %CALLNAME:PLAYER%は腕と腰の力を使い、%CALLNAME:TARGET%が突くのに合わせて
		PRINTFORML 巧みに迎え腰を使った…
	;快感に耐える
	CASE 6
		PRINTFORML %CALLNAME:PLAYER%は快楽に崩れ落ちそうになりながらも、
		PRINTFORML %CALLNAME:TARGET%に向けて尻を突き出し、好きなように犯させた…
	;快感を受け入れる
	CASE 7
		PRINTFORML %CALLNAME:TARGET%が腰を引き寄せて突き上げると、%CALLNAME:PLAYER%は一際高い声で鳴き、
		PRINTFORML あまりの快感に全身をガクガク痙攣させた…
	ENDSELECT
;騎乗位させる
CASE 195
	SELECTCASE ARG:1
	;消極的にやる
	CASE 4
		PRINTFORML %CALLNAME:PLAYER%は自分から動くのを恥ずかしがって視線を逸らしたが、
		PRINTFORML 腰は自然に快楽を求めて動いている…
	;積極的にやる
	CASE 5
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%を逆に犯すかのように、
		PRINTFORML 上半身を押さえつけて巧みに腰を振り、快楽を与えようとした…
	;快感に耐える
	CASE 6
		PRINTFORML %CALLNAME:PLAYER%は強烈な快楽と欲望に呑まれないように必死に自分を保ち、
		PRINTFORML %CALLNAME:TARGET%を犯すつもりで腰を振った…
	;快感を受け入れる
	CASE 7
		PRINTFORML %CALLNAME:PLAYER%は背筋を逸らして腰を前後に揺すり、
		PRINTFORML 膣内の敏感な部分を何度も摩擦して嬌声を上げた…
	ENDSELECT
;自慰見せつけ
CASE 3
	SELECTCASE ARG:1
	;快感に耐える
	CASE 6
		PRINTFORML %CALLNAME:PLAYER%は顔を背けて必死に欲情を抑えている…
	;快感を受け入れる
	CASE 7
		PRINTFORML %CALLNAME:PLAYER%は黙って%CALLNAME%の自慰を観賞している…
	;おねだり
	CASE 8
		PRINTFORML %CALLNAME:PLAYER%は息を荒げながら%CALLNAME:TARGET%の痴態を凝視している…
	ENDSELECT
;自慰強制
CASE 205
	SELECTCASE ARG:1
	;消極的にやる
	CASE 4
		PRINTFORML 羞恥に頬を染めながら自慰をする%CALLNAME:PLAYER%を、%CALLNAME:TARGET%はじっと見つめている…
	;積極的にやる
	CASE 5
		PRINTFORML %CALLNAME:PLAYER%はじっと見つめる%CALLNAME:TARGET%の視線に興奮しながら自分で慰めた…
	ENDSELECT
ENDSELECT
WAIT

;----------------------------------------------------------------
;REVMODEに固有のSOURCE_CHECK
;----------------------------------------------------------------
@REVMODE_SOURCE_CHECK
#DIM LCOUNT, 1

;射精した
IF TFLAG:302
	SIF TFLAG:302 >= 4
		PRINT 超
	SIF TFLAG:302 >= 2
		PRINT 大量
	PRINTL 射精
	SELECTCASE SELECTCOM
	;助手を犯す、助手を犯させる
	CASE 56, 65
		PRINTFORML 精液経験＋{S}(%CALLNAME:ASSI%)
	;それ以外
	CASEELSE
		PRINTFORML 精液経験＋{S}(%CALLNAME:TARGET%)
	ENDSELECT
ENDIF

;接触の継続状態を管理
TRYCALL TOUCH_SUCCESSION(TARGET)

;バイブなど付けっぱなしアイテムのチェック
CALL EQUIP_CHECK

;生理中
SIF TALENT:138 && (FLAG:62 & 1)
	TRYCALL EVENT_MENSTRUAL_PAIN

;同性のチェック、調教者の能力のチェック
CALL SOURCE_SEX_CHECK
CALL PLAYER_SKILL_CHECK
CALL MASTER_SKILL_CHECK

;処女喪失のチェック
IF EXP:0 && TALENT:0 && TFLAG:19
	PRINTW 処女喪失
	TALENT:0 = 0
	;処女喪失フラグ(今回のコマンド)
	TFLAG:3 = 1
	;処女喪失フラグ(今回の調教)
	TFLAG:31 = 1
	;処女喪失フラグ(ビデオ撮影)
	SIF TEQUIP:53
		TFLAG:32 |= 1
ENDIF
;素質発覚イベント
SIF EXP:0 && TFLAG:19
	CALL FINE_VIRGIN

;童貞喪失（各種計算に童貞の有無が関係するので、実際の喪失処理は最後）
;フラグ追加とか面倒なので力技
;喪失の可能性のあるコマンドを実行すれば喪失
IF (SELECTCOM == 20 || SELECTCOM == 21 || SELECTCOM == 24 || SELECTCOM == 25 || SELECTCOM == 34 || SELECTCOM == 88 || SELECTCOM == 120 || SELECTCOM == 121 || SELECTCOM == 122 || SELECTCOM == 123 || SELECTCOM == 124 || SELECTCOM == 363 || SELECTCOM == 365 || SELECTCOM == 366) && TALENT:PLAYER:154
	PRINTFORMW %CALLNAME:PLAYER%は童貞を失った
	TALENT:PLAYER:154 = 2
	;一部の反感に関係するソースを半減
	SOURCE:14 /= 2
	SOURCE:15 /= 2
	;対象のキャラ番号を記憶
	FLAG:47 = NO:TARGET
ENDIF

;ソース上限値チェック（ソースを1京でカンストさせる）
REPEAT 25
	SOURCE:COUNT = LIMIT(SOURCE:COUNT, 0, 10000000000000000)
REND

;ソース計算
FOR LCOUNT, 1, 25
	TRYCALLFORM CALC_SOURCE{LCOUNT}
	;相性による上下の処理
	CALL UP_RELATION_CHECK(LCOUNT)
NEXT
;素質などによる上下の処理（快楽系）
CALL UP_TALENT_CVA_CHECK
;素質などによる上下の処理
CALL UP_TALENT_CHECK

;愛液処理
CALL LOVE_JUICE
;絶頂処理
CALL ORGASM_PROCESS
;調教対象の射精チェック（ふたなり・オトコ）
CALL TARGET_EJAC_CHECK
;調教対象の射精チェック（噴乳）
CALL TARGET_MILK_CHECK

;主人による調教の経験値
CALL MASTER_FLAG_CHECK

;体力・気力の減少
BASE:0 -= LOSEBASE:0
BASE:1 -= LOSEBASE:1

;メッセージ表示
CALL TRAIN_MESSAGE_A

;パラメータ変動による口上
CALL KOJO_MESSAGE_PALAMCNG

;苦痛快楽経験、奉仕快楽経験のチェック
CALL EXP_GOT_CHECK

;刻印取得のチェック
CALL MARK_GOT_CHECK
;刻印取得時の口上
CALL KOJO_MESSAGE_MARKCNG

;個別イベントの呼び出し
CALL KOJO_EVENT_COM

;パラメータ上昇量上限値チェック（上昇後が1京未満になるようにする）
REPEAT 25
	;パラメータが1京を越える場合、キャップをかける
	PALAM:COUNT = LIMIT(PALAM:COUNT, 0, 9999999999999999)
	;上昇後が1京を越える場合、キャップをかける
	IF (PALAM:COUNT + UP:COUNT) >= 10000000000000000
		UP:COUNT = 10000000000000000 - PALAM:COUNT - 1
		;快Ｃ快Ｖ快Ａ快Ｂ快ＭはDOWNにもキャップをかける
		SIF COUNT == 0 || COUNT == 1 || COUNT == 2 || COUNT == 14 || COUNT == 18
			DOWN:COUNT = 9999999999990000
	ENDIF
	;上昇前が限界値だった場合、上昇させない
	SIF PALAM:COUNT >= 9999999999999999
		UP:COUNT = 0
REND

;調教ソースの表示
CALL SHOW_SOURCE

;数値変動の表示
;相性
R = NO:PLAYER
SIF RELATION:R != 0
	PRINTFORML ＜相性{RELATION:R/100}.{RELATION:R%100}倍＞

;前回の調教者が助手か主人か
TFLAG:50 = ASSIPLAY

;気力０
SIF BASE:1 <= 0
	PRINTFORML %NAME:TARGET%の★気力０★

;体力・気力の減少
PRINTFORML %NAME:TARGET%の体力-{LOSEBASE:0} 気力-{LOSEBASE:1}
IF DOWNBASE:MASTER:0
	PRINTFORML %NAME:MASTER%の体力-{DOWNBASE:MASTER:0}
	BASE:MASTER:0 -= DOWNBASE:MASTER:0
	DOWNBASE:MASTER:0 = 0
ENDIF

;テキストの表示（顔面騎乗クンニ、胸を揉ませる）
IF GROUPMATCH(SELECTCOM, 1, 5)
	PRINTL 
	CALL TEXT
	PRINTL 
ENDIF

;パラメータの上昇＆表示（DOWNもここで）
IF FLAG:23 & 1p28
	CALL UPCHECK_ANIME
ELSE
	UPCHECK
ENDIF

;フラグの初期化
TFLAG:18 = 0
;コンドーム射精した時に1になるフラグ初期化
CFLAG:105 = 0
;射精量記録フラグ初期化
TFLAG:302 = 0

;童貞喪失（各種計算に童貞の有無が関係するので、喪失処理は最後）
;TALENTが2になってる場合喪失フラグがONとみなす
SIF TALENT:PLAYER:154 == 2
	TALENT:PLAYER:154 = 0

;今回のコマンドを「前回のコマンド」とする（但し「何もしない」は除外）
SIF SELECTCOM != 7
	PREVCOM = SELECTCOM

;ムード表示用フラグのセット
TFLAG:96 = TFLAG:37 - TFLAG:95

;ソース初期化
REPEAT 25
	SOURCE:COUNT = 0
REND

;-------------------------------------------------
;相性による上下の処理
;-------------------------------------------------
@UP_RELATION_CHECK(ARG)
R = NO:PLAYER
IF RELATION:R != 0
	RELATION:R = LIMIT (RELATION:R, 0, 1000)
	SELECTCASE ARG
	;快Ｃ、快Ｖ、快Ａ、快Ｂ、快Ｍ
	CASE 0, 1, 2, 17, 24
		UP:ARG = UP:ARG * RELATION:R / 100
	;性行動、達成感、中毒充足、反感追加
	CASE 4, 5, 7, 15
		SELECTCASE UP:ARG
		CASE IS < 500000
			UP:ARG = UP:ARG * RELATION:R / 100
		CASE IS < 100000000
			UP:ARG = UP:ARG / 100 * RELATION:R
		ENDSELECT
	;欲情追加、露出、屈従
	CASE 11, 12, 13
		SELECTCASE UP:ARG
		CASE IS < 500000
			UP:ARG = UP:ARG * 100 / RELATION:R
		CASE IS < 100000000
			UP:ARG = UP:ARG / RELATION:R * 100
		ENDSELECT
	ENDSELECT
ENDIF
