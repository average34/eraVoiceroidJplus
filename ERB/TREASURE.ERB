;---------------------------------------------------------
;宝探し処理
;宝探し能力の判定、アイテム獲得、獲得時のメッセージを独立させて個別に使えるよう変更
;G	宝探し能力の判定に使用
;N	アイテムランクのテーブル
;R	手に入るITEM番号
;LOCAL:2	テーブル内での乱数に使用
;---------------------------------------------------------
@TREASURE
;変数初期化
G = 0
N = 0
R = 0
S = 0

;宝探し能力の判定
CALL TREASURE_CHECK

;アイテム獲得
CALL TREASURE_GET

;アイテム獲得の地の文
CALL TREASURE_MESSAGE

RETURN 1
;---------------------------------------------------------
;宝探し能力の判定
;---------------------------------------------------------
@TREASURE_CHECK
;基本は0～50
G = RAND:50
;技巧*10
G += ABL:COUNT:技巧 * 10
;奉仕精神*5
G += ABL:COUNT:奉仕精神 * 5

;ナンバー参照からABL参照へ
;探索素質を見る
G += ABL:COUNT:探索素質 * 10

;好感度に応じたボーナス
IF CFLAG:COUNT:2 < 100
	G -= 5
ELSEIF CFLAG:COUNT:2 < 500
	G += 1
ELSEIF CFLAG:COUNT:2 < 1500
	G += 5
ELSEIF CFLAG:COUNT:2 < 3000
	G += 10
ELSEIF CFLAG:COUNT:2 < 5000
	G += 15
ELSEIF CFLAG:COUNT:2 < 10000
	G += 20
ELSEIF CFLAG:COUNT:2 < 30000
	G += 30
ELSE
	G += 40
ENDIF

;テーブルを決定
IF G < 50
	N = 0
ELSEIF G < 100
	N = 1
ELSEIF G < 200
	N = 2
ELSEIF G < 300
	N = 3
ELSEIF G < 400
	N = 4
ELSEIF G < 500
	N = 5
ELSE
	N = 6
ENDIF

;10%でテーブルランクアップ
IF RAND:10 == 0
	N += 1
ENDIF

SIF N >= 6
	N = 6

;---------------------------------------------------------
;アイテム獲得
;---------------------------------------------------------
@TREASURE_GET
;テーブルごとに獲得アイテム決定

;テーブルランク0は消費アイテム
;25,ローション,200,
;26,媚薬,2000,
IF N == 0
	;最低ランク
	LOCAL:2 = RAND:3
	IF LOCAL:2 == 0 && ITEM:ローション < 99
		R = 25
	ELSEIF LOCAL:2 == 1 && ITEM:媚薬 < 99
		R = 26
	ELSE
		R = 0
	ENDIF
	;40%で強制的に何もなし。
	IF RAND:100 < 40
		;PRINTFORML %CALLNAME:COUNT%は何も発見できませんでした
		RETURN 0
	ELSEIF R == 0
		;R = 0の場合はお金入手
		R = 100 + RAND:100
		MONEY += R
		;PRINTFORML %CALLNAME:COUNT%は${R}を発見しました
		RETURN 1
	ELSE
		;アイテム入手
		ITEM:R += 1
		;PRINTFORML %CALLNAME:COUNT%は%ITEMNAME:R%を発見しました
		RETURN 2
	ENDIF
ENDIF

;テーブルランク1以上は各ランクのJ用アイテム
VARSET J, 0
VARSET K, 0
;お金を入手する可能性有
K:J = 0
J += 1
;J用アイテムから獲得アイテム決定
FOR LOCAL, 500, 600
	;テーブルのランク以下のJ用アイテムしか手に入らない
	SIF ITEMPRICE:LOCAL > N || ITEMPRICE:LOCAL < 1
		CONTINUE
	;99個以上は手に入らない
	SIF ITEM:LOCAL >= 99
		CONTINUE
	;体力・気力を全回復するアイテムは3つまで
	SIF LOCAL == 517 && ITEM:LOCAL >= 3
		CONTINUE
	
	;ここまで残ったJ用アイテムは獲得アイテム候補
	K:J = LOCAL
	J += 1
NEXT

LOCAL = RAND:J
R = K:LOCAL

;15-N*2%で強制的に何もなし。
IF RAND:100 < (15 - N * 2)
	;PRINTFORML %CALLNAME:COUNT%は何も発見できませんでした
	RETURN 0
ENDIF

IF R == 0
	;R = 0の場合はお金入手
	IF N ==  0
		R = 100 + RAND:100
	ELSEIF N ==  1
		R = 500 + RAND:100
	ELSEIF N ==  2
		R = 1000 + RAND:1000
	ELSEIF N ==  3
		R = 2000 + RAND:1000
	ELSEIF N ==  4
		R = 5000 + RAND:1000
	ELSEIF N ==  5
		R = 10000 + RAND:10000
	ELSE
		R = 50000 + RAND:10000
	ENDIF
	MONEY += R
	;PRINTFORML %CALLNAME:COUNT%は${R}を発見しました
	RETURN 1
ELSE
	ITEM:R += 1
	;PRINTFORML %CALLNAME:COUNT%は%ITEMNAME:R%を発見しました
	RETURN 2
ENDIF

;---------------------------------------------------------
;アイテム獲得の地の文
;---------------------------------------------------------
@TREASURE_MESSAGE
IF FLAG:23 & 1024
	IF RESULT == 0
		PRINTFORML %CALLNAME:COUNT%は何も発見できませんでした
	ELSEIF RESULT == 1
		PRINTFORML %CALLNAME:COUNT%は${R}を発見しました
	ELSEIF RESULT == 2
		PRINTFORML %CALLNAME:COUNT%は%ITEMNAME:R%を発見しました
	ENDIF
ENDIF

;---------------------------------------------------------
;%CALLNAME:MASTER%が宝探し
;---------------------------------------------------------
@TREASURE_MASTER
TFLAG:600 = 43
CALL KOJO_JUN
BASE:MASTER:耐力 -= 500
;COUNTにMASTERを指定して宝探し
COUNT = MASTER
;宝探し能力の判定
CALL TREASURE_CHECK
;レア度ＭＡＸの入手確率は最高10%
IF G >= 500 && RAND:100 < 90
	N = 5
ENDIF
;アイテム獲得
CALL TREASURE_GET
;アイテムとは別にお金入手（宝探し能力の判定変数Gを基準に算出してGに上書き）
G = G * (RAND:11 + 15) * (RAND:5 + 5)
MONEY += G

;アイテム獲得の地の文
IF RESULT == 0
	PRINTFORMW %CALLNAME:COUNT%は遺跡を探索して${G}を発見しました
ELSEIF RESULT == 1
	PRINTFORMW %CALLNAME:COUNT%は遺跡を探索して${G + R}を発見しました
ELSEIF RESULT == 2
	PRINTFORMW %CALLNAME:COUNT%は遺跡を探索して${G}と%ITEMNAME:R%を発見しました
ENDIF
;宝探し詳細報告アリなら、PRINTL 
SIF FLAG:23 & 1024
	PRINTL 

;---------------------------------------------------------
;２人で宝探し
;---------------------------------------------------------
@TREASURE_TWO
TFLAG:600 = 44
CALL KOJO_JUN
BASE:MASTER:耐力 -= 300
BASE:TARGET:体力 -= 200
;MASTERとTARGETをそれぞれ別に宝探し能力の判定
COUNT = MASTER
CALL TREASURE_CHECK
;Iを利用しMASTERの宝探し能力Gを保存
I = G

COUNT = TARGET
CALL TREASURE_CHECK
;パートナーが親愛なら合体
IF TALENT:親愛
	G = (G + I)
;恋人ならMASTERとパートナーで良い方を採用
ELSEIF TALENT:恋人
	SIF G <= I
		G = I
;それ以外は二人の平均
ELSE
	G = (G + I) / 2
ENDIF
;レア度ＭＡＸの入手確率は最高20%
IF G >= 500 && RAND:100 < 80
	N = 5
ENDIF

;アイテム獲得
CALL TREASURE_GET

;アイテムとは別にお金入手（宝探し能力の判定変数Gを基準に算出してGに上書き）
G = G * (RAND:11 + 15) * (RAND:10 + 5)
MONEY += G

;アイテム獲得の地の文
IF RESULT == 0
	PRINTFORMW %CALLNAME:MASTER%と%CALLNAME:TARGET%は２人で遺跡を探索して${G}を発見しました
ELSEIF RESULT == 1
	PRINTFORMW %CALLNAME:MASTER%と%CALLNAME:TARGET%は２人で遺跡を探索して${G + R}を発見しました
ELSEIF RESULT == 2
	PRINTFORMW %CALLNAME:MASTER%と%CALLNAME:TARGET%は２人で遺跡を探索して${G}と%ITEMNAME:R%を発見しました
ENDIF
;宝探し詳細報告アリなら、PRINTL 
SIF FLAG:23 & 1024
	PRINTL 

;---------------------------------------------------------
;アイテム使用
;---------------------------------------------------------
@USE_TREASURE_ITEM
;効果の説明文
VARSET TSTR, ""
TSTR:0 = 体力消費を少し軽減します
TSTR:1 = 体力消費を軽減します
TSTR:2 = 体力消費を大きく軽減します
TSTR:3 = 気力消費を少し軽減します
TSTR:4 = 気力消費を軽減します
TSTR:5 = 気力消費を大きく軽減します
TSTR:6 = ムードを少し上げやすくします
TSTR:7 = ムードを上げやすくします
TSTR:8 = ムードを大きく上げやすくします
TSTR:9 = 体力・気力消費を軽減し、ムードを上げやすくします
TSTR:10 = パートナーの好感度を少し上げます
TSTR:11 = パートナーの好感度を上げます
TSTR:12 = パートナーの好感度を大きく上げます
TSTR:13 = パートナーを酔わせます
TSTR:14 = パートナーを凄く酔わせます
TSTR:15 = パートナーを物凄く酔わせます
TSTR:16 = 天気を変更します
TSTR:17 = パートナーの体力・気力を完全回復させます
TSTR:18 = パートナーの体力・気力を増やします
TSTR:19 = パートナーを一時的に貧乳にします
TSTR:20 = パートナーを一時的に並乳にします
TSTR:21 = パートナーを一時的に巨乳にします
TSTR:22 = パートナーを一時的に子供にします
TSTR:23 = パートナーを一時的に大人にします
TSTR:24 = パートナーを一時的に超敏感体質にします
TSTR:25 = パートナーを一時的に動物耳にします
TSTR:26 = パートナーを一時的に魔性のテクニックの持ち主にします
TSTR:27 = パートナーを一時的に体力の上限がない状態にします
TSTR:28 = パートナーを発情させ、押し倒されるようになります。
TSTR:29 = パートナーを一時的に羽根持ちにします
TSTR:30 = パートナーを一時的に猫化します

PRINTFORML どのアイテムを使用しますか？
A = 0
REPEAT 100
	B = 500 + COUNT
	SIF TSTR:COUNT == ""
		CONTINUE
	IF ITEM:B
		LOCALS = %ITEMNAME:B% ({ITEM:B})
		PRINTFORML [{COUNT,2}] %LOCALS,22,LEFT% %TSTR:COUNT%
	ENDIF
REND
PRINTFORML [100] やっぱりやめる

INPUT
B = 500 + RESULT
IF RESULT == 100
	PRINTFORMW アイテムを使用するのを止めました
	RETURN 0
ELSEIF RESULT >= 500
	PRINTFORMW そのアイテムは持っていません
	RESTART
ELSEIF ITEM:B == 0
	PRINTFORMW そのアイテムは持っていません
	RESTART
ELSEIF (RESULT >= 0 && RESULT <= 9) && TEQUIP:3
	PRINTFORMW 複数のインセンスを併用する事はできません
	RESTART
ELSEIF RESULT >= 0 && RESULT <= 9
	PRINTFORML インセンス：%ITEMNAME:B%を使用しました
	PRINTFORMW 周囲に%ITEMNAME:B%の芳香が漂います…
	TEQUIP:3 = RESULT + 1
	ITEM:B -= 1
ELSEIF RESULT == 10
	PRINTFORML %ITEMNAME:510%を使用しました
	PRINTFORMW %CALLNAME:TARGET%の好感度が50上昇しました
	CFLAG:2 += 50
	ITEM:惚れ薬ホレールＡ -= 1
ELSEIF RESULT == 11
	PRINTFORML %ITEMNAME:511%を使用しました
	PRINTFORMW %CALLNAME:TARGET%の好感度が200上昇しました
	CFLAG:2 += 200
	ITEM:惚れ薬シチリンＸ -= 1
ELSEIF RESULT == 12
	PRINTFORML %ITEMNAME:512%を使用しました
	PRINTFORMW %CALLNAME:TARGET%の好感度が2000上昇しました
	CFLAG:2 += 2000
	ITEM:惚れ薬ヤゴコロＺ -= 1
ELSEIF RESULT == 13
	PRINTFORMW %CALLNAME:TARGET%に%ITEMNAME:513%を飲ませました
	TFLAG:47 += 20
	ITEM:銘酒「月世界」 -= 1
ELSEIF RESULT == 14
	PRINTFORMW %CALLNAME:TARGET%に%ITEMNAME:514%を飲ませました
	TFLAG:47 += 40
	ITEM:銘酒「鬼殺し」 -= 1
ELSEIF RESULT == 15
	PRINTFORMW %CALLNAME:TARGET%に%ITEMNAME:515%を飲ませました
	TFLAG:47 += 100
	ITEM:スピリタス -= 1
ELSEIF RESULT == 16
	PRINTFORML %ITEMNAME:516%を使用しました
	ITEM:お天気ママさん -= 1
	IF (TIME:5 == 0 || TIME:5 == 1 || TIME:5 == 2)
		IF RAND:3 == 0
			IF TIME:5 == 2
				PRINTFORMW 天気は変わりませんでした…
			ELSE
				PRINTFORMW 天気が曇りになりました
				STR:3 = 曇り
				TIME:5 = 2
			ENDIF
		ELSEIF RAND:2 == 0
			PRINTFORMW 天気が雨になりました
			STR:3 = 雨
			TIME:5 = 3
		ELSE
			PRINTFORMW 天気が大雨になりました
			STR:3 = 大雨
			TIME:5 = 4
		ENDIF
		IF TIME:8 == 1
			PRINTFORMW 虹が消えてしまいました…
			TIME:8 = 0
		ENDIF
	ELSE
		IF RAND:3 == 0
			IF TIME:5 == 2
				PRINTFORMW 天気は変わりませんでした…
			ELSE
				PRINTFORMW 天気が曇りになりました
				STR:3 = 曇り
				TIME:5 = 2
			ENDIF
		ELSEIF RAND:2 == 0
			PRINTFORMW 天気が晴れになりました
			STR:3 = 晴れ
			TIME:5 = 0
		ELSE
			PRINTFORMW 天気が快晴になりました
			STR:3 = 快晴
			TIME:5 = 1
		ENDIF
	ENDIF
ELSEIF RESULT == 17
	PRINTFORML %CALLNAME:TARGET%に%ITEMNAME:517%を飲ませました
	PRINTFORMW 体力・気力が完全回復しました
	BASE:体力 = MAXBASE:体力
	BASE:気力 = MAXBASE:気力
	ITEM:秘仙丹 -= 1
ELSEIF RESULT == 18
	PRINTFORML %CALLNAME:TARGET%に%ITEMNAME:518%を食べさせました
	PRINTFORMW 体力・気力上限が＋１００されました
	MAXBASE:体力 += 100
	MAXBASE:気力 += 100
	BASE:体力 += 100
	BASE:気力 += 100
	ITEM:天界の桃 -= 1
ELSEIF RESULT >= 19 && RESULT <= 21 && TEQUIP:40 & 7
	PRINTFORMW 複数のエキスを併用することはできません
	RESTART
ELSEIF RESULT == 19
	;素質発覚（隠し様がないね）
	SIF FLAG:2 == 1 && (CFLAG:60 & 32) == 0
		CFLAG:60 |= 32
	IF TALENT:貧乳
		PRINTFORMW %CALLNAME:TARGET%の胸は元々貧しいです
		RESTART
	ELSE
		PRINTFORMW %CALLNAME:TARGET%に%ITEMNAME:519%を飲ませました
		PRINTFORMW 一時的に貧乳になりました
		IF TALENT:巨乳
			TALENT:巨乳 = 0
			TEQUIP:40 |= 4
		ELSE
			TEQUIP:40 |= 2
		ENDIF
		ITEM:新芽のエキス -= 1
		TALENT:貧乳 = 1
	ENDIF
ELSEIF RESULT == 20
	;素質発覚（隠し様がないね）
	SIF FLAG:2 == 1 && (CFLAG:60 & 32) == 0
		CFLAG:60 |= 32
	IF TALENT:貧乳 == 0 && TALENT:巨乳 == 0
		PRINTFORMW %CALLNAME:TARGET%の胸は程よいと言っていいくらいです
		RESTART
	ELSE
		PRINTFORMW %CALLNAME:TARGET%に%ITEMNAME:520%を飲ませました
		PRINTFORMW 一時的に並乳になりました
		IF TALENT:貧乳
			TALENT:貧乳 = 0
			TEQUIP:40 |= 1
		ELSE
			TALENT:巨乳 = 0
			TEQUIP:40 |= 4
		ENDIF
		ITEM:若木のエキス -= 1
	ENDIF
ELSEIF RESULT == 21
	;素質発覚（隠し様がないね）
	SIF FLAG:2 == 1 && (CFLAG:60 & 32) == 0
		CFLAG:60 |= 32
	IF TALENT:巨乳
		PRINTFORMW %CALLNAME:TARGET%の胸は十分大きいです
		RESTART
	ELSE
		PRINTFORMW %CALLNAME:TARGET%に%ITEMNAME:521%を飲ませました
		PRINTFORMW 一時的に巨乳になりました
		IF TALENT:貧乳
			TALENT:貧乳 = 0
			TEQUIP:40 |= 1
		ELSE
			TEQUIP:40 |= 2
		ENDIF
		TALENT:巨乳 = 1
		ITEM:大樹のエキス -= 1
	ENDIF
ELSEIF (RESULT == 22 || RESULT == 23) && TEQUIP:40 & 24
	PRINTFORMW 複数回の体型変化は出来ません
ELSEIF RESULT == 22
	IF TALENT:小柄体型
		PRINTFORMW %CALLNAME:TARGET%は元々小柄体型です
		RESTART
	ELSE
		PRINTFORMW %CALLNAME:TARGET%に%ITEMNAME:522%を飲ませました
		PRINTFORMW 一時的に小柄体型になりました
		TEQUIP:40 |= 8
		TALENT:小柄体型 = 1
		ITEM:朝の雫 -= 1
	ENDIF
ELSEIF RESULT == 23
	IF TALENT:小柄体型 == 0
		PRINTFORMW %CALLNAME:TARGET%は元々大人体型です
		RESTART
	ELSE
		PRINTFORMW %CALLNAME:TARGET%に%ITEMNAME:523%を飲ませました
		PRINTFORMW 一時的に大人体型になりました
		TEQUIP:40 |= 16
		TALENT:小柄体型 = 0
		ITEM:夜の雫 -= 1
	ENDIF
ELSEIF RESULT == 24
	IF TALENT:濡れやすい && TALENT:Ｃ敏感 && TALENT:Ｖ敏感 && TALENT:Ａ敏感 && TALENT:Ｂ敏感
		PRINTFORMW %CALLNAME:TARGET%にはこんなもの必要ないです
		RESTART
	ELSEIF TEQUIP:40 & 64
		PRINTFORMW %CALLNAME:TARGET%は既に超敏感体質です
		RESTART
	ELSE
		PRINTFORMW %CALLNAME:TARGET%に%ITEMNAME:524%を飲ませました
		PRINTFORMW 一時的に超敏感体質になりました
		TEQUIP:40 |= 64
		;有利素質のほうは手抜き
		TALENT:濡れやすい += 1
		TALENT:Ｃ敏感 += 1
		TALENT:Ｖ敏感 += 1
		TALENT:Ａ敏感 += 1
		TALENT:Ｂ敏感 += 1
		TALENT:Ｍ敏感 += 1
		;不利素質はビット演算で記録
		IF TALENT:濡れにくい
			TEQUIP:40 |= 128
			TALENT:濡れにくい = 0
		ENDIF
		IF TALENT:Ｃ鈍感
			TEQUIP:40 |= 256
			TALENT:Ｃ鈍感 = 0
		ENDIF
		IF TALENT:Ｖ鈍感
			TEQUIP:40 |= 512
			TALENT:Ｖ鈍感 = 0
		ENDIF
		IF TALENT:Ａ鈍感
			TEQUIP:40 |= 1024
			TALENT:Ａ鈍感 = 0
		ENDIF
		IF TALENT:Ｂ鈍感
			TEQUIP:40 |= 2048
			TALENT:Ｂ鈍感 = 0
		ENDIF
		IF TALENT:Ｍ鈍感
			TEQUIP:40 |= 1p17
			TALENT:Ｍ鈍感 = 0
		ENDIF
		ITEM:紫色の秘薬 -= 1
	ENDIF
ELSEIF RESULT == 25
	IF TALENT:動物耳
		IF TEQUIP:40 & 32
			PRINTFORMW %CALLNAME:TARGET%には既に%ITEMNAME:525%を飲ませています
		ELSE
			PRINTFORMW %CALLNAME:TARGET%は元々動物耳を持っています
		ENDIF
		RESTART
	ELSE
		TEQUIP:40 |= 32
		TALENT:動物耳 = 1
		ITEM:満月の秘薬 -= 1
		TFLAG:17 = RESULT
		TFLAG:600 = 42
		CALL KOJO_JUN
		IF TFLAG:17 == 25
			PRINTFORMW %CALLNAME:TARGET%に%ITEMNAME:525%を飲ませました
			PRINTFORM 一時的に
			IF RAND:100 == 0
				IF RAND:4 == 0
					PRINTFORM くま耳
				ELSEIF RAND:3 == 0
					PRINTFORM 豚耳
				ELSEIF RAND:2 == 0
					PRINTFORM ねずみ耳
				ELSE
					PRINTFORM 牛耳
				ENDIF
			ELSEIF RAND:5 == 0
				PRINTFORM 狸耳
			ELSEIF RAND:4 == 0
				PRINTFORM 猫耳
				TEQUIP:40 |= 1p14
				TALENT:猫舌 = 1
			ELSEIF RAND:3 == 0
				PRINTFORM 犬耳
			ELSEIF RAND:2 == 0
				PRINTFORM 狐耳
			ELSE
				PRINTFORM うさ耳
			ENDIF
			PRINTFORMW になりました
		ENDIF
		RETURN 0
	ENDIF
ELSEIF RESULT == 26
	IF TEQUIP:40 & 4096
		PRINTFORMW %CALLNAME:TARGET%には既に%ITEMNAME:526%を飲ませています
		RESTART
	ELSE
		PRINTFORMW %CALLNAME:TARGET%に%ITEMNAME:526%を飲ませました
		PRINTFORMW 一時的に魔性のテクニックを得ました
		TEQUIP:40 |= 4096
		ABL:技巧 += 10
		ITEM:娼婦の秘薬 -= 1
	ENDIF
ELSEIF RESULT == 27
	IF TEQUIP:41
		PRINTFORMW %CALLNAME:TARGET%には既に%ITEMNAME:527%を飲ませています
		RESTART
	ELSE
		PRINTFORMW %CALLNAME:TARGET%に%ITEMNAME:527%を飲ませました
		PRINTFORMW 一時的に体力の上限がなくなりました
		TEQUIP:41 = 1
		ITEM:赤色の秘薬 -= 1
	ENDIF
ELSEIF RESULT == 28
	;桃色の秘薬使用
	PRINTFORMW %CALLNAME:TARGET%に%ITEMNAME:528%を飲ませました
	TEQUIP:28 += 1
	ITEM:桃色の秘薬 -= 1
	;使用した時点で押し倒されるかどうか判定
	CALL PUSH_OVER_RATE
	IF A >= 220
		;押し倒される
		PRINTFORMW %CALLNAME:TARGET%は荒い息をついて肌を紅潮させている…
	ELSE
		;押し倒されない
		PRINTFORMW %CALLNAME:TARGET%の頬はほんのり上気しているが、まだ余裕がありそうだ…
	ENDIF
	RESULT = 28
ELSEIF RESULT == 29
	;空色の秘薬使用　翼を授ける
	IF TALENT:羽
		IF TEQUIP:40 & 1p15
			PRINTFORMW %CALLNAME:TARGET%には既に%ITEMNAME:529%を飲ませています
		ELSE
			PRINTFORMW %CALLNAME:TARGET%は元々羽を持っています
		ENDIF
		RESTART
	ELSE
		PRINTFORMW %CALLNAME:TARGET%に%ITEMNAME:529%を飲ませました
		PRINTFORMW 一時的に羽が生えました
		TEQUIP:40 |= 1p15
		TALENT:羽 = 1
		ITEM:空色の秘薬 -= 1
	ENDIF
ELSEIF RESULT == 30
	;黒猫の秘薬使用　猫になる
	IF TALENT:猫舌 && TALENT:動物耳 && TALENT:しっぽ
		IF TEQUIP:40 & 1p14
			PRINTFORMW %CALLNAME:TARGET%には既に%ITEMNAME:530%を飲ませています
		ELSE
			PRINTFORMW %CALLNAME:TARGET%は元々猫です
		ENDIF
		RESTART
	ELSE
		PRINTFORMW %CALLNAME:TARGET%に%ITEMNAME:530%を飲ませました
		PRINTFORMW 一時的にネコ耳としっぽが生えました
		;猫舌
		TEQUIP:40 |= 1p14
		TALENT:猫舌 = 1
		;動物耳
		TEQUIP:40 |= 32
		TALENT:動物耳 = 1
		;しっぽ
		TEQUIP:40 |= 1p16
		TALENT:しっぽ = 1
		ITEM:黒猫の秘薬 -= 1
	ENDIF
ENDIF
TFLAG:17 = RESULT
TFLAG:600 = 42
CALL KOJO_JUN
